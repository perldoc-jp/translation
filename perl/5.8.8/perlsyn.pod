=head1 NAME
X<syntax>

=begin original

perlsyn - Perl syntax

=end original

perlsyn - Perl の文法

=head1 DESCRIPTION

=begin original

A Perl program consists of a sequence of declarations and statements
which run from the top to the bottom.  Loops, subroutines and other
control structures allow you to jump around within the code.

=end original

Perl プログラムは、宣言と文の並びから構成され、上から下へと実行されます。
ループ、サブルーチン、その他の制御機構でコードの色々なところに
ジャンプできます。

=begin original

Perl is a B<free-form> language, you can format and indent it however
you like.  Whitespace mostly serves to separate tokens, unlike
languages like Python where it is an important part of the syntax.

=end original

Perl は B<自由書式> 言語ですが、好きなように整形したりインデントしたり
できます。
空白が文法の重要な要素である Python のような言語と異なり、
空白はほとんどトークンの分割の役目です。

=begin original

Many of Perl's syntactic elements are B<optional>.  Rather than
requiring you to put parentheses around every function call and
declare every variable, you can often leave such explicit elements off
and Perl will figure out what you meant.  This is known as B<Do What I
Mean>, abbreviated B<DWIM>.  It allows programmers to be B<lazy> and to
code in a style with which they are comfortable.

=end original

Perl の多くの文法要素は B<省略可能> です。
Rather than
requiring you to put parentheses around every function call and
declare every variable, you can often leave such explicit elements off
and Perl will figure out what you meant.  This is known as B<Do What I
Mean>, abbreviated B<DWIM>.  It allows programmers to be B<lazy> and to
code in a style with which they are comfortable.
(TBT)

=begin original

Perl B<borrows syntax> and concepts from many languages: awk, sed, C,
Bourne Shell, Smalltalk, Lisp and even English.  Other
languages have borrowed syntax from Perl, particularly its regular
expression extensions.  So if you have programmed in another language
you will see familiar pieces in Perl.  They often work the same, but
see L<perltrap> for information about how they differ.

=end original

Perl は、awk, sed, C, Bourne Shell, Smalltalk, Lisp, 果ては英語といった、
多くの言語からコンセプトと B<文法を借用> しています。
他の言語も Perl から文法を借用しています; 特に正規表現拡張をです。
従って、他の言語でプログラミングしていたなら、Perl にも見たことがあるような
ものがあるでしょう。
それらはしばしば同じように動作しますが、違う点についての情報は
L<perltrap> を参照してください。

=head2 Declarations
X<declaration> X<undef> X<undefined> X<uninitialized>

=begin original

The only things you need to declare in Perl are report formats and
subroutines (and sometimes not even subroutines).  A variable holds
the undefined value (C<undef>) until it has been assigned a defined
value, which is anything other than C<undef>.  When used as a number,
C<undef> is treated as C<0>; when used as a string, it is treated as
the empty string, C<"">; and when used as a reference that isn't being
assigned to, it is treated as an error.  If you enable warnings,
you'll be notified of an uninitialized value whenever you treat
C<undef> as a string or a number.  Well, usually.  Boolean contexts,
such as:

=end original

The only things you need to declare in Perl are report formats and
subroutines (and sometimes not even subroutines).  A variable holds
the undefined value (C<undef>) until it has been assigned a defined
value, which is anything other than C<undef>.  When used as a number,
C<undef> is treated as C<0>; when used as a string, it is treated as
the empty string, C<"">; and when used as a reference that isn't being
assigned to, it is treated as an error.  If you enable warnings,
you'll be notified of an uninitialized value whenever you treat
C<undef> as a string or a number.  Well, usually.  Boolean contexts,
such as:
(TBT)

    my $a;
    if ($a) {}

=begin original

are exempt from warnings (because they care about truth rather than
definedness).  Operators such as C<++>, C<-->, C<+=>,
C<-=>, and C<.=>, that operate on undefined left values such as:

=end original

とすると(定義済みかどうかではなく、真を考慮するので)警告から免れます。
未定義の左辺値を操作する、C<++>, C<-->, C<+=>, C<-=>, C<.=> のような
演算子でも:

    my $a;
    $a++;

=begin original

are also always exempt from such warnings.

=end original

とすることでもそのような警告から免れます。

=begin original

A declaration can be put anywhere a statement can, but has no effect on
the execution of the primary sequence of statements--declarations all
take effect at compile time.  Typically all the declarations are put at
the beginning or the end of the script.  However, if you're using
lexically-scoped private variables created with C<my()>, you'll
have to make sure
your format or subroutine definition is within the same block scope
as the my if you expect to be able to access those private variables.

=end original

宣言は、文が置けるところであればどこにでも置くことができますが、
基本的な文の並びは実行時には何の効果も持ちません -- 宣言はコンパイル時に
すべての効果が表れます。
典型的なすべての宣言は、スクリプトの先頭か終端に置かれます。
しかしながら、局所変数を C<my()> を使って作成してレキシカルなスコープを
使っているのであれば、フォーマットやサブルーチンの定義を、同じブロックの
スコープの中でその局所変数にアクセスすることが可能であるようにしておく
必要があるでしょう。

=begin original

Declaring a subroutine allows a subroutine name to be used as if it were a
list operator from that point forward in the program.  You can declare a
subroutine without defining it by saying C<sub name>, thus:
X<subroutine, declaration>

=end original

サブルーチンの宣言は、プログラムの後のほうにあるサブルーチン名を
リスト演算子のように使うことを許します。
定義されていないサブルーチンの宣言を、C<sub name> と記述することで
宣言できるので、以下のようにできます。
X<subroutine, declaration>

    sub myname;
    $me = myname $0 		or die "can't get myname";

=begin original

Note that myname() functions as a list operator, not as a unary operator;
so be careful to use C<or> instead of C<||> in this case.  However, if
you were to declare the subroutine as C<sub myname ($)>, then
C<myname> would function as a unary operator, so either C<or> or
C<||> would work.

=end original

myname() 関数は、リスト演算子のように働くのであり、単項演算子としてでは
ないということに注意してください。
ですから、こういった場合に C<||> の代わりに C<or> を使うことに注意しましょう。
しかし、サブルーチンを C<sub myname ($)> のように宣言しているのであれば、
C<or> でも C<||> でもうまく行きます。

=begin original

Subroutines declarations can also be loaded up with the C<require> statement
or both loaded and imported into your namespace with a C<use> statement.
See L<perlmod> for details on this.

=end original

サブルーチンの宣言は C<require> 文を使って詰め込むこともできますし、
C<use> 文を使って自分の名前空間にロードしたりインポートしたりすることが
できます。
詳細は L<perlmod> を参照してください。

=begin original

A statement sequence may contain declarations of lexically-scoped
variables, but apart from declaring a variable name, the declaration acts
like an ordinary statement, and is elaborated within the sequence of
statements as if it were an ordinary statement.  That means it actually
has both compile-time and run-time effects.

=end original

文の並びはレキシカルスコープを持った変数の宣言を含むことができますが、
変数名の宣言とは切り離され、その宣言は通常の文のように振る舞い、
それが通常の文であるかのように文の並びに組みこまれます。
これは、そういった宣言がコンパイル時の効果と実行時の効果の両方を
持っているということです。

=head2 Comments
X<comment> X<#>

=begin original

Text from a C<"#"> character until the end of the line is a comment,
and is ignored.  Exceptions include C<"#"> inside a string or regular
expression.

=end original

コメントは C<“#”> 文字から、行末まで続き、その部分は無視されます。
例外は、文字列や正規表現の中にある C<"#"> です。

=head2 Simple Statements
X<statement> X<semicolon> X<expression> X<;>

=begin original

The only kind of simple statement is an expression evaluated for its
side effects.  Every simple statement must be terminated with a
semicolon, unless it is the final statement in a block, in which case
the semicolon is optional.  (A semicolon is still encouraged if the
block takes up more than one line, because you may eventually add
another line.)  Note that there are some operators like C<eval {}> and
C<do {}> that look like compound statements, but aren't (they're just
TERMs in an expression), and thus need an explicit termination if used
as the last item in a statement.

=end original

単純文となる唯一の種類は、その副作用のために評価される式です。
すべての単純文は、それがセミコロンを省略することのできるブロックの
最後にない限りは文を終端するためのセミコロンがなければなりません
(ブロックが二行以上に渡る場合には、セミコロンを付けることをお薦めします。
なぜなら、別の行を追加する可能性があるからです)。
C<eval {}> や C<do {}> のように、一見複合文のようにみえるけれども
そうではない(これらは単なる式における TERM です)ものがあって、
そういったものを文の最後のアイテムとして使った場合には明示的に終端する
必要があるのだということに注意してください。

=head2 Truth and Falsehood
X<truth> X<falsehood> X<true> X<false> X<!> X<not> X<negation> X<0>

=begin original

The number 0, the strings C<'0'> and C<''>, the empty list C<()>, and
C<undef> are all false in a boolean context. All other values are true.
Negation of a true value by C<!> or C<not> returns a special false value.
When evaluated as a string it is treated as C<''>, but as a number, it
is treated as 0.

=end original

The number 0, the strings C<'0'> and C<''>, the empty list C<()>, and
C<undef> are all false in a boolean context. All other values are true.
Negation of a true value by C<!> or C<not> returns a special false value.
When evaluated as a string it is treated as C<''>, but as a number, it
is treated as 0.
(TBT)

=head2 Statement Modifiers
X<statement modifier> X<modifier> X<if> X<unless> X<while>
X<until> X<foreach> X<for>

=begin original

Any simple statement may optionally be followed by a I<SINGLE> modifier,
just before the terminating semicolon (or block ending).  The possible
modifiers are:

=end original

任意の単純文には、B<一つ> の修飾子を終端のセミコロンの直前
(もしくはブロックの終端の直前)に付けることができます。
使うことのできる修飾子は以下の通りです。

    if EXPR
    unless EXPR
    while EXPR
    until EXPR
    foreach LIST

=begin original

The C<EXPR> following the modifier is referred to as the "condition".
Its truth or falsehood determines how the modifier will behave.

=end original

The C<EXPR> following the modifier is referred to as the "condition".
Its truth or falsehood determines how the modifier will behave.
(TBT)

=begin original

C<if> executes the statement once I<if> and only if the condition is
true.  C<unless> is the opposite, it executes the statement I<unless>
the condition is true (i.e., if the condition is false).

=end original

C<if> executes the statement once I<if> and only if the condition is
true.  C<unless> is the opposite, it executes the statement I<unless>
the condition is true (i.e., if the condition is false).
(TBT)

    print "Basset hounds got long ears" if length $ear >= 10;
    go_outside() and play() unless $is_raining;

=begin original

The C<foreach> modifier is an iterator: it executes the statement once
for each item in the LIST (with C<$_> aliased to each item in turn).

=end original

C<foreach> 修飾子はイテレーターです。
LIST の値それぞれ毎に文を実行します(実行中は C<$_> がそれぞれの値の
エイリアスとなります)。

    print "Hello $_!\n" foreach qw(world Dolly nurse);

=begin original

C<while> repeats the statement I<while> the condition is true.
C<until> does the opposite, it repeats the statement I<until> the
condition is true (or while the condition is false):

=end original

C<while> repeats the statement I<while> the condition is true.
C<until> does the opposite, it repeats the statement I<until> the
condition is true (or while the condition is false):
(TBT)

    # Both of these count from 0 to 10.
    print $i++ while $i <= 10;
    print $j++ until $j >  10;

=begin original

The C<while> and C<until> modifiers have the usual "C<while> loop"
semantics (conditional evaluated first), except when applied to a
C<do>-BLOCK (or to the deprecated C<do>-SUBROUTINE statement), in
which case the block executes once before the conditional is
evaluated.  This is so that you can write loops like:

=end original

修飾子 C<while> と C<until> もまた、一般的な "while loop" の意味を
持っています(条件が最初に評価される)が、do ブロック(もしくは現在では
使用を推奨されていない do サブルーチン文)に適用されるときは例外で、
このときは条件が評価されるよりも前に、一度ブロックが実行されます。
このため、次のようなループを記述することができます。

    do {
	$line = <STDIN>;
	...
    } until $line  eq ".\n";

=begin original

See L<perlfunc/do>.  Note also that the loop control statements described
later will I<NOT> work in this construct, because modifiers don't take
loop labels.  Sorry.  You can always put another block inside of it
(for C<next>) or around it (for C<last>) to do that sort of thing.
For C<next>, just double the braces:
X<next> X<last> X<redo>

=end original

L<perlfunc/do> を参照してください。
後述するループの制御文は、修飾子がループラベルを取らないために
この構造文では I<動作しない> ということにも注意してください。
申し訳ない。
こういった場合に対処するのに別のブロックを内側に入れたり(C<next> の場合)、
別のブロックで囲む(C<last> の場合)という方法が常に使えます。
C<next> では単に中かっこを二重にします。

    do {{
	next if $x == $y;
	# do something here
    }} until $x++ > $z;

=begin original

For C<last>, you have to be more elaborate:
X<last>

=end original

C<last> の場合は、もっと念入りにする必要があります:

    LOOP: { 
	    do {
		last if $x = $y**2;
		# do something here
	    } while $x++ <= $z;
    }

=begin original

B<NOTE:> The behaviour of a C<my> statement modified with a statement
modifier conditional or loop construct (e.g. C<my $x if ...>) is
B<undefined>.  The value of the C<my> variable may be C<undef>, any
previously assigned value, or possibly anything else.  Don't rely on
it.  Future versions of perl might do something different from the
version of perl you try it out on.  Here be dragons.
X<my>

=end original

B<NOTE:> The behaviour of a C<my> statement modified with a statement
modifier conditional or loop construct (e.g. C<my $x if ...>) is
B<undefined>.  The value of the C<my> variable may be C<undef>, any
previously assigned value, or possibly anything else.  Don't rely on
it.  Future versions of perl might do something different from the
version of perl you try it out on.  Here be dragons.
X<my>
(TBT)

=head2 Compound Statements
X<statement, compound> X<block> X<bracket, curly> X<curly bracket> X<brace>
X<{> X<}> X<if> X<unless> X<while> X<until> X<foreach> X<for> X<continue>

=begin original

In Perl, a sequence of statements that defines a scope is called a block.
Sometimes a block is delimited by the file containing it (in the case
of a required file, or the program as a whole), and sometimes a block
is delimited by the extent of a string (in the case of an eval).

=end original

Perl では、スコープを定義するような文の並びをブロックと呼びます。
ブロックはそれを含むファイルによって範囲が定められることがあります
(ファイルが require されたときか、プログラム全体としての場合)が、
(eval における)文字列の展開によって範囲が定められることもあります。

=begin original

But generally, a block is delimited by curly brackets, also known as braces.
We will call this syntactic construct a BLOCK.

=end original

しかし一般的には、ブロックは中かっこによって範囲が定められます。
この構文的な構造をブロックと呼びます。

=begin original

The following compound statements may be used to control flow:

=end original

以下に挙げる複合文を制御フローとして使うことができます。

    if (EXPR) BLOCK
    if (EXPR) BLOCK else BLOCK
    if (EXPR) BLOCK elsif (EXPR) BLOCK ... else BLOCK
    LABEL while (EXPR) BLOCK
    LABEL while (EXPR) BLOCK continue BLOCK
    LABEL until (EXPR) BLOCK
    LABEL until (EXPR) BLOCK continue BLOCK
    LABEL for (EXPR; EXPR; EXPR) BLOCK
    LABEL foreach VAR (LIST) BLOCK
    LABEL foreach VAR (LIST) BLOCK continue BLOCK
    LABEL BLOCK continue BLOCK

=begin original

Note that, unlike C and Pascal, these are defined in terms of BLOCKs,
not statements.  This means that the curly brackets are I<required>--no
dangling statements allowed.  If you want to write conditionals without
curly brackets there are several other ways to do it.  The following
all do the same thing:

=end original

しかし注意して欲しいのは、C や Pascal とは異なり、ブロックを取るように
定義されていて文を取るではないということです。
つまり、中かっこは I<必要なもの> で、曖昧な文が許されないということです。
中かっこなしの条件文を使いたいのであれば、いくつかのやり方があります。
以下にその例を示します。

    if (!open(FOO)) { die "Can't open $FOO: $!"; }
    die "Can't open $FOO: $!" unless open(FOO);
    open(FOO) or die "Can't open $FOO: $!";	# FOO or bust!
    open(FOO) ? 'hi mom' : die "Can't open $FOO: $!";
			# a bit exotic, that last one

=begin original

The C<if> statement is straightforward.  Because BLOCKs are always
bounded by curly brackets, there is never any ambiguity about which
C<if> an C<else> goes with.  If you use C<unless> in place of C<if>,
the sense of the test is reversed.

=end original

C<if> 文は明解です。
ブロックは常に中かっこで区切られるので、C<if> と C<else> の対応が
曖昧になるようなことは決してありません。
C<unless> を C の C<if> のように使いたいのであれば、
検査を反転します。

=begin original

The C<while> statement executes the block as long as the expression is
true (does not evaluate to the null string C<""> or C<0> or C<"0">).
The C<until> statement executes the block as long as the expression is
false.
The LABEL is optional, and if present, consists of an identifier followed
by a colon.  The LABEL identifies the loop for the loop control
statements C<next>, C<last>, and C<redo>.
If the LABEL is omitted, the loop control statement
refers to the innermost enclosing loop.  This may include dynamically
looking back your call-stack at run time to find the LABEL.  Such
desperate behavior triggers a warning if you use the C<use warnings>
pragma or the B<-w> flag.

=end original

C<while> 文は、式が真(評価した結果が空文字列 C<""> でも C<0> でも
C<"0"> でもない)である間、ブロックを実行します。
C<until> 文は、式が偽である間、ブロックを実行します。
ラベルは省略可能ですが、ある場合には、コロンを伴った識別子になります。
LABEL は C<next>、C<last>、C<redo> といったループ制御文のループを規定します。
LABEL が省略された場合、ループ制御文はそれを含むループの中で最も内側の
ループを参照します。
これは、実行時に LABEL を検出するための呼び出しスタックの動的な後戻り検索を
含むことができます。
そのような推奨されない振る舞いは、C<use warnings> プラグマや
B<-w> フラグを使った場合には警告の引き金となります。

=begin original

If there is a C<continue> BLOCK, it is always executed just before the
conditional is about to be evaluated again.  Thus it can be used to
increment a loop variable, even when the loop has been continued via
the C<next> statement.

=end original

C<continue> ブロックが存在する場合、
常に条件が再評価される直前に実行されます。
したがって、このブロックをループ変数のインクリメントのために
使うことができます。
これは、ループがC<next>文を通して継続されるときでも実行されます。

=head2 Loop Control
X<loop control> X<loop, control> X<next> X<last> X<redo> X<continue>

=begin original

The C<next> command starts the next iteration of the loop:

=end original

C<next> コマンドはループの次の繰り返しを開始します。

    LINE: while (<STDIN>) {
	next LINE if /^#/;	# discard comments
	...
    }

=begin original

The C<last> command immediately exits the loop in question.  The
C<continue> block, if any, is not executed:

=end original

C<last> コマンドはループから即座に脱出します。
C<continue> ブロックがあった場合、それは実行されません。

    LINE: while (<STDIN>) {
	last LINE if /^$/;	# exit when done with header
	...
    }

=begin original

The C<redo> command restarts the loop block without evaluating the
conditional again.  The C<continue> block, if any, is I<not> executed.
This command is normally used by programs that want to lie to themselves
about what was just input.

=end original

C<redo> コマンドは、条件の再評価をすることなしにループブロックの
再実行を行います。
C<continue> ブロックがあっても、それは I<実行されません>。
このコマンドは通常、プログラムに対する入力に関してプログラム自身を
だましたいといったときに使われます。

=begin original

For example, when processing a file like F</etc/termcap>.
If your input lines might end in backslashes to indicate continuation, you
want to skip ahead and get the next record.

=end original

たとえば、F</etc/termcap> のようなファイルを処理することを
考えてみましょう。
もし入力された行の行末が継続を示すバックスラッシュであった場合、先へ進んで
次のレコードを取り出したいと思うでしょう。

    while (<>) {
	chomp;
	if (s/\\$//) {
	    $_ .= <>;
	    redo unless eof();
	}
	# now process $_
    }

=begin original

which is Perl short-hand for the more explicitly written version:

=end original

もっとはっきりと書くと以下のようになります。

    LINE: while (defined($line = <ARGV>)) {
	chomp($line);
	if ($line =~ s/\\$//) {
	    $line .= <ARGV>;
	    redo LINE unless eof(); # not eof(ARGV)!
	}
	# now process $line
    }

=begin original

Note that if there were a C<continue> block on the above code, it would
get executed only on lines discarded by the regex (since redo skips the
continue block). A continue block is often used to reset line counters
or C<?pat?> one-time matches:

=end original

上記の例で C<continue> ブロックがあったとしたら、それは
(redo は continue ブロックをスキップするので) 正規表現によって
捨てられた行だけが実行されるということに注意してください。
continue ブロックは行カウンターをリセットするとか、
一度だけマッチする C<?pat?> をリセットするのに使われます。

    # inspired by :1,$g/fred/s//WILMA/
    while (<>) {
	?(fred)?    && s//WILMA $1 WILMA/;
	?(barney)?  && s//BETTY $1 BETTY/;
	?(homer)?   && s//MARGE $1 MARGE/;
    } continue {
	print "$ARGV $.: $_";
	close ARGV  if eof();		# reset $.
	reset	    if eof();		# reset ?pat?
    }

=begin original

If the word C<while> is replaced by the word C<until>, the sense of the
test is reversed, but the conditional is still tested before the first
iteration.

=end original

C<while> を C<until> で置き換えた場合検査の意味は逆転しますが、
繰り返しが実行されるより前に条件が検査されることは変わりありません。

=begin original

The loop control statements don't work in an C<if> or C<unless>, since
they aren't loops.  You can double the braces to make them such, though.

=end original

ループ制御文は C<if> や C<unless> 中では動作しません。
なぜならそこはループではないからです。
しかし中かっこを二重にしてこれに対処することはできます。

    if (/pattern/) {{
	last if /fred/;
	next if /barney/; # same effect as "last", but doesn't document as well
	# do something here
    }}

=begin original

This is caused by the fact that a block by itself acts as a loop that
executes once, see L<"Basic BLOCKs and Switch Statements">.

=end original

これは、ブロック自身は一度だけ実行されるループとして動作するからです;
L<"Basic BLOCKs and Switch Statements"> を参照してください。

=begin original

The form C<while/if BLOCK BLOCK>, available in Perl 4, is no longer
available.   Replace any occurrence of C<if BLOCK> by C<if (do BLOCK)>.

=end original

Perl 4 では使うことのできた C<while/if BLOCK BLOCK> という形式は、
もはや使うことができません。
C<if BLOCK> の部分を C<if (do BLOCK)> で置き換えてください。

=head2 For Loops
X<for> X<foreach>

=begin original

Perl's C-style C<for> loop works like the corresponding C<while> loop;
that means that this:

=end original

Perl の C 形式の C<for> ループは、対応する C<while> ループと同様に
動作します。
つまり、

    for ($i = 1; $i < 10; $i++) {
	...
    }

=begin original

is the same as this:

=end original

これは以下のものと同じです。

    $i = 1;
    while ($i < 10) {
	...
    } continue {
	$i++;
    }

=begin original

There is one minor difference: if variables are declared with C<my>
in the initialization section of the C<for>, the lexical scope of
those variables is exactly the C<for> loop (the body of the loop
and the control sections).
X<my>

=end original

小さな違いが一つあります:
if variables are declared with C<my>
in the initialization section of the C<for>, the lexical scope of
those variables is exactly the C<for> loop (the body of the loop
and the control sections).
X<my>
(TBT)

=begin original

Besides the normal array index looping, C<for> can lend itself
to many other interesting applications.  Here's one that avoids the
problem you get into if you explicitly test for end-of-file on
an interactive file descriptor causing your program to appear to
hang.
X<eof> X<end-of-file> X<end of file>

=end original

通常の、配列に対する添え字付けのループのほかにも、C<for> は他の
多くの興味深いアプリケーションのために借用することができます。
以下の例は、対話的なファイルのディスクリプターの終端を明示的に
検査してしまうとプログラムをハングアップしたように見えてしまう問題を
回避するものです。

    $on_a_tty = -t STDIN && -t STDOUT;
    sub prompt { print "yes? " if $on_a_tty }
    for ( prompt(); <STDIN>; prompt() ) {
	# do something
    }

=begin original

Using C<readline> (or the operator form, C<< <EXPR> >>) as the
conditional of a C<for> loop is shorthand for the following.  This
behaviour is the same as a C<while> loop conditional.
X<readline> X<< <> >>

=end original

Using C<readline> (or the operator form, C<< <EXPR> >>) as the
conditional of a C<for> loop is shorthand for the following.  This
behaviour is the same as a C<while> loop conditional.
X<readline> X<< <> >>
(TBT)

    for ( prompt(); defined( $_ = <STDIN> ); prompt() ) {
        # do something
    }

=head2 Foreach Loops
X<for> X<foreach>

=begin original

The C<foreach> loop iterates over a normal list value and sets the
variable VAR to be each element of the list in turn.  If the variable
is preceded with the keyword C<my>, then it is lexically scoped, and
is therefore visible only within the loop.  Otherwise, the variable is
implicitly local to the loop and regains its former value upon exiting
the loop.  If the variable was previously declared with C<my>, it uses
that variable instead of the global one, but it's still localized to
the loop.  This implicit localisation occurs I<only> in a C<foreach>
loop.
X<my> X<local>

=end original

C<foreach> ループは 通常のリスト値に対しての繰り返しを行い、変数
VAR にリストの要素を繰り返し一回に一つずつセットします。
変数の前に C<my> というキーワードが置かれていた場合、その変数は
レキシカルスコープを持ち、したがってそれはループの中でのみ可視となります。
このキーワードがなければ、変数はループに対してローカルとなり、ループを
抜けた後で以前の値が再度取得されます。
変数が事前に C<my> を使って宣言されていたならば、グローバルなものの
代わりにその変数を使いますが、それもループにローカルなものとなります。
この暗黙の local 化は C<foreach> の中で I<のみ> 起きます。
X<my> X<local>

=begin original

The C<foreach> keyword is actually a synonym for the C<for> keyword, so
you can use C<foreach> for readability or C<for> for brevity.  (Or because
the Bourne shell is more familiar to you than I<csh>, so writing C<for>
comes more naturally.)  If VAR is omitted, C<$_> is set to each value.
X<$_>

=end original

読みやすさのために C<foreach> を、簡潔さのために C<for> を使うことが
できます(あるいは C シェルよりも Bourne シェルに親しんでいるのなら
C<for> の方が自然でしょう)。
VAR が省略された場合には、C<$_> に値が設定されます。
X<$_>

=begin original

If any element of LIST is an lvalue, you can modify it by modifying
VAR inside the loop.  Conversely, if any element of LIST is NOT an
lvalue, any attempt to modify that element will fail.  In other words,
the C<foreach> loop index variable is an implicit alias for each item
in the list that you're looping over.
X<alias>

=end original

LIST の要素のいずれかが左辺値であった場合、ループの中で
VAR を変更することにより、対応する値を変更することができます。
Conversely, if any element of LIST is NOT an
lvalue, any attempt to modify that element will fail.
言い換えると、C<foreach> ループの帰納変数がループの対象となっている
リスト中の個々のアイテムに対するエイリアスになっているからです。
X<alias>
(TBT)

=begin original

If any part of LIST is an array, C<foreach> will get very confused if
you add or remove elements within the loop body, for example with
C<splice>.   So don't do that.
X<splice>

=end original

LIST のいずれかの部分が配列であった場合に、たとえば C<splice> を使って
ループの本体でその要素を削除したりあるいは追加したりすると
C<foreach> は非常に混乱してしまいます。
ですからそういうことをしてはいけません。
X<splice>

=begin original

C<foreach> probably won't do what you expect if VAR is a tied or other
special variable.   Don't do that either.

=end original

VAR が tie されていたりあるいは他の特殊変数であった場合には
C<foreach> はあなたのもくろみどおりには動かないでしょう。
こういうこともしてはいけません。

=begin original

Examples:

=end original

例:

    for (@ary) { s/foo/bar/ }

    for my $elem (@elements) {
	$elem *= 2;
    }

    for $count (10,9,8,7,6,5,4,3,2,1,'BOOM') {
	print $count, "\n"; sleep(1);
    }

    for (1..15) { print "Merry Christmas\n"; }

    foreach $item (split(/:[\\\n:]*/, $ENV{TERMCAP})) {
	print "Item: $item\n";
    }

=begin original

Here's how a C programmer might code up a particular algorithm in Perl:

=end original

以下の例は、C プログラマーが Perl でとあるアルゴリズムを記述するときに
使うであろうやり方です。

    for (my $i = 0; $i < @ary1; $i++) {
	for (my $j = 0; $j < @ary2; $j++) {
	    if ($ary1[$i] > $ary2[$j]) {
		last; # can't go to outer :-(
	    }
	    $ary1[$i] += $ary2[$j];
	}
	# this is where that last takes me
    }

=begin original

Whereas here's how a Perl programmer more comfortable with the idiom might
do it:

=end original

それに対して、次の例は Perl プログラマーが同じことをよりゆったりとして
行うやり方です。

    OUTER: for my $wid (@ary1) {
    INNER:   for my $jet (@ary2) {
		next OUTER if $wid > $jet;
		$wid += $jet;
	     }
	  }

=begin original

See how much easier this is?  It's cleaner, safer, and faster.  It's
cleaner because it's less noisy.  It's safer because if code gets added
between the inner and outer loops later on, the new code won't be
accidentally executed.  The C<next> explicitly iterates the other loop
rather than merely terminating the inner one.  And it's faster because
Perl executes a C<foreach> statement more rapidly than it would the
equivalent C<for> loop.

=end original

どのくらいこれが簡単になったように見えますか? これは明確で、安全で、
高速です。
これは余計なものが少ないので明確なのです。
これは後で内側のループと外側のループとの間にコードを付加えた場合でも、
それを間違って実行することがないので安全なのです。
C<next> は内側のループを終了するのではなく、外側のループの繰り返しを
行います。
そしてこれは、Perl は C<foreach> 文をそれと等価な C<for> ループよりも
すばやく実行するので高速なのです。

=head2 Basic BLOCKs and Switch Statements
X<switch> X<block> X<case>

=begin original

A BLOCK by itself (labeled or not) is semantically equivalent to a
loop that executes once.  Thus you can use any of the loop control
statements in it to leave or restart the block.  (Note that this is
I<NOT> true in C<eval{}>, C<sub{}>, or contrary to popular belief
C<do{}> blocks, which do I<NOT> count as loops.)  The C<continue>
block is optional.

=end original

ブロックそれ自身は(ラベルが付いていようがついてなかろうが)一度だけ
実行されるループと、文法的には等価なものです。
このため、ブロックから脱出するためやブロックの再スタートのために
任意のループ制御文を使うことができます(これは C<eval{}>、C<sub{}>、
さらに一般的な認識とは異なり I<ループではない> C<do{}> ブロックに対しては
I<真ではない> ということに注意してください)。
C<continue> ブロックは省略することができます。

=begin original

The BLOCK construct is particularly nice for doing case
structures.

=end original

BLOCK 構造は case 構造を行うのに都合が良いです。

    SWITCH: {
	if (/^abc/) { $abc = 1; last SWITCH; }
	if (/^def/) { $def = 1; last SWITCH; }
	if (/^xyz/) { $xyz = 1; last SWITCH; }
	$nothing = 1;
    }

=begin original

There is no official C<switch> statement in Perl, because there are
already several ways to write the equivalent.

=end original

Perl には正式な C<switch> 文というものはありませんが、それは
すでに等価なものを記述する方法が幾つかあるからです。

=begin original

However, starting from Perl 5.8 to get switch and case one can use
the Switch extension and say:

=end original

しかし、Perl 5.8 から、Switch 拡張を使うことで、switch-case を使えます:

	use Switch;

=begin original

after which one has switch and case.  It is not as fast as it could be
because it's not really part of the language (it's done using source
filters) but it is available, and it's very flexible.

=end original

after which one has switch and case.  It is not as fast as it could be
because it's not really part of the language (it's done using source
filters) but it is available, and it's very flexible.
(TBT)

=begin original

In addition to the above BLOCK construct, you could write

=end original

先の BLOCK 構造の例に加え、次のように書くこともできます。

    SWITCH: {
	$abc = 1, last SWITCH  if /^abc/;
	$def = 1, last SWITCH  if /^def/;
	$xyz = 1, last SWITCH  if /^xyz/;
	$nothing = 1;
    }

=begin original

(That's actually not as strange as it looks once you realize that you can
use loop control "operators" within an expression.  That's just the binary
comma operator in scalar context.  See L<perlop/"Comma Operator">.)

=end original

(これは、式の中にあるループを制御できる“演算子”のように見えますが、
実際にはそれほど奇異なものではありません。
単に通常のカンマ演算子です。
L<perlop/"Comma Operator"> を参照してください。)

=begin original

or

=end original

あるいは:

    SWITCH: {
	/^abc/ && do { $abc = 1; last SWITCH; };
	/^def/ && do { $def = 1; last SWITCH; };
	/^xyz/ && do { $xyz = 1; last SWITCH; };
	$nothing = 1;
    }

=begin original

or formatted so it stands out more as a "proper" C<switch> statement:

=end original

書式付けしてより“適切な”C<switch> 文のようにします。

    SWITCH: {
	/^abc/ 	    && do {
			    $abc = 1;
			    last SWITCH;
		       };

	/^def/ 	    && do {
			    $def = 1;
			    last SWITCH;
		       };

	/^xyz/ 	    && do {
			    $xyz = 1;
			    last SWITCH;
		        };
	$nothing = 1;
    }

=begin original

or

=end original

あるいは:

    SWITCH: {
	/^abc/ and $abc = 1, last SWITCH;
	/^def/ and $def = 1, last SWITCH;
	/^xyz/ and $xyz = 1, last SWITCH;
	$nothing = 1;
    }

=begin original

or even, horrors,

=end original

さらにはこういった書き方もあります。

    if (/^abc/)
	{ $abc = 1 }
    elsif (/^def/)
	{ $def = 1 }
    elsif (/^xyz/)
	{ $xyz = 1 }
    else
	{ $nothing = 1 }

=begin original

A common idiom for a C<switch> statement is to use C<foreach>'s aliasing to make
a temporary assignment to C<$_> for convenient matching:

=end original

C<switch> 文のための一般的なイディオムとは、マッチングに便利な C<$_> に 
一時的に代入させるために C<foreach> のエイリアス化を使うというものです:

    SWITCH: for ($where) {
		/In Card Names/     && do { push @flags, '-e'; last; };
		/Anywhere/          && do { push @flags, '-h'; last; };
		/In Rulings/        && do {                    last; };
		die "unknown value for form variable where: `$where'";
	    }

=begin original

Another interesting approach to a switch statement is arrange
for a C<do> block to return the proper value:

=end original

switch 文のためのもう一つの興味深いアプローチは、C<do> ブロックを
適切な値を返すようにアレンジするというものです。

    $amode = do {
	if     ($flag & O_RDONLY) { "r" }	# XXX: isn't this 0?
	elsif  ($flag & O_WRONLY) { ($flag & O_APPEND) ? "a" : "w" }
	elsif  ($flag & O_RDWR)   {
	    if ($flag & O_CREAT)  { "w+" }
	    else                  { ($flag & O_APPEND) ? "a+" : "r+" }
	}
    };

=begin original

Or 

=end original

または:

        print do {
            ($flags & O_WRONLY) ? "write-only"          :
            ($flags & O_RDWR)   ? "read-write"          :
                                  "read-only";
        };

=begin original

Or if you are certain that all the C<&&> clauses are true, you can use
something like this, which "switches" on the value of the
C<HTTP_USER_AGENT> environment variable.

=end original

またはあなたが C<&&> 節が真であることに確信を持っているのなら、
以下のように C<HTTP_USER_AGENT> 環境変数の値に応じて "switch" できます。

    #!/usr/bin/perl 
    # pick out jargon file page based on browser
    $dir = 'http://www.wins.uva.nl/~mes/jargon';
    for ($ENV{HTTP_USER_AGENT}) { 
	$page  =    /Mac/            && 'm/Macintrash.html'
		 || /Win(dows )?NT/  && 'e/evilandrude.html'
		 || /Win|MSIE|WebTV/ && 'm/MicroslothWindows.html'
		 || /Linux/          && 'l/Linux.html'
		 || /HP-UX/          && 'h/HP-SUX.html'
		 || /SunOS/          && 's/ScumOS.html'
		 ||                     'a/AppendixB.html';
    }
    print "Location: $dir/$page\015\012\015\012";

=begin original

That kind of switch statement only works when you know the C<&&> clauses
will be true.  If you don't, the previous C<?:> example should be used.

=end original

こういった switch 文は、C<&&> 節が真になるということを分かっているときにのみ
うまくいきます。
そうでなければ、C<?:> を使いましょう。

=begin original

You might also consider writing a hash of subroutine references
instead of synthesizing a C<switch> statement.

=end original

あるいは C<switch> 文の代わりにサブルーチンのリファレンスのハッシュを
書くことを考えるかもしれません。

=head2 Goto
X<goto>

=begin original

Although not for the faint of heart, Perl does support a C<goto>
statement.  There are three forms: C<goto>-LABEL, C<goto>-EXPR, and
C<goto>-&NAME.  A loop's LABEL is not actually a valid target for
a C<goto>; it's just the name of the loop.

=end original

気弱な人のためでないにも関らず、Perl は C<goto> 文をサポートしています。
C<goto>-LABEL、C<goto>-EXPR、C<goto>-&NAME の三つの形式があります。
ループのラベルは実際には C<goto> の正当なターゲットではなく、
ループの名前にすぎません。

=begin original

The C<goto>-LABEL form finds the statement labeled with LABEL and resumes
execution there.  It may not be used to go into any construct that
requires initialization, such as a subroutine or a C<foreach> loop.  It
also can't be used to go into a construct that is optimized away.  It
can be used to go almost anywhere else within the dynamic scope,
including out of subroutines, but it's usually better to use some other
construct such as C<last> or C<die>.  The author of Perl has never felt the
need to use this form of C<goto> (in Perl, that is--C is another matter).

=end original

C<goto>-LABEL 形式は LABEL でラベル付けされた文を見つけだし、そこから
実行を再開します。
これはサブルーチンであるとか C<foreach> ループのような
初期化を必要とするような構造へ飛び込むために使うことはできません。
また、最適化されて無くなってしまうような構造へ飛び込むこともできません。
動的スコープの中以外のほとんどの場所へは、サブルーチンの
外も含めて移動することができます。
しかし、通常は C<last> や C<die> のような別のやり方を使ったほうが良いでしょう。
Perl の作者は、未だかつてこの形式の C<goto> を使うことが
必要だと感じたことはありません(Perl の場合です。C の場合はまた別の話です)。

=begin original

The C<goto>-EXPR form expects a label name, whose scope will be resolved
dynamically.  This allows for computed C<goto>s per FORTRAN, but isn't
necessarily recommended if you're optimizing for maintainability:

=end original

C<goto>-EXPR 形式は動的に解決されるスコープを持っているラベル名を
期待しています。
これによって FORTRAN の計算型 C<goto> が実現できますが、
これは保守性に重きを置くのであれば使うことは止めた方が良いでしょう。

    goto(("FOO", "BAR", "GLARCH")[$i]);

=begin original

The C<goto>-&NAME form is highly magical, and substitutes a call to the
named subroutine for the currently running subroutine.  This is used by
C<AUTOLOAD()> subroutines that wish to load another subroutine and then
pretend that the other subroutine had been called in the first place
(except that any modifications to C<@_> in the current subroutine are
propagated to the other subroutine.)  After the C<goto>, not even C<caller()>
will be able to tell that this routine was called first.

=end original

C<goto>-&NAME は高度にmagicalで、名前付きサブルーチンの呼び出しを
カレントで実行されているサブルーチンに置き換えます。
これは別のサブルーチンをロードして、最初の場所で呼び出された
別のサブルーチンを要求することをしようとする
C<AUTOLOAD()> サブルーチンで使われていてます
(カレントのサブルーチンにおける C<@_> に対するもの以外の変更は、
別のサブルーチンへ伝播します)。
C<goto> の後で、C<caller()> でなくてもこのサブルーチンが
最初に呼ばれたのだということを伝えることすらできるでしょう。

=begin original

In almost all cases like this, it's usually a far, far better idea to use the
structured control flow mechanisms of C<next>, C<last>, or C<redo> instead of
resorting to a C<goto>.  For certain applications, the catch and throw pair of
C<eval{}> and die() for exception processing can also be a prudent approach.

=end original

このようなケースのほとんどすべての場合、C<goto> に頼るのではなくて
C<next>、C<last>、C<redo>といった制御フロー機構を使うことが、
ずっとずっと良いアイデアでしょう。
一部のアプリケーションに対しては、C<eval{}> と die() を
catch と throw のペアとして例外処理を行うための賢明なアプローチとして
使うことができるでしょう。

=head2 PODs: Embedded Documentation
X<POD> X<documentation>

=begin original

Perl has a mechanism for intermixing documentation with source code.
While it's expecting the beginning of a new statement, if the compiler
encounters a line that begins with an equal sign and a word, like this

=end original

Perl は、ソースコードとドキュメントとを混ぜ書きするための仕掛けを
持っています。
新しい文の始まりが期待されているときに、コンパイラは
以下の例のような = 記号で始まっている語を見つけると:

    =head1 Here There Be Pods!

=begin original

Then that text and all remaining text up through and including a line
beginning with C<=cut> will be ignored.  The format of the intervening
text is described in L<perlpod>.

=end original

そのテキストと、C<=cut> で始まる行までの内容を無視します。
間に入るテキストの書式は L<perlpod> で説明されています。

=begin original

This allows you to intermix your source code
and your documentation text freely, as in

=end original

これによって、ソースコードとドキュメントとを以下に示す例のように
自由に混ぜることができるようになります。

    =item snazzle($)

    The snazzle() function will behave in the most spectacular
    form that you can possibly imagine, not even excepting
    cybernetic pyrotechnics.

    =cut back to the compiler, nuff of this pod stuff!

    sub snazzle($) {
	my $thingie = shift;
	.........
    }

=begin original

Note that pod translators should look at only paragraphs beginning
with a pod directive (it makes parsing easier), whereas the compiler
actually knows to look for pod escapes even in the middle of a
paragraph.  This means that the following secret stuff will be
ignored by both the compiler and the translators.

=end original

コンパイラはパラグラフの途中に pod エスケープがあったとしてもそれを
認識できるのに、pod トランスレータは pod 指示子で始まっている
パラグラフのみに注目すべき(これは構文解析を簡単にするためです)で
あるということに注意して下さい。
つまり、以下の例にある secret stuff はコンパイラからも、
トランスレータからも無視されるということです。

    $a=3;
    =secret stuff
     warn "Neither POD nor CODE!?"
    =cut back
    print "got $a\n";

=begin original

You probably shouldn't rely upon the C<warn()> being podded out forever.
Not all pod translators are well-behaved in this regard, and perhaps
the compiler will become pickier.

=end original

この例の C<warn()> のようなものが、将来に渡って無視されるということに
依存すべきではありません。
すべての pod トランスレータがそのように振る舞うわけではありませんし、
コンパイラは将来これを無視しないようになるかもしれません。

=begin original

One may also use pod directives to quickly comment out a section
of code.

=end original

pod 指示子を、コードの一部を手っ取り早くコメントアウトするために
使うこともできます。

=head2 Plain Old Comments (Not!)
X<comment> X<line> X<#> X<preprocessor> X<eval>

=begin original

Perl can process line directives, much like the C preprocessor.  Using
this, one can control Perl's idea of filenames and line numbers in
error or warning messages (especially for strings that are processed
with C<eval()>).  The syntax for this mechanism is the same as for most
C preprocessors: it matches the regular expression

=end original

C のプリプロセッサと同じように、Perl は行指示子を処理できます。
これを使うことによって、エラーメッセージや警告メッセージにある
ファイル名や行番号を制御することができます
(特に、C<eval()> で処理される文字列のために)。
この仕組みの構文は C のプリプロセッサとほとんど同じで、正規表現

    # example: '# line 42 "new_filename.plx"'
    /^\#   \s*
      line \s+ (\d+)   \s*
      (?:\s("?)([^"]+)\2)? \s*
     $/x

=begin original

with C<$1> being the line number for the next line, and C<$3> being
the optional filename (specified with or without quotes).

=end original

にマッチしたものの C<$1> が次の行の行番号となり、省略することもできる
C<$3> は(クォートありかなしで指定された)ファイル名となります。

=begin original

There is a fairly obvious gotcha included with the line directive:
Debuggers and profilers will only show the last source line to appear
at a particular line number in a given file.  Care should be taken not
to cause line number collisions in code you'd like to debug later.

=end original

There is a fairly obvious gotcha included with the line directive:
Debuggers and profilers will only show the last source line to appear
at a particular line number in a given file.  Care should be taken not
to cause line number collisions in code you'd like to debug later.
(TBT)

=begin original

Here are some examples that you should be able to type into your command
shell:

=end original

コマンドシェルでタイプすることのできる例をいくつか挙げます:

    % perl
    # line 200 "bzzzt"
    # the `#' on the previous line must be the first char on line
    die 'foo';
    __END__
    foo at bzzzt line 201.

    % perl
    # line 200 "bzzzt"
    eval qq[\n#line 2001 ""\ndie 'foo']; print $@;
    __END__
    foo at - line 2001.

    % perl
    eval qq[\n#line 200 "foo bar"\ndie 'foo']; print $@;
    __END__
    foo at foo bar line 200.

    % perl
    # line 345 "goop"
    eval "\n#line " . __LINE__ . ' "' . __FILE__ ."\"\ndie 'foo'";
    print $@;
    __END__
    foo at goop line 345.

=begin meta

Created: KIMURA Koichi
Updated: Kentaro Shirakata <argrath@ub32.org>

=end meta

=cut

