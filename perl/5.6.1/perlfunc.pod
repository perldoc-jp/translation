=head1 NAME

perlfunc - Perl builtin functions

=head1 DESCRIPTION

=begin original

The functions in this section can serve as terms in an expression.
They fall into two major categories: list operators and named unary
operators.  These differ in their precedence relationship with a
following comma.  (See the precedence table in L<perlop>.)  List
operators take more than one argument, while unary operators can never
take more than one argument.  Thus, a comma terminates the argument of
a unary operator, but merely separates the arguments of a list
operator.  A unary operator generally provides a scalar context to its
argument, while a list operator may provide either scalar or list
contexts for its arguments.  If it does both, the scalar arguments will
be first, and the list argument will follow.  (Note that there can ever
be only one such list argument.)  For instance, splice() has three scalar
arguments followed by a list, whereas gethostbyname() has four scalar
arguments.

=end original

この節の関数は、式の中で項として使うことができます。
これらは、大きく 2 つに分けられます:
リスト演算子と名前付き単項演算子です。
これらの違いは、その後に出て来るコンマとの優先順位の関係にあります。　
(L<perlop> の優先順位の表を参照してください。)
リスト演算子は 2 個以上の引数をとるのに対して、
単項演算子が複数の引数をとることはありません。
つまり、コンマは単項演算子の引数の終わりとなりますが、
リスト演算子の場合には、引数の区切りでしかありません。
単項演算子は一般に、
引数に対してスカラコンテキストを与えるのに対して、
スカラ演算子の場合には、引数に対してスカラコンテキストを与える場合も、
リストコンテキストを与える場合もあります。
1 つのリスト演算子が
両方のコンテキストを与える場合には、スカラ引数がいくつか並び、
最後にリスト引数が 1 つ続きます。
(リスト引数は 1 つだけです。)
たとえば、splice() は 3 つのスカラ引数に 1 つのリスト引数が続きます。
一方 gethostbyname() は 4 つのスカラ引数を持ちます。

=begin original

In the syntax descriptions that follow, list operators that expect a
list (and provide list context for the elements of the list) are shown
with LIST as an argument.  Such a list may consist of any combination
of scalar arguments or list values; the list values will be included
in the list as if each individual element were interpolated at that
point in the list, forming a longer single-dimensional list value.
Elements of the LIST should be separated by commas.

=end original

後に載せる構文記述では、リストをとり
(そのリストの要素にリストコンテキストを与える)
リスト演算子は、引数として LIST をとるように書いています。
そのようなリストには、任意のスカラ引数の組み合わせやリスト値を
含めることができ、リスト値はリストの中に、
個々の要素が展開されたように埋め込まれます。
1 次元の長いリスト値が形成されることになります。
LIST の要素は、コンマで区切られている必要があります。

=begin original

Any function in the list below may be used either with or without
parentheses around its arguments.  (The syntax descriptions omit the
parentheses.)  If you use the parentheses, the simple (but occasionally
surprising) rule is this: It I<looks> like a function, therefore it I<is> a
function, and precedence doesn't matter.  Otherwise it's a list
operator or unary operator, and precedence does matter.  And whitespace
between the function and left parenthesis doesn't count--so you need to
be careful sometimes:

=end original

以下のリストの関数はすべて、引数の前後の括弧は省略可能と
なっています。
(構文記述では省略しています。)
括弧を使うときには、
単純な (しかし、ときには驚く結果となる) 規則が適用できます:
I<関数に見える>ならば、I<それは関数>で、優先順位は関係ありません。
そう見えなければ、それはリスト演算子か単項演算子で、優先順位が
関係します。
また、関数と開き括弧の間の空白は関係ありませんので、
ときに気を付けなければなりません:

    print 1+2+4;	# Prints 7.
    print(1+2) + 4;	# Prints 3.
    print (1+2)+4;	# Also prints 3!
    print +(1+2)+4;	# Prints 7.
    print ((1+2)+4);	# Prints 7.

=begin original

If you run Perl with the B<-w> switch it can warn you about this.  For
example, the third line above produces:

    print (...) interpreted as function at - line 1.
    Useless use of integer addition in void context at - line 1.

=end original

Perl に B<-w> スイッチを付けて実行すれば、こういったものには
警告を出してくれます。
たとえば、上記の 3 つめは:

         print (...) interpreted as function at - line 1.
         Useless use of integer addition in void context at - line 1.

となります。

=begin original

A few functions take no arguments at all, and therefore work as neither
unary nor list operators.  These include such functions as C<time>
and C<endpwent>.  For example, C<time+86_400> always means
C<time() + 86_400>.

=end original

いくつかの関数は引数を全くとらないので、単項演算子としても
リスト演算子としても動作しません。
このような関数としては C<time> や C<endpwent> があります。
例えば、C<time+86_400> は常に C<time() + 86_400> として扱われます。

=begin original

For functions that can be used in either a scalar or list context,
nonabortive failure is generally indicated in a scalar context by
returning the undefined value, and in a list context by returning the
null list.

=end original

スカラコンテキストでも、リストコンテキストでも使える関数は、
致命的でないエラーを示すために、スカラコンテキストでは
未定義値を返し、リストコンテキストでは空リストを返します。

Remember the following important rule: There is B<no rule> that relates
the behavior of an expression in list context to its behavior in scalar
context, or vice versa.  It might do two totally different things.
Each operator and function decides which sort of value it would be most
appropriate to return in scalar context.  Some operators return the
length of the list that would have been returned in list context.  Some
operators return the first value in the list.  Some operators return the
last value in the list.  Some operators return a count of successful
operations.  In general, they do what you want, unless you want
consistency.

An named array in scalar context is quite different from what would at
first glance appear to be a list in scalar context.  You can't get a list
like C<(1,2,3)> into being in scalar context, because the compiler knows
the context at compile time.  It would generate the scalar comma operator
there, not the list construction version of the comma.  That means it
was never a list to start with.

In general, functions in Perl that serve as wrappers for system calls
of the same name (like chown(2), fork(2), closedir(2), etc.) all return
true when they succeed and C<undef> otherwise, as is usually mentioned
in the descriptions below.  This is different from the C interfaces,
which return C<-1> on failure.  Exceptions to this rule are C<wait>,
C<waitpid>, and C<syscall>.  System calls also set the special C<$!>
variable on failure.  Other functions do not, except accidentally.

=head2 Perl Functions by Category

Here are Perl's functions (including things that look like
functions, like some keywords and named operators)
arranged by category.  Some functions appear in more
than one place.

=over 4

=item Functions for SCALARs or strings

C<chomp>, C<chop>, C<chr>, C<crypt>, C<hex>, C<index>, C<lc>, C<lcfirst>,
C<length>, C<oct>, C<ord>, C<pack>, C<q/STRING/>, C<qq/STRING/>, C<reverse>,
C<rindex>, C<sprintf>, C<substr>, C<tr///>, C<uc>, C<ucfirst>, C<y///>

=item Regular expressions and pattern matching

C<m//>, C<pos>, C<quotemeta>, C<s///>, C<split>, C<study>, C<qr//>

=item Numeric functions

C<abs>, C<atan2>, C<cos>, C<exp>, C<hex>, C<int>, C<log>, C<oct>, C<rand>,
C<sin>, C<sqrt>, C<srand>

=item Functions for real @ARRAYs

C<pop>, C<push>, C<shift>, C<splice>, C<unshift>

=item Functions for list data

C<grep>, C<join>, C<map>, C<qw/STRING/>, C<reverse>, C<sort>, C<unpack>

=item Functions for real %HASHes

C<delete>, C<each>, C<exists>, C<keys>, C<values>

=item Input and output functions

C<binmode>, C<close>, C<closedir>, C<dbmclose>, C<dbmopen>, C<die>, C<eof>,
C<fileno>, C<flock>, C<format>, C<getc>, C<print>, C<printf>, C<read>,
C<readdir>, C<rewinddir>, C<seek>, C<seekdir>, C<select>, C<syscall>,
C<sysread>, C<sysseek>, C<syswrite>, C<tell>, C<telldir>, C<truncate>,
C<warn>, C<write>

=item Functions for fixed length data or records

C<pack>, C<read>, C<syscall>, C<sysread>, C<syswrite>, C<unpack>, C<vec>

=item Functions for filehandles, files, or directories

C<-I<X>>, C<chdir>, C<chmod>, C<chown>, C<chroot>, C<fcntl>, C<glob>,
C<ioctl>, C<link>, C<lstat>, C<mkdir>, C<open>, C<opendir>,
C<readlink>, C<rename>, C<rmdir>, C<stat>, C<symlink>, C<umask>,
C<unlink>, C<utime>

=item Keywords related to the control flow of your perl program

C<caller>, C<continue>, C<die>, C<do>, C<dump>, C<eval>, C<exit>,
C<goto>, C<last>, C<next>, C<redo>, C<return>, C<sub>, C<wantarray>

=item Keywords related to scoping

C<caller>, C<import>, C<local>, C<my>, C<our>, C<package>, C<use>

=item Miscellaneous functions

C<defined>, C<dump>, C<eval>, C<formline>, C<local>, C<my>, C<our>, C<reset>,
C<scalar>, C<undef>, C<wantarray>

=item Functions for processes and process groups

C<alarm>, C<exec>, C<fork>, C<getpgrp>, C<getppid>, C<getpriority>, C<kill>,
C<pipe>, C<qx/STRING/>, C<setpgrp>, C<setpriority>, C<sleep>, C<system>,
C<times>, C<wait>, C<waitpid>

=item Keywords related to perl modules

C<do>, C<import>, C<no>, C<package>, C<require>, C<use>

=item Keywords related to classes and object-orientedness

C<bless>, C<dbmclose>, C<dbmopen>, C<package>, C<ref>, C<tie>, C<tied>,
C<untie>, C<use>

=item Low-level socket functions

C<accept>, C<bind>, C<connect>, C<getpeername>, C<getsockname>,
C<getsockopt>, C<listen>, C<recv>, C<send>, C<setsockopt>, C<shutdown>,
C<socket>, C<socketpair>

=item System V interprocess communication functions

C<msgctl>, C<msgget>, C<msgrcv>, C<msgsnd>, C<semctl>, C<semget>, C<semop>,
C<shmctl>, C<shmget>, C<shmread>, C<shmwrite>

=item Fetching user and group info

C<endgrent>, C<endhostent>, C<endnetent>, C<endpwent>, C<getgrent>,
C<getgrgid>, C<getgrnam>, C<getlogin>, C<getpwent>, C<getpwnam>,
C<getpwuid>, C<setgrent>, C<setpwent>

=item Fetching network info

C<endprotoent>, C<endservent>, C<gethostbyaddr>, C<gethostbyname>,
C<gethostent>, C<getnetbyaddr>, C<getnetbyname>, C<getnetent>,
C<getprotobyname>, C<getprotobynumber>, C<getprotoent>,
C<getservbyname>, C<getservbyport>, C<getservent>, C<sethostent>,
C<setnetent>, C<setprotoent>, C<setservent>

=item Time-related functions

C<gmtime>, C<localtime>, C<time>, C<times>

=item Functions new in perl5

C<abs>, C<bless>, C<chomp>, C<chr>, C<exists>, C<formline>, C<glob>,
C<import>, C<lc>, C<lcfirst>, C<map>, C<my>, C<no>, C<our>, C<prototype>, 
C<qx>, C<qw>, C<readline>, C<readpipe>, C<ref>, C<sub*>, C<sysopen>, C<tie>,
C<tied>, C<uc>, C<ucfirst>, C<untie>, C<use>

* - C<sub> was a keyword in perl4, but in perl5 it is an
operator, which can be used in expressions.

=item Functions obsoleted in perl5

C<dbmclose>, C<dbmopen>

=back

=head2 Portability

=begin original

Perl was born in Unix and can therefore access all common Unix
system calls.  In non-Unix environments, the functionality of some
Unix system calls may not be available, or details of the available
functionality may differ slightly.  The Perl functions affected
by this are:

=end original

Perl は Unix 環境で生まれたので、全ての共通する Unix システムコールに
アクセスします。非 Unix 環境では、いくつかの Unix システムコールの
機能が使えなかったり、使える機能の詳細が多少異なったりします。
これによる影響を受ける Perl 関数は以下のものです:

C<-X>, C<binmode>, C<chmod>, C<chown>, C<chroot>, C<crypt>,
C<dbmclose>, C<dbmopen>, C<dump>, C<endgrent>, C<endhostent>,
C<endnetent>, C<endprotoent>, C<endpwent>, C<endservent>, C<exec>,
C<fcntl>, C<flock>, C<fork>, C<getgrent>, C<getgrgid>, C<gethostent>,
C<getlogin>, C<getnetbyaddr>, C<getnetbyname>, C<getnetent>,
C<getppid>, C<getprgp>, C<getpriority>, C<getprotobynumber>,
C<getprotoent>, C<getpwent>, C<getpwnam>, C<getpwuid>,
C<getservbyport>, C<getservent>, C<getsockopt>, C<glob>, C<ioctl>,
C<kill>, C<link>, C<lstat>, C<msgctl>, C<msgget>, C<msgrcv>,
C<msgsnd>, C<open>, C<pipe>, C<readlink>, C<rename>, C<select>, C<semctl>,
C<semget>, C<semop>, C<setgrent>, C<sethostent>, C<setnetent>,
C<setpgrp>, C<setpriority>, C<setprotoent>, C<setpwent>,
C<setservent>, C<setsockopt>, C<shmctl>, C<shmget>, C<shmread>,
C<shmwrite>, C<socket>, C<socketpair>, C<stat>, C<symlink>, C<syscall>,
C<sysopen>, C<system>, C<times>, C<truncate>, C<umask>, C<unlink>,
C<utime>, C<wait>, C<waitpid>

=begin original

For more information about the portability of these functions, see
L<perlport> and other available platform-specific documentation.

=end original

これらの関数の移植性に関するさらなる情報については、
L<perlport> とその他のプラットホーム固有のドキュメントを参照して下さい。

=head2 Alphabetical Listing of Perl Functions

=over 8

=item I<-X> FILEHANDLE

=item I<-X> EXPR

=item I<-X>

=begin original

A file test, where X is one of the letters listed below.  This unary
operator takes one argument, either a filename or a filehandle, and
tests the associated file to see if something is true about it.  If the
argument is omitted, tests C<$_>, except for C<-t>, which tests STDIN.
Unless otherwise documented, it returns C<1> for true and C<''> for false, or
the undefined value if the file doesn't exist.  Despite the funny
names, precedence is the same as any other named unary operator, and
the argument may be parenthesized like any other unary operator.  The
operator may be any of:

=end original

X は以下にあげる文字で、ファイルテストを行ないます。
この単項演算子は、ファイル名かファイルハンドルを唯一の
引数として動作し、「あること」について真であるか否かを
判定した結果を返します。
引数が省略されると、C<-t> では STDIN を調べますが、その他は C<$_> を調べます。
特に記述されていなければ、真として C<1> を返し、偽として
C<''> を返し、ファイルが存在しなければ、未定義値を返します。
みかけは変わっていますが、優先順位は名前付き単項演算子と同じで、
他の単項演算子と同じく、引数を括弧で括ることもできます。
演算子には以下のものがあります:

X<-r>X<-w>X<-x>X<-o>X<-R>X<-W>X<-X>X<-O>X<-e>X<-z>X<-s>X<-f>X<-d>X<-l>X<-p>
X<-S>X<-b>X<-c>X<-t>X<-u>X<-g>X<-k>X<-T>X<-B>X<-M>X<-A>X<-C>

=begin original

    -r	File is readable by effective uid/gid.
    -w	File is writable by effective uid/gid.
    -x	File is executable by effective uid/gid.
    -o	File is owned by effective uid.

=end original

    -r  ファイルが実効 uid/gid で読み出し可
    -w  ファイルが実効 uid/gid で書き込み可
    -x  ファイルが実効 uid/gid で実行可
    -o  ファイルが実効 uid の所有物

=begin original

    -R	File is readable by real uid/gid.
    -W	File is writable by real uid/gid.
    -X	File is executable by real uid/gid.
    -O	File is owned by real uid.

=end original

    -R  ファイルが実 uid/gid で読み出し可
    -W  ファイルが実 uid/gid で書き込み可
    -X  ファイルが実 uid/gid で実行可
    -O  ファイルが実 uid の所有物

=begin original

    -e	File exists.
    -z	File has zero size (is empty).
    -s	File has nonzero size (returns size in bytes).

=end original

    -e  ファイルが存在する
    -z  ファイルの大きさがゼロ(空)
    -s  ファイルの大きさがゼロ以外 (バイト単位での大きさを返す)

=begin original

    -f	File is a plain file.
    -d	File is a directory.
    -l	File is a symbolic link.
    -p	File is a named pipe (FIFO), or Filehandle is a pipe.
    -S	File is a socket.
    -b	File is a block special file.
    -c	File is a character special file.
    -t	Filehandle is opened to a tty.

=end original

    -f  ファイルは通常ファイル
    -d  ファイルはディレクトリ
    -l  ファイルはシンボリックリンク
    -p  ファイルは名前付きパイプ (FIFO) またはファイルハンドルはパイプ
    -S  ファイルはソケット
    -b  ファイルはブロック特殊ファイル
    -c  ファイルはキャラクタ特殊ファイル
    -t  ファイルハンドルは tty にオープンされている

=begin original

    -u	File has setuid bit set.
    -g	File has setgid bit set.
    -k	File has sticky bit set.

=end original

    -u  ファイルの setuid ビットがセットされている
    -g  ファイルの setgid ビットがセットされている
    -k  ファイルの sticky ビットがセットされている

=begin original
    -T	File is an ASCII text file.
    -B	File is a "binary" file (opposite of -T).

=end original

    -T  ファイルは ASCII テキストファイル
    -B  ファイルは「バイナリ」ファイル (-T の反対)

=begin original
    -M	Age of file in days when script started.
    -A	Same for access time.
    -C	Same for inode change time.

=end original

    -M  スクリプト実行開始時のファイル修正からの日数
    -A  同様にアクセスがあってからの日数
    -C  同様に inode が変更されてからの日数




があります。

Example:

    while (<>) {
	chomp;
	next unless -f $_;	# ignore specials
	#...
    }

=begin original

The interpretation of the file permission operators C<-r>, C<-R>,
C<-w>, C<-W>, C<-x>, and C<-X> is by default based solely on the mode
of the file and the uids and gids of the user.  There may be other
reasons you can't actually read, write, or execute the file.  Such
reasons may be for example network filesystem access controls, ACLs
(access control lists), read-only filesystems, and unrecognized
executable formats.

=end original

ファイルのパーミッション演算子 C<-r>, C<-R>, C<-w>, C<-W>, C<-x>,
C<-X> の解釈は、ファイルのモードとユーザの実効／実 uid と実効／実 gid のみから判断されます。
実際にファイルが読めたり、書けたり、実行できたりするためには、
別の条件が必要かもしれません。
条件としてはネットワークファイルシステムアクセスコントロール、
ACL(アクセスコントロールリスト)、読み込み専用ファイルシステム、
認識できない実行ファイルフォーマット、などがあります。

=begin original

Also note that, for the superuser on the local filesystems, the C<-r>,
C<-R>, C<-w>, and C<-W> tests always return 1, and C<-x> and C<-X> return 1
if any execute bit is set in the mode.  Scripts run by the superuser
may thus need to do a stat() to determine the actual mode of the file,
or temporarily set their effective uid to something else.

=end original

ローカルファイルシステムのスーパーユーザには、
C<-r>, C<-R>, C<-w>, C<-W> に対して、常に 1 が返り、モード中のい
ずれかの実行許可ビットが立っていれば、C<-x>, C<-X> にも 1 
が返ることにも注意してください。
スーパーユーザが実行するスクリプトでは、ファイルのモードを調べるために
は、stat() を行なうか、実効 uid を一時的に別のものにする
必要があるでしょう。

=begin original

If you are using ACLs, there is a pragma called C<filetest> that may
produce more accurate results than the bare stat() mode bits.
When under the C<use filetest 'access'> the above-mentioned filetests
will test whether the permission can (not) be granted using the
access() family of system calls.  Also note that the C<-x> and C<-X> may
under this pragma return true even if there are no execute permission
bits set (nor any extra execute permission ACLs).  This strangeness is
due to the underlying system calls' definitions.  Read the
documentation for the C<filetest> pragma for more information.

=end original

If you are using ACLs, there is a pragma called C<filetest> that may
produce more accurate results than the bare stat() mode bits.
When under the C<use filetest 'access'> the above-mentioned filetests
will test whether the permission can (not) be granted using the
access() family of system calls.  Also note that the C<-x> and C<-X> may
under this pragma return true even if there are no execute permission
bits set (nor any extra execute permission ACLs).  This strangeness is
due to the underlying system calls' definitions.  Read the
documentation for the C<filetest> pragma for more information.
(*TBT*)

=begin original

Note that C<-s/a/b/> does not do a negated substitution.  Saying
C<-exp($foo)> still works as expected, however--only single letters
following a minus are interpreted as file tests.

=end original

C<-s/a/b> は、置換演算 (s///) の符号反転ではありません。
しかし、C<-exp($foo)> は期待どおりに動作します。
マイナス記号の後に英字が 1 字続くときにのみ、ファイルテストと
解釈されます。

=begin original

The C<-T> and C<-B> switches work as follows.  The first block or so of the
file is examined for odd characters such as strange control codes or
characters with the high bit set.  If too many strange characters (>30%)
are found, it's a C<-B> file, otherwise it's a C<-T> file.  Also, any file
containing null in the first block is considered a binary file.  If C<-T>
or C<-B> is used on a filehandle, the current stdio buffer is examined
rather than the first block.  Both C<-T> and C<-B> return true on a null
file, or a file at EOF when testing a filehandle.  Because you have to
read a file to do the C<-T> test, on most occasions you want to use a C<-f>
against the file first, as in C<next unless -f $file && -T $file>.

=end original

ファイルテスト C<-T> と C<-B> の動作原理は、次のようになっています。
ファイルの最初の数ブロックを調べて、変わった制御コードや
上位ビットがセットされているような、通常のテキストには現れない文字を探します。
そのような文字が、たくさん (>30%) 見つかるようであれば、
そのファイルは C<-B> ファイルであると判断され、
それほど見つからなければ、C<-T> ファイルとなります。
最初のブロックにヌル文字が含まれるファイルも、
バイナリファイルとみなされます。
C<-T> や C<-B> をファイルハンドルに対して用いると、
最初のブロックを調べる代わりに、標準入出力バッファを調べます。
調べたファイルの中身が何もないときや、
ファイルハンドルを調べたときに EOF に達して
いたときには、C<-T> も C<-B> も「真」を返します。
C<-T> テストをするためにはファイルを読み込まないといけないので、
たいていは C<next unless -f $file && -T $file> というような形で
まず調べたいファイルに対して C<-f> を使いたいはずです。

=begin original

If any of the file tests (or either the C<stat> or C<lstat> operators) are given
the special filehandle consisting of a solitary underline, then the stat
structure of the previous file test (or stat operator) is used, saving
a system call.  (This doesn't work with C<-t>, and you need to remember
that lstat() and C<-l> will leave values in the stat structure for the
symbolic link, not the real file.)  Example:

=end original

どのファイルテスト (あるいは、C<stat> や C<lstat>) 演算子にも、
下線だけから成る特別なファイルハンドルを与えると、
前回のファイルテスト (や stat) の stat 構造体が使われ、
システムコールを省きます
(C<-t> には使えませんし、lstat() や C<-l> は実ファイルではなく、
シンボリックリンクの情報を stat 構造体に残すことを
覚えておく必要があります)。例:

    print "Can do.\n" if -r $a || -w _ || -x _;

    stat($filename);
    print "Readable\n" if -r _;
    print "Writable\n" if -w _;
    print "Executable\n" if -x _;
    print "Setuid\n" if -u _;
    print "Setgid\n" if -g _;
    print "Sticky\n" if -k _;
    print "Text\n" if -T _;
    print "Binary\n" if -B _;

=item abs VALUE

=item abs

=begin original

Returns the absolute value of its argument.
If VALUE is omitted, uses C<$_>.

=end original

引数の絶対値を返します。
VALUEが省略された場合は、 C<$_> を使います。

=item accept NEWSOCKET,GENERICSOCKET

=begin original

Accepts an incoming socket connect, just as the accept(2) system call
does.  Returns the packed address if it succeeded, false otherwise.
See the example in L<perlipc/"Sockets: Client/Server Communication">.

=end original

accept(2) システムコールと同様に、着信するソケットの接続を受け付けます。
成功時にはパックされたアドレスを返し、失敗すれば偽を返します。
L<perlipc/"Sockets: Client/Server Communication">
の例を参照してください。

=begin original

On systems that support a close-on-exec flag on files, the flag will
be set for the newly opened file descriptor, as determined by the
value of $^F.  See L<perlvar/$^F>.

=end original

ファイルに対する close-on-exec フラグをサポートしているシステムでは、
フラグは$^F の値で決定される、新しくオープンされたファイル記述子に対して
セットされます。L<perlvar/$^F>を参照してください。

=item alarm SECONDS

=item alarm

=begin original

Arranges to have a SIGALRM delivered to this process after the
specified number of seconds have elapsed.  If SECONDS is not specified,
the value stored in C<$_> is used. (On some machines,
unfortunately, the elapsed time may be up to one second less than you
specified because of how seconds are counted.)  Only one timer may be
counting at once.  Each call disables the previous timer, and an
argument of C<0> may be supplied to cancel the previous timer without
starting a new one.  The returned value is the amount of time remaining
on the previous timer.

=end original

指定した秒数が経過した後に、自プロセスに SIGALRM が
送られてくるようにします。SECONDS が指定されていない場合は、
C<$_>に格納されている値を使います。
(マシンによっては、秒の
数え方が異なるため、指定した秒数よりも最大で 1 秒少ない経過時間となります。)
一度には 1 つのタイマだけが設定可能です。
呼び出しを行なう度に、以前のタイマを無効にしますし、
新しくタイマを起動しないで以前のタイマをキャンセルするために
引数に C<0> を指定して呼び出すことができます。
以前のタイマの残り時間が、返り値となります。

=begin original

For delays of finer granularity than one second, you may use Perl's
four-argument version of select() leaving the first three arguments
undefined, or you might be able to use the C<syscall> interface to
access setitimer(2) if your system supports it.  The Time::HiRes module
from CPAN may also prove useful.

=end original

1 秒より精度の高いスリープを行なうには、
Perl の 4 引数版 select() を最初の 3 引数を未定義にして使うか、
setitimer(2) をサポートしているシステムでは、Perl の
C<syscall> インタフェースを使ってアクセスすることができます。
CPAN の Time::HiRes モジュールも有用でしょう。

=begin original

It is usually a mistake to intermix C<alarm> and C<sleep> calls.
(C<sleep> may be internally implemented in your system with C<alarm>)

=end original

C<alarm> と C<sleep> を混ぜて使うのは普通は間違いです。
(C<sleep> は内部的に C<alarm> を使って内部的に実装されているかもしれません)

=begin original

If you want to use C<alarm> to time out a system call you need to use an
C<eval>/C<die> pair.  You can't rely on the alarm causing the system call to
fail with C<$!> set to C<EINTR> because Perl sets up signal handlers to
restart system calls on some systems.  Using C<eval>/C<die> always works,
modulo the caveats given in L<perlipc/"Signals">.

=end original

C<alarm> をシステムコールの時間切れのために使いたいなら、
C<eval>/C<die> のペアで使う必要があります。
システムコールが失敗したときに C<$!> に C<EINTR> がセットされることに
頼ってはいけません。なぜならシステムによっては Perl は 
システムコールを再開するためにシグナルハンドラを設定するからです。
C<eval>/C<die> は常にうまく動きます。
注意点については L<perlipc/"Signals"> を参照して下さい。

    eval {
	local $SIG{ALRM} = sub { die "alarm\n" }; # NB: \n required
	alarm $timeout;
	$nread = sysread SOCKET, $buffer, $size;
	alarm 0;
    };
    if ($@) {
	die unless $@ eq "alarm\n";   # propagate unexpected errors
    	# timed out
    }
    else {
    	# didn't
    }

=item atan2 Y,X

=begin original

Returns the arctangent of Y/X in the range -PI to PI.

=end original

-πからπの範囲で Y/X の逆正接を返します。

=begin original

For the tangent operation, you may use the C<Math::Trig::tan>
function, or use the familiar relation:

=end original

正接を求めたいときは、C<Math::Trig::tan> を使うか、
以下のよく知られた関係を使ってください。

    sub tan { sin($_[0]) / cos($_[0])  }

=item bind SOCKET,NAME

=begin original

Binds a network address to a socket, just as the bind system call
does.  Returns true if it succeeded, false otherwise.  NAME should be a
packed address of the appropriate type for the socket.  See the examples in
L<perlipc/"Sockets: Client/Server Communication">.

=end original

bind(2) システムコールと同様に、ネットワークアドレスをソケットに結び付けます。
成功時には真を返し、失敗時には偽を返します。
NAME は、ソケットに対する、適切な型のパックされたアドレスでなければなりません。
L<perlipc/"Sockets: Client/Server Communication"> の例を参照してください。

=item binmode FILEHANDLE, DISCIPLINE

=item binmode FILEHANDLE

=begin original

Arranges for FILEHANDLE to be read or written in "binary" or "text" mode
on systems where the run-time libraries distinguish between binary and
text files.  If FILEHANDLE is an expression, the value is taken as the
name of the filehandle.  DISCIPLINE can be either of C<":raw"> for
binary mode or C<":crlf"> for "text" mode.  If the DISCIPLINE is
omitted, it defaults to C<":raw">.

=end original

バイナリファイルとテキストファイルを区別する OS において、
FILEHANDLE を「バイナリ」または「テキスト」で読み書きするように
指定します。
FILEHANDLE が式である場合には、その式の値がファイルハンドルの
名前として使われます。
DISCIPLINE はバイナリモードを示す C<":raw"> か
テキストモードを示す C<":crlf"> のどちらかです。
DISCIPLINE を省略すると、デフォルトとして C<":raw">  が使われます。

=begin original

binmode() should be called after open() but before any I/O is done on
the filehandle.

=end original

binmode() は open() の後、ファイルハンドルに対する入出力が
行われる前に呼び出されるべきです。

=begin original

On many systems binmode() currently has no effect, but in future, it
will be extended to support user-defined input and output disciplines.
On some systems binmode() is necessary when you're not working with a
text file.  For the sake of portability it is a good idea to always use
it when appropriate, and to never use it when it isn't appropriate.

=end original

多くのシステムでは現在のところ binmode() は何の効果もありませんが、
将来ユーザーが定義した入出力機能に対応するように拡張される予定です。
テキストファイルでないものを扱う場合に binmode() が必要な
システムもあります。
移植性のために、適切なときには常にこれを使い、適切でないときには
決して使わないというのは良い考えです。

=begin original

In other words:  Regardless of platform, use binmode() on binary
files, and do not use binmode() on text files.

=end original

言い換えると: プラットフォームに関わらず、
バイナリファイルに対しては binmode() を使ってください。
テキストファイルには binmode() を使わないでください。

=begin original

The C<open> pragma can be used to establish default disciplines.
See L<open>.

=end original

C<open> プラグマを使って、デフォルトの動作を設定できます。
L<open> を参照して下さい。

=begin original

The operating system, device drivers, C libraries, and Perl run-time
system all work together to let the programmer treat a single
character (C<\n>) as the line terminator, irrespective of the external
representation.  On many operating systems, the native text file
representation matches the internal representation, but on some
platforms the external representation of C<\n> is made up of more than
one character.

=end original

The operating system, device drivers, C libraries, and Perl run-time
system all work together to let the programmer treat a single
character (C<\n>) as the line terminator, irrespective of the external
representation.  On many operating systems, the native text file
representation matches the internal representation, but on some
platforms the external representation of C<\n> is made up of more than
one character.
(*TBT*)

Mac OS and all variants of Unix use a single character to end each line
in the external representation of text (even though that single
character is not necessarily the same across these platforms).
Consequently binmode() has no effect on these operating systems.  In
other systems like VMS, MS-DOS and the various flavors of MS-Windows
your program sees a C<\n> as a simple C<\cJ>, but what's stored in text
files are the two characters C<\cM\cJ>.  That means that, if you don't
use binmode() on these systems, C<\cM\cJ> sequences on disk will be
converted to C<\n> on input, and any C<\n> in your program will be
converted back to C<\cM\cJ> on output.  This is what you want for text
files, but it can be disastrous for binary files.

Another consequence of using binmode() (on some systems) is that
special end-of-file markers will be seen as part of the data stream.
For systems from the Microsoft family this means that if your binary
data contains C<\cZ>, the I/O subsystem will regard it as the end of
the file, unless you use binmode().

binmode() is not only important for readline() and print() operations,
but also when using read(), seek(), sysread(), syswrite() and tell()
(see L<perlport> for more details).  See the C<$/> and C<$\> variables
in L<perlvar> for how to manually set your input and output
line-termination sequences.

=item bless REF,CLASSNAME

=item bless REF

=begin original

This function tells the thingy referenced by REF that it is now an object
in the CLASSNAME package.  If CLASSNAME is omitted, the current package
is used.  Because a C<bless> is often the last thing in a constructor,
it returns the reference for convenience.  Always use the two-argument
version if the function doing the blessing might be inherited by a
derived class.  See L<perltoot> and L<perlobj> for more about the blessing
(and blessings) of objects.

=end original

この関数は、REF で渡された オブジェクトに対し、
CLASSNAME 内のオブジェクトとなったことを伝えます。
CLASSNAME が省略された場合には、その時点のパッケージとなります。
C<bless> は通常、コンストラクタの最後に置かれますので、
簡便のためにそのリファレンスを返します。
bless される関数が派生クラスによって継承される場合は、
常に 2 引数版を使ってください。
オブジェクトの bless (や再 bless) について、
詳しくは L<perltoot> と L<perlobj> を参照してください。

=begin original

Consider always blessing objects in CLASSNAMEs that are mixed case.
Namespaces with all lowercase names are considered reserved for
Perl pragmata.  Builtin types have all uppercase names, so to prevent
confusion, you may wish to avoid such package names as well.  Make sure
that CLASSNAME is a true value.

=end original

大文字小文字が混じっている CLASSNAME のオブジェクトは常に bless することを
考慮してください。
全て小文字の名前を持つ名前空間は Perl プラグマのために予約されています。
組み込みの型は全て大文字の名前を持ちますので、混乱を避けるために、
パッケージ名としてこのような名前は避けるべきです。
CLASSNAME は真の値を持つようにしてください。

=begin original

See L<perlmod/"Perl Modules">.

=end original

L<perlmod/"Perl Modules"> を参照して下さい。

=item caller EXPR

=item caller

=begin original

Returns the context of the current subroutine call.  In scalar context,
returns the caller's package name if there is a caller, that is, if
we're in a subroutine or C<eval> or C<require>, and the undefined value
otherwise.  In list context, returns

=end original

その時点のサブルーチン呼び出しのコンテキストを返します。
スカラコンテキストでは、呼び元がある場合
(サブルーチン、C<eval>、C<require> の中にいるとき) には
呼び出し元のパッケージ名を返し、その他のときには未定義値を返します。
リストコンテキストでは、

    ($package, $filename, $line) = caller;

を返します。

=begin original

With EXPR, it returns some extra information that the debugger uses to
print a stack trace.  The value of EXPR indicates how many call frames
to go back before the current one.

=end original

EXPR を付けると、デバッガがスタックトレースを表示するために使う情報を返します。
EXPR の値は、現状から数えて、
いくつ前のコールフレームまで戻るかを示します。

    ($package, $filename, $line, $subroutine, $hasargs,
    $wantarray, $evaltext, $is_require, $hints, $bitmask) = caller($i);

=begin original

Here $subroutine may be C<(eval)> if the frame is not a subroutine
call, but an C<eval>.  In such a case additional elements $evaltext and
C<$is_require> are set: C<$is_require> is true if the frame is created by a
C<require> or C<use> statement, $evaltext contains the text of the
C<eval EXPR> statement.  In particular, for an C<eval BLOCK> statement,
$filename is C<(eval)>, but $evaltext is undefined.  (Note also that
each C<use> statement creates a C<require> frame inside an C<eval EXPR>)
frame.  C<$hasargs> is true if a new instance of C<@_> was set up for the
frame.  C<$hints> and C<$bitmask> contain pragmatic hints that the caller
was compiled with.  The C<$hints> and C<$bitmask> values are subject to
change between versions of Perl, and are not meant for external use.

=end original

Here $subroutine may be C<(eval)> if the frame is not a subroutine
call, but an C<eval>.  In such a case additional elements $evaltext and
C<$is_require> are set: C<$is_require> is true if the frame is created by a
C<require> or C<use> statement, $evaltext contains the text of the
C<eval EXPR> statement.  In particular, for an C<eval BLOCK> statement,
$filename is C<(eval)>, but $evaltext is undefined.  (Note also that
each C<use> statement creates a C<require> frame inside an C<eval EXPR>)
frame.  C<$hasargs> is true if a new instance of C<@_> was set up for the
frame.  C<$hints> and C<$bitmask> contain pragmatic hints that the caller
was compiled with.  The C<$hints> and C<$bitmask> values are subject to
change between versions of Perl, and are not meant for external use.
(*TBT*)

=begin original

Furthermore, when called from within the DB package, caller returns more
detailed information: it sets the list variable C<@DB::args> to be the
arguments with which the subroutine was invoked.

=end original

さらに、DB パッケージの中から呼ばれた場合は、caller は
より詳細な情報を返します。
サブルーチンが起動されたときの引数を変数 C<@DB::args> に設定します。

=begin original

Be aware that the optimizer might have optimized call frames away before
C<caller> had a chance to get the information.  That means that C<caller(N)>
might not return information about the call frame you expect it do, for
C<< N > 1 >>.  In particular, C<@DB::args> might have information from the 
previous time C<caller> was called.

=end original

C<caller> が情報を得る前にオプティマイザが呼び出しフレームを最適化して
しまうかもしれないことに注意してください。
これは、C<caller(N)> が C<< N > 1 >> のとき、
あなたが予測した呼び出しフレームの情報を返さないかもしれないことを意味します。
特に、C<@DB::args> は C<caller> が前回呼び出された時の情報を
持っているかもしれません。

=item chdir EXPR

=begin original

Changes the working directory to EXPR, if possible.  If EXPR is omitted,
changes to the directory specified by C<$ENV{HOME}>, if set; if not,
changes to the directory specified by C<$ENV{LOGDIR}>.  If neither is
set, C<chdir> does nothing.  It returns true upon success, false
otherwise.  See the example under C<die>.

=end original

(可能であれば、) カレントディレクトリを EXPR に移します。
EXPR を指定しないと、C<$ENV{HOME}> が設定されていれば、
そのディレクトリに移ります。そうでなく、C<$ENV{LOGDIR}>が設定されていれば、
そのディレクトリに移ります。どちらも設定されていなければ、C<chdir> は何もしません。
成功時には真を返し、そうでなければ偽を返します。
C<die> の項の例を参照してください。

=item chmod LIST

=begin original

Changes the permissions of a list of files.  The first element of the
list must be the numerical mode, which should probably be an octal
number, and which definitely should I<not> a string of octal digits:
C<0644> is okay, C<'0644'> is not.  Returns the number of files
successfully changed.  See also L</oct>, if all you have is a string.

=end original

LIST に含まれるファイルの、パーミッションを変更します。
LIST の最初の要素は、数値表現のモードでなければなりません。
C<0644> は OK ですが、 C<'0644'> はだめです。
変更に成功したファイルの数を返します。
文字列を使いたい場合は、L</oct> を参照してください。

    $cnt = chmod 0755, 'foo', 'bar';
    chmod 0755, @executables;
    $mode = '0644'; chmod $mode, 'foo';      # !!! sets mode to
                                             # --w----r-T
    $mode = '0644'; chmod oct($mode), 'foo'; # this is better
    $mode = 0644;   chmod $mode, 'foo';      # this is best

=begin original

You can also import the symbolic C<S_I*> constants from the Fcntl
module:

=end original

シンボリックな C<S_I*> 定数を Fcntl モジュールからインポートすることもできます。

    use Fcntl ':mode';

    chmod S_IRWXU|S_IRGRP|S_IXGRP|S_IROTH|S_IXOTH, @executables;
    # This is identical to the chmod 0755 of the above example.

=item chomp VARIABLE

=item chomp LIST

=item chomp

=begin original

This safer version of L</chop> removes any trailing string
that corresponds to the current value of C<$/> (also known as
$INPUT_RECORD_SEPARATOR in the C<English> module).  It returns the total
number of characters removed from all its arguments.  It's often used to
remove the newline from the end of an input record when you're worried
that the final record may be missing its newline.  When in paragraph
mode (C<$/ = "">), it removes all trailing newlines from the string.
When in slurp mode (C<$/ = undef>) or fixed-length record mode (C<$/> is
a reference to an integer or the like, see L<perlvar>) chomp() won't
remove anything.  
If VARIABLE is omitted, it chomps C<$_>.  Example:

=end original

より安全な C<chop> (以下を参照してください) です。
C<$/> (C<English> モジュールでは、$INPUT_RECORD_SEPARATOR
とも言う) のその時点の値に対応する行末文字を削除します。
全ての引数から削除した文字数の合計を返します。
入力レコードから、改行を削除したいのだけれど、最後のレコードには改行が
入っているのかわからないような場合に、使用できます。
段落モード (C<$/ = "">) では、レコードの最後の改行をすべて取り除きます。
吸い込みモード (C<$/ = undef>) や 固定長レコードモード
(C<$/> が整数へのリファレンスや類似のものの場合。L<perlvar>を参照してください)
では、chomp() は何も取り除きません。
VARIABLE が省略されると、$_ を対象として chomp します。
例:

=begin original

    while (<>) {
	chomp;	# avoid \n on last field
	@array = split(/:/);
	# ...
    }

=end original

    while (<>) {
        chomp;  # 最後のフィールドの \n を避ける
        @array = split(/:/);
        #...
    }

=begin original

If VARIABLE is a hash, it chomps the hash's values, but not its keys.

You can actually chomp anything that's an lvalue, including an assignment:

=end original

左辺値であれば、代入を含めて、任意のものを chomp できます:

    chomp($cwd = `pwd`);
    chomp($answer = <STDIN>);

=begin original

If you chomp a list, each element is chomped, and the total number of
characters removed is returned.

=end original

リストを chomp すると、個々の要素が chomp され、
削除された文字数の合計が返されます。

=item chop VARIABLE

=item chop LIST

=item chop

=begin original

Chops off the last character of a string and returns the character
chopped.  It is much more efficient than C<s/.$//s> because it neither
scans nor copies the string.  If VARIABLE is omitted, chops C<$_>.
If VARIABLE is a hash, it chops the hash's values, but not its keys.

=end original

文字列の最後の文字を切り捨てて、その切り取った文字を返します。
文字列の検索もコピーも行ないませんので
C<s/\n//> よりも、ずっと効率的です。
VARIABLE が省略されると、$_ を対象として chop します。
VARIABLE がハッシュの場合、ハッシュの value を chop しますが、
key は chop しません。

=begin original

You can actually chop anything that's an lvalue, including an assignment.

=end original

実際のところ、代入を含む左辺値となりうるなんでも chop できます。

=begin original

If you chop a list, each element is chopped.  Only the value of the
last C<chop> is returned.

=end original

リストを chop すると、個々の要素が chop されます。
最後の C<chop> の値だけが返されます。

=begin original

Note that C<chop> returns the last character.  To return all but the last
character, use C<substr($string, 0, -1)>.

=end original

C<chop> は最後の文字を返すことに注意してください。
最後以外の全ての文字を返すためには、C<substr($string, 0, -1)> を
使ってください。

=item chown LIST

=begin original

Changes the owner (and group) of a list of files.  The first two
elements of the list must be the I<numeric> uid and gid, in that
order.  A value of -1 in either position is interpreted by most
systems to leave that value unchanged.  Returns the number of files
successfully changed.

=end original

LIST に含まれるファイルの所有者 (とグループ) を変更します。
LIST の最初の 2 つの要素には、I<数値表現> の uid と gid を
この順序で与えなければなりません。
どちらかの値を -1 にすると、ほとんどのシステムではその値は
変更しないと解釈します。
変更に成功したファイルの数が返されます。

    $cnt = chown $uid, $gid, 'foo', 'bar';
    chown $uid, $gid, @filenames;

=begin original

Here's an example that looks up nonnumeric uids in the passwd file:

=end original

passwd ファイルから数値表現でない uid を検索する例を
示します:

    print "User: ";
    chomp($user = <STDIN>);
    print "Files: ";
    chomp($pattern = <STDIN>);

    ($login,$pass,$uid,$gid) = getpwnam($user)
	or die "$user not in passwd file";

    @ary = glob($pattern);	# expand filenames
    chown $uid, $gid, @ary;

=begin original

On most systems, you are not allowed to change the ownership of the
file unless you're the superuser, although you should be able to change
the group to any of your secondary groups.  On insecure systems, these
restrictions may be relaxed, but this is not a portable assumption.
On POSIX systems, you can detect this condition this way:

=end original

ほとんどのシステムでは、スーパーユーザーだけがファイルの所有者を
変更できますが、グループは実行者の副グループに変更できるべきです。
安全でないシステムでは、この制限はゆるめられています。
しかしこれは移植性のある仮定ではありません。
POSIX システムでは、以下のようにしてこの条件を検出できます:

    use POSIX qw(sysconf _PC_CHOWN_RESTRICTED);
    $can_chown_giveaway = not sysconf(_PC_CHOWN_RESTRICTED);

=item chr NUMBER

=item chr

=begin original

Returns the character represented by that NUMBER in the character set.
For example, C<chr(65)> is C<"A"> in either ASCII or Unicode, and
chr(0x263a) is a Unicode smiley face (but only within the scope of
a C<use utf8>).  For the reverse, use L</ord>.  
See L<utf8> for more about Unicode.

=end original

特定の文字セットでの NUMBER で表わされる文字を返します。
たとえば、C<chr(65)> は ASCII と Unicode の両方で C<"A"> となります。
chr(0x263a) は Unicode のスマイリーフェイスです
(ただし C<use utf8> のスコープ内のみです)。
逆を行うためには、L</ord> を参照してください。
Unicode についてもっと知りたいなら、L<utf8> を参照してください。

If NUMBER is omitted, uses C<$_>.

=item chroot FILENAME

=item chroot

=begin original

This function works like the system call by the same name: it makes the
named directory the new root directory for all further pathnames that
begin with a C</> by your process and all its children.  (It doesn't
change your current working directory, which is unaffected.)  For security
reasons, this call is restricted to the superuser.  If FILENAME is
omitted, does a C<chroot> to C<$_>.

=end original

同じ名前のシステムコールと同じことをします。
現在のプロセス及び子プロセスに対して、C</>で始まるパス名に関して
指定されたディレクトリを新しいルートディレクトリとして扱います。
(これはカレントディレクトリを変更しません。カレントディレクトリはそのままです)。
セキュリティ上の理由により、この呼び出しはスーパーユーザーしか行えません。
FILENAME を省略すると、C<$_> へ C<chroot> します。

=item close FILEHANDLE

=item close

=begin original

Closes the file or pipe associated with the file handle, returning true
only if stdio successfully flushes buffers and closes the system file
descriptor.  Closes the currently selected filehandle if the argument
is omitted.

=end original

FILEHANDLE に対応したファイルまたはパイプをクローズします。
標準入出力が正常にバッファのフラッシュを行なって、
ファイル記述子のクローズしたときにのみ「真」を返します。
引数が省略された場合、現在選択されているファイルハンドルをクローズします。

=begin original

You don't have to close FILEHANDLE if you are immediately going to do
another C<open> on it, because C<open> will close it for you.  (See
C<open>.)  However, an explicit C<close> on an input file resets the line
counter (C<$.>), while the implicit close done by C<open> does not.

=end original

クローズしてすぐにまた、同じファイルハンドルに
対してオープンを行なう場合には、C<open> が自動的に C<close>
を行ないますので、close FILEHANDLE する必要はありません
(C<open> を参照してください)。
ただし、明示的にクローズを行なったときにのみ入力ファイルの
行番号 (C<$.>) のリセットが行なわれ、C<open> によって行なわれる
暗黙の C<close>では行なわれません。

=begin original

If the file handle came from a piped open C<close> will additionally
return false if one of the other system calls involved fails or if the
program exits with non-zero status.  (If the only problem was that the
program exited non-zero C<$!> will be set to C<0>.)  Closing a pipe 
also waits for the process executing on the pipe to complete, in case you
want to look at the output of the pipe afterwards, and 
implicitly puts the exit status value of that command into C<$?>.

=end original

ファイルハンドルがパイプつきオープンなら、
C<close> はその他のシステムコールが失敗したり
プログラムが非ゼロのステータスで終了した場合にも偽を返します
(プログラムが非ゼロで終了しただけの場合は、C<$!>がC<0>にセットされます)。
後でパイプの出力を見たい場合のために、
パイプのクローズでは、パイプ上で実行されている
プロセスの完了を待ちます。また
自動的にコマンドのステータス値を C<$?> に設定します。

=begin original

Prematurely closing the read end of a pipe (i.e. before the process
writing to it at the other end has closed it) will result in a
SIGPIPE being delivered to the writer.  If the other end can't
handle that, be sure to read all the data before closing the pipe.

=end original

途中で(つまり、書き込み側が閉じる前に)
パイプの読み込み側が閉じた場合、
書き込み側に SIGPIPE が配送されます。
書き込み側がこれを扱えない場合、パイプを閉じる前に
確実に全てのデータが読み込まれるようにする必要があります。

=begin original

Example:

=end original

例:

    open(OUTPUT, '|sort >foo')  # pipe to sort
        or die "Can't start sort: $!";
    #...			# print stuff to output
    close OUTPUT		# wait for sort to finish
        or warn $! ? "Error closing sort pipe: $!"
                   : "Exit status $? from sort";
    open(INPUT, 'foo')		# get sort's results
        or die "Can't open 'foo' for input: $!";

=begin original

FILEHANDLE may be an expression whose value can be used as an indirect
filehandle, usually the real filehandle name.

=end original

FILEHANDLE は式でもかまいません。この場合、値は相対ファイルハンドルと
して扱われ、普通は実際のファイルハンドル名です。

=item closedir DIRHANDLE

=begin original

Closes a directory opened by C<opendir> and returns the success of that
system call.

=end original

C<opendir> でオープンしたディレクトリをクローズし、
システムコールの返り値を返します。

=begin original

DIRHANDLE may be an expression whose value can be used as an indirect
dirhandle, usually the real dirhandle name.

=end original

DIRHANDLE は式でもかまいません。この場合、値は相対ディレクトリハンドルと
して扱われ、普通は実際のディレクトリハンドル名です。

=item connect SOCKET,NAME

=begin original

Attempts to connect to a remote socket, just as the connect system call
does.  Returns true if it succeeded, false otherwise.  NAME should be a
packed address of the appropriate type for the socket.  See the examples in
L<perlipc/"Sockets: Client/Server Communication">.

=end original

connect(2) システムコールと同様に、リモートソケット
への接続を試みます。　成功時には真を返し、失敗時には
偽を返します。　NAME は、ソケットに対する、適切な型の
パックされたアドレスでなければなりません。
L<perlipc/"Sockets: Client/Server Communication">. の例を参照してください。

=item continue BLOCK

=begin original

Actually a flow control statement rather than a function.  If there is a
C<continue> BLOCK attached to a BLOCK (typically in a C<while> or
C<foreach>), it is always executed just before the conditional is about to
be evaluated again, just like the third part of a C<for> loop in C.  Thus
it can be used to increment a loop variable, even when the loop has been
continued via the C<next> statement (which is similar to the C C<continue>
statement).

=end original

実際には関数ではなく、実行制御文です。
C<continue> BLOCK が BLOCK (典型的には C<while> または C<foreach> の中)にあると、
これは条件文が再評価される直前に常に実行されます。
これは C における C<for> ループの 3 番目の部分と同様です。
従って、これは C<next> 文
(これは C の C<continue> 文と似ています)を使ってループが繰り返されるときでも
ループ変数を増やしたいときに使えます。

=begin original

C<last>, C<next>, or C<redo> may appear within a C<continue>
block.  C<last> and C<redo> will behave as if they had been executed within
the main block.  So will C<next>, but since it will execute a C<continue>
block, it may be more entertaining.

=end original

C<last>, C<next>, C<redo> が C<continue> ブロック内に現れる可能性があります。
C<last> と C<redo> はメインブロックの中で実行されたのと同じように振舞います。
C<next> の場合は、C<continue> ブロックを実行することになるので、
より面白いことになります。

    while (EXPR) {
	### redo always comes here
	do_something;
    } continue {
	### next always comes here
	do_something_else;
	# then back the top to re-check EXPR
    }
    ### last always comes here

=begin original

Omitting the C<continue> section is semantically equivalent to using an
empty one, logically enough.  In that case, C<next> goes directly back
to check the condition at the top of the loop.

=end original

C<continue> 節を省略するのは、文法的には空の節を指定したのと同じで、
論理的には十分です。
この場合、C<next> は直接ループ先頭の条件チェックに戻ります。

=item cos EXPR

=item cos

=begin original

Returns the cosine of EXPR (expressed in radians).  If EXPR is omitted,
takes cosine of C<$_>.

=end original

(ラジアンで示した) EXPR の余弦を返します。　EXPR が
省略されたときには、C<$_> の余弦を取ります。

=begin original

For the inverse cosine operation, you may use the C<Math::Trig::acos()>
function, or use this relation:

=end original

逆余弦を求めるためには、C<Math::Trig::acos()> 関数を使うか、
以下の関係を使ってください。

    sub acos { atan2( sqrt(1 - $_[0] * $_[0]), $_[0] ) }

=item crypt PLAINTEXT,SALT

=begin original

Encrypts a string exactly like the crypt(3) function in the C library
(assuming that you actually have a version there that has not been
extirpated as a potential munition).  This can prove useful for checking
the password file for lousy passwords, amongst other things.  Only the
guys wearing white hats should do this.

=end original

C ライブラリの crypt(3) 関数と全く同じように、文字列を暗号化します
(一時的な必需品として、まだ絶滅していないバージョンを持っていると仮定しています)。
パスワードファイルの中で、
あまり良くないものをチェックするために使うことができます。
公正な人だけが、これを行なうべきです。

=begin original

Note that C<crypt> is intended to be a one-way function, much like breaking
eggs to make an omelette.  There is no (known) corresponding decrypt
function.  As a result, this function isn't all that useful for
cryptography.  (For that, see your nearby CPAN mirror.)

=end original

C<crypt> は一方向関数を指向していることに注意してください。
オムレツを作るのに卵を割るようなものです。
(知られている限り)対応する複合化関数はありません。
結果として、この関数はあらゆる暗号に対して有効というわけではありません
(このためには、お近くの CPAN を参照してください)。

=begin original

When verifying an existing encrypted string you should use the encrypted
text as the salt (like C<crypt($plain, $crypted) eq $crypted>).  This
allows your code to work with the standard C<crypt> and with more
exotic implementations.  When choosing a new salt create a random two
character string whose characters come from the set C<[./0-9A-Za-z]>
(like C<join '', ('.', '/', 0..9, 'A'..'Z', 'a'..'z')[rand 64, rand 64]>).

=end original

When verifying an existing encrypted string you should use the encrypted
text as the salt (like C<crypt($plain, $crypted) eq $crypted>).  This
allows your code to work with the standard C<crypt> and with more
exotic implementations.  When choosing a new salt create a random two
character string whose characters come from the set C<[./0-9A-Za-z]>
(like C<join '', ('.', '/', 0..9, 'A'..'Z', 'a'..'z')[rand 64, rand 64]>).
(*TBT*)

=begin original

Here's an example that makes sure that whoever runs this program knows
their own password:

=end original

プログラムを実行する人が、
自分のパスワードを知っていることを確認する例です:

    $pwd = (getpwuid($<))[1];

    system "stty -echo";
    print "Password: ";
    chomp($word = <STDIN>);
    print "\n";
    system "stty echo";

    if (crypt($word, $pwd) ne $pwd) {
	die "Sorry...\n";
    } else {
	print "ok\n";
    }

=begin original

Of course, typing in your own password to whoever asks you
for it is unwise.

=end original

もちろん、自分自身のパスワードを誰にでも入力するのは賢明ではありません。

=begin original

The L<crypt> function is unsuitable for encrypting large quantities
of data, not least of all because you can't get the information
back.  Look at the F<by-module/Crypt> and F<by-module/PGP> directories
on your favorite CPAN mirror for a slew of potentially useful
modules.

=end original

L<crypt> 関数は大量のデータの暗号化には向いていません。
これは情報を戻せないという理由だけではありません。
あなたのお好みの CPAN ミラー の F<by-module/Crypt> と F<by-module/PGP>
ディレクトリを見れば、あなたの役に立ちそうなモジュールが大量にあります。

=item dbmclose HASH

=begin original

[This function has been largely superseded by the C<untie> function.]

Breaks the binding between a DBM file and a hash.

=end original

[この関数は、C<untie> 関数に大きくとって代わられました。]

DBM ファイルとハッシュの連結をはずします。

=item dbmopen HASH,DBNAME,MASK

=begin original

[This function has been largely superseded by the C<tie> function.]

This binds a dbm(3), ndbm(3), sdbm(3), gdbm(3), or Berkeley DB file to a
hash.  HASH is the name of the hash.  (Unlike normal C<open>, the first
argument is I<not> a filehandle, even though it looks like one).  DBNAME
is the name of the database (without the F<.dir> or F<.pag> extension if
any).  If the database does not exist, it is created with protection
specified by MASK (as modified by the C<umask>).  If your system supports
only the older DBM functions, you may perform only one C<dbmopen> in your
program.  In older versions of Perl, if your system had neither DBM nor
ndbm, calling C<dbmopen> produced a fatal error; it now falls back to
sdbm(3).

=end original

[この関数は、C<tie> 関数に大きくとって代わられました。]

dbm(3), ndbm(3), sdbm(3), gdbm(3) ファイルまたは Berkeley DB
ファイルを連想配列に結び付けます。　HASH は、その連想配列の名前です。
(普通の C<open> とは違って、最初の引数はファイルハンドル
I<ではありません>。まあ、似たようなものですが。)
DBNAME は、データベースの名前です (拡張子の .dir や
.pag はもしあってもつけません)。
データベースが存在しなければ、
MODE MASK (を C<umask> で修正したもの) で指定されたモードで
作られます。　古い DBM 関数のみをサポートしている
システムでは、プログラム中で 1 度だけ dbmopen() を実行することができます。
昔のバージョンのPerlでは、DBM も ndbm も持っていないシステムでは、
dbmopen() を呼び出すと致命的エラーになります。
現在では sdbm(3) にフォールバックします。

=begin original

If you don't have write access to the DBM file, you can only read hash
variables, not set them.  If you want to test whether you can write,
either use file tests or try setting a dummy hash entry inside an C<eval>,
which will trap the error.

=end original

DBM ファイルに対して、書き込み権が無いときには、ハッシュ
配列を読みだすことだけができ、設定することはできません。
書けるか否かを調べたい場合には、ファイルテスト
演算子を使うか、エラーをトラップしてくれる、C<eval>
の中で、ダミーのハッシュエントリを設定してみることになります。

=begin original

Note that functions such as C<keys> and C<values> may return huge lists
when used on large DBM files.  You may prefer to use the C<each>
function to iterate over large DBM files.  Example:

=end original

大きな DBM ファイルを扱うときには、C<keys> や C<values>
のような関数は、巨大なリストを返します。　大きな DBM
ファイルでは、C<each> 関数を使って繰り返しを行なった
方が良いかもしれません。　例:

    # print out history file offsets
    dbmopen(%HIST,'/usr/lib/news/history',0666);
    while (($key,$val) = each %HIST) {
	print $key, ' = ', unpack('L',$val), "\n";
    }
    dbmclose(%HIST);

=begin original

See also L<AnyDBM_File> for a more general description of the pros and
cons of the various dbm approaches, as well as L<DB_File> for a particularly
rich implementation.

=end original

様々な dbm 手法に対する利点欠点に関するより一般的な記述および
特にリッチな実装である L<DB_File> に関しては
L<AnyDBM_File> も参照してください。

=begin original

You can control which DBM library you use by loading that library
before you call dbmopen():

=end original


    use DB_File;
    dbmopen(%NS_Hist, "$ENV{HOME}/.netscape/history.db")
	or die "Can't open netscape history file: $!";

=item defined EXPR

=item defined

=begin original

Returns a Boolean value telling whether EXPR has a value other than
the undefined value C<undef>.  If EXPR is not present, C<$_> will be
checked.

=end original

左辺値 EXPR が未定義値 C<undef> 以外の値を持つか否かを示す、ブール値を
返します。EXPR がない場合は、C<$_> がチェックされます。

=begin original

Many operations return C<undef> to indicate failure, end of file,
system error, uninitialized variable, and other exceptional
conditions.  This function allows you to distinguish C<undef> from
other values.  (A simple Boolean test will not distinguish among
C<undef>, zero, the empty string, and C<"0">, which are all equally
false.)  Note that since C<undef> is a valid scalar, its presence
doesn't I<necessarily> indicate an exceptional condition: C<pop>
returns C<undef> when its argument is an empty array, I<or> when the
element to return happens to be C<undef>.

=end original

多くの演算子が、EOF や未初期化変数、システムエラーといった、
例外的な条件で C<undef> を返すようになっています。
この関数は、配列の要素の参照のように、
実際にヌル文字列を返す演算について、未定義のヌルスカラと、
定義済みのヌルスカラを区別するときに使います。

=begin original

You may also use C<defined(&func)> to check whether subroutine C<&func>
has ever been defined.  The return value is unaffected by any forward
declarations of C<&foo>.  Note that a subroutine which is not defined
may still be callable: its package may have an C<AUTOLOAD> method that
makes it spring into existence the first time that it is called -- see
L<perlsub>.

=end original

C<defined(&func)> とすることでサブルーチン C<&func> の存在を、
確かめることもできます。
返り値は C<&foo> の前方定義には影響されません。
定義されていないサブルーチンも呼び出し可能であることに注意してください。
最初に呼び出されたときに存在するようにするための
C<AUTOLOAD> メソッドを持ったパッケージかもしれません。
L<perlsub> を参照して下さい。

=begin original

Use of C<defined> on aggregates (hashes and arrays) is deprecated.  It
used to report whether memory for that aggregate has ever been
allocated.  This behavior may disappear in future versions of Perl.
You should instead use a simple test for size:

=end original

あらかじめ定義された予約変数に対して用いても、
直感的に想像できるような値を返すことは保証されていません。
この振る舞いは将来のバージョンの Perl では消滅するかもしれません。
代わりに以下のテストを使うべきです。

    if (@an_array) { print "has array elements\n" }
    if (%a_hash)   { print "has hash members\n"   }

=begin original

When used on a hash element, it tells you whether the value is defined,
not whether the key exists in the hash.  Use L</exists> for the latter
purpose.

Examples:

=end original

ハッシュの要素に対して用いると、value が定義されているか否かを
返すものであって、ハッシュに key が存在するか否かを返すのではありません。
この用途には、L</exists> を使ってください。

例:

    print if defined $switch{'D'};
    print "$val\n" while defined($val = pop(@ary));
    die "Can't readlink $sym: $!"
	unless defined($value = readlink $sym);
    sub foo { defined &$bar ? &$bar(@_) : die "No bar"; }
    $debugging = 0 unless defined $debugging;

=begin original

Note:  Many folks tend to overuse C<defined>, and then are surprised to
discover that the number C<0> and C<""> (the zero-length string) are, in fact,
defined values.  For example, if you say

=end original

注意: 多くの人々が C<defined> を使いすぎて、C<0> と C<"">(空文字列) が実際のところ
定義された値であることに驚くようです。例えば、以下のように書くと:

    "ab" =~ /a(.*)b/;

=begin original

The pattern match succeeds, and C<$1> is defined, despite the fact that it
matched "nothing".  But it didn't really match nothing--rather, it
matched something that happened to be zero characters long.  This is all
very above-board and honest.  When a function returns an undefined value,
it's an admission that it couldn't give you an honest answer.  So you
should use C<defined> only when you're questioning the integrity of what
you're trying to do.  At other times, a simple comparison to C<0> or C<""> is
what you want.

=end original

パターンマッチングが成功し、C<$1> が定義されても、実際には
「なし」にマッチしています。しかしこれは何にもマッチしていないわけでは
ありません--何かにはマッチしているのですが、たまたまそれが長さ 0 だっただけです。
これは非常に率直で正直なことです。
関数が未定義値を返すとき、正直な答えを返すことができないことを告白しています。
ですので、あなたが自分がしようとしていることの完全性を確認するときにだけ
C<defined> を使うべきです。
その他の場合では、単に C<0> または C<""> と比較するというのがあなたの
求めているものです。

=begin original

See also L</undef>, L</exists>, L</ref>.

=end original

L</undef>, L</exists>, L</ref> も参照してください。

=item delete EXPR

=begin original

Given an expression that specifies a hash element, array element, hash slice,
or array slice, deletes the specified element(s) from the hash or array.
In the case of an array, if the array elements happen to be at the end,
the size of the array will shrink to the highest element that tests 
true for exists() (or 0 if no such element exists).

=end original

ハッシュ要素、配列要素、ハッシュスライス、配列スライスを指定する式を取り、
指定された要素をハッシュや配列からを削除します。
配列の場合、配列要素が最後にあった場合は、
配列の大きさは exists() が真を返す最後尾の要素に縮みます
(そのような要素がない場合は 0 になります)。

=begin original

Returns each element so deleted or the undefined value if there was no such
element.  Deleting from C<$ENV{}> modifies the environment.  Deleting from
a hash tied to a DBM file deletes the entry from the DBM file.  Deleting
from a C<tie>d hash or array may not necessarily return anything.

=end original

削除された値か、(何も削除されなかった場合には) 未定義値が返されます。
C<$ENV{}> から削除を行なうと、実際に環境変数を変更します。
DBM ファイルに tie された配列からの削除は、その DBM ファイルからエントリを
削除します。
しかし、C<tie> されたハッシュや配列からの削除は、
値を返すとは限りません。

=begin original

Deleting an array element effectively returns that position of the array
to its initial, uninitialized state.  Subsequently testing for the same
element with exists() will return false.  Note that deleting array
elements in the middle of an array will not shift the index of the ones
after them down--use splice() for that.  See L</exists>.

=end original

配列要素を削除した場合、配列の位置は初期の、初期化されていない状態になります。
引き続いて同じ要素に対して exists() でテストすると偽を返します。
配列の途中の配列要素を削除してもインデックスはシフトしないことに
注意してください -- このためには splice() を使ってください。
L</exists> を参照してください。

=begin original

The following (inefficiently) deletes all the values of %HASH and @ARRAY:

=end original

以下は、%HASH と @ARRAY のすべての値を(非効率的に)削除します:

    foreach $key (keys %HASH) {
	delete $HASH{$key};
    }

    foreach $index (0 .. $#ARRAY) {
	delete $ARRAY[$index];
    }

=begin original

And so do these:

=end original

そして以下のようにもできます:

    delete @HASH{keys %HASH};

    delete @ARRAY[0 .. $#ARRAY];

=begin original

But both of these are slower than just assigning the empty list
or undefining %HASH or @ARRAY:

=end original

しかし、これら二つは単に空リストを代入するか、%HASH や @ARRAY を
undef するより遅いです:

    %HASH = ();		# completely empty %HASH
    undef %HASH;	# forget %HASH ever existed

    @ARRAY = ();	# completely empty @ARRAY
    undef @ARRAY;	# forget @ARRAY ever existed

=begin original

Note that the EXPR can be arbitrarily complicated as long as the final
operation is a hash element, array element,  hash slice, or array slice
lookup:

=end original

最終的な操作がハッシュ要素、配列要素、ハッシュスライス、配列スライスの
いずれかである限りは、EXPR には任意の複雑な式を置くことができます:

    delete $ref->[$x][$y]{$key};
    delete @{$ref->[$x][$y]}{$key1, $key2, @morekeys};

    delete $ref->[$x][$y][$index];
    delete @{$ref->[$x][$y]}[$index1, $index2, @moreindices];

=item die LIST

=begin original

Outside an C<eval>, prints the value of LIST to C<STDERR> and
exits with the current value of C<$!> (errno).  If C<$!> is C<0>,
exits with the value of C<<< ($? >> 8) >>> (backtick `command`
status).  If C<<< ($? >> 8) >>> is C<0>, exits with C<255>.  Inside
an C<eval(),> the error message is stuffed into C<$@> and the
C<eval> is terminated with the undefined value.  This makes
C<die> the way to raise an exception.

=end original

C<eval> の外では、LIST の値を C<STDERR> に出力し、その時点の
C<$!> (errno) の値で exit します。
C<$!> の値が C<0> ならば、
C<<< ($? >> 8) >>> (backtick `command` のステータス) の値で exitします。
C<<< ($? >> 8) >>> も C<0> であれば、C<255> で exit することになります。
C<eval> の中で使用すると、エラーメッセージが、
C<$@> に入れられます。
C<eval> は中断され、未定義値を返します。
これが C<die> が例外を発生させる方法です。

=begin original

Equivalent examples:

=end original

等価な例:

    die "Can't cd to spool: $!\n" unless chdir '/usr/spool/news';
    chdir '/usr/spool/news' or die "Can't cd to spool: $!\n"

=begin original

If the value of EXPR does not end in a newline, the current script line
number and input line number (if any) are also printed, and a newline
is supplied.  Note that the "input line number" (also known as "chunk")
is subject to whatever notion of "line" happens to be currently in
effect, and is also available as the special variable C<$.>.
See L<perlvar/"$/"> and L<perlvar/"$.">.

=end original

EXPR の評価結果が改行で終わっていなければ、その時点の
スクリプト名とスクリプトの行番号、(もしあれば) 
入力ファイルの行番号と改行文字が、続けて表示されます。
「入力行番号」("chunk" とも呼ばれます)は
「行」という概念が現在有効であると仮定しています。
また特殊変数 C<$.> でも利用可能です。
L<perlvar/"$/"> と L<perlvar/"$."> も参照してください。

=begin original

Hint: sometimes appending C<", stopped"> to your message
will cause it to make better sense when the string C<"at foo line 123"> is
appended.  Suppose you are running script "canasta".

=end original

ヒント: メッセージの最後を C<", stopped"> のようなもので
終わるようにしておけば、C<"at foo line 123"> のように
追加されて、わかりやすくなります。
"canasta" というスクリプトを実行しているとすると、

    die "/etc/games is no good";
    die "/etc/games is no good, stopped";

=begin original

produce, respectively

=end original

は、それぞれ以下のように表示します。

    /etc/games is no good at canasta line 123.
    /etc/games is no good, stopped at canasta line 123.

=begin original

See also exit(), warn(), and the Carp module.

=end original

exit() と warn() と Carp モジュールも参照してください。

=begin original

If LIST is empty and C<$@> already contains a value (typically from a
previous eval) that value is reused after appending C<"\t...propagated">.
This is useful for propagating exceptions:

=end original

LIST が空で C<$@> が(典型的には前回の eval で)既に値を持っている場合、
値は C<"\t...propagated"> を追加した後再利用されます。
これは例外を伝播させる場合に有効です:

    eval { ... };
    die unless $@ =~ /Expected exception/;

=begin original

If C<$@> is empty then the string C<"Died"> is used.

=end original

C<$@> が空の場合、C<"Died"> が使われます。

=begin original

die() can also be called with a reference argument.  If this happens to be
trapped within an eval(), $@ contains the reference.  This behavior permits
a more elaborate exception handling implementation using objects that
maintain arbitrary state about the nature of the exception.  Such a scheme
is sometimes preferable to matching particular string values of $@ using
regular expressions.  Here's an example:

=end original

die() はリファレンス引数と共に呼び出すこともできます。
eval() 内部でこのように呼び出された場合、$@ はリファレンスを持ちます。
この振る舞いは、例外の性質にすいて任意の状態を管理するオブジェクトを使った
より複雑な例外ハンドリングの実装を可能にします。
このようなスキーマは $@ の特定の文字列値を正規表現を使って
マッチングするときに時々好まれます。以下に例を示します:

    eval { ... ; die Some::Module::Exception->new( FOO => "bar" ) };
    if ($@) {
        if (ref($@) && UNIVERSAL::isa($@,"Some::Module::Exception")) {
            # handle Some::Module::Exception
        }
        else {
            # handle all other possible exceptions
        }
    }

=begin original

Because perl will stringify uncaught exception messages before displaying
them, you may want to overload stringification operations on such custom
exception objects.  See L<overload> for details about that.

=end original

perl は捕らえられなかった例外のメッセージを表示する前に文字列化するので、
このようなカスタム例外オブジェクトの文字列化をオーバーロードしたいと
思うかもしれません。これに関する詳細は L<overload> を参照してください。

=begin original

You can arrange for a callback to be run just before the C<die>
does its deed, by setting the C<$SIG{__DIE__}> hook.  The associated
handler will be called with the error text and can change the error
message, if it sees fit, by calling C<die> again.  See
L<perlvar/$SIG{expr}> for details on setting C<%SIG> entries, and
L<"eval BLOCK"> for some examples.  Although this feature was meant
to be run only right before your program was to exit, this is not
currently the case--the C<$SIG{__DIE__}> hook is currently called
even inside eval()ed blocks/strings!  If one wants the hook to do
nothing in such situations, put

=end original

You can arrange for a callback to be run just before the C<die>
does its deed, by setting the C<$SIG{__DIE__}> hook.  The associated
handler will be called with the error text and can change the error
message, if it sees fit, by calling C<die> again.  See
L<perlvar/$SIG{expr}> for details on setting C<%SIG> entries, and
L<"eval BLOCK"> for some examples.  Although this feature was meant
to be run only right before your program was to exit, this is not
currently the case--the C<$SIG{__DIE__}> hook is currently called
even inside eval()ed blocks/strings!  If one wants the hook to do
nothing in such situations, put
(*TBT*)

	die @_ if $^S;

=begin original

as the first line of the handler (see L<perlvar/$^S>).  Because
this promotes strange action at a distance, this counterintuitive
behavior may be fixed in a future release.  

=end original

をハンドラの最初の行に置いてください(L<perlvar/$^S>参照)。
これは離れたところで不思議な行動を引き起こすので、
この直感的でない振る舞いは将来のリリースで修正されるかもしれません。

=item do BLOCK

=begin original

Not really a function.  Returns the value of the last command in the
sequence of commands indicated by BLOCK.  When modified by a loop
modifier, executes the BLOCK once before testing the loop condition.
(On other statements the loop modifiers test the conditional first.)

=end original

実際は関数ではありません。
BLOCK で示されるコマンド列の最後の値を返します。
ループ修飾子で修飾すると、
ループ条件を調べる前に 1 度、BLOCK を実行します。
(これ以外の実行文は、ループ修飾子により、条件が最初に
調べられます。)

=begin original

C<do BLOCK> does I<not> count as a loop, so the loop control statements
C<next>, C<last>, or C<redo> cannot be used to leave or restart the block.
See L<perlsyn> for alternative strategies.

=end original

C<do BLOCK> はループとしては I<扱われません>。
従って、C<next>, C<last>, C<redo> といったループ制御文は
ブロックから抜けたり再開することはできません。
その他の戦略については L<perlsyn> を参照して下さい。

=item do SUBROUTINE(LIST)

=begin original

A deprecated form of subroutine call.  See L<perlsub>.

=end original

推奨されない形のサブルーチン呼び出しです。L<perlsub>を
参照してください。

=item do EXPR

=begin original

Uses the value of EXPR as a filename and executes the contents of the
file as a Perl script.  Its primary use is to include subroutines
from a Perl subroutine library.

=end original

EXPR の値をファイル名として用い、そのファイルの中身を
Perl のスクリプトとして実行します。
主に、Perl のサブルーチンライブラリからサブルーチンを
インクルードするために用います。

    do 'stat.pl';

=begin original

is just like

=end original

は、

    scalar eval `cat stat.pl`;

=begin original

except that it's more efficient and concise, keeps track of the current
filename for error messages, searches the @INC libraries, and updates
C<%INC> if the file is found.  See L<perlvar/Predefined Names> for these
variables.  It also differs in that code evaluated with C<do FILENAME>
cannot see lexicals in the enclosing scope; C<eval STRING> does.  It's the
same, however, in that it does reparse the file every time you call it,
so you probably don't want to do this inside a loop.

=end original

と同じようなものですが、より効率的で、簡潔であり、
エラーメッセージでファイル名がわかる、
カレントディレクトリでファイルが見つからなかったときに
@INC ライブラリを検索する、ファイルがあったときに C<%INC> を更新する、
といったことがあります。
L<perlvar/Predefined Names> も参照してください。
C<do FILENAME> で評価されたコードは、入れ子のスコープにある
レキシカル変数を見ることができないのに対し、C<eval STRING>ではできる、
という違いがあります。
しかし、呼び出すたびにファイルを解析し直すという点では同じですから、
ループ内でこれを使おうなどとは、間違っても思ったりしないように。

=begin original

If C<do> cannot read the file, it returns undef and sets C<$!> to the
error.  If C<do> can read the file but cannot compile it, it
returns undef and sets an error message in C<$@>.   If the file is
successfully compiled, C<do> returns the value of the last expression
evaluated.

=end original

C<do>がファイルを読み込めなかった場合、undef を返して C<$!> に
エラーを設定します。C<do> がファイルを読み込めたがコンパイルできなかった場合、
undef を返して C<$@> にエラーメッセージを設定します。
ファイルのコンパイルに成功した場合、C<do> は最後に評価した表現の値を返します。

=begin original

Note that inclusion of library modules is better done with the
C<use> and C<require> operators, which also do automatic error checking
and raise an exception if there's a problem.

=end original

ライブラリモジュールのインクルードには、C<use> 演算子や
C<require> 演算子を使った方が良いでしょう。
これらは自動的にエラーをチェックして、問題があれば例外を発生させます。

=begin original

You might like to use C<do> to read in a program configuration
file.  Manual error checking can be done this way:

=end original

C<do> をプログラム設定ファイルを読み込むのに使いたいかもしれません。
手動のエラーチェックは以下のようにして行えます:

    # read in config files: system first, then user 
    for $file ("/share/prog/defaults.rc",
               "$ENV{HOME}/.someprogrc") 
   {
	unless ($return = do $file) {
	    warn "couldn't parse $file: $@" if $@;
	    warn "couldn't do $file: $!"    unless defined $return;
	    warn "couldn't run $file"       unless $return;
	}
    }

=item dump LABEL

=item dump

=begin original

This function causes an immediate core dump.  See also the B<-u>
command-line switch in L<perlrun>, which does the same thing.
Primarily this is so that you can use the B<undump> program (not
supplied) to turn your core dump into an executable binary after
having initialized all your variables at the beginning of the
program.  When the new binary is executed it will begin by executing
a C<goto LABEL> (with all the restrictions that C<goto> suffers).
Think of it as a goto with an intervening core dump and reincarnation.
If C<LABEL> is omitted, restarts the program from the top.

=end original

この関数は即座にコアダンプを行ないます。
同様のことを行う L<perlrun> の B<-u> オプションも参照してください。
プログラムの先頭で、
すべての変数を初期化したあとのコアダンプを B<undump>
プログラム(提供していません)を使って実行ファイルに返ることができます。
この新しいバイナリが実行されると、C<goto LABEL> から始めます
(C<goto> に関する制限はすべて適用されます)。
コアダンプをはさんで再生する goto と考えてください。
C<LABEL> が省略されると、プログラムを先頭から再開します。

=begin original

B<WARNING>: Any files opened at the time of the dump will I<not>
be open any more when the program is reincarnated, with possible
resulting confusion on the part of Perl.  

=end original

B<警告>: dump 時点でオープンされていたファイルは、
プログラムが再生されたときには、もはやオープンされていません。
Perl を部分的に混乱させる可能性があります。

=begin original

This function is now largely obsolete, partly because it's very
hard to convert a core file into an executable, and because the
real compiler backends for generating portable bytecode and compilable
C code have superseded it.

=end original

This function is now largely obsolete, partly because it's very
hard to convert a core file into an executable, and because the
real compiler backends for generating portable bytecode and compilable
C code have superseded it.
(*TBT*)

=begin original

If you're looking to use L<dump> to speed up your program, consider
generating bytecode or native C code as described in L<perlcc>.  If
you're just trying to accelerate a CGI script, consider using the
C<mod_perl> extension to B<Apache>, or the CPAN module, Fast::CGI.
You might also consider autoloading or selfloading, which at least
make your program I<appear> to run faster.  

=end original

If you're looking to use L<dump> to speed up your program, consider
generating bytecode or native C code as described in L<perlcc>.  If
you're just trying to accelerate a CGI script, consider using the
C<mod_perl> extension to B<Apache>, or the CPAN module, Fast::CGI.
You might also consider autoloading or selfloading, which at least
make your program I<appear> to run faster.  
(*TBT*)

=item each HASH

=begin original

When called in list context, returns a 2-element list consisting of the
key and value for the next element of a hash, so that you can iterate over
it.  When called in scalar context, returns only the key for the next
element in the hash.

=end original

リストコンテキストで呼び出した場合は、
ハッシュの次の 要素 に対する、key と 要素 からなる
2 要素のリストを返しますので、ハッシュ上での繰り返しを
行なうことができます。
スカラコンテキストで呼び出した場合は、
ハッシュの次の要素のための key を返します。

=begin original

Entries are returned in an apparently random order.  The actual random
order is subject to change in future versions of perl, but it is guaranteed
to be in the same order as either the C<keys> or C<values> function
would produce on the same (unmodified) hash.

=end original

エントリは見かけ上、ランダムな順序で返されます。
実際のランダムな順番は perl の将来のバージョンでは変わるかもしれませんが、
C<keys> や C<values> 関数が同じ(変更されていない)ハッシュに対して
生成するのと同じ順番であることは保証されます。

=begin original

When the hash is entirely read, a null array is returned in list context
(which when assigned produces a false (C<0>) value), and C<undef> in
scalar context.  The next call to C<each> after that will start iterating
again.  There is a single iterator for each hash, shared by all C<each>,
C<keys>, and C<values> function calls in the program; it can be reset by
reading all the elements from the hash, or by evaluating C<keys HASH> or
C<values HASH>.  If you add or delete elements of a hash while you're
iterating over it, you may get entries skipped or duplicated, so
don't.  Exception: It is always safe to delete the item most recently
returned by C<each()>, which means that the following code will work:

=end original

ハッシュをすべて読み込んでしまうと、リストコンテキストでは空配列が
返されます(これは代入されると、偽 (C<0>) となります)。
スカラコンテキストでは C<undef> が返されます。
そのあと、もう一度 C<each> を呼び出すと、
再び繰り返しを始めます。
ハッシュごとに反復子が 1 つあり、プログラム中のすべての
C<each> 関数、C<keys> 関数、C<values> 関数で共用されます。
反復子は、配列の要素をすべて読むことによって、またはC<keys HASH>,
C<values HASH>を評価することでリセットすることができます。
繰り返しを行なっている間に、ハッシュに要素を追加したり削除したりすると、
要素が飛ばされたり重複したりするので、してはいけません。
例外: 一番最近に C<each()> から返されたものを削除するのは常に安全です。
これは以下のようなコードが正しく動くことを意味します:

        while (($key, $value) = each %hash) {
          print $key, "\n";
          delete $hash{$key};   # This is safe
        }

=begin original

The following prints out your environment like the printenv(1) program,
only in a different order:

=end original

以下のプログラムは、順番が異なるものの、
printenv(1) プログラムのように環境変数を表示します:

    while (($key,$value) = each %ENV) {
	print "$key=$value\n";
    }

=begin original

See also C<keys>, C<values> and C<sort>.

=end original

C<keys> や C<values> や C<sort> も参照してください。

=item eof FILEHANDLE

=item eof ()

=item eof

=begin original

Returns 1 if the next read on FILEHANDLE will return end of file, or if
FILEHANDLE is not open.  FILEHANDLE may be an expression whose value
gives the real filehandle.  (Note that this function actually
reads a character and then C<ungetc>s it, so isn't very useful in an
interactive context.)  Do not read from a terminal file (or call
C<eof(FILEHANDLE)> on it) after end-of-file is reached.  File types such
as terminals may lose the end-of-file condition if you do.

=end original

次に FILEHANDLE 上で読み込みを行なったときに、 EOF
が返されるときか、FILEHANDLE がオープンされていないと、
1 を返します。
FILEHANDLE は、値が実際のファイルハンドルを示す式であってもかまいません。
(この関数は、実際に文字を読み、C<ungetc> を行ないますので、
対話型の場合には、それ程有用ではありません。)
端末ファイルは EOF に達した後にさらに読み込んだり C<eof(FILEHANDLE)> を
呼び出したりしてはいけません。
そのようなことをすると、端末のようなファイルタイプは
EOF 状態を失ってしまうかもしれません。

=begin original

An C<eof> without an argument uses the last file read.  Using C<eof()>
with empty parentheses is very different.  It refers to the pseudo file
formed from the files listed on the command line and accessed via the
C<< <> >> operator.  Since C<< <> >> isn't explicitly opened,
as a normal filehandle is, an C<eof()> before C<< <> >> has been
used will cause C<@ARGV> to be examined to determine if input is
available.

=end original

引数を省略した C<eof> は、最後に読み込みを行なったファイル
を使います。　空の括弧をつけた C<eof()> は大きく異なります。
これはコマンドラインのファイルリストで構成され、C<< <> >> 演算子経由で
アクセスされる擬似ファイルを示すために用いられます。
通常のファイルハンドルと違って C<< <> >> は明示的にオープンされないので、
C<< <> >> を使う前に C<eof()> を使うと、
入力が正常か確認するために C<@ARGV> がテストされます。

=begin original

In a C<< while (<>) >> loop, C<eof> or C<eof(ARGV)> can be used to
detect the end of each file, C<eof()> will only detect the end of the
last file.  Examples:

=end original

C< while (<>) >> ループの中では、
個々のファイルの終わりを調べるには、
C<eof>かC<eof(ARGV)>を用います。C<eof()>は最後のファイルの終わりのみを
調べます。例:

    # reset line numbering on each input file
    while (<>) {
	next if /^\s*#/;	# skip comments 
	print "$.\t$_";
    } continue {
	close ARGV  if eof;	# Not eof()!
    }

    # insert dashes just before last line of last file
    while (<>) {
	if (eof()) {		# check for end of current file
	    print "--------------\n";
	    close(ARGV);	# close or last; is needed if we
				# are reading from the terminal
	}
	print;
    }

=begin original

Practical hint: you almost never need to use C<eof> in Perl, because the
input operators typically return C<undef> when they run out of data, or if
there was an error.

=end original

現実的なヒント: Perl で C<eof> が必要となることは、ほとんどありません。
基本的には、
データがなくなったときやエラーがあったときに、入力演算子が
C<undef> を返してくれるからです。

=item eval EXPR

=item eval BLOCK

=begin original

In the first form, the return value of EXPR is parsed and executed as if it
were a little Perl program.  The value of the expression (which is itself
determined within scalar context) is first parsed, and if there weren't any
errors, executed in the lexical context of the current Perl program, so
that any variable settings or subroutine and format definitions remain
afterwards.  Note that the value is parsed every time the eval executes.
If EXPR is omitted, evaluates C<$_>.  This form is typically used to
delay parsing and subsequent execution of the text of EXPR until run time.

=end original

第一の形式では、EXPR の返り値が Perl のプログラムであるかのように、解析され、
実行されます。
式の値(それ自身スカラコンテキストの中で決定される)はまずパーズされ、エラーがなければ
Perl プログラムのレキシカルコンテキストの中で実行されますので、変数の設定、
サブルーチンやフォーマットの定義は、その後も残っています。　
返される値は eval が実行されるごとにパーズされます。
サブルーチン同様に、最後に評価した式の値か、return 文の値です。
EXPR を省略すると、C<$_> を評価します。
この形は主に
EXPR のテキストのパーズと実行を実行時にまで遅延させるのに用います。

=begin original

In the second form, the code within the BLOCK is parsed only once--at the
same time the code surrounding the eval itself was parsed--and executed
within the context of the current Perl program.  This form is typically
used to trap exceptions more efficiently than the first (see below), while
also providing the benefit of checking the code within BLOCK at compile
time.

=end original

第二の形式では、BLOCK 内部のコードは一度だけパースされ--コードを
囲む eval 自身がパースされるのと同じ時点です--現在の Perl プログラムの
コンテキストで実行されます。
この形式は典型的には第一の形式より効率的に例外をトラップします(後述)。
また BLOCK 内部のコードはコンパイル時にチェックされるという利点を提供します。

=begin original

The final semicolon, if any, may be omitted from the value of EXPR or within
the BLOCK.

=end original

最後のセミコロンは、もしあれば、EXPR の値や BLOCK の中身から省くことができます。

=begin original

In both forms, the value returned is the value of the last expression
evaluated inside the mini-program; a return statement may be also used, just
as with subroutines.  The expression providing the return value is evaluated
in void, scalar, or list context, depending on the context of the eval itself.
See L</wantarray> for more on how the evaluation context can be determined.

=end original

どちらの形式でも、返される値はミニプログラムの内部で最後に評価された
表現の値です; サブルーチンと同様、 return ステートメントも使えます。
返り値として提供される表現は、eval 自身のコンテキストに依存して
無効・スカラ・リストのいずれかのコンテキストで評価されます。
評価コンテキストの決定方法についての詳細は L</wantarray> を参照してください。

=begin original

If there is a syntax error or runtime error, or a C<die> statement is
executed, an undefined value is returned by C<eval>, and C<$@> is set to the
error message.  If there was no error, C<$@> is guaranteed to be a null
string.  Beware that using C<eval> neither silences perl from printing
warnings to STDERR, nor does it stuff the text of warning messages into C<$@>.
To do either of those, you have to use the C<$SIG{__WARN__}> facility.  See
L</warn> and L<perlvar>.

=end original

構文エラーや実行エラーが発生するか、C<die> 文が実行されると、
C<eval> の値として未定義値が返され、C<$@> にエラーメッセージが設定されます。
エラーがなければ、C<$@> は、空文字列であることが保証されます。　
C<eval> を、STDERR に警告メッセージを表示させない目的や、
警告メッセージを C<$@> に格納する目的では使わないでください。
そのような用途では、C<$SIG{__WARN__}> 機能を使わなければなりません。
L</warn> と L<perlvar> を参照してください。

=begin original

Note that, because C<eval> traps otherwise-fatal errors, it is useful for
determining whether a particular feature (such as C<socket> or C<symlink>)
is implemented.  It is also Perl's exception trapping mechanism, where
the die operator is used to raise exceptions.

=end original

C<eval> は、致命的エラーとなるようなものをトラップすることができますから、
(C<socket> や C<symlink> といった) 
特定の機能が実装されているかを、調べるために使うことができます。
die 演算子が例外を発生させるものとすれば、
これはまた、Perl の例外捕捉機能と捉えることもできます。

=begin original

If the code to be executed doesn't vary, you may use the eval-BLOCK
form to trap run-time errors without incurring the penalty of
recompiling each time.  The error, if any, is still returned in C<$@>.
Examples:

=end original

実行するコードが変わらないのであれば、毎回多量の再コンパイルすることなしに、
実行時エラーのトラップを行なうために、
eval-BLOCK 形式を使うことができます。
エラーがあれば、やはり $@ に返されます。例:

    # make divide-by-zero nonfatal
    eval { $answer = $a / $b; }; warn $@ if $@;

    # same thing, but less efficient
    eval '$answer = $a / $b'; warn $@ if $@;

    # a compile-time error
    eval { $answer = };			# WRONG

    # a run-time error
    eval '$answer =';	# sets $@

Due to the current arguably broken state of C<__DIE__> hooks, when using
the C<eval{}> form as an exception trap in libraries, you may wish not
to trigger any C<__DIE__> hooks that user code may have installed.
You can use the C<local $SIG{__DIE__}> construct for this purpose,
as shown in this example:

    # a very private exception trap for divide-by-zero
    eval { local $SIG{'__DIE__'}; $answer = $a / $b; };
    warn $@ if $@;

=begin original

This is especially significant, given that C<__DIE__> hooks can call
C<die> again, which has the effect of changing their error messages:

=end original

これは特に顕著です。与えられた C<__DIE__> フックは C<die> をもう一度
呼び出すことができ、これによってエラーメッセージを変える効果があります:

    # __DIE__ hooks may modify error messages
    {
       local $SIG{'__DIE__'} =
              sub { (my $x = $_[0]) =~ s/foo/bar/g; die $x };
       eval { die "foo lives here" };
       print $@ if $@;                # prints "bar lives here"
    }

=begin original
Because this promotes action at a distance, this counterintuitive behavior
may be fixed in a future release.

=end original

これは距離の離れた行動であるため、この直感的でない振る舞いは
将来のリリースでは修正されるかもしれません。

=begin original

With an C<eval>, you should be especially careful to remember what's
being looked at when:

=end original

C<eval> では、以下のような場合に、
何が調べられるかに特に注意しておくことが必要です:

    eval $x;		# CASE 1
    eval "$x";		# CASE 2

    eval '$x';		# CASE 3
    eval { $x };	# CASE 4

    eval "\$$x++";	# CASE 5
    $$x++;		# CASE 6

=begin original

Cases 1 and 2 above behave identically: they run the code contained in
the variable $x.  (Although case 2 has misleading double quotes making
the reader wonder what else might be happening (nothing is).)  Cases 3
and 4 likewise behave in the same way: they run the code C<'$x'>, which
does nothing but return the value of $x.  (Case 4 is preferred for
purely visual reasons, but it also has the advantage of compiling at
compile-time instead of at run-time.)  Case 5 is a place where
normally you I<would> like to use double quotes, except that in this
particular situation, you can just use symbolic references instead, as
in case 6.

=end original

上記の CASE 1 と CASE 2 の動作は同一で、変数 $x 内の
コードを実行します。
(ただし、CASE 2 では、必要のないダブルクォートによって、
読む人が何が起こるか混乱することでしょう (何も起こりませんが)。)
同様に CASE 3 と CASE 4 の動作も等しく、$x の値を返す以外に
何もしない C<$x> というコードを実行します
(純粋に見た目の問題で、CASE 4 が好まれますが、
実行時でなくコンパイル時にコンパイルされるという利点もあります)。
CASE 5 の場合は、通常ダブルクォートを使用します。
この状況を除けば、CASE 6 のように、単に
シンボリックリファレンスを使えば良いでしょう。

=begin original

C<eval BLOCK> does I<not> count as a loop, so the loop control statements
C<next>, C<last>, or C<redo> cannot be used to leave or restart the block.

=end original

C<eval BLOCK> はループとして I<扱われません>。
従って、C<next>, C<last>, C<redo> といったループ制御文でブロックから離れたり
再実行したりはできません。

=item exec LIST

=item exec PROGRAM LIST

=begin original

The C<exec> function executes a system command I<and never returns>--
use C<system> instead of C<exec> if you want it to return.  It fails and
returns false only if the command does not exist I<and> it is executed
directly instead of via your system's command shell (see below).

=end original

C<exec> 関数は、システムのコマンドを実行し、I<戻ってはきません>。
戻って欲しい場合には、C<exec>ではなく C<system> 関数を使ってください。
コマンドが存在せず、I<しかも> システムのコマンドシェル経由でなく
直接コマンドを実行しようとした場合にのみこの関数は失敗して偽を返します。

=begin original

Since it's a common mistake to use C<exec> instead of C<system>, Perl
warns you if there is a following statement which isn't C<die>, C<warn>,
or C<exit> (if C<-w> is set  -  but you always do that).   If you
I<really> want to follow an C<exec> with some other statement, you
can use one of these styles to avoid the warning:

=end original

C<system> の代わりに C<exec> を使うというよくある間違いを防ぐために、
引き続く文が C<die>, C<warn>, C<exit>(C<-w> がセットされている場合 -
でもいつもセットしてますよね) 以外の場合、Perl は警告を出します。
もし I<本当に> C<exec> の後に他の文を書きたい場合、
以下のどちらかのスタイルを使うことで警告を回避できます:

    exec ('foo')   or print STDERR "couldn't exec foo: $!";
    { exec ('foo') }; print STDERR "couldn't exec foo: $!";

=begin original

If there is more than one argument in LIST, or if LIST is an array
with more than one value, calls execvp(3) with the arguments in LIST.
If there is only one scalar argument or an array with one element in it,
the argument is checked for shell metacharacters, and if there are any,
the entire argument is passed to the system's command shell for parsing
(this is C</bin/sh -c> on Unix platforms, but varies on other platforms).
If there are no shell metacharacters in the argument, it is split into
words and passed directly to C<execvp>, which is more efficient.  
Examples:

=end original

LIST に複数の引数がある場合か、LIST が複数の値を持つ
配列の場合には、LIST の引数を使って、execvp(3) を呼び出します。
1 つのスカラ引数のみまたは要素が 1 つの配列の場合には、その引数から
シェルのメタ文字をチェックし、もし、メタ文字があれば、
引数全体をシステムのコマンドシェル(これはUnix では
C</bin/sh -c> ですが、システムによって異なります)に渡して解析させます。
もし、メタキャラクタがなければ、その引数を単語に分け、
より効率的な C<execvp> に直接渡します。例:

    exec '/bin/echo', 'Your arguments are: ', @ARGV;
    exec "sort $outfile | uniq";

=begin original

If you don't really want to execute the first argument, but want to lie
to the program you are executing about its own name, you can specify
the program you actually want to run as an "indirect object" (without a
comma) in front of the LIST.  (This always forces interpretation of the
LIST as a multivalued list, even if there is only a single scalar in
the list.)  Example:

=end original

第一引数に指定するものを本当に実行したいが、実行する
プログラムに対して別の名前を教えたい場合には、LISTの前に、
「間接オブジェクト」(コンマなし) として、実際に
実行したいプログラムを指定することができます。
(これによって、LIST に単一のスカラしかなくても、複数
値のリストであるように、LIST の解釈を行ないます。)
例:

    $shell = '/bin/csh';
    exec $shell '-sh';		# pretend it's a login shell

=begin original

or, more directly,

=end original

あるいは、より直接的に、

    exec {'/bin/csh'} '-sh';	# pretend it's a login shell

When the arguments get executed via the system shell, results will
be subject to its quirks and capabilities.  See L<perlop/"`STRING`">
for details.

Using an indirect object with C<exec> or C<system> is also more
secure.  This usage (which also works fine with system()) forces
interpretation of the arguments as a multivalued list, even if the
list had just one argument.  That way you're safe from the shell
expanding wildcards or splitting up words with whitespace in them.

    @args = ( "echo surprise" );

    exec @args;               # subject to shell escapes
                                # if @args == 1
    exec { $args[0] } @args;  # safe even with one-arg list

The first version, the one without the indirect object, ran the I<echo>
program, passing it C<"surprise"> an argument.  The second version
didn't--it tried to run a program literally called I<"echo surprise">,
didn't find it, and set C<$?> to a non-zero value indicating failure.

Beginning with v5.6.0, Perl will attempt to flush all files opened for
output before the exec, but this may not be supported on some platforms
(see L<perlport>).  To be safe, you may need to set C<$|> ($AUTOFLUSH
in English) or call the C<autoflush()> method of C<IO::Handle> on any
open handles in order to avoid lost output.

Note that C<exec> will not call your C<END> blocks, nor will it call
any C<DESTROY> methods in your objects.

=item exists EXPR

=begin original

Given an expression that specifies a hash element or array element,
returns true if the specified element in the hash or array has ever
been initialized, even if the corresponding value is undefined.  The
element is not autovivified if it doesn't exist.

=end original

ハッシュ要素か配列要素を示す表現が与えられ、
指定された要素が、ハッシュか配列に存在すれば、
たとえ対応する value が未定義でも真を返します。
要素が存在しなかった場合は自動活性化されません。

    print "Exists\n" 	if exists $hash{$key};
    print "Defined\n" 	if defined $hash{$key};
    print "True\n"      if $hash{$key};

    print "Exists\n" 	if exists $array[$index];
    print "Defined\n" 	if defined $array[$index];
    print "True\n"      if $array[$index];

=begin original

A hash or array element can be true only if it's defined, and defined if
it exists, but the reverse doesn't necessarily hold true.

=end original

ハッシュまたは配列要素は、定義されているときにのみ真となり、
存在しているときにのみ定義されますが、逆は必ずしも真で
はありません。

=begin original

Given an expression that specifies the name of a subroutine,
returns true if the specified subroutine has ever been declared, even
if it is undefined.  Mentioning a subroutine name for exists or defined
does not count as declaring it.  Note that a subroutine which does not
exist may still be callable: its package may have an C<AUTOLOAD>
method that makes it spring into existence the first time that it is
called -- see L<perlsub>.

=end original

引数としてサブルーチンの名前が指定された場合、
指定されたサブルーチンが宣言されていれば(たとえ未定義でも)
真を返します。
exists や defined のために言及されているサブルーチン名は
宣言としてのカウントに入りません。
存在しないサブルーチンでも呼び出し可能かもしれないことに注意してください:
パッケージが C<AUTOLOAD> メソッドを持っていて、最初に呼び出された時に
存在を作り出すかもしれません -- L<perlsub> を参照してください。

    print "Exists\n" 	if exists &subroutine;
    print "Defined\n" 	if defined &subroutine;

=begin original

Note that the EXPR can be arbitrarily complicated as long as the final
operation is a hash or array key lookup or subroutine name:

=end original

最終的な操作がハッシュや配列の key による検索またはサブルーチン名である限りは、
EXPR には任意の複雑な式を置くことができます:

    if (exists $ref->{A}->{B}->{$key}) 	{ }
    if (exists $hash{A}{B}{$key}) 	{ }

    if (exists $ref->{A}->{B}->[$ix]) 	{ }
    if (exists $hash{A}{B}[$ix]) 	{ }

    if (exists &{$ref->{A}{B}{$key}})   { }

=begin original

Although the deepest nested array or hash will not spring into existence
just because its existence was tested, any intervening ones will.
Thus C<< $ref->{"A"} >> and C<< $ref->{"A"}->{"B"} >> will spring
into existence due to the existence test for the $key element above.
This happens anywhere the arrow operator is used, including even:

=end original

Although the deepest nested array or hash will not spring into existence
just because its existence was tested, any intervening ones will.
Thus C<< $ref->{"A"} >> and C<< $ref->{"A"}->{"B"} >> will spring
into existence due to the existence test for the $key element above.
This happens anywhere the arrow operator is used, including even:
(*TBT*)

    undef $ref;
    if (exists $ref->{"Some key"})	{ }
    print $ref; 	    # prints HASH(0x80d3d5c)

This surprising autovivification in what does not at first--or even
second--glance appear to be an lvalue context may be fixed in a future
release.

See L<perlref/"Pseudo-hashes: Using an array as a hash"> for specifics
on how exists() acts when used on a pseudo-hash.

Use of a subroutine call, rather than a subroutine name, as an argument
to exists() is an error.

    exists &sub;	# OK
    exists &sub();	# Error

=item exit EXPR

=begin original

Evaluates EXPR and exits immediately with that value.    Example:

=end original

EXPR を評価し、即座にその値を持って終了します。例:

    $ans = <STDIN>;
    exit 0 if $ans =~ /^[Xx]/;

=begin original

See also C<die>.  If EXPR is omitted, exits with C<0> status.  The only
universally recognized values for EXPR are C<0> for success and C<1>
for error; other values are subject to interpretation depending on the
environment in which the Perl program is running.  For example, exiting
69 (EX_UNAVAILABLE) from a I<sendmail> incoming-mail filter will cause
the mailer to return the item undelivered, but that's not true everywhere.

=end original

C<die> も参照してください。　EXPR が省略された場合に
は、ステータスを C<0> として終了します。
EXPR の値として広く利用可能なのは C<0> が成功で C<1> がエラーということだけです。
その他の値は、 Perl が実行される環境によって異なる解釈がされる
可能性があります。
例えば、 I<sendmail> 到着メールフィルタから 69 (EX_UNAVAILABLE) で終了すると
メーラーはアイテムを配達せずに差し戻しますが、
これはいつでも真ではありません。

=begin original

Don't use C<exit> to abort a subroutine if there's any chance that
someone might want to trap whatever error happened.  Use C<die> instead,
which can be trapped by an C<eval>.

=end original

誰かが発生したエラーをトラップしようと考えている可能性がある場合は、
サブルーチンの中断に C<exit> を使わないでください。
代わりに C<eval> でトラップできる C<die> を使ってください。

=begin original

The exit() function does not always exit immediately.  It calls any
defined C<END> routines first, but these C<END> routines may not
themselves abort the exit.  Likewise any object destructors that need to
be called are called before the real exit.  If this is a problem, you
can call C<POSIX:_exit($status)> to avoid END and destructor processing.
See L<perlmod> for details.

=end original

exit()関数は常に直ちに終了するわけではありません。まず、
定義されている END ルーチンを呼び出しますが、
C<END> ルーチン自身は exit を止められません。
同様に、呼び出す必要のあるオブジェクトデストラクタは
すべて、実際の終了前に呼び出されます。
これが問題になる場合は、END やデストラクタが実行されることを
防ぐために C<POSIX:_exit($status)> を呼び出してください。
詳しくは L<perlmod> を参照してください。

=item exp EXPR

=item exp

=begin original

Returns I<e> (the natural logarithm base) to the power of EXPR.  
If EXPR is omitted, gives C<exp($_)>.

=end original

I<e> (自然対数の底) の EXPR 乗を返します。　
EXPR を省略した場合には、C<exp($_)> を返します。

=item fcntl FILEHANDLE,FUNCTION,SCALAR

=begin original

Implements the fcntl(2) function.  You'll probably have to say

    use Fcntl;

first to get the correct constant definitions.  Argument processing and
value return works just like C<ioctl> below.  
For example:

=end original

fcntl(2) 関数を実装します。　正しい定数定
義を得るために、まず、

    use Fcntl;

と書くことが必要でしょう。　引数の処理と返り値につい
ては、下記の C<ioctl> と同様に動作します。例:

    use Fcntl;
    fcntl($filehandle, F_GETFL, $packed_return_buffer)
	or die "can't fcntl F_GETFL: $!";

=begin original

You don't have to check for C<defined> on the return from C<fnctl>.
Like C<ioctl>, it maps a C<0> return from the system call into
C<"0 but true"> in Perl.  This string is true in boolean context and C<0>
in numeric context.  It is also exempt from the normal B<-w> warnings
on improper numeric conversions.

=end original

You don't have to check for C<defined> on the return from C<fnctl>.
Like C<ioctl>, it maps a C<0> return from the system call into
C<"0 but true"> in Perl.  This string is true in boolean context and C<0>
in numeric context.  It is also exempt from the normal B<-w> warnings
on improper numeric conversions.
(*TBT*)

=begin original

Note that C<fcntl> will produce a fatal error if used on a machine that
doesn't implement fcntl(2).  See the Fcntl module or your fcntl(2)
manpage to learn what functions are available on your system.

=end original

fcntl(2)が実装されていないマシンでは、
C<fcntl>は致命的エラーを引き起こします。
システムでどの関数が利用可能かについては Fcntl モジュールや
fcntl(2) man ページを参照してください。

=item fileno FILEHANDLE

=begin original

Returns the file descriptor for a filehandle, or undefined if the
filehandle is not open.  This is mainly useful for constructing
bitmaps for C<select> and low-level POSIX tty-handling operations.
If FILEHANDLE is an expression, the value is taken as an indirect
filehandle, generally its name.

=end original

ファイルハンドルに対するファイル記述子を返します。
ファイルハンドルがオープンしていない場合は未定義値を返します。
これは主に C<select>や低レベル POSIX tty 操作に対する、ビットマップを
構成するときに便利です。FILEHANDLE が式であれば、
その値が間接ファイルハンドル(普通は名前)として使われます。

=begin original

You can use this to find out whether two handles refer to the 
same underlying descriptor:

=end original

これを、二つのハンドルが同じ記述子を参照しているかどうかを
調べるのに使えます:

    if (fileno(THIS) == fileno(THAT)) {
	print "THIS and THAT are dups\n";
    } 

=item flock FILEHANDLE,OPERATION

=begin original

Calls flock(2), or an emulation of it, on FILEHANDLE.  Returns true
for success, false on failure.  Produces a fatal error if used on a
machine that doesn't implement flock(2), fcntl(2) locking, or lockf(3).
C<flock> is Perl's portable file locking interface, although it locks
only entire files, not records.

=end original

FILEHANDLE に対して flock(2)、またはそのエミュレーションを呼び出します。
成功時には真を、失敗時には偽を返します。
flock(2), fcntl(2) ロック, lockf(3) のいずれかを実装していない
マシンで使うと、致命的エラーが発生します。
C<flock> は Perl の移植性のあるファイルロックインターフェースです。
しかしレコードではなく、ファイル全体のみをロックします。

=begin original

Two potentially non-obvious but traditional C<flock> semantics are
that it waits indefinitely until the lock is granted, and that its locks
B<merely advisory>.  Such discretionary locks are more flexible, but offer
fewer guarantees.  This means that files locked with C<flock> may be
modified by programs that do not also use C<flock>.  See L<perlport>,
your port's specific documentation, or your system-specific local manpages
for details.  It's best to assume traditional behavior if you're writing
portable programs.  (But if you're not, you should as always feel perfectly
free to write for your own system's idiosyncrasies (sometimes called
"features").  Slavish adherence to portability concerns shouldn't get
in the way of your getting your job done.)

=end original

Two potentially non-obvious but traditional C<flock> semantics are
that it waits indefinitely until the lock is granted, and that its locks
B<merely advisory>.  Such discretionary locks are more flexible, but offer
fewer guarantees.  This means that files locked with C<flock> may be
modified by programs that do not also use C<flock>.  See L<perlport>,
your port's specific documentation, or your system-specific local manpages
for details.  It's best to assume traditional behavior if you're writing
portable programs.  (But if you're not, you should as always feel perfectly
free to write for your own system's idiosyncrasies (sometimes called
"features").  Slavish adherence to portability concerns shouldn't get
in the way of your getting your job done.)
(*TBT*)

OPERATION is one of LOCK_SH, LOCK_EX, or LOCK_UN, possibly combined with
LOCK_NB.  These constants are traditionally valued 1, 2, 8 and 4, but
you can use the symbolic names if you import them from the Fcntl module,
either individually, or as a group using the ':flock' tag.  LOCK_SH
requests a shared lock, LOCK_EX requests an exclusive lock, and LOCK_UN
releases a previously requested lock.  If LOCK_NB is bitwise-or'ed with
LOCK_SH or LOCK_EX then C<flock> will return immediately rather than blocking
waiting for the lock (check the return status to see if you got it).

To avoid the possibility of miscoordination, Perl now flushes FILEHANDLE
before locking or unlocking it.

Note that the emulation built with lockf(3) doesn't provide shared
locks, and it requires that FILEHANDLE be open with write intent.  These
are the semantics that lockf(3) implements.  Most if not all systems
implement lockf(3) in terms of fcntl(2) locking, though, so the
differing semantics shouldn't bite too many people.

Note also that some versions of C<flock> cannot lock things over the
network; you would need to use the more system-specific C<fcntl> for
that.  If you like you can force Perl to ignore your system's flock(2)
function, and so provide its own fcntl(2)-based emulation, by passing
the switch C<-Ud_flock> to the F<Configure> program when you configure
perl.

=begin original

Here's a mailbox appender for BSD systems.

=end original

BSD システムでのメールボックスへの追加処理の例を示します。

    use Fcntl ':flock'; # import LOCK_* constants

    sub lock {
	flock(MBOX,LOCK_EX);
	# and, in case someone appended
	# while we were waiting...
	seek(MBOX, 0, 2);
    }

    sub unlock {
	flock(MBOX,LOCK_UN);
    }

    open(MBOX, ">>/usr/spool/mail/$ENV{'USER'}")
	    or die "Can't open mailbox: $!";

    lock();
    print MBOX $msg,"\n\n";
    unlock();

=begin original

On systems that support a real flock(), locks are inherited across fork()
calls, whereas those that must resort to the more capricious fcntl()
function lose the locks, making it harder to write servers.

=end original

真の flock() に対応しているシステムではロックは fork() を通して
継承されるのに対して、より不安定な fcntl() に頼らなければならない場合、
サーバを書くのはより難しくなります。

=begin original

See also L<DB_File> for other flock() examples.

=end original

その他の flock() の例としては L<DB_File> も参照してください。

=item fork

=begin original

Does a fork(2) system call to create a new process running the
same program at the same point.  It returns the child pid to the
parent process, C<0> to the child process, or C<undef> if the fork is
unsuccessful.  File descriptors (and sometimes locks on those descriptors)
are shared, while everything else is copied.  On most systems supporting
fork(), great care has gone into making it extremely efficient (for
example, using copy-on-write technology on data pages), making it the
dominant paradigm for multitasking over the last few decades.

=end original

同じプログラムの同じ地点から開始する新しいプロセスを作成するために
システムコール fork(2) を行ないます。
親プロセスには、チャイルドプロセスの pid を、
チャイルドプロセスに C<0> を返しますが、
fork に失敗したときには、C<undef>を返します。
ファイル記述子(および記述子に関連するロック)は共有され、
その他の全てはコピーされます。
fork() に対応するほとんどのシステムでは、
これを極めて効率的にするために多大な努力が払われてきました
(例えば、データページへの copy-on-write テクノロジーなどです)。
これはここ 20 年にわたるマルチタスクに関する主要なパラダイムとなっています。

=begin original

Beginning with v5.6.0, Perl will attempt to flush all files opened for
output before forking the child process, but this may not be supported
on some platforms (see L<perlport>).  To be safe, you may need to set
C<$|> ($AUTOFLUSH in English) or call the C<autoflush()> method of
C<IO::Handle> on any open handles in order to avoid duplicate output.

=end original

v5.6.0 から、Perl は小プロセスを fork する前に出力用にオープンしている全ての
ファイルをフラッシュしようとします。
しかし、これは対応していないプラットフォームもあります(L<perlport> を参照してください)。
安全のためには、出力が重複するのを避けるためには、
全てのオープンしているハンドルに対して C<$|> (English モジュールでは
$AUTOFLUSH) を設定するか、
C<IO::Handle> モジュールの C<autoflush()>メソッドをを呼ぶ必要が
あるかもしれません。

=begin original

If you C<fork> without ever waiting on your children, you will
accumulate zombies.  On some systems, you can avoid this by setting
C<$SIG{CHLD}> to C<"IGNORE">.  See also L<perlipc> for more examples of
forking and reaping moribund children.

=end original

チャイルドプロセスの終了を待たずに、C<fork> を繰り返せば、
ゾンビをためこむことになります。
C<$SIG{CHLD}> に C<"IGNORE"> を指定することでこれを回避できるシステムもあります。
fork と消滅しかけている子プロセスを回収するための更なる例については
L<perlpc> も参照してください。

=begin original

Note that if your forked child inherits system file descriptors like
STDIN and STDOUT that are actually connected by a pipe or socket, even
if you exit, then the remote server (such as, say, a CGI script or a
backgrounded job launched from a remote shell) won't think you're done.
You should reopen those to F</dev/null> if it's any issue.

=end original

fork した子プロセスが STDIN や STDOUT といったシステムファイル記述子を
継承する場合、(CGI スクリプトやリモートシェルといった
バックグラウンドジョブのような)リモートサーバは考え通りに
動かないでしょう。このような場合では
これらを F</dev/null> として再オープンするべきです。

=item format

=begin original

Declare a picture format for use by the C<write> function.  For
example:

=end original

C<write> 関数で使うピクチャーフォーマットを宣言します。例:

    format Something =
	Test: @<<<<<<<< @||||| @>>>>>
	      $str,     $%,    '$' . int($num)
    .

    $str = "widget";
    $num = $cost/$quantity;
    $~ = 'Something';
    write;

=begin original

See L<perlform> for many details and examples.

=end original

詳細と例については L<perlform> を参照して下さい。

=item formline PICTURE,LIST

=begin original

This is an internal function used by C<format>s, though you may call it,
too.  It formats (see L<perlform>) a list of values according to the
contents of PICTURE, placing the output into the format output
accumulator, C<$^A> (or C<$ACCUMULATOR> in English).
Eventually, when a C<write> is done, the contents of
C<$^A> are written to some filehandle, but you could also read C<$^A>
yourself and then set C<$^A> back to C<"">.  Note that a format typically
does one C<formline> per line of form, but the C<formline> function itself
doesn't care how many newlines are embedded in the PICTURE.  This means
that the C<~> and C<~~> tokens will treat the entire PICTURE as a single line.
You may therefore need to use multiple formlines to implement a single
record format, just like the format compiler.

=end original

これは、C<format> が使用する内部関数ですが、直接呼び出すこともできます。
これは、PICTURE の内容にしたがって、
LIST の値を整形し (perlform manpage を参照してください)、
結果をフォーマット出力アキュムレータ
C<$^A>(English モジュールでは C<$ACCUMULATOR>) に納めます。
最終的に、C<write> が実行されると、C<$^A> の中身が、
何らかのファイルハンドルに書き出されますが、自分で C<$^A> を読んで、
C<$^A> の内容を C<""> に戻してもかまいません。
format は通常、1 行ごとに C<formline> を行ないますが、
C<formline> 関数自身は、PICTURE の中にいくつの改行が入っているかは、
関係がありません。
これは、C<~> と C<~~>トークンは PICTURE 全体を一行として扱うことを意味します。
従って、1 レコードフォーマットを実装するためには
フォーマットコンパイラのような複数 formline を使う必要があります。

=begin original

Be careful if you put double quotes around the picture, because an C<@>
character may be taken to mean the beginning of an array name.
C<formline> always returns true.  See L<perlform> for other examples.

=end original

ダブルクォートで PICTURE を囲む場合には、C<@> という文字が
配列名の始まりと解釈されますので、注意してください。
C<formline> は常に真を返します。
その他の例については L<perlform> を参照してください。

=item getc FILEHANDLE

=item getc

=begin original

Returns the next character from the input file attached to FILEHANDLE,
or the undefined value at end of file, or if there was an error.
If FILEHANDLE is omitted, reads from STDIN.  This is not particularly
efficient.  However, it cannot be used by itself to fetch single
characters without waiting for the user to hit enter.  For that, try
something more like:

=end original

FILEHANDLE につながれている入力ファイルから、次の一文字を返します。
ファイルの最後、またはエラーが発生した場合は、未定義値を返します。
FILEHANDLE が省略された場合には、STDIN から読み込みを行ないます。
これは特に効率的ではありません。
しかし、これはユーザーがリターンキーを押すのを待つことなく
一文字を読み込む用途には使えません。
そのような場合には、以下のようなものを試して見てください:

    if ($BSD_STYLE) {
	system "stty cbreak </dev/tty >/dev/tty 2>&1";
    }
    else {
	system "stty", '-icanon', 'eol', "\001";
    }

    $key = getc(STDIN);

    if ($BSD_STYLE) {
	system "stty -cbreak </dev/tty >/dev/tty 2>&1";
    }
    else {
	system "stty", 'icanon', 'eol', '^@'; # ASCII null
    }
    print "\n";

=begin original

Determination of whether $BSD_STYLE should be set
is left as an exercise to the reader.

=end original

$BSD_STYLE をセットするべきかどうかを決定する方法については
読者への宿題として残しておきます。

=begin original

The C<POSIX::getattr> function can do this more portably on
systems purporting POSIX compliance.  See also the C<Term::ReadKey>
module from your nearest CPAN site; details on CPAN can be found on
L<perlmodlib/CPAN>.

=end original

C<POSIX::getattr> 関数は POSIX 準拠を主張するシステムで
これをより移植性のある形で行います。お近くの CPAN サイトから
C<Term::ReadKey> モジュールも参照して下さい; CPAN に関する詳細は
L<perlmodlib/CPAN> にあります。

=item getlogin

=begin original

Implements the C library function of the same name, which on most
systems returns the current login from F</etc/utmp>, if any.  If null,
use C<getpwuid>.

=end original

同じ名前の C ライブラリ関数の実装です。
多くのシステムでは、もしあれば、/etc/utmp から現在のログイン名を返します。
ヌルであれば、getpwuid() を使ってください。

    $login = getlogin || getpwuid($<) || "Kilroy";

=begin original

Do not consider C<getlogin> for authentication: it is not as
secure as C<getpwuid>.

=end original

C<getlogin> を認証に使ってはいけません。これは C<getpwuid> のように
安全ではありません。

=item getpeername SOCKET

=begin original

Returns the packed sockaddr address of other end of the SOCKET connection.

=end original

SOCKET コネクションの向こう側のパックされた aockaddr アドレスを返します。

    use Socket;
    $hersockaddr    = getpeername(SOCK);
    ($port, $iaddr) = sockaddr_in($hersockaddr);
    $herhostname    = gethostbyaddr($iaddr, AF_INET);
    $herstraddr     = inet_ntoa($iaddr);

=item getpgrp PID

=begin original

Returns the current process group for the specified PID.  Use
a PID of C<0> to get the current process group for the
current process.  Will raise an exception if used on a machine that
doesn't implement getpgrp(2).  If PID is omitted, returns process
group of current process.  Note that the POSIX version of C<getpgrp>
does not accept a PID argument, so only C<PID==0> is truly portable.

=end original

指定された PID の現在のプロセスグループを返します。
PID に C<0> を与えるとカレントプロセスの指定となります。
getpgrp(2) を実装していないマシンで実行し
た場合には、例外が発生します。PID を省略する
とカレントプロセスのプロセスグループを返します。
POSIX 版の C<getpgrp> は PID 引数を受け付けないので、
C<PID==0> のみが完全に移植性があります。

=item getppid

=begin original

Returns the process id of the parent process.

=end original

親プロセスのプロセス id を返します。

=item getpriority WHICH,WHO

=begin original

Returns the current priority for a process, a process group, or a user.
(See L<getpriority(2)>.)  Will raise a fatal exception if used on a
machine that doesn't implement getpriority(2).

=end original

プロセス、プロセスグループ、ユーザに対する現在の優先度を返します。
(L<getpriority(2)> を参照してください。)
getpriority(2) を実装していない
マシンで実行した場合には、例外が発生します。

=item getpwnam NAME

=item getgrnam NAME

=item gethostbyname NAME

=item getnetbyname NAME

=item getprotobyname NAME

=item getpwuid UID

=item getgrgid GID

=item getservbyname NAME,PROTO

=item gethostbyaddr ADDR,ADDRTYPE

=item getnetbyaddr ADDR,ADDRTYPE

=item getprotobynumber NUMBER

=item getservbyport PORT,PROTO

=item getpwent

=item getgrent

=item gethostent

=item getnetent

=item getprotoent

=item getservent

=item setpwent

=item setgrent

=item sethostent STAYOPEN

=item setnetent STAYOPEN

=item setprotoent STAYOPEN

=item setservent STAYOPEN

=item endpwent

=item endgrent

=item endhostent

=item endnetent

=item endprotoent

=item endservent

=begin original

These routines perform the same functions as their counterparts in the
system library.  In list context, the return values from the
various get routines are as follows:

=end original

これらのルーチンは、システムライブラリの同名の関数を実行します。
リストコンテキストでは、さまざまな
get ルーチンからの返り値は、次のようになります:

    ($name,$passwd,$uid,$gid,
       $quota,$comment,$gcos,$dir,$shell,$expire) = getpw*
    ($name,$passwd,$gid,$members) = getgr*
    ($name,$aliases,$addrtype,$length,@addrs) = gethost*
    ($name,$aliases,$addrtype,$net) = getnet*
    ($name,$aliases,$proto) = getproto*
    ($name,$aliases,$port,$proto) = getserv*

=begin original

(If the entry doesn't exist you get a null list.)

=end original

(エントリが存在しなければ、空リストが返されます。)

The exact meaning of the $gcos field varies but it usually contains
the real name of the user (as opposed to the login name) and other
information pertaining to the user.  Beware, however, that in many
system users are able to change this information and therefore it
cannot be trusted and therefore the $gcos is tainted (see
L<perlsec>).  The $passwd and $shell, user's encrypted password and
login shell, are also tainted, because of the same reason.

=begin original

In scalar context, you get the name, unless the function was a
lookup by name, in which case you get the other thing, whatever it is.
(If the entry doesn't exist you get the undefined value.)  For example:

=end original

スカラコンテキストでは、*nam、*byname といった NAME
で検索するもの以外は、name を返し、NAME で検索するものは、
何か別のものを返します。
(エントリが存在しなければ、未定義値が返ります。)たとえば:

    $uid   = getpwnam($name);
    $name  = getpwuid($num);
    $name  = getpwent();
    $gid   = getgrnam($name);
    $name  = getgrgid($num;
    $name  = getgrent();
    #etc.

In I<getpw*()> the fields $quota, $comment, and $expire are special
cases in the sense that in many systems they are unsupported.  If the
$quota is unsupported, it is an empty scalar.  If it is supported, it
usually encodes the disk quota.  If the $comment field is unsupported,
it is an empty scalar.  If it is supported it usually encodes some
administrative comment about the user.  In some systems the $quota
field may be $change or $age, fields that have to do with password
aging.  In some systems the $comment field may be $class.  The $expire
field, if present, encodes the expiration period of the account or the
password.  For the availability and the exact meaning of these fields
in your system, please consult your getpwnam(3) documentation and your
F<pwd.h> file.  You can also find out from within Perl what your
$quota and $comment fields mean and whether you have the $expire field
by using the C<Config> module and the values C<d_pwquota>, C<d_pwage>,
C<d_pwchange>, C<d_pwcomment>, and C<d_pwexpire>.  Shadow password
files are only supported if your vendor has implemented them in the
intuitive fashion that calling the regular C library routines gets the
shadow versions if you're running under privilege or if there exists
the shadow(3) functions as found in System V ( this includes Solaris
and Linux.)  Those systems which implement a proprietary shadow password
facility are unlikely to be supported.

=begin original

The $members value returned by I<getgr*()> is a space separated list of
the login names of the members of the group.

=end original

I<getgr*()> によって返る値 $members は、グループのメンバの
ログイン名をスペースで区切ったものです。

=begin original

For the I<gethost*()> functions, if the C<h_errno> variable is supported in
C, it will be returned to you via C<$?> if the function call fails.  The
C<@addrs> value returned by a successful call is a list of the raw
addresses returned by the corresponding system library call.  In the
Internet domain, each address is four bytes long and you can unpack it
by saying something like:

    ($a,$b,$c,$d) = unpack('C4',$addr[0]);

=end original

I<gethost*()> 関数では、C で C<h_errno> 変数がサポートされていれば、
関数呼出が失敗したときに、C<$?> を通して、その値が返されます。
成功時に返される C<@addrs> 値は、対応するシステムコールが返す、
生のアドレスのリストです。
インターネットドメインでは、個々のアドレスは、4 バイト長で、
以下のようにして unpack することができます。

    ($a,$b,$c,$d) = unpack('C4',$addr[0]);

=begin original

The Socket library makes this slightly easier:

=end original

Socket ライブラリを使うともう少し簡単になります。

    use Socket;
    $iaddr = inet_aton("127.1"); # or whatever address
    $name  = gethostbyaddr($iaddr, AF_INET);

    # or going the other way
    $straddr = inet_ntoa($iaddr);

=begin original

If you get tired of remembering which element of the return list
contains which return value, by-name interfaces are provided
in standard modules: C<File::stat>, C<Net::hostent>, C<Net::netent>,
C<Net::protoent>, C<Net::servent>, C<Time::gmtime>, C<Time::localtime>,
and C<User::grent>.  These override the normal built-ins, supplying
versions that return objects with the appropriate names
for each field.  For example:

=end original

返り値のリストの何番目がどの要素かを覚えるのに疲れたなら、
名前ベースのインターフェースが標準モジュールで提供されています:
C<File::stat>, C<Net::hostent>, C<Net::netent>,
C<Net::protoent>, C<Net::servent>, C<Time::gmtime>, C<Time::localtime>,
C<User::grent> です。これらは通常の組み込みを上書きし、
それぞれのフィールドに適切な名前をつけたオブジェクトを返します。
例:

   use File::stat;
   use User::pwent;
   $is_his = (stat($filename)->uid == pwent($whoever)->uid);

=begin original

Even though it looks like they're the same method calls (uid), 
they aren't, because a C<File::stat> object is different from 
a C<User::pwent> object.

=end original

同じメソッド(uid)を呼び出しているように見えますが、違います。
なぜなら C<File::stat> オブジェクトは C<User::pwent> オブジェクトとは
異なるからです。

=item getsockname SOCKET

=begin original

Returns the packed sockaddr address of this end of the SOCKET connection,
in case you don't know the address because you have several different
IPs that the connection might have come in on.

=end original

SOCKET 接続のこちら側の pack された sockaddr アドレスを返します。
複数の異なる IP から接続されるためにアドレスがわからない場合に使います。

    use Socket;
    $mysockaddr = getsockname(SOCK);
    ($port, $myaddr) = sockaddr_in($mysockaddr);
    printf "Connect to %s [%s]\n", 
       scalar gethostbyaddr($myaddr, AF_INET),
       inet_ntoa($myaddr);

=item getsockopt SOCKET,LEVEL,OPTNAME

=begin original

Returns the socket option requested, or undef if there is an error.

=end original

要求されたソケットオプションを返します。
エラーの場合には、undef を返します。

=item glob EXPR

=item glob

=begin original

Returns the value of EXPR with filename expansions such as the
standard Unix shell F</bin/csh> would do.  This is the internal function
implementing the C<< <*.c> >> operator, but you can use it directly.
If EXPR is omitted, C<$_> is used.  The C<< <*.c> >> operator is
discussed in more detail in L<perlop/"I/O Operators">.

=end original

EXPR の値を、標準 Unix シェル F</bin/csh> が行なうように、
ファイル名の展開を行なって返します。
これは、C<< <*.c> >> 演算子を実装する内部関数ですが、
直接使用することもできます。
EXPR を省略すると、C<$_>が使われます。
C<< <*.c> >>演算子については
L<perlop/"I/O Operators"> でより詳細に議論しています。

=begin original

Beginning with v5.6.0, this operator is implemented using the standard
C<File::Glob> extension.  See L<File::Glob> for details.

=end original

v5.6.0 から、この演算子は標準の C<File::Glob> 拡張を使って
実装されています。詳細は L<File::Glob> を参照して下さい。

=item gmtime EXPR

=begin original

Converts a time as returned by the time function to a 8-element list
with the time localized for the standard Greenwich time zone.
Typically used as follows:

=end original

time 関数が返す時刻を、グリニッジタイムゾーンで測った時刻として、
8 要素のリストに変換します。
通常は、以下のようにして使用します。

    #  0    1    2     3     4    5     6     7  
    ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday) =
					    gmtime(time);

=begin original

All list elements are numeric, and come straight out of the C `struct
tm'.  $sec, $min, and $hour are the seconds, minutes, and hours of the
specified time.  $mday is the day of the month, and $mon is the month
itself, in the range C<0..11> with 0 indicating January and 11
indicating December.  $year is the number of years since 1900.  That
is, $year is C<123> in year 2023.  $wday is the day of the week, with
0 indicating Sunday and 3 indicating Wednesday.  $yday is the day of
the year, in the range C<0..364> (or C<0..365> in leap years.)  

=end original

すべてのリスト要素は数値で、C の `struct tm'  構造体から
直接持ってきます。
$sec, $min, $hour は指定された時刻の秒、分、時です。
$mday は月の何日目か、$mon は月の値です。
月の値は C<0..11> で、0 が 1 月、11 が 12 月です。
$year は 1900 年からの年数です。
つまり、$year が C<123> なら 2023 年です。
$wday は曜日で、0 が日曜日、3 が木曜日です。
$yday はその年の何日目かで、C<0..364> の値を取ります
(うるう年は C<0..365> です)。

=begin original

Note that the $year element is I<not> simply the last two digits of
the year.  If you assume it is, then you create non-Y2K-compliant
programs--and you wouldn't want to do that, would you?

=end original

$year 要素は単純に年の下 2 桁を返す I<のではない> ことに注意してください。
もしそう仮定してしまうと、Y2K 準拠でないプログラムを作ってしまいます--
そうはしたくないでしょう?

=begin original

The proper way to get a complete 4-digit year is simply:

=end original

完全な 4 桁の年を得る正しい方法は単純です:

	$year += 1900;

=begin original

And to get the last two digits of the year (e.g., '01' in 2001) do:

=end original

そして年の下 2 桁(2001 年なら '01')を得るには次のようにします:

	$year = sprintf("%02d", $year % 100);

=begin original

If EXPR is omitted, C<gmtime()> uses the current time (C<gmtime(time)>).

=end original

EXPR が省略されると、C<gmtime()> は現在の時刻を使います (C<gmtime(time)>)。

In scalar context, C<gmtime()> returns the ctime(3) value:

    $now_string = gmtime;  # e.g., "Thu Oct 13 04:54:34 1994"

=begin original

Also see the C<timegm> function provided by the C<Time::Local> module,
and the strftime(3) function available via the POSIX module.

=end original

C<Time::Local> で提供されている C<timegm> 関数も参照して下さい。
また、POSIX モジュールで strftime(3) 関数が利用可能です。

This scalar value is B<not> locale dependent (see L<perllocale>), but
is instead a Perl builtin.  Also see the C<Time::Local> module, and the
strftime(3) and mktime(3) functions available via the POSIX module.  To
get somewhat similar but locale dependent date strings, set up your
locale environment variables appropriately (please see L<perllocale>)
and try for example:

    use POSIX qw(strftime);
    $now_string = strftime "%a %b %e %H:%M:%S %Y", gmtime;

=begin original

Note that the C<%a> and C<%b> escapes, which represent the short forms
of the day of the week and the month of the year, may not necessarily
be three characters wide in all locales.

=end original

C<%a> と C<%b>、つまり曜日と月の短い表現は全てのロケールで 3 文字であるとは
限らないことに注意してください。

=item goto LABEL

=item goto EXPR

=item goto &NAME

=begin original

The C<goto-LABEL> form finds the statement labeled with LABEL and resumes
execution there.  It may not be used to go into any construct that
requires initialization, such as a subroutine or a C<foreach> loop.  It
also can't be used to go into a construct that is optimized away,
or to get out of a block or subroutine given to C<sort>.
It can be used to go almost anywhere else within the dynamic scope,
including out of subroutines, but it's usually better to use some other
construct such as C<last> or C<die>.  The author of Perl has never felt the
need to use this form of C<goto> (in Perl, that is--C is another matter).

=end original

C<goto-LABEL> の形式は、LABEL というラベルの付いた文を
探して、そこへ実行を移すものです。　サブルーチンや
C<foreach> ループなど、何らかの初期化が必要な構造の中に
入り込むことは許されません。　最適化によってなくなってしまう構造の中にも
goto することはできません。
また、C<sort>で与えられたブロックやサブルーチンから外へ出ることもできません。
これ以外は、サブルーチンの外を含む、動的スコープ内の
ほとんどすべての場所へ行くために使用できますが、普通は、
C<last> や C<die> といった別の構造を使った方が良いでしょう。
Perl の作者はこの形式の C<goto> を使う必要を感じたことは、
1 度もありません (Perl では。C は別のお話です)。

=begin original

The C<goto-EXPR> form expects a label name, whose scope will be resolved
dynamically.  This allows for computed C<goto>s per FORTRAN, but isn't
necessarily recommended if you're optimizing for maintainability:

=end original

C<goto-EXPR> の形式はラベル名を予測し、このスコープは動的に解決されます。
これにより FORTRAN のような算術 C<goto> が可能になりますが、
保守性を重視するならお勧めしません。

    goto ("FOO", "BAR", "GLARCH")[$i];

=begin original

The C<goto-&NAME> form is quite different from the other forms of C<goto>.
In fact, it isn't a goto in the normal sense at all, and doesn't have
the stigma associated with other gotos.  Instead, it
substitutes a call to the named subroutine for the currently running
subroutine.  This is used by C<AUTOLOAD> subroutines that wish to load
another subroutine and then pretend that the other subroutine had been
called in the first place (except that any modifications to C<@_>
in the current subroutine are propagated to the other subroutine.)
After the C<goto>, not even C<caller> will be able to tell that this
routine was called first.

=end original

C<goto-&NAME> の形式は、その他の C<goto> の形式とはかなり
異なったものです。
実際、これは普通の感覚でいうところのどこかへ行くものでは全くなく、
他の goto が持つ不名誉を持っていません。これは、実行中の
サブルーチンを、NAME で指定されたサブルーチンの呼び出しで置き換えます。
これは、C<AUTOLOAD> サブルーチンが別のサブルーチンをロードして、
その別のサブルーチンが最初に呼ばれたようにするために使われます
(ただし、現在のサブルーチンで C<@_> を修正した場合には、
その別のサブルーチンに伝えられます)。
C<goto> のあとは、C<caller> でさえも、現在のサブルーチンが
最初に呼び出されたと言うことができません。

=begin original

NAME needn't be the name of a subroutine; it can be a scalar variable
containing a code reference, or a block which evaluates to a code
reference.

=end original

NAME はサブルーチンの名前である必要はありません; コードリファレンスを
含むスカラ値や、コードリファレンスと評価されるブロックでも構いません。

=item grep BLOCK LIST

=item grep EXPR,LIST

=begin original

This is similar in spirit to, but not the same as, grep(1) and its
relatives.  In particular, it is not limited to using regular expressions.

=end original

これは grep(1) とその親類と同じようなものですが、同じではありません。
特に、正規表現の使用に制限されません。

=begin original

Evaluates the BLOCK or EXPR for each element of LIST (locally setting
C<$_> to each element) and returns the list value consisting of those
elements for which the expression evaluated to true.  In scalar
context, returns the number of times the expression was true.

=end original

LIST の個々の要素に対して、BLOCK か EXPR を評価し
(C<$_> は、ローカルに個々の要素が設定されます) 、
その要素のうち、評価した式が真となったものからなるリスト値が返されます。
スカラコンテキストでは、式が真となった回数を返します。　例:

    @foo = grep(!/^#/, @bar);    # weed out comments

=begin original

or equivalently,

=end original

あるいは等価な例として:

    @foo = grep {!/^#/} @bar;    # weed out comments

=begin original

Note that C<$_> is an alias to the list value, so it can be used to
modify the elements of the LIST.  While this is useful and supported,
it can cause bizarre results if the elements of LIST are not variables.
Similarly, grep returns aliases into the original list, much as a for
loop's index variable aliases the list elements.  That is, modifying an
element of a list returned by grep (for example, in a C<foreach>, C<map>
or another C<grep>) actually modifies the element in the original list.
This is usually something to be avoided when writing clear code.

=end original

C<$_> は、LIST の値へのエイリアスですので、LIST の要素を
変更するために使うことができます。
これは、便利でサポートされていますが、
LIST の要素が変数でないと、悲惨な結果になります。
同様に、grep は元のリストへのエイリアスを返します。
for ループのインデックス変数がリスト要素のエイリアスであるのと
同様です。
つまり、grep で返されたリストの要素を
(C<foreach>, C<map>, または他の C<grep> で)修正すると
元のリストの要素が変更されます。
これはきれいなコードを書こうとする邪魔になることが多いです。

See also L</map> for a list composed of the results of the BLOCK or EXPR.

=item hex EXPR

=item hex

=begin original

Interprets EXPR as a hex string and returns the corresponding value.
(To convert strings that might start with either 0, 0x, or 0b, see
L</oct>.)  If EXPR is omitted, uses C<$_>.

=end original

EXPR を 16 進数の文字列と解釈して、対応する値を返します。
(0, 0x, 0b で始まる文字列の変換には、L</oct> を
参照してください。)
EXPR が省略されると、C<$_> を使用します。

    print hex '0xAf'; # prints '175'
    print hex 'aF';   # same

=begin original

Hex strings may only represent integers.  Strings that would cause
integer overflow trigger a warning.

=end original

16 進文字列は整数のみを表現します。整数オーバーフローを起こすような
文字列は警告を引き起こします。

=item import

=begin original

There is no builtin C<import> function.  It is just an ordinary
method (subroutine) defined (or inherited) by modules that wish to export
names to another module.  The C<use> function calls the C<import> method
for the package used.  See also L</use>, L<perlmod>, and L<Exporter>.

=end original

組み込みの C<import> 関数というものはありません。
これは単に、別のモジュールに名前をエクスポートしたいモジュールが
定義した(継承した)、通常のメソッド(サブルーチン)です。
C<use> 関数は、使用されるパッケージのために、C<import>
メソッドを呼び出します。L</use>, L<perlmod>, L<Exporter>を
参照してください。

=item index STR,SUBSTR,POSITION

=item index STR,SUBSTR

=begin original

The index function searches for one string within another, but without
the wildcard-like behavior of a full regular-expression pattern match.
It returns the position of the first occurrence of SUBSTR in STR at
or after POSITION.  If POSITION is omitted, starts searching from the
beginning of the string.  The return value is based at C<0> (or whatever
you've set the C<$[> variable to--but don't do that).  If the substring
is not found, returns one less than the base, ordinarily C<-1>.

=end original

index 関数は ある文字列をもうひとつの文字列から検索しますが、
完全正規表現パターンマッチのワイルドカード的な振る舞いはしません。
STR の中の POSITION の位置以降で、最初に SUBSTR が見つかった位置を返します。
POSITION が省略された場合には、STR の最初から探し始めます。
返り値のベースは、C<0> (もしくは、変数 C<$[> に設定した値です--が、
これは使ってはいけません)。
SURSTR が見つからなかった場合には、ベースよりも 1 小さい値、
通常は C<-1> が返されます。

=item int EXPR

=item int

=begin original

Returns the integer portion of EXPR.  If EXPR is omitted, uses C<$_>.
You should not use this function for rounding: one because it truncates
towards C<0>, and two because machine representations of floating point
numbers can sometimes produce counterintuitive results.  For example,
C<int(-6.725/0.025)> produces -268 rather than the correct -269; that's
because it's really more like -268.99999999999994315658 instead.  Usually,
the C<sprintf>, C<printf>, or the C<POSIX::floor> and C<POSIX::ceil>
functions will serve you better than will int().

=end original

EXPR の整数部を返します。　EXPR を省略すると、C<$_> を
使います。この関数を丸めのために使うべきではありません。
第一の理由として C<0> の方向への切捨てを行うから、第二の理由として
浮動小数点数の機械表現は時々直感に反した結果を生み出すからです。
たとえば、C<int(-6.725/0.025)> は正しい結果である -269 ではなく
-268 を返します。
これは実際には -268.99999999999994315658 というような値になっているからです。
通常、C<sprintf>, C<printf>, C<POSIX::floor>, C<POSIX::ceil> の方が
int() より便利です。

=item ioctl FILEHANDLE,FUNCTION,SCALAR

=begin original

Implements the ioctl(2) function.  You'll probably first have to say

=end original

ioctl(2) 関数を実装します。　正しい関数の
定義を得るために、おそらく最初に

    require "ioctl.ph";	# probably in /usr/local/lib/perl/ioctl.ph

=begin original

to get the correct function definitions.  If F<ioctl.ph> doesn't
exist or doesn't have the correct definitions you'll have to roll your
own, based on your C header files such as F<< <sys/ioctl.h> >>.
(There is a Perl script called B<h2ph> that comes with the Perl kit that
may help you in this, but it's nontrivial.)  SCALAR will be read and/or
written depending on the FUNCTION--a pointer to the string value of SCALAR
will be passed as the third argument of the actual C<ioctl> call.  (If SCALAR
has no string value but does have a numeric value, that value will be
passed rather than a pointer to the string value.  To guarantee this to be
true, add a C<0> to the scalar before using it.)  The C<pack> and C<unpack>
functions may be needed to manipulate the values of structures used by
C<ioctl>.  

=end original

としなくてはならないでしょう。
ioctl.h がないか、間違った定義をしている場合には、
F<< <sys/ioctl.ph> >>のような C のヘッダファイルをもとに、
自分で作らなければなりません。
(Perl の配布キットに入っている B<h2ph> という
Perl スクリプトがこれを手助けしてくれるでしょうが、これは重要です。)
FOUNCTION に応じて SCALAR が読み書きされます。
SCALAR の文字列値へのポインタが、実際の C<ioctl> コールの
3 番目の引数として渡されます。　(SCALAR が文字列値を
持っておらず、数値を持っている場合には、文字列値へのポインタの代わりに、
その値が渡されます。このことを保証するためには、使用する前に SCALAR に
C<0> を足してください。)
C<ioctl> で使われる構造体の値を操作するには、
C<pack> 関数と C<unpack> 関数が必要となるでしょう。

=begin original

The return value of C<ioctl> (and C<fcntl>) is as follows:

=end original

C<ioctl> (と C<fcntl>) の返り値は、以下のようになります:

	if OS returns:		then Perl returns:
	    -1	  		  undefined value
	     0	 		string "0 but true"
	anything else		    that number

=begin original

Thus Perl returns true on success and false on failure, yet you can
still easily determine the actual value returned by the operating
system:

=end original

つまり Perl は、成功時に「真」、失敗時に「偽」を返す
ことになり、OS が実際に返した値も、以下のように簡単に知ることができます。

    $retval = ioctl(...) || -1;
    printf "System returned %d\n", $retval;

The special string "C<0> but true" is exempt from B<-w> complaints
about improper numeric conversions.

Here's an example of setting a filehandle named C<REMOTE> to be
non-blocking at the system level.  You'll have to negotiate C<$|>
on your own, though.

    use Fcntl qw(F_GETFL F_SETFL O_NONBLOCK);

    $flags = fcntl(REMOTE, F_GETFL, 0)
                or die "Can't get flags for the socket: $!\n";

    $flags = fcntl(REMOTE, F_SETFL, $flags | O_NONBLOCK)
                or die "Can't set flags for the socket: $!\n";

=item join EXPR,LIST

=begin original

Joins the separate strings of LIST into a single string with fields
separated by the value of EXPR, and returns that new string.  Example:

=end original

LIST の個別の文字列を、EXPR の値で区切って
1 つの文字列につなげ、その文字列を返します。例:

    $rec = join(':', $login,$passwd,$uid,$gid,$gcos,$home,$shell);

Beware that unlike C<split>, C<join> doesn't take a pattern as its
first argument.  Compare L</split>.

=item keys HASH

=begin original

Returns a list consisting of all the keys of the named hash.  (In
scalar context, returns the number of keys.)  The keys are returned in
an apparently random order.  The actual random order is subject to
change in future versions of perl, but it is guaranteed to be the same
order as either the C<values> or C<each> function produces (given
that the hash has not been modified).  As a side effect, it resets
HASH's iterator.

=end original

指定したハッシュのすべての key からなる、リストを返します。
(スカラコンテキストでは、key の数を返します。)
実際のランダムな順番は perl の将来のバージョンでは変わるかもしれませんが、
C<values> や C<each> 関数が同じ(変更されていない)ハッシュに対して
生成するのと同じ順番であることは保証されます。
副作用として、HASH の反復子を初期化します。

=begin original

Here is yet another way to print your environment:

=end original

環境変数
を表示する別の例です:

    @keys = keys %ENV;
    @values = values %ENV;
    while (@keys) { 
	print pop(@keys), '=', pop(@values), "\n";
    }

=begin original

or how about sorted by key:

=end original

key でソートしてもいいでしょう:

    foreach $key (sort(keys %ENV)) {
	print $key, '=', $ENV{$key}, "\n";
    }

The returned values are copies of the original keys in the hash, so
modifying them will not affect the original hash.  Compare L</values>.

To sort a hash by value, you'll need to use a C<sort> function.
Here's a descending numeric sort of a hash by its values:

    foreach $key (sort { $hash{$b} <=> $hash{$a} } keys %hash) {
	printf "%4d %s\n", $hash{$key}, $key;
    }

As an lvalue C<keys> allows you to increase the number of hash buckets
allocated for the given hash.  This can gain you a measure of efficiency if
you know the hash is going to get big.  (This is similar to pre-extending
an array by assigning a larger number to $#array.)  If you say

    keys %hash = 200;

then C<%hash> will have at least 200 buckets allocated for it--256 of them,
in fact, since it rounds up to the next power of two.  These
buckets will be retained even if you do C<%hash = ()>, use C<undef
%hash> if you want to free the storage while C<%hash> is still in scope.
You can't shrink the number of buckets allocated for the hash using
C<keys> in this way (but you needn't worry about doing this by accident,
as trying has no effect).

See also C<each>, C<values> and C<sort>.

=item kill SIGNAL, LIST

=begin original

Sends a signal to a list of processes.  Returns the number of
processes successfully signaled (which is not necessarily the
same as the number actually killed).

=end original

プロセスのリストにシグナルを送ります。シグナル送信に成功したプロセスの
数を返します
(実際に kill に成功したプロセスと同じとは限りません)。

    $cnt = kill 1, $child1, $child2;
    kill 9, @goners;

=begin original

If SIGNAL is zero, no signal is sent to the process.  This is a
useful way to check that the process is alive and hasn't changed
its UID.  See L<perlport> for notes on the portability of this
construct.

=end original

SIGNAL がゼロの場合、プロセスにシグナルは送られません。
これはプロセスが生きていて、 UID が代わっていないことを調べる時に
有用です。
この構成の移植性に関する注意については L<perlport> を参照して下さい。

=begin original

Unlike in the shell, if SIGNAL is negative, it kills
process groups instead of processes.  (On System V, a negative I<PROCESS>
number will also kill process groups, but that's not portable.)  That
means you usually want to use positive not negative signals.  You may also
use a signal name in quotes.  See L<perlipc/"Signals"> for details.

=end original

シェルとは異なり、シグナルに負の数を与えると、
プロセスではなくプロセスグループに対して kill を行ないます。
(Syetem V では、プロセス番号として負の値を与えても、
プロセスグループの kill を行ないますが、
移植性がありません。)
すなわち、通常は、負のシグナルは用いず、正のシグナルを使うことになります。
シグナル名をクォートして使うこともできます。
詳細はL<perlipc/"Signals">を参照してください。

=item last LABEL

=item last

=begin original

The C<last> command is like the C<break> statement in C (as used in
loops); it immediately exits the loop in question.  If the LABEL is
omitted, the command refers to the innermost enclosing loop.  The
C<continue> block, if any, is not executed:

=end original

C<last> コマンドは、(ループ内で使った) C の C<break> 文と
同じようなもので、LABEL で指定されるループを即座に抜けます。
LABEL が省略されると、一番内側のループが対象となります。
C<continue> ブロックがあっても実行されません:

    LINE: while (<STDIN>) {
	last LINE if /^$/;	# exit when done with header
	#...
    }

=begin original

C<last> cannot be used to exit a block which returns a value such as
C<eval {}>, C<sub {}> or C<do {}>, and should not be used to exit
a grep() or map() operation.

=end original

C<last> は C<eval {}>, C<sub {}>, C<do {}> といった
値を返すブロックを終了するのには使えません。
また grep() や map() 操作を終了するのに使うべきではありません。

=begin original

Note that a block by itself is semantically identical to a loop
that executes once.  Thus C<last> can be used to effect an early
exit out of such a block.

=end original

ブロックはそれ自体文法的には一度だけ実行されるループと同等であることに
注意してください。従って、C<last> でそのようなブロックを
途中で抜け出すことができます。

See also L</continue> for an illustration of how C<last>, C<next>, and
C<redo> work.

=item lc EXPR

=item lc

=begin original

Returns an lowercased version of EXPR.  This is the internal function
implementing the C<\L> escape in double-quoted strings.
Respects current LC_CTYPE locale if C<use locale> in force.  See L<perllocale>
and L<utf8>.

=end original

EXPR を小文字に変換したものを返します。
これは、ダブルクォート文字列における、
C<\L> エスケープを実装する内部関数です。
C<use locale> が有効な場合は、現在の LC_CTYPE ロケールを参照します。
L<perllocale> と L<utf8> を参照してください。

=begin original

If EXPR is omitted, uses C<$_>.

=end original

EXPR が省略されると、C<$_>を使います。

=item lcfirst EXPR

=item lcfirst

=begin original

Returns the value of EXPR with the first character lowercased.  This is
the internal function implementing the C<\l> escape in double-quoted strings.
Respects current LC_CTYPE locale if C<use locale> in force.  See L<perllocale>. 

=end original

最初の文字だけを小文字にした、EXPR を返します。
これは、ダブルクォート文字列における、C<\l> エスケープを
実装する内部関数です。
C<use locale> が有効な場合は、現在の LC_CTYPE ロケールを参照します。
L<perllocale> を参照してください。

=begin original

If EXPR is omitted, uses C<$_>.

=end original

EXPR が省略されると、C<$_>を使います。

=item length EXPR

=item length

=begin original

Returns the length in characters of the value of EXPR.  If EXPR is
omitted, returns length of C<$_>.  Note that this cannot be used on 
an entire array or hash to find out how many elements these have.
For that, use C<scalar @array> and C<scalar keys %hash> respectively.

=end original

EXPR の値の文字列長を返します。　EXPR が省略されたと
きには、C<$_> の長さを返します。
これは配列やハッシュ全体に対してどれだけの要素を含んでいるかを
調べるためには使えません。
そのような用途には、それぞれ C<scalar @array> と C<scalar keys %hash> を
利用してください。

=item link OLDFILE,NEWFILE

=begin original

Creates a new filename linked to the old filename.  Returns true for
success, false otherwise. 

=end original

OLDFILE にリンクされた、新しいファイル NEWFILE を作
ります。　成功時には true を、失敗時には false を返します。

=item listen SOCKET,QUEUESIZE

=begin original

Does the same thing that the listen system call does.  Returns true if
it succeeded, false otherwise.  See the example in 
L<perlipc/"Sockets: Client/Server Communication">.

=end original

listen システムコールと同じことをします。成功時には真を返し、
失敗時には偽を返します。
L<perlipc/"Sockets: Client/Server Communication">の例を参照してください。

=item local EXPR

=begin original

You really probably want to be using C<my> instead, because C<local> isn't
what most people think of as "local".  See 
L<perlsub/"Private Variables via my()"> for details.

=end original

あなたはが本当に望んでいるのは C<my> の方でしょう。
C<local> はほとんどの人々が「ローカル」と考えるものと違うからです。
詳細はL<perlsub/"Private Variables
via my()">を参照してください。

=begin original

A local modifies the listed variables to be local to the enclosing
block, file, or eval.  If more than one value is listed, the list must
be placed in parentheses.  See L<perlsub/"Temporary Values via local()">
for details, including issues with tied arrays and hashes.

=end original

"local" はリストアップされた変数を、囲っているブロック、
ファイル、eval の中で、ローカルなものにします。
複数の値を指定する場合は、リストは括弧でくくらなければなりません。
tie した配列とハッシュに関する事項を含む詳細については
L<perlsub/"Temporary Values via local()"> を参照してください。

=item localtime EXPR

=begin original

Converts a time as returned by the time function to a 9-element list
with the time analyzed for the local time zone.  Typically used as
follows:

=end original

time 関数が返す時刻を、ローカルなタイムゾーンで測った時刻として、
9 要素の配列に変換します。
通常は、以下のようにして使用します。

    #  0    1    2     3     4    5     6     7     8
    ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) =
						localtime(time);

=begin original

All list elements are numeric, and come straight out of the C `struct
tm'.  $sec, $min, and $hour are the seconds, minutes, and hours of the
specified time.  $mday is the day of the month, and $mon is the month
itself, in the range C<0..11> with 0 indicating January and 11
indicating December.  $year is the number of years since 1900.  That
is, $year is C<123> in year 2023.  $wday is the day of the week, with
0 indicating Sunday and 3 indicating Wednesday.  $yday is the day of
the year, in the range C<0..364> (or C<0..365> in leap years.)  $isdst
is true if the specified time occurs during daylight savings time,
false otherwise.

=end original

すべてのリスト要素は数値で、C の `struct tm'  構造体から
直接持ってきます。
$sec, $min, $hour は指定された時刻の秒、分、時です。
$mday は月の何日目か、$mon は月の値です。
月の値は C<0..11> で、0 が 1 月、11 が 12 月です。
$year は 1900 年からの年数です。
つまり、$year が C<123> なら 2023 年です。
$wday は曜日で、0 が日曜日、3 が木曜日です。
$yday はその年の何日目かで、C<0..364> の値を取ります
(うるう年は C<0..365> です)。
$isdst は指定された時刻が夏時間の場合は真、そうでなければ偽です。

=begin original

Note that the $year element is I<not> simply the last two digits of
the year.  If you assume it is, then you create non-Y2K-compliant
programs--and you wouldn't want to do that, would you?

=end original

$year 要素が単に西暦の下 2 桁を表している I<のではない> ことに
注意してください。これを仮定すると、Y2K 問題を含んだプログラムを
作ることになります。それはお望みじゃないでしょう?

=begin original

The proper way to get a complete 4-digit year is simply:

=end original

完全な 4 桁の西暦を得るには単に以下のようにしてください:

	$year += 1900;

=begin original

And to get the last two digits of the year (e.g., '01' in 2001) do:

=end original

そして西暦の下 2 桁(2001 年では '01')がほしい場合は以下のようにします:

	$year = sprintf("%02d", $year % 100);

=begin original

If EXPR is omitted, C<localtime()> uses the current time (C<localtime(time)>).

=end original

EXPR が省略されると、localtime(time()) を行ないます。

=begin original

In scalar context, C<localtime()> returns the ctime(3) value:

=end original

スカラコンテキストでは、ctime(3) の値を返します:

    $now_string = localtime;  # e.g., "Thu Oct 13 04:54:34 1994"

This scalar value is B<not> locale dependent, see L<perllocale>, but
instead a Perl builtin.  Also see the C<Time::Local> module
(to convert the second, minutes, hours, ... back to seconds since the
stroke of midnight the 1st of January 1970, the value returned by
time()), and the strftime(3) and mktime(3) functions available via the
POSIX module.  To get somewhat similar but locale dependent date
strings, set up your locale environment variables appropriately
(please see L<perllocale>) and try for example:

    use POSIX qw(strftime);
    $now_string = strftime "%a %b %e %H:%M:%S %Y", localtime;

Note that the C<%a> and C<%b>, the short forms of the day of the week
and the month of the year, may not necessarily be three characters wide.

=item lock

    lock I<THING>

This function places an advisory lock on a variable, subroutine,
or referenced object contained in I<THING> until the lock goes out
of scope.  This is a built-in function only if your version of Perl
was built with threading enabled, and if you've said C<use Threads>.
Otherwise a user-defined function by this name will be called.  See
L<Thread>.

=item log EXPR

=item log

=begin original

Returns the natural logarithm (base I<e>) of EXPR.  If EXPR is omitted,
returns log of C<$_>.  To get the log of another base, use basic algebra:
The base-N log of a number is equal to the natural log of that number
divided by the natural log of N.  For example:

=end original

EXPR の (I<e> を底とする) 自然対数を返します。
EXPR が省略されると、C<$_> の対数を返します。
底の異なる対数を求めるためには、基礎代数を利用してください:
ある数の N を底とする対数は、その数の自然対数を N の自然対数で割ったものです。
例:

    sub log10 {
	my $n = shift;
	return log($n)/log(10);
    } 

=begin original

See also L</exp> for the inverse operation.

=end original

逆操作については L</exp> を参照して下さい。

=item lstat FILEHANDLE

=item lstat EXPR

=item lstat

=begin original

Does the same thing as the C<stat> function (including setting the
special C<_> filehandle) but stats a symbolic link instead of the file
the symbolic link points to.  If symbolic links are unimplemented on
your system, a normal C<stat> is done.

=end original

(特別なファイルハンドルである C<_> の設定を含めて)
C<stat> 関数と同じことをしますが、シンボリックリンクが
指しているファイルではなく、シンボリックリンク自体の stat をとります。
シンボリックリンクがシステムに実装されていないと、通常の C<stat> が行なわれます。

=begin original

If EXPR is omitted, stats C<$_>.

=end original

EXPR が省略されると、C<$_> の stat をとります。

=item m//

=begin original

The match operator.  See L<perlop>.

=end original

マッチ演算子です。L<perlop> を参照してください。

=item map BLOCK LIST

=item map EXPR,LIST

=begin original

Evaluates the BLOCK or EXPR for each element of LIST (locally setting
C<$_> to each element) and returns the list value composed of the
results of each such evaluation.  In scalar context, returns the
total number of elements so generated.  Evaluates BLOCK or EXPR in
list context, so each element of LIST may produce zero, one, or
more elements in the returned value.

=end original

LIST の個々の要素に対して、BLOCK か EXPR を評価し
(C<$_> は、ローカルに個々の要素が設定されます) 、それぞれの
評価結果からなるリスト値が返されます。
スカラコンテキストでは、生成された要素の数を返します。　BLOCK や
EXPR をリストコンテキストで評価しますので、LIST の
個々の要素によって作られる、返り値であるリストの要素数は、
0 個の場合もあれば、複数の場合もあります。

    @chars = map(chr, @nums);

=begin original

translates a list of numbers to the corresponding characters.  And

=end original

は、数のリストを対応する文字に変換します。また、

    %hash = map { getkey($_) => $_ } @array;

=begin original

is just a funny way to write

=end original

は以下のものをちょっと変わった書き方で書いたものです。

    %hash = ();
    foreach $_ (@array) {
	$hash{getkey($_)} = $_;
    }

Note that C<$_> is an alias to the list value, so it can be used to
modify the elements of the LIST.  While this is useful and supported,
it can cause bizarre results if the elements of LIST are not variables.
Using a regular C<foreach> loop for this purpose would be clearer in
most cases.  See also L</grep> for an array composed of those items of
the original list for which the BLOCK or EXPR evaluates to true.

C<{> starts both hash references and blocks, so C<map { ...> could be either
the start of map BLOCK LIST or map EXPR, LIST. Because perl doesn't look
ahead for the closing C<}> it has to take a guess at which its dealing with
based what it finds just after the C<{>. Usually it gets it right, but if it
doesn't it won't realize something is wrong until it gets to the C<}> and
encounters the missing (or unexpected) comma. The syntax error will be
reported close to the C<}> but you'll need to change something near the C<{>
such as using a unary C<+> to give perl some help:

    %hash = map {  "\L$_", 1  } @array  # perl guesses EXPR.  wrong
    %hash = map { +"\L$_", 1  } @array  # perl guesses BLOCK. right
    %hash = map { ("\L$_", 1) } @array  # this also works
    %hash = map {  lc($_), 1  } @array  # as does this.
    %hash = map +( lc($_), 1 ), @array  # this is EXPR and works!

    %hash = map  ( lc($_), 1 ), @array  # evaluates to (1, @array)

or to force an anon hash constructor use C<+{>

   @hashes = map +{ lc($_), 1 }, @array # EXPR, so needs , at end

and you get list of anonymous hashes each with only 1 entry.

=item mkdir FILENAME,MASK

=item mkdir FILENAME

=begin original

Creates the directory specified by FILENAME, with permissions
specified by MASK (as modified by C<umask>).  If it succeeds it
returns true, otherwise it returns false and sets C<$!> (errno).
If omitted, MASK defaults to 0777.

=end original

FILENAME で指定したディレクトリを、MASK で指定した許
可モード (を C<umask> で修正したもの) で作成します。
成功時には true を返し、失敗時には false を返して C<$!> (errno)
を設定します。MASK を省略すると、0777 とみなします。

In general, it is better to create directories with permissive MASK,
and let the user modify that with their C<umask>, than it is to supply
a restrictive MASK and give the user no way to be more permissive.
The exceptions to this rule are when the file or directory should be
kept private (mail files, for instance).  The perlfunc(1) entry on
C<umask> discusses the choice of MASK in more detail.

=item msgctl ID,CMD,ARG

=begin original

Calls the System V IPC function msgctl(2).  You'll probably have to say

    use IPC::SysV;

first to get the correct constant definitions.  If CMD is C<IPC_STAT>,
then ARG must be a variable which will hold the returned C<msqid_ds>
structure.  Returns like C<ioctl>: the undefined value for error,
C<"0 but true"> for zero, or the actual return value otherwise.  See also
L<perlipc/"SysV IPC">, C<IPC::SysV>, and C<IPC::Semaphore> documentation.

=end original

System V IPC 関数 msgctl を呼び出します。正しい定数定義を得るために、まず

    use IPC::SysV;

と宣言する必要があるでしょう。
CMD が C<IPC_STAT> であれば、ARG は返される C<msqid_ds> 構造体を
納める変数でなければなりません。
C<ioctl> と同じように、エラー時には未定義値、
ゼロのときは C<"0 but true">、それ以外なら、その値そのものを返します。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::Semaphore> も参照してください。

=item msgget KEY,FLAGS

=begin original

Calls the System V IPC function msgget(2).  Returns the message queue
id, or the undefined value if there is an error.  See also
L<perlipc/"SysV IPC"> and C<IPC::SysV> and C<IPC::Msg> documentation.

=end original

System V IPC 関数 msgget を呼び出します。
メッセージキューの ID か、エラー時には未定義値を返します。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::Msg> も参照してください。

=item msgrcv ID,VAR,SIZE,TYPE,FLAGS

=begin original

Calls the System V IPC function msgrcv to receive a message from
message queue ID into variable VAR with a maximum message size of
SIZE.  Note that when a message is received, the message type as a
native long integer will be the first thing in VAR, followed by the
actual message.  This packing may be opened with C<unpack("l! a*")>.
Taints the variable.  Returns true if successful, or false if there is
an error.  See also L<perlipc/"SysV IPC">, C<IPC::SysV>, and
C<IPC::SysV::Msg> documentation.

=end original

System V IPC 関数 msgrcv を呼び出し、メッセージキュー ID から、
変数 VAR に最大メッセージ長 SIZE のメッセージを受信します。
メッセージが受信された時、ネイティブな long 整数のメッセージタイプが
VAR の先頭となり、実際のメッセージが続きます。
このパッキングは C<unpack("l! a*")> で展開できます。
変数は汚染されます。
成功時には真を返し、エラー時には偽を返します。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::SysV::Msg> も参照してください。

=item msgsnd ID,MSG,FLAGS

=begin original

Calls the System V IPC function msgsnd to send the message MSG to the
message queue ID.  MSG must begin with the native long integer message
type, and be followed by the length of the actual message, and finally
the message itself.  This kind of packing can be achieved with
C<pack("l! a*", $type, $message)>.  Returns true if successful,
or false if there is an error.  See also C<IPC::SysV>
and C<IPC::SysV::Msg> documentation.

=end original

System V IPC 関数 msgsnd を呼び出し、メッセージキュ
ー ID にメッセージ MSG を送信します。　MSG の先頭は、
ネイティブなlong 整数のメッセージタイプでなければならず、
メッセージの長さ、メッセージ本体と続きます。これは、
C<pack("l! a*", $type, $message)> として生成できます。
成功時には真を、エラー時には偽を返します。
C<IPC::SysV> と C<IPC::SysV::Msg> も参照してください。

=item my EXPR

=item my EXPR : ATTRIBUTES

=begin original

A C<my> declares the listed variables to be local (lexically) to the
enclosing block, file, or C<eval>.  If
more than one value is listed, the list must be placed in parentheses.  See
L<perlsub/"Private Variables via my()"> for details.

=end original

C<my> はリストアップされた変数を、囲っているブロック、ファイル、
C<eval> の中で
ローカルな (レキシカルな) ものにします。　複数の値を並べる場合には、
括弧で括る必要があります。
詳しくはL<perlsub/"Private Variables via my()">を参照してください。

=item next LABEL

=item next

=begin original

The C<next> command is like the C<continue> statement in C; it starts
the next iteration of the loop:

=end original

C<next> コマンドは、C での C<continue> 文のようなもので、
ループの次の繰り返しを開始します:

    LINE: while (<STDIN>) {
	next LINE if /^#/;	# discard comments
	#...
    }

=begin original

Note that if there were a C<continue> block on the above, it would get
executed even on discarded lines.  If the LABEL is omitted, the command
refers to the innermost enclosing loop.

=end original

C<continue> ブロックが存在すれば、たとえ捨てられる行に
あっても、それが実行されます。
LABEL が省略されると、このコマンドはもっとも内側のループを参照します。

=begin original

C<next> cannot be used to exit a block which returns a value such as
C<eval {}>, C<sub {}> or C<do {}>, and should not be used to exit
a grep() or map() operation.

=end original

C<next> は C<eval {}>, C<sub {}>, C<do {}> のように値を返す
ブロックから抜けるのには使えません。
また、grep() や map() 操作から抜けるのに使うべきではありません。

=begin original

Note that a block by itself is semantically identical to a loop
that executes once.  Thus C<next> will exit such a block early.

=end original

ブロック自身は一回だけ実行されるループと文法的に同一であることに
注意してください。
従って、C<next> はそのようなブロックから早く抜けるのに使えます。

=begin original

See also L</continue> for an illustration of how C<last>, C<next>, and
C<redo> work.

=end original

C<last>, C<next>, C<redo> がどのように働くかについては
L</continue> も参照して下さい。

=item no Module LIST

=begin original

See the L</use> function, which C<no> is the opposite of.

=end original

L</use> 関数を参照してください。C<no> は、その逆を行なうものです。

=item oct EXPR

=item oct

=begin original

Interprets EXPR as an octal string and returns the corresponding
value.  (If EXPR happens to start off with C<0x>, interprets it as a
hex string.  If EXPR starts off with C<0b>, it is interpreted as a
binary string.)  The following will handle decimal, binary, octal, and
hex in the standard Perl or C notation:

=end original

EXPR を 8 進数文字列と解釈して、対応する値を返します。
(EXPR が C<0x> で始まるときには、16 進数文字列と
解釈します。EXPR が C<0b>で始まるときは、
2 進数文字列と解釈します。)　以下の例は、標準的な Perl や C の記法での
10 進数、2 進数、8 進数、16 進数を扱います:

    $val = oct($val) if $val =~ /^0/;

=begin original

If EXPR is omitted, uses C<$_>.   To go the other way (produce a number
in octal), use sprintf() or printf():

=end original

EXPR を省略すると、C<$_> を使用します。(8 進数を扱う)その他の方法としては
sprintf() または printf()があります。

    $perms = (stat("filename"))[2] & 07777;
    $oct_perms = sprintf "%lo", $perms;

=begin original

The oct() function is commonly used when a string such as C<644> needs
to be converted into a file mode, for example. (Although perl will
automatically convert strings into numbers as needed, this automatic
conversion assumes base 10.)

=end original

oct() 関数は例えば、 C<644> といった文字列をファイルモードに変換する
時によく使います。
(Perl は必要に応じて自動的に文字列を数値に変換しますが、
この自動変換は十進数を仮定します。)

=item open FILEHANDLE,MODE,LIST

=item open FILEHANDLE,EXPR

=item open FILEHANDLE

=begin original

Opens the file whose filename is given by EXPR, and associates it with
FILEHANDLE.  If FILEHANDLE is an expression, its value is used as the
name of the real filehandle wanted.  (This is considered a symbolic
reference, so C<use strict 'refs'> should I<not> be in effect.)

=end original

EXPR でファイル名を指定するファイルをオープンして、
FILEHANDLE に対応させます。　FILEHANDLE が式のときに
は、その値が実際に使用するファイルハンドルの名前として
使われます。
(これはシンボリックリファレンスと考えられますので、
C<use strict 'refs'> はI<効果がありません>。)

=begin original

If EXPR is omitted, the scalar
variable of the same name as the FILEHANDLE contains the filename.
(Note that lexical variables--those declared with C<my>--will not work
for this purpose; so if you're using C<my>, specify EXPR in your call
to open.)  See L<perlopentut> for a kinder, gentler explanation of opening
files.

=end original

EXPR を省略すると、FILEHANDLE と同じ
名前のスカラ変数に、ファイル名が入れられているものとして処理されます。
(C<my> で宣言されたレキシカル変数はこの目的には使えません。
従って、C<my>を使った場合は、open を呼び出すときに EXPR を指定してください。)
ファイルのオープンに関するより親切な説明については L<perlopentut> を参照して下さい。

=begin original

If MODE is C<< '<' >> or nothing, the file is opened for input.
If MODE is C<< '>' >>, the file is truncated and opened for
output, being created if necessary.  If MODE is C<<< '>>' >>>,
the file is opened for appending, again being created if necessary. 
You can put a C<'+'> in front of the C<< '>' >> or C<< '<' >> to indicate that
you want both read and write access to the file; thus C<< '+<' >> is almost
always preferred for read/write updates--the C<< '+>' >> mode would clobber the
file first.  You can't usually use either read-write mode for updating
textfiles, since they have variable length records.  See the B<-i>
switch in L<perlrun> for a better approach.  The file is created with
permissions of C<0666> modified by the process' C<umask> value.

=end original

MODE が C<< '<' >> か、何も付けなかった場合には、
入力用としてオープンされることになります。
MODE が C<< '>' >> の場合には、ファイルは空にされたあと出力用に
オープンされ、必要ならファイルが作成されます。
MODE が C<<< '>>' >>> の場合には、追記用にオープンされ、
必要ならファイルが作成されます。
C<< '>' >> や C<< '<' >> の前に C<'+'> を置くことで、
ファイルを読み書き両用にすることもできます。
従って、C<< '+<' >> はほとんどの場合読み書き更新をするために適したものです。
C<< '+>' >> は最初にファイルを上書きします。
テキストファイルを更新する場合には普通はどちらの読み書きモードも使えません。
可変長のレコードを持つからです。
よりよい手法については L<perlrun> の  B<-i> オプションを参照して下さい。
作成されるファイルのパーミッションは C<0666> を
プロセスの C<umask> の値で修正したものになります。

These various prefixes correspond to the fopen(3) modes of C<'r'>, C<'r+'>,
C<'w'>, C<'w+'>, C<'a'>, and C<'a+'>.

In the 2-arguments (and 1-argument) form of the call the mode and
filename should be concatenated (in this order), possibly separated by
spaces.  It is possible to omit the mode if the mode is C<< '<' >>.

=begin original

If the filename begins with C<'|'>, the filename is interpreted as a
command to which output is to be piped, and if the filename ends with a
C<'|'>, the filename is interpreted as a command which pipes output to
us.  See L<perlipc/"Using open() for IPC">
for more examples of this.  (You are not allowed to C<open> to a command
that pipes both in I<and> out, but see L<IPC::Open2>, L<IPC::Open3>,
and L<perlipc/"Bidirectional Communication with Another Process">
for alternatives.)

=end original

ファイル名の先頭に C<'|'> を付けると、
そのファイル名をコマンドとして解釈し、ファイルハンドルへの
出力がパイプを通じて、そのコマンドへ入力されます。
逆にファイル名の最後に C<'|'> を付けた場合には、
同様にファイル名をコマンドと解釈し、そのコマンドの出力が
パイプを通じて、ファイルハンドルから入力として
読み込むことができるようになります。
これに関する例については L<perlipc/"Using open() for IPC"> を参照してください。
(C<open> を入出力両用にパイプすることは出来ませんが
代替案としては L<IPC::Open2>, L<IPC::Open3>,
L<perlipc/"Bidirectional Communication with Another Process">
を参照してください。)

If MODE is C<'|-'>, the filename is interpreted as a
command to which output is to be piped, and if MODE is
C<'-|'>, the filename is interpreted as a command which pipes output to
us.  In the 2-arguments (and 1-argument) form one should replace dash
(C<'-'>) with the command.  See L<perlipc/"Using open() for IPC">
for more examples of this.  (You are not allowed to C<open> to a command
that pipes both in I<and> out, but see L<IPC::Open2>, L<IPC::Open3>,
and L<perlipc/"Bidirectional Communication"> for alternatives.)

=begin original

In the 2-arguments (and 1-argument) form opening C<'-'> opens STDIN
and opening C<< '>-' >> opens STDOUT.  

=end original

2 引数(と 1 引数)で C<'-'> を open すると STDIN がオープンされ、
C<< '>-' >> を open すると STDOUT がオープンされます。

=begin original

Open returns
nonzero upon success, the undefined value otherwise.  If the C<open>
involved a pipe, the return value happens to be the pid of the
subprocess.

=end original

open は、成功時には、
ゼロ以外を返し、失敗時には未定義値を返します。
パイプに関る C<open> のときには、返り値はサブプロセスの pid となります。

If you're unfortunate enough to be running Perl on a system that
distinguishes between text files and binary files (modern operating
systems don't care), then you should check out L</binmode> for tips for
dealing with this.  The key distinction between systems that need C<binmode>
and those that don't is their text file formats.  Systems like Unix, MacOS, and
Plan9, which delimit lines with a single character, and which encode that
character in C as C<"\n">, do not need C<binmode>.  The rest need it.

When opening a file, it's usually a bad idea to continue normal execution
if the request failed, so C<open> is frequently used in connection with
C<die>.  Even if C<die> won't do what you want (say, in a CGI script,
where you want to make a nicely formatted error message (but there are
modules that can help with that problem)) you should always check
the return value from opening a file.  The infrequent exception is when
working with an unopened filehandle is actually what you want to do.

Examples:

    $ARTICLE = 100;
    open ARTICLE or die "Can't find article $ARTICLE: $!\n";
    while (<ARTICLE>) {...

    open(LOG, '>>/usr/spool/news/twitlog');	# (log is reserved)
    # if the open fails, output is discarded

    open(DBASE, '+<', 'dbase.mine')		# open for update
	or die "Can't open 'dbase.mine' for update: $!";

    open(DBASE, '+<dbase.mine')			# ditto
	or die "Can't open 'dbase.mine' for update: $!";

    open(ARTICLE, '-|', "caesar <$article")     # decrypt article
	or die "Can't start caesar: $!";

    open(ARTICLE, "caesar <$article |")		# ditto
	or die "Can't start caesar: $!";

    open(EXTRACT, "|sort >/tmp/Tmp$$")		# $$ is our process id
	or die "Can't start sort: $!";

    # process argument list of files along with any includes

    foreach $file (@ARGV) {
	process($file, 'fh00');
    }

    sub process {
	my($filename, $input) = @_;
	$input++;		# this is a string increment
	unless (open($input, $filename)) {
	    print STDERR "Can't open $filename: $!\n";
	    return;
	}

	local $_;
	while (<$input>) {		# note use of indirection
	    if (/^#include "(.*)"/) {
		process($1, $input);
		next;
	    }
	    #...		# whatever
	}
    }

=begin original

You may also, in the Bourne shell tradition, specify an EXPR beginning
with C<< '>&' >>, in which case the rest of the string is interpreted as the
name of a filehandle (or file descriptor, if numeric) to be
duped and opened.  You may use C<&> after C<< > >>, C<<< >> >>>,
C<< < >>, C<< +> >>, C<<< +>> >>>, and C<< +< >>.  The
mode you specify should match the mode of the original filehandle.
(Duping a filehandle does not take into account any existing contents of
stdio buffers.)  Duping file handles is not yet supported for 3-argument
open().

=end original

Bourne シェルの慣例にしたがって、EXPR の先頭に C<< '>&' >>
を付けると、EXPR の残りの文字列をファイルハンドル名
(数字であれば、ファイル記述子) と解釈して、それを dup
してオープンします。　C<&> は、C<< > >>, C<<< >> >>>,
C<< < >>, C<< +> >>, C<<< +>> >>>, and C<< +< >>というモード指定に
付けることができます。
指定するモード指定は、もとのファイルハンドルのモードと
合っていないといけません。
(ファイルハンドルの複製は既に存在する stdio バッファの内容に含めません)
ファイルハンドルの複製は 3 引数 open() ではまだ対応していません。

=begin original

Here is a script that saves, redirects, and restores STDOUT and
STDERR:

=end original

STDOUT と STDERR をいったん保存し、
リダイレクトし、元に戻すスクリプトを示します。

    #!/usr/bin/perl
    open(OLDOUT, ">&STDOUT");
    open(OLDERR, ">&STDERR");

    open(STDOUT, '>', "foo.out") || die "Can't redirect stdout";
    open(STDERR, ">&STDOUT")     || die "Can't dup stdout";

    select(STDERR); $| = 1;	# make unbuffered
    select(STDOUT); $| = 1;	# make unbuffered

    print STDOUT "stdout 1\n";	# this works for
    print STDERR "stderr 1\n"; 	# subprocesses too

    close(STDOUT);
    close(STDERR);

    open(STDOUT, ">&OLDOUT");
    open(STDERR, ">&OLDERR");

    print STDOUT "stdout 2\n";
    print STDERR "stderr 2\n";

=begin original

If you specify C<< '<&=N' >>, where C<N> is a number, then Perl will do an
equivalent of C's C<fdopen> of that file descriptor; this is more
parsimonious of file descriptors.  For example:

=end original

C<N> を数値として、C<< '<&=N' >> と指定すると、Perl は、
そのファイル記述子に対する C の C<fdopen> と同じことを行ないます。
これはファイル記述子をより節約します。たとえば:

    open(FILEHANDLE, "<&=$fd")

=begin original

Note that this feature depends on the fdopen() C library function.
On many UNIX systems, fdopen() is known to fail when file descriptors
exceed a certain value, typically 255. If you need more file
descriptors than that, consider rebuilding Perl to use the C<sfio>
library.

=end original

この機能は fdopen() C ライブラリ関数に依存します。
多くの UNIX システムでは、fdopen() はファイル記述子がある値
(典型的には 255)を超えた場合に失敗することが知られています。
これより多くのファイル記述子が必要な場合は、
C<sfio> ライブラリを使って Perl を再ビルドすることを検討して下さい。

=begin original

If you open a pipe on the command C<'-'>, i.e., either C<'|-'> or C<'-|'>
with 2-arguments (or 1-argument) form of open(), then
there is an implicit fork done, and the return value of open is the pid
of the child within the parent process, and C<0> within the child
process.  (Use C<defined($pid)> to determine whether the open was successful.)
The filehandle behaves normally for the parent, but i/o to that
filehandle is piped from/to the STDOUT/STDIN of the child process.
In the child process the filehandle isn't opened--i/o happens from/to
the new STDOUT or STDIN.  Typically this is used like the normal
piped open when you want to exercise more control over just how the
pipe command gets executed, such as when you are running setuid, and
don't want to have to scan shell commands for metacharacters.
The following triples are more or less equivalent:

=end original

2 引数(または 1 引数)の形の open() で
C<'-|'> や C<'|-'> というふうに、C<'-'> というコマンドに
パイプをオープンすると、fork が行なわれ、open の返り値として、
親プロセスにはチャイルドプロセスの pid が、チャイルドプロセスには
C<0> が返されます。
(open が成功したかどうかは、
C<defined($pid)> のようにして調べることができます。)
親プロセスでは、このファイルハンドルは
通常通りに動作しますが、行なわれる入出力は、
チャイルドプロセスの STDIN/STDOUT にパイプされます。
チャイルドプロセス側では、そのファイルハンドルは
オープンされず、入出力は新しい STDOUT か STDIN に対して行なわれます。
これは、setuid で実行して、シェルコマンドのメタ文字を
検索させたくないような場合に、パイプコマンドの起動の仕方を
制御したいとき、普通のパイプのopen と同じように使います。
以下の組み合わせは、だいたい同じものです:

    open(FOO, "|tr '[a-z]' '[A-Z]'");
    open(FOO, '|-', "tr '[a-z]' '[A-Z]'");
    open(FOO, '|-') || exec 'tr', '[a-z]', '[A-Z]';

    open(FOO, "cat -n '$file'|");
    open(FOO, '-|', "cat -n '$file'");
    open(FOO, '-|') || exec 'cat', '-n', $file;

=begin original

See L<perlipc/"Safe Pipe Opens"> for more examples of this.

=end original

これに関する更なる例については L<perlipc/"Safe Pipe Opens"> を参照して下さい。

Beginning with v5.6.0, Perl will attempt to flush all files opened for
output before any operation that may do a fork, but this may not be
supported on some platforms (see L<perlport>).  To be safe, you may need
to set C<$|> ($AUTOFLUSH in English) or call the C<autoflush()> method
of C<IO::Handle> on any open handles.

=begin original

On systems that support a
close-on-exec flag on files, the flag will be set for the newly opened
file descriptor as determined by the value of $^F.  See L<perlvar/$^F>.

=end original

ファイルに対する close-on-exec フラグをサポートしているシステムでは、
フラグは$^F の値で決定される、新しくオープンされたファイル記述子に対して
セットされます。L<perlvar/$^F>を参照してください。

=begin original

Closing any piped filehandle causes the parent process to wait for the
child to finish, and returns the status value in C<$?>.

=end original

パイプのファイルハンドルを close することで、
親プロセスは、チャイルドプロセスの終了を待ち、C<$?> に
ステータス値を返します。

=begin original

The filename passed to 2-argument (or 1-argument) form of open()
will have leading and trailing
whitespace deleted, and the normal redirection characters
honored.  This property, known as "magic open", 
can often be used to good effect.  A user could specify a filename of
F<"rsh cat file |">, or you could change certain filenames as needed:

=end original

2 引数(と 1 引数)の形の open() に渡されたファイル名は、
はじめと終わりの空白が取り除かれ、
通常のリダイレクト文字列を受け付けます。
この機能は "magic open" として知られていますが、
普通いい効果をもたらします。
ユーザーは F<"rsh cat file |"> といったファイル名を指定できますし、
特定のファイル名を必要に応じて変更できます。

    $filename =~ s/(.*\.gz)\s*$/gzip -dc < $1|/;
    open(FH, $filename) or die "Can't open $filename: $!";

=begin original

Use 3-argument form to open a file with arbitrary weird characters in it,

=end original

妙な文字が含まれているようなファイル名をオープンするには、
3 引数の形を使います。

    open(FOO, '<', $file);

=begin original

otherwise it's necessary to protect any leading and trailing whitespace:

=end original

あるいは、次のようにして、最初と最後の空白を保護します:

    $file =~ s#^(\s)#./$1#;
    open(FOO, "< $file\0");

(this may not work on some bizarre filesystems).  One should
conscientiously choose between the I<magic> and 3-arguments form
of open():

    open IN, $ARGV[0];

will allow the user to specify an argument of the form C<"rsh cat file |">,
but will not work on a filename which happens to have a trailing space, while

    open IN, '<', $ARGV[0];

will have exactly the opposite restrictions.

If you want a "real" C C<open> (see L<open(2)> on your system), then you
should use the C<sysopen> function, which involves no such magic (but
may use subtly different filemodes than Perl open(), which is mapped
to C fopen()).  This is
another way to protect your filenames from interpretation.  For example:

    use IO::Handle;
    sysopen(HANDLE, $path, O_RDWR|O_CREAT|O_EXCL)
	or die "sysopen $path: $!";
    $oldfh = select(HANDLE); $| = 1; select($oldfh);
    print HANDLE "stuff $$\n";
    seek(HANDLE, 0, 0);
    print "File contains: ", <HANDLE>;

Using the constructor from the C<IO::Handle> package (or one of its
subclasses, such as C<IO::File> or C<IO::Socket>), you can generate anonymous
filehandles that have the scope of whatever variables hold references to
them, and automatically close whenever and however you leave that scope:

    use IO::File;
    #...
    sub read_myfile_munged {
	my $ALL = shift;
	my $handle = new IO::File;
	open($handle, "myfile") or die "myfile: $!";
	$first = <$handle>
	    or return ();     # Automatically closed here.
	mung $first or die "mung failed";	# Or here.
	return $first, <$handle> if $ALL;	# Or here.
	$first;					# Or here.
    }

=begin original

See L</seek> for some details about mixing reading and writing.

=end original

読み書きを混ぜる場合の詳細については L</seek> を参照して下さい。

=item opendir DIRHANDLE,EXPR

=begin original

Opens a directory named EXPR for processing by C<readdir>, C<telldir>,
C<seekdir>, C<rewinddir>, and C<closedir>.  Returns true if successful.
DIRHANDLEs have their own namespace separate from FILEHANDLEs.

=end original

C<readdir>、C<telldir>、C<seekdir>、C<rewinddir>、
C<closedir> で処理するために、EXPR で指定された名前の
ディレクトリをオープンします。成功時には真を返します。
DIRHANDLE は、FILEHANDLE とは別に名前空間を持っています。

=item ord EXPR

=item ord

=begin original

Returns the numeric (ASCII or Unicode) value of the first character of EXPR.  If
EXPR is omitted, uses C<$_>.  For the reverse, see L</chr>.
See L<utf8> for more about Unicode.

=end original

EXPR の最初の文字の数値としての(ASCII または Unicode) 値を返します。
EXPR を省略した場合には、C<$_> を使用します。
逆のことをするには L</chr> を参照してください。
Unicode については L<utf8> を参照してください。

=item our EXPR

=begin original

An C<our> declares the listed variables to be valid globals within
the enclosing block, file, or C<eval>.  That is, it has the same
scoping rules as a "my" declaration, but does not create a local
variable.  If more than one value is listed, the list must be placed
in parentheses.  The C<our> declaration has no semantic effect unless
"use strict vars" is in effect, in which case it lets you use the
declared global variable without qualifying it with a package name.
(But only within the lexical scope of the C<our> declaration.  In this
it differs from "use vars", which is package scoped.)

=end original

C<our> は挙げられた変数をブロック、ファイル、C<eval> 内で
グローバルとして宣言します。
これは "my" 宣言と同じスコープルールをもちますが、
ローカル変数を作りません。
複数の変数を指定する場合は、変数は括弧で囲わなければなりません。
C<our> 宣言は "use strict vars" が有効でなければ
意味論的な効果はもちません。
この場合、宣言したグローバル変数をパッケージ名で修飾せずに使えます。
(しかし C<our> 宣言のレキシカルスコープの内側だけです。
これは "use vars" と異なります。こちらはパッケージスコープです。)

An C<our> declaration declares a global variable that will be visible
across its entire lexical scope, even across package boundaries.  The
package in which the variable is entered is determined at the point
of the declaration, not at the point of use.  This means the following
behavior holds:

    package Foo;
    our $bar;		# declares $Foo::bar for rest of lexical scope
    $bar = 20;

    package Bar;
    print $bar;		# prints 20

Multiple C<our> declarations in the same lexical scope are allowed
if they are in different packages.  If they happened to be in the same
package, Perl will emit warnings if you have asked for them.

    use warnings;
    package Foo;
    our $bar;		# declares $Foo::bar for rest of lexical scope
    $bar = 20;

    package Bar;
    our $bar = 30;	# declares $Bar::bar for rest of lexical scope
    print $bar;		# prints 30

    our $bar;		# emits warning

=item pack TEMPLATE,LIST

=begin original

Takes a LIST of values and converts it into a string using the rules
given by the TEMPLATE.  The resulting string is the concatenation of
the converted values.  Typically, each converted value looks
like its machine-level representation.  For example, on 32-bit machines
a converted integer may be represented by a sequence of 4 bytes.

=end original

LIST の値を TEMPLATE で与えられたルールを用いて
文字列に変換します。
結果の文字列は変換した値を連結したものです。
典型的には、それぞれの変換された値はマシンレベルの表現のように見えます。
例えば、32-bit マシンでは、整数を変換すると 4 バイトのデータとして
表現されます。

=begin original

The TEMPLATE is a
sequence of characters that give the order and type of values, as
follows:

=end original

TEMPLATE は、以下の
ような値の型と順番を指定する文字を並べたものです:

=begin original

    a	A string with arbitrary binary data, will be null padded.
    A	An ASCII string, will be space padded.
    Z	A null terminated (asciz) string, will be null padded.

    b	A bit string (ascending bit order inside each byte, like vec()).
    B	A bit string (descending bit order inside each byte).
    h	A hex string (low nybble first).
    H	A hex string (high nybble first).

    c	A signed char value.
    C	An unsigned char value.  Only does bytes.  See U for Unicode.

    s	A signed short value.
    S	An unsigned short value.
	  (This 'short' is _exactly_ 16 bits, which may differ from
	   what a local C compiler calls 'short'.  If you want
	   native-length shorts, use the '!' suffix.)

    i	A signed integer value.
    I	An unsigned integer value.
	  (This 'integer' is _at_least_ 32 bits wide.  Its exact
           size depends on what a local C compiler calls 'int',
           and may even be larger than the 'long' described in
           the next item.)

    l	A signed long value.
    L	An unsigned long value.
	  (This 'long' is _exactly_ 32 bits, which may differ from
	   what a local C compiler calls 'long'.  If you want
	   native-length longs, use the '!' suffix.)

    n	An unsigned short in "network" (big-endian) order.
    N	An unsigned long in "network" (big-endian) order.
    v	An unsigned short in "VAX" (little-endian) order.
    V	An unsigned long in "VAX" (little-endian) order.
	  (These 'shorts' and 'longs' are _exactly_ 16 bits and
	   _exactly_ 32 bits, respectively.)

    q	A signed quad (64-bit) value.
    Q	An unsigned quad value.
	  (Quads are available only if your system supports 64-bit
	   integer values _and_ if Perl has been compiled to support those.
           Causes a fatal error otherwise.)

    f	A single-precision float in the native format.
    d	A double-precision float in the native format.

    p	A pointer to a null-terminated string.
    P	A pointer to a structure (fixed-length string).

    u	A uuencoded string.
    U	A Unicode character number.  Encodes to UTF-8 internally.
	Works even if C<use utf8> is not in effect.

    w	A BER compressed integer.  Its bytes represent an unsigned
	integer in base 128, most significant digit first, with as
        few digits as possible.  Bit eight (the high bit) is set
        on each byte except the last.

    x	A null byte.
    X	Back up a byte.
    @	Null fill to absolute position.

=end original

    a   任意のバイナリデータを含む文字列、ヌル文字で埋める
    A   ASCII 文字列、スペース文字で埋める
    Z	ヌル文字終端(asciz)文字列、ヌル文字で埋める

    b   ビット列 (バイトごとに昇ビット順、vec() と同じ)
    B   ビット列 (バイトごとに降ビット順)
    h   16 進数文字列 (低位ニブルが先)
    H   16 進数文字列 (高位ニブルが先)

    c   signed char 値
    C   unsigned char 値。バイトのみ。Unicode 用は U。

    s   signed short 値
    S   unsigned short 値
	  (ここでの 'short' は「正確に」 16 bit です。
	   ローカルの C コンパイラの 'short' のサイズとは違うかもしれません。
	   ネイティブな長さの short を使いたい場合は、
	   '!' 前置詞を使ってください。)

    i   signed int 値
    I   unsigned int 値
	  (ここでの 'integer' は 「最低」 32 bits 幅です。
           正確なサイズはローカルの C コンパイラの
           'int'のサイズに依存します。
           また、下記の 'long' より大きい可能性もあります)

    l   signed long 値
    L   unsigned long 値
	  (ここでの 'long' は「正確に」32 bits です。これは
	   ローカルの C コンパイラの 'long' のサイズとは違うかもしれません。
	   ネイティブな長さの 'long' が必要な場合は、'!' 前置詞を使ってください)

    n   "network" 順序 (ビッグエンディアン) の unsigned  short
    N   "network" 順序 (ビッグエンディアン) の unsigned long
    v   "VAX" 順序 (リトルエンディアン) の unsigned short
    V   "VAX" 順序 (リトルエンディアン) の unsigned long
	  (これらの 'short' と 'long' は「正確に」それぞれ 16 bits と
	   32 bits です)

    q	符号付き 64-bit 数
    Q	符号なし 64-bit 数。
	  (Quads are available only if your system supports 64-bit
	   integer values _and_ if Perl has been compiled to support those.
           Causes a fatal error otherwise.)

    f   機種依存の単精度浮動小数点数
    d   機種依存の倍精度浮動小数点数

    p   ヌル文字で終端する文字列へのポインタ
    P   構造体 (固定長文字列) へのポインタ

    u   uuencode 文字列
    U	Unicode 文字番号。内部的に UTF-8 にエンコードされます。
	C<use utf8> が有効でなくても動作します。

    w	A BER compressed integer.  Its bytes represent an unsigned
	integer in base 128, most significant digit first, with as
        few digits as possible.  Bit eight (the high bit) is set
        on each byte except the last.

    x   ヌル文字
    X   1 文字後退
    @   絶対位置までヌル文字で埋める

=begin original

The following rules apply:

=end original

以下の条件が適用されます:

=over 8

=item *

=begin original

Each letter may optionally be followed by a number giving a repeat
count.  With all types except C<a>, C<A>, C<Z>, C<b>, C<B>, C<h>,
C<H>, and C<P> the pack function will gobble up that many values from
the LIST.  A C<*> for the repeat count means to use however many items are
left, except for C<@>, C<x>, C<X>, where it is equivalent
to C<0>, and C<u>, where it is equivalent to 1 (or 45, what is the
same).

=end original

これらの文字の後には、繰り返し数を示す数字を付けることができます。
C<a>, C<A>, C<Z>, C<b>, C<B>, C<h>, C<H>, C<P> 以外の全ての型では、
LIST からその数の値を取り出して使います。
くり返し数に C<*> を指定すると、その時点で残っているすべての要素を意味します。
ただし、C<@>, C<x>, C<X>では C<0> と C<u> と等価で、
1 (あるいは 45 でも同じ) と等価です。

=begin original

When used with C<Z>, C<*> results in the addition of a trailing null
byte (so the packed result will be one longer than the byte C<length>
of the item).

=end original

C<*> が C<Z> と共に使われた場合、末尾にヌルバイトをつけます
(従ってパックされた結果は要素の C<length> の値より 1 大きくなります)。

=begin original

The repeat count for C<u> is interpreted as the maximal number of bytes
to encode per line of output, with 0 and 1 replaced by 45.

=end original

C<u> での繰り返し回数は、出力行毎に最大何バイトまでをエンコードするかを
示します。0 と 1 は 45 として扱われます。

=item *

=begin original

The C<a>, C<A>, and C<Z> types gobble just one value, but pack it as a
string of length count, padding with nulls or spaces as necessary.  When
unpacking, C<A> strips trailing spaces and nulls, C<Z> strips everything
after the first null, and C<a> returns data verbatim.  When packing,
C<a>, and C<Z> are equivalent.

=end original

C<a>, C<A>, C<Z> という型を使うと、値を一つだけ取り出して使いますが、
繰り返し数で示す長さの文字列となるように、必要に
応じてヌル文字かスペース文字を付け足します。unpack
するとき、C<A> は後続のスペース文字やヌル文字を取り除きます。
C<Z>は最初のヌル文字以降の全てを取り除きます。
C<a>はデータをそのまま返します。
pack するときは、 C<a> と C<Z> は等価です。

If the value-to-pack is too long, it is truncated.  If too long and an
explicit count is provided, C<Z> packs only C<$count-1> bytes, followed
by a null byte.  Thus C<Z> always packs a trailing null byte under
all circumstances.

=item *

=begin original

Likewise, the C<b> and C<B> fields pack a string that many bits long.
Each byte of the input field of pack() generates 1 bit of the result.
Each result bit is based on the least-significant bit of the corresponding
input byte, i.e., on C<ord($byte)%2>.  In particular, bytes C<"0"> and
C<"1"> generate bits 0 and 1, as do bytes C<"\0"> and C<"\1">.

=end original

同様に、C<b> や C<B> は、繰り返し数で示すビット長のビット列に pack します。
pack() の入力フィールドの各バイトは結果の 1 ビットを生成します。
結果ビットのそれぞれは対応する入力バイトの最下位ビットを基にします
(つまり C<ord($byte)%2>)。
特に、バイト C<"0"> と C<"1"> は
バイト C<"\0"> と C<"\1"> と同様に、ビット 0 と 1 を生成します。

Starting from the beginning of the input string of pack(), each 8-tuple
of bytes is converted to 1 byte of output.  With format C<b>
the first byte of the 8-tuple determines the least-significant bit of a
byte, and with format C<B> it determines the most-significant bit of
a byte.

If the length of the input string is not exactly divisible by 8, the
remainder is packed as if the input string were padded by null bytes
at the end.  Similarly, during unpack()ing the "extra" bits are ignored.

If the input string of pack() is longer than needed, extra bytes are ignored.
A C<*> for the repeat count of pack() means to use all the bytes of
the input field.  On unpack()ing the bits are converted to a string
of C<"0">s and C<"1">s.

=item *

=begin original

The C<h> and C<H> fields pack a string that many nybbles (4-bit groups,
representable as hexadecimal digits, 0-9a-f) long.

=end original

C<h> や C<H> は、多ニブル長(16 進文字である 0-9a-f で表現可能な
4 ビットグループ)のニブル列に pack します。

Each byte of the input field of pack() generates 4 bits of the result.
For non-alphabetical bytes the result is based on the 4 least-significant
bits of the input byte, i.e., on C<ord($byte)%16>.  In particular,
bytes C<"0"> and C<"1"> generate nybbles 0 and 1, as do bytes
C<"\0"> and C<"\1">.  For bytes C<"a".."f"> and C<"A".."F"> the result
is compatible with the usual hexadecimal digits, so that C<"a"> and
C<"A"> both generate the nybble C<0xa==10>.  The result for bytes
C<"g".."z"> and C<"G".."Z"> is not well-defined.

Starting from the beginning of the input string of pack(), each pair
of bytes is converted to 1 byte of output.  With format C<h> the
first byte of the pair determines the least-significant nybble of the
output byte, and with format C<H> it determines the most-significant
nybble.

If the length of the input string is not even, it behaves as if padded
by a null byte at the end.  Similarly, during unpack()ing the "extra"
nybbles are ignored.

If the input string of pack() is longer than needed, extra bytes are ignored.
A C<*> for the repeat count of pack() means to use all the bytes of
the input field.  On unpack()ing the bits are converted to a string
of hexadecimal digits.

=item *

=begin original

The C<p> type packs a pointer to a null-terminated string.  You are
responsible for ensuring the string is not a temporary value (which can
potentially get deallocated before you get around to using the packed result).
The C<P> type packs a pointer to a structure of the size indicated by the
length.  A NULL pointer is created if the corresponding value for C<p> or
C<P> is C<undef>, similarly for unpack().

=end original

C<p>は、ヌル文字終端文字列へのポインタを pack します。
文字列が一時的な値でない(つまり pack された結果を使う前に文字列が開放されない) ことに
責任を持つ必要があります
C<P> は、指定した長さの構造体へのポインタを pack します。
C<p>またはC<P>に対応する値が C<undef> だった場合、
unpack() と同様にヌルポインタが作成されます。

=item *

The C</> template character allows packing and unpacking of strings where
the packed structure contains a byte count followed by the string itself.
You write I<length-item>C</>I<string-item>.

The I<length-item> can be any C<pack> template letter,
and describes how the length value is packed.
The ones likely to be of most use are integer-packing ones like
C<n> (for Java strings), C<w> (for ASN.1 or SNMP)
and C<N> (for Sun XDR).

The I<string-item> must, at present, be C<"A*">, C<"a*"> or C<"Z*">.
For C<unpack> the length of the string is obtained from the I<length-item>,
but if you put in the '*' it will be ignored.

    unpack 'C/a', "\04Gurusamy";        gives 'Guru'
    unpack 'a3/A* A*', '007 Bond  J ';  gives (' Bond','J')
    pack 'n/a* w/a*','hello,','world';  gives "\000\006hello,\005world"

The I<length-item> is not returned explicitly from C<unpack>.

Adding a count to the I<length-item> letter is unlikely to do anything
useful, unless that letter is C<A>, C<a> or C<Z>.  Packing with a
I<length-item> of C<a> or C<Z> may introduce C<"\000"> characters,
which Perl does not regard as legal in numeric strings.

=item *

The integer types C<s>, C<S>, C<l>, and C<L> may be
immediately followed by a C<!> suffix to signify native shorts or
longs--as you can see from above for example a bare C<l> does mean
exactly 32 bits, the native C<long> (as seen by the local C compiler)
may be larger.  This is an issue mainly in 64-bit platforms.  You can
see whether using C<!> makes any difference by

	print length(pack("s")), " ", length(pack("s!")), "\n";
	print length(pack("l")), " ", length(pack("l!")), "\n";

C<i!> and C<I!> also work but only because of completeness;
they are identical to C<i> and C<I>.

The actual sizes (in bytes) of native shorts, ints, longs, and long
longs on the platform where Perl was built are also available via
L<Config>:

       use Config;
       print $Config{shortsize},    "\n";
       print $Config{intsize},      "\n";
       print $Config{longsize},     "\n";
       print $Config{longlongsize}, "\n";

(The C<$Config{longlongsize}> will be undefine if your system does
not support long longs.) 

=item *

The integer formats C<s>, C<S>, C<i>, C<I>, C<l>, and C<L>
are inherently non-portable between processors and operating systems
because they obey the native byteorder and endianness.  For example a
4-byte integer 0x12345678 (305419896 decimal) be ordered natively
(arranged in and handled by the CPU registers) into bytes as

 	0x12 0x34 0x56 0x78	# big-endian
 	0x78 0x56 0x34 0x12	# little-endian

Basically, the Intel and VAX CPUs are little-endian, while everybody
else, for example Motorola m68k/88k, PPC, Sparc, HP PA, Power, and
Cray are big-endian.  Alpha and MIPS can be either: Digital/Compaq
used/uses them in little-endian mode; SGI/Cray uses them in big-endian mode.

The names `big-endian' and `little-endian' are comic references to
the classic "Gulliver's Travels" (via the paper "On Holy Wars and a
Plea for Peace" by Danny Cohen, USC/ISI IEN 137, April 1, 1980) and
the egg-eating habits of the Lilliputians.

Some systems may have even weirder byte orders such as

 	0x56 0x78 0x12 0x34
 	0x34 0x12 0x78 0x56

You can see your system's preference with

 	print join(" ", map { sprintf "%#02x", $_ }
                            unpack("C*",pack("L",0x12345678))), "\n";

The byteorder on the platform where Perl was built is also available
via L<Config>:

	use Config;
	print $Config{byteorder}, "\n";

=begin original

Byteorders C<'1234'> and C<'12345678'> are little-endian, C<'4321'>
and C<'87654321'> are big-endian.

=end original

C<'1234'> と C<'12345678'> はリトルエンディアン、
C<'4321'> と C<'87654321'> はビッグエンディアンです。

=begin original

If you want portable packed integers use the formats C<n>, C<N>,
C<v>, and C<V>, their byte endianness and size is known.
See also L<perlport>.

=end original

移植性のあるパック化された整数がほしい場合は、
C<n>, C<N>, C<v>, C<V> を使ってください。
これらはバイトエンディアンとサイズが固定されています。
L<perlport> も参照して下さい。

=item *

=begin original

Real numbers (floats and doubles) are in the native machine format only;
due to the multiplicity of floating formats around, and the lack of a
standard "network" representation, no facility for interchange has been
made.  This means that packed floating point data written on one machine
may not be readable on another - even if both use IEEE floating point
arithmetic (as the endian-ness of the memory representation is not part
of the IEEE spec).  See also L<perlport>.

=end original

実数 (float と double) は、
機種依存のフォーマットしかありません。
いろんな浮動小数点数のフォーマットが在り、
標準的な "network" 表現といったものがないため、
データ交換のための機能は用意してありません。
つまり、あるマシンで pack した浮動小数点数は、
別のマシンでは読めないかもしれないということです。
たとえ双方で IEEE フォーマットの浮動小数点数演算を
行なっていてもです (IEEE の仕様では、メモリ表現上のバイト順序までは、
規定されていないからです)。L<perlport>も参照してください。

=begin original

Note that Perl uses doubles internally for all numeric calculation, and
converting from double into float and thence back to double again will
lose precision (i.e., C<unpack("f", pack("f", $foo)>) will not in general
equal $foo).

=end original

Perl では、すべての数値演算のために、
内部的に double を使用しています。
double から float へ変換し、それから再び double に戻すと
精度が落ちることになります (つまり、C<unpack("f", pack("f", $foo)>)
は、一般には $foo と同じではないということです)。

=item *

If the pattern begins with a C<U>, the resulting string will be treated
as Unicode-encoded. You can force UTF8 encoding on in a string with an
initial C<U0>, and the bytes that follow will be interpreted as Unicode
characters. If you don't want this to happen, you can begin your pattern
with C<C0> (or anything else) to force Perl not to UTF8 encode your
string, and then follow this with a C<U*> somewhere in your pattern.

=item *

You must yourself do any alignment or padding by inserting for example
enough C<'x'>es while packing.  There is no way to pack() and unpack()
could know where the bytes are going to or coming from.  Therefore
C<pack> (and C<unpack>) handle their output and input as flat
sequences of bytes.

=item *

=begin original

A comment in a TEMPLATE starts with C<#> and goes to the end of line.

=end original

TEMPLATE の中の C<#> から行末まではコメントです。

=item *

=begin original

If TEMPLATE requires more arguments to pack() than actually given, pack()
assumes additional C<""> arguments.  If TEMPLATE requires less arguments
to pack() than actually given, extra arguments are ignored.

=end original

TEMPLATE が要求する引数の数が pack() が実際に与えている数より多い場合、
追加の C<""> 引数があるものと仮定します。
TEMPLATE が要求する引数の数の方が少ない場合、余分の引数は無視されます。

=back

=begin original

Examples:

=end original

例:

    $foo = pack("CCCC",65,66,67,68);
    # foo eq "ABCD"
    $foo = pack("C4",65,66,67,68);
    # same thing
    $foo = pack("U4",0x24b6,0x24b7,0x24b8,0x24b9);
    # same thing with Unicode circled letters

    $foo = pack("ccxxcc",65,66,67,68);
    # foo eq "AB\0\0CD"

    # note: the above examples featuring "C" and "c" are true
    # only on ASCII and ASCII-derived systems such as ISO Latin 1
    # and UTF-8.  In EBCDIC the first example would be
    # $foo = pack("CCCC",193,194,195,196);

    $foo = pack("s2",1,2);
    # "\1\0\2\0" on little-endian
    # "\0\1\0\2" on big-endian

    $foo = pack("a4","abcd","x","y","z");
    # "abcd"

    $foo = pack("aaaa","abcd","x","y","z");
    # "axyz"

    $foo = pack("a14","abcdefg");
    # "abcdefg\0\0\0\0\0\0\0"

    $foo = pack("i9pl", gmtime);
    # a real struct tm (on my system anyway)

    $utmp_template = "Z8 Z8 Z16 L";
    $utmp = pack($utmp_template, @utmp1);
    # a struct utmp (BSDish)

    @utmp2 = unpack($utmp_template, $utmp);
    # "@utmp1" eq "@utmp2"

    sub bintodec {
	unpack("N", pack("B32", substr("0" x 32 . shift, -32)));
    }

    $foo = pack('sx2l', 12, 34);
    # short 12, two zero bytes padding, long 34
    $bar = pack('s@4l', 12, 34);
    # short 12, zero fill to position 4, long 34
    # $foo eq $bar

=begin original

The same template may generally also be used in unpack().

=end original

一般には、pack で使用したものと同じテンプレートが、
unpack 関数でも使用できます。

=item package NAMESPACE

=item package 

Declares the compilation unit as being in the given namespace.  The scope
of the package declaration is from the declaration itself through the end
of the enclosing block, file, or eval (the same as the C<my> operator).
All further unqualified dynamic identifiers will be in this namespace.
A package statement affects only dynamic variables--including those
you've used C<local> on--but I<not> lexical variables, which are created
with C<my>.  Typically it would be the first declaration in a file to
be included by the C<require> or C<use> operator.  You can switch into a
package in more than one place; it merely influences which symbol table
is used by the compiler for the rest of that block.  You can refer to
variables and filehandles in other packages by prefixing the identifier
with the package name and a double colon:  C<$Package::Variable>.
If the package name is null, the C<main> package as assumed.  That is,
C<$::sail> is equivalent to C<$main::sail> (as well as to C<$main'sail>,
still seen in older code).

If NAMESPACE is omitted, then there is no current package, and all
identifiers must be fully qualified or lexicals.  This is stricter
than C<use strict>, since it also extends to function names.

=begin original

See L<perlmod/"Packages"> for more information about packages, modules,
and classes.  See L<perlsub> for other scoping issues.

=end original

パッケージ、モジュール、クラスに関するさらなる情報については
L<perlmod/"Packages"> を参照してください。
その他のスコープに関する話題については L<perlsub> を参照してください。

=item pipe READHANDLE,WRITEHANDLE

=begin original

Opens a pair of connected pipes like the corresponding system call.
Note that if you set up a loop of piped processes, deadlock can occur
unless you are very careful.  In addition, note that Perl's pipes use
stdio buffering, so you may need to set C<$|> to flush your WRITEHANDLE
after each command, depending on the application.

=end original

対応するシステムコールと同じように、
接続されたパイプのペアをオープンします。
パイプでプロセスをループにするときには、よほど気を付けないと、
デッドロックが起こり得ます。
さらに、Perl のパイプでは、stdio のバッファリングを使いますから、
アプリケーションによっては、
コマンドごとに WRITEHANDLE をフラッシュするように、
C<$|> を設定することが必要になるかもしれません。

=begin original

See L<IPC::Open2>, L<IPC::Open3>, and L<perlipc/"Bidirectional Communication">
for examples of such things.

=end original

これらに関する例については、L<IPC::Open2>, L<IPC::Open3>,
L<perlipc/"Bidirectional Communication"> を参照して下さい。

=begin original

On systems that support a close-on-exec flag on files, the flag will be set
for the newly opened file descriptors as determined by the value of $^F.
See L<perlvar/$^F>.

=end original

ファイルに対する close-on-exec フラグをサポートしているシステムでは、
フラグは$^F の値で決定される、新しくオープンされたファイル記述子に対して
セットされます。L<perlvar/$^F>を参照してください。

=item pop ARRAY

=item pop

=begin original

Pops and returns the last value of the array, shortening the array by
one element.  Has an effect similar to

=end original

配列の最後の値をポップして返し、配列の大きさを 1 だけ小さくします。
これは、以下と同じ効果があります。

    $ARRAY[$#ARRAY--]

=begin original

If there are no elements in the array, returns the undefined value
(although this may happen at other times as well).  If ARRAY is
omitted, pops the C<@ARGV> array in the main program, and the C<@_>
array in subroutines, just like C<shift>.

=end original

指定された配列に要素がなければ、未定義値が返されます
(しかしこれは他の場合にも起こり得ます)。ARRAY が省略されると、
C<shift> と同様に、メインプログラムでは C<@ARGV>が、サブルーチンではC<@_>が使われます。

=item pos SCALAR

=item pos

=begin original

Returns the offset of where the last C<m//g> search left off for the variable
in question (C<$_> is used when the variable is not specified).  May be
modified to change that offset.  Such modification will also influence
the C<\G> zero-width assertion in regular expressions.  See L<perlre> and
L<perlop>.

=end original

対象の変数に対して、前回の C<m//g> が終了した場所の
オフセットを返します(変数が指定されなかった場合は C<$_> が使われます)。
そのオフセットを変えるために、変更することができます。
そのような変更は正規表現における C<\G> ゼロ幅アサートにも
影響を与えます。
L<perlre> と L<perlop> を参照してください。

=item print FILEHANDLE LIST

=item print LIST

=item print

=begin original

Prints a string or a list of strings.  Returns true if successful.
FILEHANDLE may be a scalar variable name, in which case the variable
contains the name of or a reference to the filehandle, thus introducing
one level of indirection.  (NOTE: If FILEHANDLE is a variable and
the next token is a term, it may be misinterpreted as an operator
unless you interpose a C<+> or put parentheses around the arguments.)
If FILEHANDLE is omitted, prints by default to standard output (or
to the last selected output channel--see L</select>).  If LIST is
also omitted, prints C<$_> to the currently selected output channel.
To set the default output channel to something other than STDOUT
use the select operation.  The current value of C<$,> (if any) is
printed between each LIST item.  The current value of C<$\> (if
any) is printed after the entire LIST has been printed.  Because
print takes a LIST, anything in the LIST is evaluated in list
context, and any subroutine that you call will have one or more of
its expressions evaluated in list context.  Also be careful not to
follow the print keyword with a left parenthesis unless you want
the corresponding right parenthesis to terminate the arguments to
the print--interpose a C<+> or put parentheses around all the
arguments.

=end original

文字列か文字列のリストを出力します。
成功時には、真を返します。FILEHANDLE は、
スカラ変数名でもよく、その場合には、その変数にファイル
ハンドル名またはそのリファレンスが
入っているものとして扱われますから、一段階の間接指定が行なえます。
(注: FILEHANDLE に変数を使い、次のトークンが「項」のときには、
間に C<+> を置くか、引数の前後を括弧で括らなければ、
誤って解釈されることがあります。)
FILEHANDLE を省略した場合には、標準出力 (か、
最後に選択された出力チャネル --- L</select>を参照) に出力します。
LIST も省略すると、C<$_> を現在選択されている出力チャネルに
出力することになります。
デフォルトの出力チャネルを STDOUT 以外にしたければ、select 演算子を
使ってください。
C<$,> の値が(もしあれば)各 LIST 要素の間に出力されます。
print の引数は LIST なので、LISTの中のものは、
すべてリストコンテキストで評価されます。
サブルーチンの呼び出しがあれば、リストコンテキストでは、
複数の値を返すかもしれません。また、すべての
引数を括弧で括るのでなければ、print というキーワードの
次に開き括弧を書いてはいけません。
"print" と引数の間に C<+> を書くか、
すべての引数を括弧で括ってください。

=begin original

Note that if you're storing FILEHANDLES in an array or other expression,
you will have to use a block returning its value instead:

=end original

もし FILESHANDLES を配列やその他の表現に保存している場合は、
代わりにその値を返すブロックを使う必要があります:

    print { $files[$i] } "stuff\n";
    print { $OK ? STDOUT : STDERR } "stuff\n";

=item printf FILEHANDLE FORMAT, LIST

=item printf FORMAT, LIST

=begin original

Equivalent to C<print FILEHANDLE sprintf(FORMAT, LIST)>, except that C<$\>
(the output record separator) is not appended.  The first argument
of the list will be interpreted as the C<printf> format.  If C<use locale> is
in effect, the character used for the decimal point in formatted real numbers
is affected by the LC_NUMERIC locale.  See L<perllocale>.

=end original

C<$\>(出力レコードセパレータ)を追加しないことを除けば、
C<print FILEHANDLE sprintf(FORMAT, LIST)> と等価です。
リストの最初の要素は、C<printf> フォーマットと解釈されます。
C<use locale>が効力をもっていれば、小数点に使われる文字は
LC_NUMERIC ロケールの影響を受けます。
L<perllocale> を参照してください。

=begin original

Don't fall into the trap of using a C<printf> when a simple
C<print> would do.  The C<print> is more efficient and less
error prone.

=end original

単純な C<print> を使うべきところで C<printf> を使ってしまう
罠にかからないようにしてください。
C<print> はより効率的で、間違いが起こりにくいです。

=item prototype FUNCTION

=begin original

Returns the prototype of a function as a string (or C<undef> if the
function has no prototype).  FUNCTION is a reference to, or the name of,
the function whose prototype you want to retrieve.

=end original

関数のプロトタイプを文字列として返します(関数にプロトタイプがない場合は
C<undef> を返します)。
FUNCTION はプロトタイプを得たい関数の名前、またはリファレンスです。

=begin original

If FUNCTION is a string starting with C<CORE::>, the rest is taken as a
name for Perl builtin.  If the builtin is not I<overridable> (such as
C<qw//>) or its arguments cannot be expressed by a prototype (such as
C<system>) returns C<undef> because the builtin does not really behave
like a Perl function.  Otherwise, the string describing the equivalent
prototype is returned.

=end original

FUNCTION が C<CORE::> で始まっている場合、残りは Perl ビルドインの名前として
扱われます。
このビルドインが(C<qw//> のように) I<オーバーライド可能> でない、
またはこの引数が(C<system> のように)プロトタイプとして記述できない場合、
C<undef> を返します。なぜならビルドインは実際に Perl 関数のように
振舞わないからです。それ以外では、
等価なプロトタイプを表現した文字列が返されます。

=item push ARRAY,LIST

=begin original

Treats ARRAY as a stack, and pushes the values of LIST
onto the end of ARRAY.  The length of ARRAY increases by the length of
LIST.  Has the same effect as

    for $value (LIST) {
	$ARRAY[++$#ARRAY] = $value;
    }

but is more efficient.  Returns the new number of elements in the array.

=end original

ARRAY をスタックとして扱い、LIST 内の値を ARRAY の終わりにプッシュします。
ARRAY の大きさは、LIST の長さ分だけ大きくなります。これは、

    for $value (LIST) {
	$ARRAY[++$#ARRAY] = $value;
    }

とするのと同じ効果がありますが、より効率的です。
変更後の配列の要素数を返します。

=item q/STRING/

=item qq/STRING/

=item qr/STRING/

=item qx/STRING/

=item qw/STRING/

=begin original

Generalized quotes.  See L<perlop/"Regexp Quote-Like Operators">.

=end original

汎用のクォートです。
L<perlop/"Regexp Quote-Like Operators">を参照してください。

=item quotemeta EXPR

=item quotemeta

=begin original

Returns the value of EXPR with all non-"word"
characters backslashed.  (That is, all characters not matching
C</[A-Za-z_0-9]/> will be preceded by a backslash in the
returned string, regardless of any locale settings.)
This is the internal function implementing
the C<\Q> escape in double-quoted strings.

=end original

EXPR の中のすべての非英数字キャラクタをバックスラッシュで
エスケープしたものを返します
(つまり、C</[A-Za-z_0-9]/> にマッチしない全ての文字の前には
ロケールに関わらずバックスラッシュが前置されます)。
これは、ダブルクォート文字列での C<\Q> エスケープを
実装するための内部関数です。

=begin original

If EXPR is omitted, uses C<$_>.

=end original

EXPR が省略されると、C<$_> を使います。

=item rand EXPR

=item rand

=begin original

Returns a random fractional number greater than or equal to C<0> and less
than the value of EXPR.  (EXPR should be positive.)  If EXPR is
omitted, the value C<1> is used.  Automatically calls C<srand> unless
C<srand> has already been called.  See also C<srand>.

=end original

C<0> 以上 EXPR の値未満の小数の乱数値を返します。
(EXPRは正の数である必要があります。)
EXPR を省略すると、C<1> とみなします。
C<srand> が既に呼ばれている場合以外は、自動的に C<srand> 関数を
呼び出します。C<srand> も参照してください。

=begin original

(Note: If your rand function consistently returns numbers that are too
large or too small, then your version of Perl was probably compiled
with the wrong number of RANDBITS.)

=end original

(注: もし、rand 関数が、常に大きい値ばかりや、小さい数ばかりを
返すようなら、お使いになっている Perl が、
良くない RANDBITS を使ってコンパイルされている可能性があります。)

=item read FILEHANDLE,SCALAR,LENGTH,OFFSET

=item read FILEHANDLE,SCALAR,LENGTH

=begin original

Attempts to read LENGTH bytes of data into variable SCALAR from the
specified FILEHANDLE.  Returns the number of bytes actually read, C<0>
at end of file, or undef if there was an error.  SCALAR will be grown
or shrunk to the length actually read.  If SCALAR needs growing, the
new bytes will be zero bytes.  An OFFSET may be specified to place
the read data into some other place in SCALAR than the beginning.
The call is actually implemented in terms of stdio's fread(3) call.
To get a true read(2) system call, see C<sysread>.

=end original

指定した FILEHANDLE から、変数 SCALAR に LENGTH バイトの
データを読み込みます。
実際に読み込まれたバイト数、
ファイル終端の場合は C<0>、エラーの場合は undef のいずれかを返します。
SCALAR は、実際に読み込んだ長さに応じて、伸び縮みします。
SCALAR が伸びた場合、伸びた部分はバイト 0 になります。
OFFSET を指定すると、SCALAR の先頭以外の場所から、
読み込みを行なうことができます。
この関数は、stdio ライブラリの fread(3) 関数を使って実装しています。
本当の read(2) システムコールを利用するには、C<sysread> を参照してください。

=item readdir DIRHANDLE

=begin original

Returns the next directory entry for a directory opened by C<opendir>.
If used in list context, returns all the rest of the entries in the
directory.  If there are no more entries, returns an undefined value in
scalar context or a null list in list context.

=end original

C<opendir> でオープンしたディレクトリで、
次のディレクトリエントリを返します。
リストコンテキストで用いると、
そのディレクトリの残りのエントリを、すべて返します。
エントリが残っていない場合には、スカラコンテキストでは未定義値を、
リストコンテキストでは空リストを返します。

=begin original

If you're planning to filetest the return values out of a C<readdir>, you'd
better prepend the directory in question.  Otherwise, because we didn't
C<chdir> there, it would have been testing the wrong file.

=end original

C<readdir> の返り値をファイルテストに使おうと計画しているなら、
頭にディレクトリをつける必要があります。
さもなければ、ここでは C<chdir> はしないので、
間違ったファイルをテストしてしまうことになるでしょう。

    opendir(DIR, $some_dir) || die "can't opendir $some_dir: $!";
    @dots = grep { /^\./ && -f "$some_dir/$_" } readdir(DIR);
    closedir DIR;

=item readline EXPR

=begin original

Reads from the filehandle whose typeglob is contained in EXPR.  In scalar
context, each call reads and returns the next line, until end-of-file is
reached, whereupon the subsequent call returns undef.  In list context,
reads until end-of-file is reached and returns a list of lines.  Note that
the notion of "line" used here is however you may have defined it
with C<$/> or C<$INPUT_RECORD_SEPARATOR>).  See L<perlvar/"$/">.

=end original

typeglob が EXPR に含まれているファイルハンドルから読み込みます。
スカラコンテキストでは、呼び出し毎に一行読み込んで返します。
ファイルの最後まで読み込んだら、以後の呼び出しでは undef を返します。
リストコンテキストでは、ファイルの最後まで読み込んで、
行のリストを返します。
ここでの「行」とは、C<$/> または C<$INPUT_RECORD_SEPARATOR> で
定義されることに注意してください。L<perlvar/"$/"> も参照して下さい。

=begin original

When C<$/> is set to C<undef>, when readline() is in scalar
context (i.e. file slurp mode), and when an empty file is read, it
returns C<''> the first time, followed by C<undef> subsequently.

=end original

C<$/> に C<undef> を設定した場合は、readline() はスカラコンテキスト
(つまりファイル吸い込みモード)となり、
空のファイルを読み込んだ場合は、最初は C<''> を返し、
それ以降は C<undef> を返します。

=begin original

This is the internal function implementing the C<< <EXPR> >>
operator, but you can use it directly.  The C<< <EXPR> >>
operator is discussed in more detail in L<perlop/"I/O Operators">.

=end original

これは C<< <EXPR> >> 演算子を実装している内部関数ですが、
直接使うこともできます。C<< <EXPR> >> 演算子についての
さらなる詳細については L<perlop/"I/O Operators"> で議論されています。

    $line = <STDIN>;
    $line = readline(*STDIN);		# same thing

=item readlink EXPR

=item readlink

=begin original

Returns the value of a symbolic link, if symbolic links are
implemented.  If not, gives a fatal error.  If there is some system
error, returns the undefined value and sets C<$!> (errno).  If EXPR is
omitted, uses C<$_>.

=end original

シンボリックリンクが実装されていれば、
シンボリックリンクの値を返します。
実装されていないときには、致命的エラーとなります。
何らかのシステムエラーが検出されると、
未定義値を返し、C<$!> (errno)を設定します。
EXPR を省略すると、C<$_> を使用します。

=item readpipe EXPR

=begin original

EXPR is executed as a system command.
The collected standard output of the command is returned.
In scalar context, it comes back as a single (potentially
multi-line) string.  In list context, returns a list of lines
(however you've defined lines with C<$/> or C<$INPUT_RECORD_SEPARATOR>).
This is the internal function implementing the C<qx/EXPR/>
operator, but you can use it directly.  The C<qx/EXPR/>
operator is discussed in more detail in L<perlop/"I/O Operators">.

=end original

EXPR がシステムコマンドとして実行されます。
コマンドの標準出力の内容が返されます。
スカラコンテキストでは、単一の(内部的に複数行の)文字列を返します。
リストコンテキストでは、行のリストを返します
(但し、行は C<$/> または C<$INPUT_RECORD_SEPARATOR> で定義されます)。
これは C<qx/EXPR/> 演算子を実装する内部関数ですが、
直接使うことも出来ます。
C<qx/EXPR/> 演算子は L<perlop/"I/O Operators"> でより詳細に
述べられています。

=item recv SOCKET,SCALAR,LENGTH,FLAGS

=begin original

Receives a message on a socket.  Attempts to receive LENGTH bytes of
data into variable SCALAR from the specified SOCKET filehandle.  SCALAR
will be grown or shrunk to the length actually read.  Takes the same
flags as the system call of the same name.  Returns the address of the
sender if SOCKET's protocol supports this; returns an empty string
otherwise.  If there's an error, returns the undefined value.  This call
is actually implemented in terms of recvfrom(2) system call.  See
L<perlipc/"UDP: Message Passing"> for examples.

=end original

ソケット上のメッセージを受信します。
指定されたファイルハンドル SOCKET から、変数 SCALAR に
LENGTH バイトのデータを読み込もうとします。
SCALAR は、実際に読まれた長さによって、大きくなったり、
小さくなったりします。
同名のシステムコールと同じ FLAGS を使います。
SOCKET のプロトコルが対応していれば、送信側のアドレスを返します。
エラー発生時には、未定義値を返します。
実際には、C のrecvfrom(2) を呼びます。
例についてはL<perlipc/"UDP: Message Passing">を参照してください。

=item redo LABEL

=item redo

=begin original

The C<redo> command restarts the loop block without evaluating the
conditional again.  The C<continue> block, if any, is not executed.  If
the LABEL is omitted, the command refers to the innermost enclosing
loop.  This command is normally used by programs that want to lie to
themselves about what was just input:

=end original

redo コマンドは、条件を再評価しないで、ループブロックの始めからもう一度
実行を開始します。C<continue> ブロックがあっても、実行されません。
LABEL が省略されると、このコマンドは、もっとも内側のループを参照します。
このコマンドは通常、自分への入力を欺くために使用します:

    # a simpleminded Pascal comment stripper
    # (warning: assumes no { or } in strings)
    LINE: while (<STDIN>) {
	while (s|({.*}.*){.*}|$1 |) {}
	s|{.*}| |;
	if (s|{.*| |) {
	    $front = $_;
	    while (<STDIN>) {
		if (/}/) {	# end of comment?
		    s|^|$front\{|;
		    redo LINE;
		}
	    }
	}
	print;
    }

C<redo> cannot be used to retry a block which returns a value such as
C<eval {}>, C<sub {}> or C<do {}>, and should not be used to exit
a grep() or map() operation.

Note that a block by itself is semantically identical to a loop
that executes once.  Thus C<redo> inside such a block will effectively
turn it into a looping construct.

=begin original

See also L</continue> for an illustration of how C<last>, C<next>, and
C<redo> work.

=end original

C<last>, C<next>, C<redo> がどのように働くかについては
L</continue> を参照して下さい。

=item ref EXPR

=item ref

=begin original

Returns a true value if EXPR is a reference, false otherwise.  If EXPR
is not specified, C<$_> will be used.  The value returned depends on the
type of thing the reference is a reference to.
Builtin types include:

=end original

EXPR がリファレンスであれば、真を返し、さもなくば、
偽を返します。EXPR が指定されなければ、C<$_> が使われます。
返される値は、リファレンスが参照するものの型に依存します。
組み込みの型には、以下のものがあります。

    SCALAR
    ARRAY
    HASH
    CODE
    REF
    GLOB
    LVALUE

=begin original

If the referenced object has been blessed into a package, then that package
name is returned instead.  You can think of C<ref> as a C<typeof> operator.

=end original

参照されるオブジェクトが、何らかのパッケージに
bless されたものであれば、これらの代わりに、
そのパッケージ名が返されます。
C<ref> は、C<typeof> 演算子のように考えることができます。

    if (ref($r) eq "HASH") {
	print "r is a reference to a hash.\n";
    }
    unless (ref($r)) {
	print "r is not a reference at all.\n";
    }
    if (UNIVERSAL::isa($r, "HASH")) {  # for subclassing
	print "r is a reference to something that isa hash.\n";
    } 

=begin original

See also L<perlref>.

=end original

L<perlref> も参照してください。

=item rename OLDNAME,NEWNAME

=begin original

Changes the name of a file; an existing file NEWNAME will be
clobbered.  Returns true for success, false otherwise.

=end original

ファイルの名前を変更します。NEWNAME というファイルが
既に存在した場合、上書きされるかもしれません。
成功時には真、失敗時には偽を返します。

=begin original

Behavior of this function varies wildly depending on your system
implementation.  For example, it will usually not work across file system
boundaries, even though the system I<mv> command sometimes compensates
for this.  Other restrictions include whether it works on directories,
open files, or pre-existing files.  Check L<perlport> and either the
rename(2) manpage or equivalent system documentation for details.

=end original

この関数の振る舞いはシステムの実装に大きく依存して異なります。
普通はファイルシステムにまたがってパス名を付け替えることはできません。
システムの I<mv> がこれを補完している場合でもそうです。
その他の制限には、ディレクトリ、オープンしているファイル、既に存在している
ファイルに対して使えるか、といったことを含みます。
詳しくは、L<perlport> および rename(2) man ページあるいは同様の
システムドキュメントを参照してください。

=item require VERSION

=item require EXPR

=item require

=begin original

Demands some semantics specified by EXPR, or by C<$_> if EXPR is not
supplied.

=end original

指定する EXPR か、EXPR 省略時には C<$_> によって、
いくつかの意味を使い分けます。

=begin original

If a VERSION is specified as a literal of the form v5.6.1,
demands that the current version of Perl (C<$^V> or $PERL_VERSION) be
at least as recent as that version, at run time.  (For compatibility
with older versions of Perl, a numeric argument will also be interpreted
as VERSION.)  Compare with L</use>, which can do a similar check at
compile time.

=end original

VERSION が v5.6.1 の形で指定されると、実行時の
Perl の現在のバージョン(C<$^V> または $PERL_VERSION)が
そのバージョン以降であることを要求します。
(古いバージョンの Perl との互換性のために、数値の引数も
VERSION として解釈されます)
L</use> と似ていますが、これはコンパイル時にチェックされます。

    require v5.6.1;	# run time version check
    require 5.6.1;	# ditto
    require 5.005_03;	# float version allowed for compatibility

=begin original

Otherwise, demands that a library file be included if it hasn't already
been included.  The file is included via the do-FILE mechanism, which is
essentially just a variety of C<eval>.  Has semantics similar to the following
subroutine:

=end original

それ以外の場合には、既に読み込まれていないときに、
読み込むライブラリファイルを要求するものとなります。
そのファイルは、基本的には C<eval> の一種である、
do-FILE によって読み込まれます。　意味的には、
次のようなサブルーチンと同じようなものです:

    sub require {
	my($filename) = @_;
	return 1 if $INC{$filename};
	my($realfilename,$result);
	ITER: {
	    foreach $prefix (@INC) {
		$realfilename = "$prefix/$filename";
		if (-f $realfilename) {
		    $INC{$filename} = $realfilename;
		    $result = do $realfilename;
		    last ITER;
		}
	    }
	    die "Can't find $filename in \@INC";
	}
	delete $INC{$filename} if $@ || !$result;
	die $@ if $@;
	die "$filename did not return true value" unless $result;
	return $result;
    }

=begin original

Note that the file will not be included twice under the same specified
name.  The file must return true as the last statement to indicate
successful execution of any initialization code, so it's customary to
end such a file with C<1;> unless you're sure it'll return true
otherwise.  But it's better just to put the C<1;>, in case you add more
statements.

=end original

ファイルは、同じ名前で 2 回読み込まれることはありません。
初期化コードの実行がうまくいったことを示すために、
ファイルは真を返さなければなりませんから、
真を返すようになっている自信がある場合を除いては、
ファイルの最後に C<1;> と書くのが習慣です。
実行文を追加するような場合に備えて、C<1;> と書いておいた方が
良いでしょう。

=begin original

If EXPR is a bareword, the require assumes a "F<.pm>" extension and
replaces "F<::>" with "F</>" in the filename for you,
to make it easy to load standard modules.  This form of loading of
modules does not risk altering your namespace.

=end original

EXPR が裸の単語であるときには、標準モジュールのロードを
簡単にするように、require は拡張子が "F<.pm>" であり、
"F<::>" を "F</>" に変えたものがファイル名であると仮定します。
この形式のモジュールロードは、
名前空間を変更してしまう危険はありません。

=begin original

In other words, if you try this:

=end original

言い換えると、以下のようにすると:

        require Foo::Bar;    # a splendid bareword 

=begin original

The require function will actually look for the "F<Foo/Bar.pm>" file in the 
directories specified in the C<@INC> array.

But if you try this:

=end original

require 関数は C<@INC> 配列で指定されたディレクトリにある
"F<Foo/Bar.pm>" ファイルを探します。

しかし、以下のようにすると:

        $class = 'Foo::Bar';
        require $class;	     # $class is not a bareword
    #or
        require "Foo::Bar";  # not a bareword because of the ""

=begin original

The require function will look for the "F<Foo::Bar>" file in the @INC array and 
will complain about not finding "F<Foo::Bar>" there.  In this case you can do:

=end original

require 関数は @INC 配列の "F<Foo::Bar>" ファイルを探し、
おそらくそこに "F<Foo::Bar>" がないと文句をいうことになるでしょう。
このような場合には、以下のようにします:

        eval "require $class";

=begin original

For a yet-more-powerful import facility, see L</use> and L<perlmod>.

=end original

より強力な import 機能については、このドキュメントの
L</use> の項と、L<perlmod> をご覧ください。

=item reset EXPR

=item reset

=begin original

Generally used in a C<continue> block at the end of a loop to clear
variables and reset C<??> searches so that they work again.  The
expression is interpreted as a list of single characters (hyphens
allowed for ranges).  All variables and arrays beginning with one of
those letters are reset to their pristine state.  If the expression is
omitted, one-match searches (C<?pattern?>) are reset to match again.  Resets
only variables or searches in the current package.  Always returns
1.  Examples:

=end original

通常、ループの最後に、変数をクリアし、C<??> 検索を再び
動作するようにリセットするため、C<continue> ブロックで使われます。
EXPR は、文字を並べたもの (範囲を指定するのに、ハイフンが使えます) と
解釈されます。
名前がその文字のいずれかで始まる変数や配列は、
最初の状態にリセットされます。
EXPR を省略すると、1 回検索 (C<?PATTERN?>) を再びマッチするように
リセットできます。
カレントパッケージの変数もしくは検索だけがリセットされます。
常に 1 を返します。例:

    reset 'X';		# reset all X variables
    reset 'a-z';	# reset lower case variables
    reset;		# just reset ?one-time? searches

=begin original

Resetting C<"A-Z"> is not recommended because you'll wipe out your
C<@ARGV> and C<@INC> arrays and your C<%ENV> hash.  Resets only package
variables--lexical variables are unaffected, but they clean themselves
up on scope exit anyway, so you'll probably want to use them instead.
See L</my>.

=end original

reset C<"A-Z"> とすると、C<@ARGV>, C<@INC>配列やC<%ENV>ハッシュも
なくなってしまいますから、止めた方が良いでしょう。
パッケージ変数だけがリセットされます。
レキシカル変数は、影響を受けませんが、スコープから外れれば、
自動的に綺麗になりますので、これからは、こちらを使うようにした方が
よいでしょう。　このドキュメントの L</my> の項を参照してください。

=item return EXPR

=item return

=begin original

Returns from a subroutine, C<eval>, or C<do FILE> with the value 
given in EXPR.  Evaluation of EXPR may be in list, scalar, or void
context, depending on how the return value will be used, and the context
may vary from one execution to the next (see C<wantarray>).  If no EXPR
is given, returns an empty list in list context, the undefined value in
scalar context, and (of course) nothing at all in a void context.

=end original

サブルーチン, C<eval>, C<do FILE>  から EXPR で与えられた値をもって、
リターンします。
EXPR の評価は、返り値がどのように使われるかによって
リスト、スカラ、void コンテキストになります。
またコンテキストは実行毎に変わります(C<wantarray> を参照してください)。
EXPR が指定されなかった場合は、
リストコンテキストでは空リストを、
スカラコンテキストでは未定義値を返します。
そして(もちろん)void コンテキストでは何も返しません。

=begin original

(Note that in the absence of a explicit C<return>, a subroutine, eval,
or do FILE will automatically return the value of the last expression
evaluated.)

=end original

(サブルーチン, eval, do FILE に明示的に C<return> が
なければ、最後に評価された値で、自動的にリターンします。)

=item reverse LIST

=begin original

In list context, returns a list value consisting of the elements
of LIST in the opposite order.  In scalar context, concatenates the
elements of LIST and returns a string value with all characters
in the opposite order.

=end original

リストコンテキストでは、LIST を構成するよ要素を逆順に並べた
リスト値を返します。
スカラコンテキストでは、LIST の要素を連結して、
全ての文字を逆順にした文字列を返します。

    print reverse <>;		# line tac, last line first

    undef $/;			# for efficiency of <>
    print scalar reverse <>;	# character tac, last line tsrif

This operator is also handy for inverting a hash, although there are some
caveats.  If a value is duplicated in the original hash, only one of those
can be represented as a key in the inverted hash.  Also, this has to
unwind one hash and build a whole new one, which may take some time
on a large hash, such as from a DBM file.

    %by_name = reverse %by_address;	# Invert the hash

=item rewinddir DIRHANDLE

=begin original

Sets the current position to the beginning of the directory for the
C<readdir> routine on DIRHANDLE.

=end original

DIRHANDLE に対する C<readdir> ルーチンの現在位置を
ディレクトリの最初に設定します。

=item rindex STR,SUBSTR,POSITION

=item rindex STR,SUBSTR

=begin original

Works just like index() except that it returns the position of the LAST
occurrence of SUBSTR in STR.  If POSITION is specified, returns the
last occurrence at or before that position.

=end original

STR 中で最後に見つかった SUBSTR の位置を返すことを除いて、
index() と同じように動作します。　POSITION を指定すると、
その位置より前の、最後の位置を返します。

=item rmdir FILENAME

=item rmdir

=begin original

Deletes the directory specified by FILENAME if that directory is empty.  If it
succeeds it returns true, otherwise it returns false and sets C<$!> (errno).  If
FILENAME is omitted, uses C<$_>.

=end original

FILENAME で指定したディレクトリが空であれば、
そのディレクトリを削除します。
成功時には真を返し、失敗時には偽を返し、C<$!> (errno) を設定します。
FILENAMEを省略した場合には、C<$_> を使用します。

=item s///

=begin original

The substitution operator.  See L<perlop>.

=end original

置換演算子。L<perlop> を参照してください。

=item scalar EXPR

=begin original

Forces EXPR to be interpreted in scalar context and returns the value
of EXPR.

=end original

EXPR を強制的にスカラコンテキストで解釈されるようにして、
EXPR の値を返します。

    @counts = ( scalar @a, scalar @b, scalar @c );

=begin original

There is no equivalent operator to force an expression to
be interpolated in list context because in practice, this is never
needed.  If you really wanted to do so, however, you could use
the construction C<@{[ (some expression) ]}>, but usually a simple
C<(some expression)> suffices.

=end original

式を強制的にリストコンテキストで解釈させるようにする演算子はありません。
理論的には不要だからです。それでも、もしそうしたいのなら、
C<@{[ (some expression) ]}> という構造を使えます。
しかし、普通は単に C<(some expression)> とすれば十分です。

=begin original

Because C<scalar> is unary operator, if you accidentally use for EXPR a
parenthesized list, this behaves as a scalar comma expression, evaluating
all but the last element in void context and returning the final element
evaluated in scalar context.  This is seldom what you want.

=end original

C<scalar> は単項演算子なので、EXPR として括弧でくくったリストを使った場合、
これはスカラカンマ表現として振舞い、最後以外の全ては void コンテキストとして
扱われ、最後の要素をスカラコンテキストとして扱った結果が返されます。
これがあなたの望むものであることはめったにないでしょう。

=begin original

The following single statement:

=end original

以下の 1 つの文は:

	print uc(scalar(&foo,$bar)),$baz;

=begin original

is the moral equivalent of these two:

=end original

以下の 2 つの文と等価です。

	&foo;
	print(uc($bar),$baz);

=begin original

See L<perlop> for more details on unary operators and the comma operator.

=end original

単項演算子とカンマ演算子に関する詳細については L<perlop> を参照して下さい。

=item seek FILEHANDLE,POSITION,WHENCE

=begin original

Sets FILEHANDLE's position, just like the C<fseek> call of C<stdio>.
FILEHANDLE may be an expression whose value gives the name of the
filehandle.  The values for WHENCE are C<0> to set the new position to
POSITION, C<1> to set it to the current position plus POSITION, and
C<2> to set it to EOF plus POSITION (typically negative).  For WHENCE
you may use the constants C<SEEK_SET>, C<SEEK_CUR>, and C<SEEK_END>
(start of the file, current position, end of the file) from the Fcntl
module.  Returns C<1> upon success, C<0> otherwise.

=end original

C<stdio> ライブラリの C<fseek> 関数のように、FILEHANDLE の
ファイルポインタを任意の位置に設定します。
FILEHANDLE は、実際のファイルハンドル名を与える式でもかまいません。
WHENCE の値が、C<0> ならば、
新しい位置を POSITION の位置へ、C<1> ならば、現在位置から
POSITION 加えた位置へ、C<2> ならば、EOF から POSITION
だけ加えた位置へ、新しい位置を設定します。
この値には、Fcntl モジュールで使われている C<SEEK_SET>、
C<SEEK_CUR>、C<SEEK_END> 
(ファイルの先頭、現在位置、ファイルの最後)という定数を使うこともできます。
成功時には、C<1> を、失敗時には C<0> を返します。

=begin original

If you want to position file for C<sysread> or C<syswrite>, don't use
C<seek>--buffering makes its effect on the file's system position
unpredictable and non-portable.  Use C<sysseek> instead.

=end original

C<sysread> や C<syswrite> のためにファイルの位置を指定したい場合は、
C<seek> は使えません -- バッファリングのために動作は予測不能で
移植性のないものになってしまいます。代わりに C<sysseek> を使ってください。

Due to the rules and rigors of ANSI C, on some systems you have to do a
seek whenever you switch between reading and writing.  Amongst other
things, this may have the effect of calling stdio's clearerr(3).
A WHENCE of C<1> (C<SEEK_CUR>) is useful for not moving the file position:

    seek(TEST,0,1);

This is also useful for applications emulating C<tail -f>.  Once you hit
EOF on your read, and then sleep for a while, you might have to stick in a
seek() to reset things.  The C<seek> doesn't change the current position,
but it I<does> clear the end-of-file condition on the handle, so that the
next C<< <FILE> >> makes Perl try again to read something.  We hope.

If that doesn't work (some stdios are particularly cantankerous), then
you may need something more like this:

    for (;;) {
	for ($curpos = tell(FILE); $_ = <FILE>;
             $curpos = tell(FILE)) {
	    # search for some stuff and put it into files
	}
	sleep($for_a_while);
	seek(FILE, $curpos, 0);
    }

=item seekdir DIRHANDLE,POS

=begin original

Sets the current position for the C<readdir> routine on DIRHANDLE.  POS
must be a value returned by C<telldir>.  Has the same caveats about
possible directory compaction as the corresponding system library
routine.

=end original

DIRHANDLE での C<readdir> ルーチンの現在位置を設定します。
POS は、C<telldir> が返す値でなければなりません。
同名のシステムライブラリルーチンと同じく、
ディレクトリ縮小時の問題が考えられます。

=item select FILEHANDLE

=item select

=begin original

Returns the currently selected filehandle.  Sets the current default
filehandle for output, if FILEHANDLE is supplied.  This has two
effects: first, a C<write> or a C<print> without a filehandle will
default to this FILEHANDLE.  Second, references to variables related to
output will refer to this output channel.  For example, if you have to
set the top of form format for more than one output channel, you might
do the following:

=end original

その時点で、選択されていたファイルハンドルを返します。
FILEHANDLE を指定した場合には、その値を出力のデフォルト
ファイルハンドルに設定します。
これには、2 つの効果があります。
まず、ファイルハンドルを指定しないで
C<write> や C<print> を行なった場合のデフォルトが、
この FILEHANDLE になります。
もう一つは、出力関連の変数への参照は、
この出力チャネルを参照するようになります。
たとえば、複数の出力チャネルに対して、ページ先頭フォーマットを
設定しなければならないのであれば、
以下のようにしなければならないでしょう。

    select(REPORT1);
    $^ = 'report1_top';
    select(REPORT2);
    $^ = 'report2_top';

=begin original

FILEHANDLE may be an expression whose value gives the name of the
actual filehandle.  Thus:

=end original

FILEHANDLE は、実際のファイルハンドルの名前を示す、
式でもかまいません。
つまり、以下のようなものです:

    $oldfh = select(STDERR); $| = 1; select($oldfh);

=begin original

Some programmers may prefer to think of filehandles as objects with
methods, preferring to write the last example as:

=end original

ファイルハンドルはメソッドを持ったオブジェクトであると
考えることを好むプログラマもいるかもしれません。
そのような場合のための最後の例は以下のようなものです。

    use IO::Handle;
    STDERR->autoflush(1);

=item select RBITS,WBITS,EBITS,TIMEOUT

=begin original

This calls the select(2) system call with the bit masks specified, which
can be constructed using C<fileno> and C<vec>, along these lines:

=end original

これは、select(2) システムコールを、指定したビットマスクで呼び出します。
ビットマスクは、C<fileno> と
C<vec> を使って、以下のようにして作成できます。

    $rin = $win = $ein = '';
    vec($rin,fileno(STDIN),1) = 1;
    vec($win,fileno(STDOUT),1) = 1;
    $ein = $rin | $win;

=begin original

If you want to select on many filehandles you might wish to write a
subroutine:

=end original

複数のファイルハンドルに select を行ないたいのであれば、
以下のようにします。

    sub fhbits {
	my(@fhlist) = split(' ',$_[0]);
	my($bits);
	for (@fhlist) {
	    vec($bits,fileno($_),1) = 1;
	}
	$bits;
    }
    $rin = fhbits('STDIN TTY SOCK');

=begin original

The usual idiom is:

=end original

通常は、

    ($nfound,$timeleft) =
      select($rout=$rin, $wout=$win, $eout=$ein, $timeout);

=begin original

or to block until something becomes ready just do this

=end original

のように使い、いずれかの準備が整うまでブロックするには、
以下のようにします。

    $nfound = select($rout=$rin, $wout=$win, $eout=$ein, undef);

=begin original

Most systems do not bother to return anything useful in $timeleft, so
calling select() in scalar context just returns $nfound.

=end original

ほとんどのシステムではわざわざ意味のある値を $timeleft に返さないので、
select() をスカラコンテキストで呼び出すと、単に $nfound を返します。

=begin original

Any of the bit masks can also be undef.  The timeout, if specified, is
in seconds, which may be fractional.  Note: not all implementations are
capable of returning the$timeleft.  If not, they always return
$timeleft equal to the supplied $timeout.

=end original

どのビットマスクにも undef を設定することができます。
TIMEOUT を指定するときは、秒数で指定し、小数でかまいません。
注: すべての実装で、$timeleft が
返せるものではありません。
その場合、$timeleft には、常に指定した TIMEOUT と同じ値が返されます。

=begin original

You can effect a sleep of 250 milliseconds this way:

=end original

250 ミリ秒の sleep と同じ効果が、以下のようにして得られます。

    select(undef, undef, undef, 0.25);

=begin original

B<WARNING>: One should not attempt to mix buffered I/O (like C<read>
or <FH>) with C<select>, except as permitted by POSIX, and even
then only on POSIX systems.  You have to use C<sysread> instead.

=end original

B<警告>: バッファ付き I/O (C<read> や <FH>) と C<select> を
混ぜて使ってはいけません(例外: POSIX で認められている形で使い、
POSIX システムでだけ動かす場合を除きます)。
代わりに C<sysread> を使わなければなりません。

=item semctl ID,SEMNUM,CMD,ARG

=begin original

Calls the System V IPC function C<semctl>.  You'll probably have to say

    use IPC::SysV;

first to get the correct constant definitions.  If CMD is IPC_STAT or
GETALL, then ARG must be a variable which will hold the returned
semid_ds structure or semaphore value array.  Returns like C<ioctl>:
the undefined value for error, "C<0 but true>" for zero, or the actual
return value otherwise.  The ARG must consist of a vector of native
short integers, which may be created with C<pack("s!",(0)x$nsem)>.
See also L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::Semaphore>
documentation.

=end original

System V IPC 関数 C<semctl> を呼び出します。正しい定数定義を得るために、まず

    use IPC::SysV;

と宣言する必要があるでしょう。
CMD が、IPC_STAT か GETALL のときには、ARG は、返される
semid_ds 構造体か、セマフォ値の配列を納める変数でなければなりません。
C<ioctl> と同じように、エラー時には
未定義値、ゼロのときは C<"0 but true">、それ以外なら、
その値そのものを返します。
ARG はネイティブな short int のベクターから成っていなければなりません。
これは C<pack("s!",(0)x$nsem)> で作成できます。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::Semaphore> も参照してください。

=item semget KEY,NSEMS,FLAGS

=begin original

Calls the System V IPC function semget.  Returns the semaphore id, or
the undefined value if there is an error.  See also
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::SysV::Semaphore>
documentation.

=end original

System V IPC 関数 semget を呼び出します。
セマフォ ID か、エラー時には未定義値を返します。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::SysV::Semaphore> も参照してください。

=item semop KEY,OPSTRING

=begin original

Calls the System V IPC function semop to perform semaphore operations
such as signaling and waiting.  OPSTRING must be a packed array of
semop structures.  Each semop structure can be generated with
C<pack("sss", $semnum, $semop, $semflag)>.  The number of semaphore
operations is implied by the length of OPSTRING.  Returns true if
successful, or false if there is an error.  As an example, the
following code waits on semaphore $semnum of semaphore id $semid:

=end original

シグナルを送信や、待ち合わせなどのセマフォ操作を行な
うために、System V IPC 関数 semop を呼び出します。
OPSTRING は、semop 構造体の pack された配列でなければなりません。
semop 構造体は、それぞれ、
C<pack("sss", $semnum, $semop, $semflag)> のように作る
ことができます。
セマフォ操作の数は、OPSTRING の長さからわかります。
成功時には真を、エラー時には偽を返します。
以下の例は、セマフォ ID $semid のセマフォ $semnum で
待ち合わせを行ないます。

    $semop = pack("sss", $semnum, -1, 0);
    die "Semaphore trouble: $!\n" unless semop($semid, $semop);

=begin original

To signal the semaphore, replace C<-1> with C<1>.  See also
L<perlipc/"SysV IPC">, C<IPC::SysV>, and C<IPC::SysV::Semaphore>
documentation.

=end original

セマフォにシグナルを送るには、C<-1>を C<1> に変更してください。
L<perlipc/"SysV IPC">, C<IPC::SysV>, C<IPC::SysV::Semaphore> も参照してください。

=item send SOCKET,MSG,FLAGS,TO

=item send SOCKET,MSG,FLAGS

=begin original

Sends a message on a socket.  Takes the same flags as the system call
of the same name.  On unconnected sockets you must specify a
destination to send TO, in which case it does a C C<sendto>.  Returns
the number of characters sent, or the undefined value if there is an
error.  The C system call sendmsg(2) is currently unimplemented.
See L<perlipc/"UDP: Message Passing"> for examples.

=end original

SOCKET に MSG を送ります。
FLAGS には、同名のシステムコールと同じフラグを指定します。
接続していないソケットには、送信先 TO を指定しなければならず、
この場合、C の C<sendto> を実行します。
送信した文字数か、エラー時には、未定義値を返します。
C の システムコール sendmsg(2) は現在実装されていません。
例については L<perlipc/"UDP: Message Passing"> を参照してください。

=item setpgrp PID,PGRP

=begin original

Sets the current process group for the specified PID, C<0> for the current
process.  Will produce a fatal error if used on a machine that doesn't
implement POSIX setpgid(2) or BSD setpgrp(2).  If the arguments are omitted,
it defaults to C<0,0>.  Note that the BSD 4.2 version of C<setpgrp> does not
accept any arguments, so only C<setpgrp(0,0)> is portable.  See also
C<POSIX::setsid()>.

=end original

指定した PID (C<0> を指定するとカレントプロセス) に
対するプロセスグループを設定します。
POSIX setpgrp(2) または BSD setpgrp(2) が実装されていないマシンでは、
致命的エラーが発生します。
引数が省略された場合は、C<0,0>が使われます。
BSD 4.2 版の C<setpgrp> は引数を取ることができないので、
C<setpgrp(0,0)> のみが移植性があることに注意してください。
C<POSIX::setsid()> も参照してください。

=item setpriority WHICH,WHO,PRIORITY

=begin original

Sets the current priority for a process, a process group, or a user.
(See setpriority(2).)  Will produce a fatal error if used on a machine
that doesn't implement setpriority(2).

=end original

プロセス、プロセスグループ、ユーザに対する優先順位を設定します。
(setpriority(2) を参照してください。)
setpriority(2) が実装されていないマシンでは、
致命的エラーが発生します。

=item setsockopt SOCKET,LEVEL,OPTNAME,OPTVAL

=begin original

Sets the socket option requested.  Returns undefined if there is an
error.  OPTVAL may be specified as C<undef> if you don't want to pass an
argument.

=end original

要求したソケットオプションを設定します。
エラー時には、未定義値が返されます。
引数を渡したくない場合には、OPTVAL に C<undef> を指定します。

=item shift ARRAY

=item shift

=begin original

Shifts the first value of the array off and returns it, shortening the
array by 1 and moving everything down.  If there are no elements in the
array, returns the undefined value.  If ARRAY is omitted, shifts the
C<@_> array within the lexical scope of subroutines and formats, and the
C<@ARGV> array at file scopes or within the lexical scopes established by
the C<eval ''>, C<BEGIN {}>, C<INIT {}>, C<CHECK {}>, and C<END {}>
constructs.

=end original

配列の最初の値を取り出して、その値を返し、配列を一つ
短くして、すべての要素を前へずらします。
配列に要素がなければ、未定義値を返します。
ARRAY を省略すると、
サブルーチンやフォーマットのレキシカルスコープでは C<@_> を、
ファイルスコープまたは
C<eval ''>, C<BEGIN {}>, C<INIT {}>, C<CHECK {}>, C<END {}>
で作成されたレキシカルスコープでは C<@ARGV> が用いられます。

=begin original

See also C<unshift>, C<push>, and C<pop>.  C<shift> and C<unshift> do the
same thing to the left end of an array that C<pop> and C<push> do to the
right end.

=end original

C<unshift>、C<push>、C<pop> も参照してください。
C<shift> と C<unshift> は、C<pop> と
C<push> が配列の右端で行なうことを、左端で行ないます。

=item shmctl ID,CMD,ARG

=begin original

Calls the System V IPC function shmctl.  You'll probably have to say

    use IPC::SysV;

first to get the correct constant definitions.  If CMD is C<IPC_STAT>,
then ARG must be a variable which will hold the returned C<shmid_ds>
structure.  Returns like ioctl: the undefined value for error, "C<0> but
true" for zero, or the actual return value otherwise.
See also L<perlipc/"SysV IPC"> and C<IPC::SysV> documentation.

=end original

System V IPC 関数 shmctl を呼び出します。正しい定数定義を得るために、まず

    use IPC::SysV;

と宣言する必要があるでしょう。
CMD が、C<IPC_STAT> ならば、ARG は、返される C<shmid_ds> 構造体を
納める変数でなければなりません。
ioctl と同じように、エラー時には未定義値、ゼロのときは "C<0> but true"、
それ以外なら、その値そのものを返します。
L<perlipc/"SysV IPC"> と C<IPC::SysV> も参照してください。

=item shmget KEY,SIZE,FLAGS

=begin original

Calls the System V IPC function shmget.  Returns the shared memory
segment id, or the undefined value if there is an error.
See also L<perlipc/"SysV IPC"> and C<IPC::SysV> documentation.

=end original

System V IPC 関数 shmget を呼び出します。
共有メモリのセグメント ID か、エラー時には未定義値を返します。
L<perlipc/"SysV IPC"> と C<IPC::SysV> も参照してください。

=item shmread ID,VAR,POS,SIZE

=item shmwrite ID,STRING,POS,SIZE

=begin original

Reads or writes the System V shared memory segment ID starting at
position POS for size SIZE by attaching to it, copying in/out, and
detaching from it.  When reading, VAR must be a variable that will
hold the data read.  When writing, if STRING is too long, only SIZE
bytes are used; if STRING is too short, nulls are written to fill out
SIZE bytes.  Return true if successful, or false if there is an error.
shmread() taints the variable. See also L<perlipc/"SysV IPC">,
C<IPC::SysV> documentation, and the C<IPC::Shareable> module from CPAN.

=end original

System V 共有メモリセグメント ID に対し、アタッチして、
コピーを行ない、デタッチするという形で、位置 POS
から、サイズ SIZE だけ、読み込みか書き込みを行ないます。
読み込み時には、VAR は読み込んだデータを納める
変数でなければなりません。
書き込み時には、STRING が長すぎても、SIZE バイトだけが使われます。
STRING が短すぎる場合には、SIZE バイトを埋めるために、
ヌル文字が書き込まれます。
成功時には真を、エラー時には偽を返します。
shmread() は変数を汚染します。
L<perlipc/"SysV IPC"> と C<IPC::SysV> の文章と、
CPAN の C<IPC::Shareable> も参照してください。

=item shutdown SOCKET,HOW

=begin original

Shuts down a socket connection in the manner indicated by HOW, which
has the same interpretation as in the system call of the same name.

=end original

同名のシステムコールと同じように解釈される HOW によって、
指定された方法でソケット接続のシャットダウンを行ないます。

    shutdown(SOCKET, 0);    # I/we have stopped reading data
    shutdown(SOCKET, 1);    # I/we have stopped writing data
    shutdown(SOCKET, 2);    # I/we have stopped using this socket

This is useful with sockets when you want to tell the other
side you're done writing but not done reading, or vice versa.
It's also a more insistent form of close because it also 
disables the file descriptor in any forked copies in other
processes.

=item sin EXPR

=item sin

=begin original

Returns the sine of EXPR (expressed in radians).  If EXPR is omitted,
returns sine of C<$_>.

=end original

(ラジアンで示した) EXPR の正弦を返します。
EXPR が省略されたときには、C<$_> の正弦を返します。

=begin original

For the inverse sine operation, you may use the C<Math::Trig::asin>
function, or use this relation:

=end original

逆正弦を求めるためには、C<Math::Trig::asin> 関数を使うか、
以下の関係を使ってください:

    sub asin { atan2($_[0], sqrt(1 - $_[0] * $_[0])) }

=item sleep EXPR

=item sleep

=begin original

Causes the script to sleep for EXPR seconds, or forever if no EXPR.
May be interrupted if the process receives a signal such as C<SIGALRM>.
Returns the number of seconds actually slept.  You probably cannot
mix C<alarm> and C<sleep> calls, because C<sleep> is often implemented
using C<alarm>.

=end original

スクリプトを EXPR で指定した秒数 (省略時には、永久に)
スリープさせます。
そのプロセスには、C<SIGALRM>のようなシグナルを
受信すると、割り込みがかかります。
実際にスリープした秒数を返します。C<sleep> は、C<alarm> を
使って実装されることが多いので、C<alarm> と
C<sleep> は、おそらく混ぜて使用することはできません。

=begin original

On some older systems, it may sleep up to a full second less than what
you requested, depending on how it counts seconds.  Most modern systems
always sleep the full amount.  They may appear to sleep longer than that,
however, because your process might not be scheduled right away in a
busy multitasking system.

=end original

古いシステムでは、どのように秒を数えるかによって、要求した秒数に完全に
満たないうちに、スリープから抜ける場合があります。
最近のシステムでは、常に完全にスリープします。
しかし、負荷の高いマルチタスクシステムでは
正しくスケジューリングされないがために
より長い時間スリープすることがあります。

For delays of finer granularity than one second, you may use Perl's
C<syscall> interface to access setitimer(2) if your system supports
it, or else see L</select> above.  The Time::HiRes module from CPAN
may also help.

=begin original

See also the POSIX module's C<pause> function.

=end original

POSIX モジュールの C<pause> 関数も参照して下さい。

=item socket SOCKET,DOMAIN,TYPE,PROTOCOL

=begin original

Opens a socket of the specified kind and attaches it to filehandle
SOCKET.  DOMAIN, TYPE, and PROTOCOL are specified the same as for
the system call of the same name.  You should C<use Socket> first
to get the proper definitions imported.  See the examples in
L<perlipc/"Sockets: Client/Server Communication">.

=end original

指定した種類のソケットをオープンし、ファイルハンドル
SOCKET にアタッチします。
DOMAIN、TYPE、PROTOCOL は、
同名のシステムコールと同じように指定します。
適切な定義を import するために、まず、C<use Socket> とする
とよいでしょう。
例については L<perlipc/"Sockets: Client/Server Communication">
を参照してください。

=begin original

On systems that support a close-on-exec flag on files, the flag will
be set for the newly opened file descriptor, as determined by the
value of $^F.  See L<perlvar/$^F>.

=end original

ファイルに対する close-on-exec フラグをサポートしているシステムでは、
フラグは$^F の値で決定される、新しくオープンされたファイル記述子に対して
セットされます。L<perlvar/$^F>を参照してください。

=item socketpair SOCKET1,SOCKET2,DOMAIN,TYPE,PROTOCOL

=begin original

Creates an unnamed pair of sockets in the specified domain, of the
specified type.  DOMAIN, TYPE, and PROTOCOL are specified the same as
for the system call of the same name.  If unimplemented, yields a fatal
error.  Returns true if successful.

=end original

指定した DOMAIN に、指定した TYPE で名前の無いソケットのペアを生成します。
DOMAIN、TYPE、PROTOCOL は、
同名のシステムコールと同じように指定します。
実装されていない場合には、致命的エラーとなります。
成功時には真を返します。

=begin original

On systems that support a close-on-exec flag on files, the flag will
be set for the newly opened file descriptors, as determined by the value
of $^F.  See L<perlvar/$^F>.

=end original

ファイルに対する close-on-exec フラグをサポートしているシステムでは、
フラグは$^F の値で決定される、新しくオープンされたファイル記述子に対して
セットされます。L<perlvar/$^F>を参照してください。

Some systems defined C<pipe> in terms of C<socketpair>, in which a call
to C<pipe(Rdr, Wtr)> is essentially:

    use Socket;
    socketpair(Rdr, Wtr, AF_UNIX, SOCK_STREAM, PF_UNSPEC);
    shutdown(Rdr, 1);        # no more writing for reader
    shutdown(Wtr, 0);        # no more reading for writer

See L<perlipc> for an example of socketpair use.

=item sort SUBNAME LIST

=item sort BLOCK LIST

=item sort LIST

=begin original

Sorts the LIST and returns the sorted list value.  If SUBNAME or BLOCK
is omitted, C<sort>s in standard string comparison order.  If SUBNAME is
specified, it gives the name of a subroutine that returns an integer
less than, equal to, or greater than C<0>, depending on how the elements
of the list are to be ordered.  (The C<< <=> >> and C<cmp>
operators are extremely useful in such routines.)  SUBNAME may be a
scalar variable name (unsubscripted), in which case the value provides
the name of (or a reference to) the actual subroutine to use.  In place
of a SUBNAME, you can provide a BLOCK as an anonymous, in-line sort
subroutine.

=end original

LIST をソートし、ソートされたリスト値を返します。
SUBNAME や BLOCK を省略すると、標準の文字列比較の順番でソートが
行なわれます。
SUBNAME を指定すると、それは、リストの
要素をどのような順番に並べるかに応じて、負、ゼロ、正の整数を返す
サブルーチンの名前であると解釈されます。
(このようなルーチンには、C<< <=> >> 演算子や cmp 演算子が、
たいへん便利です。)
SUBNAME は、スカラ変数名(添え字なし)でもよく、
その場合には、その値が使用する実際のサブルーチンの
名前(またはそのリファレンス)と解釈されます。
SUBNAME の代わりに、無名のインライン
ソートルーチンとして、BLOCK を書くことができます。

=begin original

If the subroutine's prototype is C<($$)>, the elements to be compared
are passed by reference in C<@_>, as for a normal subroutine.  This is
slower than unprototyped subroutines, where the elements to be
compared are passed into the subroutine
as the package global variables $a and $b (see example below).  Note that
in the latter case, it is usually counter-productive to declare $a and
$b as lexicals.

=end original

サブルーチンのプロトタイプが C<($$)>の場合、
比較する要素は通常のサブルーチンと同じように C<@_> の中に
リファレンスとして渡されます。
これはプロトタイプなしのサブルーチンより遅いです。
この場合は比較のためサブルーチンに渡される 2 つの
要素は、パッケージのグローバル変数 $a と $b で渡されます
(次の例を参照してください)。
後者の場合、レキシカルに $a と $b を宣言するのは普通逆効果になります。

=begin original

In either case, the subroutine may not be recursive.  The values to be
compared are always passed by reference, so don't modify them.

=end original

どちらの場合でも、サブルーチンは再帰的であってはなりません。
$a や $b はリファレンスによって渡されるので、変更しないでください。

=begin original

You also cannot exit out of the sort block or subroutine using any of the
loop control operators described in L<perlsyn> or with C<goto>.

=end original

また、ソートブロックやサブルーチンから L<perlsyn> で説明されている
ループ制御子や C<goto> を使って抜けてはいけません。

=begin original

When C<use locale> is in effect, C<sort LIST> sorts LIST according to the
current collation locale.  See L<perllocale>.

=end original

C<use locale> が有効の場合、C<sort LIST> は LIST を現在の比較ロケールに
従ってソートします。L<perllocale> を参照して下さい。

Examples:

    # sort lexically
    @articles = sort @files;

    # same thing, but with explicit sort routine
    @articles = sort {$a cmp $b} @files;

    # now case-insensitively
    @articles = sort {uc($a) cmp uc($b)} @files;

    # same thing in reversed order
    @articles = sort {$b cmp $a} @files;

    # sort numerically ascending
    @articles = sort {$a <=> $b} @files;

    # sort numerically descending
    @articles = sort {$b <=> $a} @files;

    # this sorts the %age hash by value instead of key
    # using an in-line function
    @eldest = sort { $age{$b} <=> $age{$a} } keys %age;

    # sort using explicit subroutine name
    sub byage {
	$age{$a} <=> $age{$b};	# presuming numeric
    }
    @sortedclass = sort byage @class;

    sub backwards { $b cmp $a }
    @harry  = qw(dog cat x Cain Abel);
    @george = qw(gone chased yz Punished Axed);
    print sort @harry;
	    # prints AbelCaincatdogx
    print sort backwards @harry;
	    # prints xdogcatCainAbel
    print sort @george, 'to', @harry;
	    # prints AbelAxedCainPunishedcatchaseddoggonetoxyz

    # inefficiently sort by descending numeric compare using
    # the first integer after the first = sign, or the
    # whole record case-insensitively otherwise

    @new = sort {
	($b =~ /=(\d+)/)[0] <=> ($a =~ /=(\d+)/)[0]
			    ||
	            uc($a)  cmp  uc($b)
    } @old;

    # same thing, but much more efficiently;
    # we'll build auxiliary indices instead
    # for speed
    @nums = @caps = ();
    for (@old) {
	push @nums, /=(\d+)/;
	push @caps, uc($_);
    }

    @new = @old[ sort {
			$nums[$b] <=> $nums[$a]
				 ||
			$caps[$a] cmp $caps[$b]
		       } 0..$#old
	       ];

    # same thing, but without any temps
    @new = map { $_->[0] }
           sort { $b->[1] <=> $a->[1]
                           ||
                  $a->[2] cmp $b->[2]
           } map { [$_, /=(\d+)/, uc($_)] } @old;

    # using a prototype allows you to use any comparison subroutine
    # as a sort subroutine (including other package's subroutines)
    package other;
    sub backwards ($$) { $_[1] cmp $_[0]; }	# $a and $b are not set here

    package main;
    @new = sort other::backwards @old;

=begin original

If you're using strict, you I<must not> declare $a
and $b as lexicals.  They are package globals.  That means
if you're in the C<main> package and type

=end original

use strict している場合、$a と $b をレキシカルとして
宣言してはI<いけません>。これはパッケージグローバルです。
つまり、C<main> パッケージで以下のように書いた場合:

    @articles = sort {$b <=> $a} @files;

=begin original

then C<$a> and C<$b> are C<$main::a> and C<$main::b> (or C<$::a> and C<$::b>),
but if you're in the C<FooPack> package, it's the same as typing

=end original

C<$a> と C<$b> は C<$main::a> と C<$main::b> (または C<$::a> と C<$::b>) を
意味しますが、C<FooPack> パッケージ内の場合、これは以下と同じになります:

    @articles = sort {$FooPack::b <=> $FooPack::a} @files;

=begin original

The comparison function is required to behave.  If it returns
inconsistent results (sometimes saying C<$x[1]> is less than C<$x[2]> and
sometimes saying the opposite, for example) the results are not
well-defined.

=end original

比較関数は一貫した振る舞いをすることが求められます。
一貫しない結果を返す(例えば、あるときは C<$x[1]> が C<$x[2]> より小さいと返し、
またあるときは逆を返す)場合、結果は未定義です。

=item splice ARRAY,OFFSET,LENGTH,LIST

=item splice ARRAY,OFFSET,LENGTH

=item splice ARRAY,OFFSET

=item splice ARRAY

=begin original

Removes the elements designated by OFFSET and LENGTH from an array, and
replaces them with the elements of LIST, if any.  In list context,
returns the elements removed from the array.  In scalar context,
returns the last element removed, or C<undef> if no elements are
removed.  The array grows or shrinks as necessary.
If OFFSET is negative then it starts that far from the end of the array.
If LENGTH is omitted, removes everything from OFFSET onward.
If LENGTH is negative, leaves that many elements off the end of the array.
If both OFFSET and LENGTH are omitted, removes everything.

The following equivalences hold (assuming C<$[ == 0>):

=end original

ARRAY から OFFSET、LENGTH で指定される要素を取り除き、
LIST があれば、それを代わりに挿入します。
リストコンテキストでは、配列から取り除かれた要素を返します。
スカラコンテキストでは、取り除かれた最後の要素を返します。
要素が取り除かれなかった場合は C<undef> を返します。
配列は、必要に応じて、大きくなったり、小さくなったりします。
OFFSET が負の数の場合は、配列の最後からの距離を示します。
LENGTH が省略されると、OFFSET 以降のすべての要素を取り除きます。
LENGTH が負の数の場合は、指定された数だけ配列の最後から要素を取り除きます。
OFFSET と LENGTH の両方が省略されると、全ての要素を取り除きます。

以下は、(C<$[ == 0> と仮定すると) それぞれ、等価です。

    push(@a,$x,$y)	splice(@a,@a,0,$x,$y)
    pop(@a)		splice(@a,-1)
    shift(@a)		splice(@a,0,1)
    unshift(@a,$x,$y)	splice(@a,0,0,$x,$y)
    $a[$x] = $y		splice(@a,$x,1,$y)

=begin original

Example, assuming array lengths are passed before arrays:

=end original

次の例では、配列の前に、それぞれの配列の大きさが渡されるものとしています:

    sub aeq {	# compare two list values
	my(@a) = splice(@_,0,shift);
	my(@b) = splice(@_,0,shift);
	return 0 unless @a == @b;	# same len?
	while (@a) {
	    return 0 if pop(@a) ne pop(@b);
	}
	return 1;
    }
    if (&aeq($len,@foo[1..$len],0+@bar,@bar)) { ... }

=item split /PATTERN/,EXPR,LIMIT

=item split /PATTERN/,EXPR

=item split /PATTERN/

=item split

=begin original

Splits a string into a list of strings and returns that list.  By default,
empty leading fields are preserved, and empty trailing ones are deleted.

=end original

文字列を文字列のリストに分割して、リストを返します。
デフォルトでは、行頭の空白は保存され、末尾の空白は削除されます。

=begin original

In scalar context, returns the number of fields found and splits into
the C<@_> array.  Use of split in scalar context is deprecated, however,
because it clobbers your subroutine arguments.

=end original

スカラコンテキストの場合には、見つかったフィールドの数を返し、
配列 C<@_> に分割結果を設定します。
しかし、スカラコンテキストでの split の使用は推奨されません。
サブルーチンの引数を上書きしてしまうからです。

=begin original

If EXPR is omitted, splits the C<$_> string.  If PATTERN is also omitted,
splits on whitespace (after skipping any leading whitespace).  Anything
matching PATTERN is taken to be a delimiter separating the fields.  (Note
that the delimiter may be longer than one character.)

=end original

EXPR を省略すると、文字列 C<$_> を split します。
もし、PATTERN も省略すると、
(先頭の空白文字をスキップした後) 空白で split します。
PATTERN にマッチするものは、フィールドを分割する
デリミタとして扱われます。(デリミタは、1 文字とは限りません。)

=begin original

If LIMIT is specified and positive, splits into no more than that
many fields (though it may split into fewer).  If LIMIT is unspecified
or zero, trailing null fields are stripped (which potential users
of C<pop> would do well to remember).  If LIMIT is negative, it is
treated as if an arbitrarily large LIMIT had been specified.

=end original

正の数の LIMIT を指定した場合には、
最大その数までのフィールドに split します (しかし、
それより少ないことはあります)。
LIMIT を指定しないかゼロなら、末尾の空フィールドを捨ててしまいます
(C<pop> を行なうときには気を付けないといけません)。
LIMIT が負ならば、LIMIT に任意の大きな数を指定したのと同じことになります。

=begin original

A pattern matching the null string (not to be confused with
a null pattern C<//>, which is just one member of the set of patterns
matching a null string) will split the value of EXPR into separate
characters at each point it matches that way.  For example:

    print join(':', split(/ */, 'hi there'));

produces the output 'h:i:t:h:e:r:e'.

=end original

空文字列にマッチするパターン (ヌルパターン C<//> と混同しないでください。
これは、空文字列にマッチするパターンの一つでしかありません) は、
どの場所にもマッチし、EXPR の値を1 文字ずつに分割します。たとえば、

    print join(':', split(/ */, 'hi there'));

は、'h:i:t:h:e:r:e' という出力になります。

=begin original

Empty leading (or trailing) fields are produced when there are positive width
matches at the beginning (or end) of the string; a zero-width match at the
beginning (or end) of the string does not produce an empty field.  For
example:

=end original

先頭と末尾の空フィールドは、文字列の先頭または末尾で 0 でない幅でマッチした
場合は生成されます。幅 0 でマッチした場合は生成されません。例えば:

   print join(':', split(/(?=\w)/, 'hi there!'));

=begin original

produces the output 'h:i :t:h:e:r:e!'.

=end original

これの出力は 'h:i :t:h:e:r:e!' となります。

=begin original

The LIMIT parameter can be used to split a line partially

=end original

LIMIT を使うと、行を部分的に split することができます。

    ($login, $passwd, $remainder) = split(/:/, $_, 3);

=begin original

When assigning to a list, if LIMIT is omitted, Perl supplies a LIMIT
one larger than the number of variables in the list, to avoid
unnecessary work.  For the list above LIMIT would have been 4 by
default.  In time critical applications it behooves you not to split
into more fields than you really need.

=end original

リストへ代入するとき、LIMIT を省略すると、Perl は、
無駄な仕事を避けるため、そのリストの変数の数より、1 つだけ大きい
LIMIT が与えられたものとして処理を行ないます。
上のリストの場合には、LIMIT はデフォルトで 4 になります。
時間が問題となるアプリケーションでは、
必要以上のフィールドに分けないようにする必要があります。

=begin original

If the PATTERN contains parentheses, additional list elements are
created from each matching substring in the delimiter.

=end original

PATTERN に括弧が含まれていると、デリミタ内の部分文字列にマッチするものも、
リスト要素に含まれるようになります。

    split(/([,-])/, "1-10,20", 3);

=begin original

produces the list value

=end original

は、以下のリスト値を生成します。

    (1, '-', 10, ',', 20)

=begin original

If you had the entire header of a normal Unix email message in $header,
you could split it up into fields and their values this way:

=end original

$header に Unix E メールメッセージヘッダ全体が入っているとすると、
以下のようにしてフィールドとその値に分割できます:

    $header =~ s/\n\s+/ /g;  # fix continuation lines
    %hdrs   =  (UNIX_FROM => split /^(\S*?):\s*/m, $header);

=begin original

The pattern C</PATTERN/> may be replaced with an expression to specify
patterns that vary at runtime.  (To do runtime compilation only once,
use C</$variable/o>.)

=end original

/PATTERN/ は、実行時に変わるパターンを指定する式で置き換えることができます。
(実行時のコンパイルを 1 度にするために、/$variable/o を使ってください。)

=begin original

As a special case, specifying a PATTERN of space (C<' '>) will split on
white space just as C<split> with no arguments does.  Thus, C<split(' ')> can
be used to emulate B<awk>'s default behavior, whereas C<split(/ /)>
will give you as many null initial fields as there are leading spaces.
A C<split> on C</\s+/> is like a C<split(' ')> except that any leading
whitespace produces a null first field.  A C<split> with no arguments
really does a C<split(' ', $_)> internally.

=end original

特別な場合として、PATTERN にスペース (C<' '>) を指定すると、
引数なしの C<split> のように空白で split を行ないます。
つまり、
C<split(' ')> は、B<awk> のデフォルトの動作をエミュレートするために
使うことができ、C<split(/ /)> は行頭のスペースの数に応じた空フィールドが
できます。
C<split /\s+/> は C<split(' ')> と同様ですが、
先頭の空白は先頭の空フィールドとなります。
引数なしの C<split> は内部的には C<split(' ', $_)> を実行します。

=begin original

A PATTERN of C</^/> is treated as if it were C</^/m>, since it isn't
much use otherwise.

Example:

=end original

PATTERN に C</^/> を指定すると C</^/m> として扱われます。
他の意味に使われることはまずないからです。

例:

    open(PASSWD, '/etc/passwd');
    while (<PASSWD>) {
        chomp;
        ($login, $passwd, $uid, $gid,
         $gcos, $home, $shell) = split(/:/);
	#...
    }


=item sprintf FORMAT, LIST

=begin original

Returns a string formatted by the usual C<printf> conventions of the C
library function C<sprintf>.  See below for more details
and see L<sprintf(3)> or L<printf(3)> on your system for an explanation of
the general principles.

For example:

=end original

普通の C 言語の C<printf> 記法のフォーマットで、整形された文字列を返します。
一般的な原則の説明については以下の説明と、システムの
L<sprintf(3)> または L<printf(3)> の説明を参照してください。

例:

        # Format number with up to 8 leading zeroes
        $result = sprintf("%08d", $number);

        # Round number to 3 digits after decimal point
        $rounded = sprintf("%.3f", $number);

=begin original

Perl does its own C<sprintf> formatting--it emulates the C
function C<sprintf>, but it doesn't use it (except for floating-point
numbers, and even then only the standard modifiers are allowed).  As a
result, any non-standard extensions in your local C<sprintf> are not
available from Perl.

=end original

Perl は C<sprintf> フォーマット処理を自力で行います。
これは C の C<sprintf> 関数をエミュレートしますが、
C の関数は使いません(浮動小数点を除きますが、それでも標準の
記述子のみが利用できます)。
従って、ローカルな非標準の C<sprintf> 拡張機能は Perl では使えません。

=begin original

Unlike C<printf>, C<sprintf> does not do what you probably mean when you
pass it an array as your first argument. The array is given scalar context,
and instead of using the 0th element of the array as the format, Perl will
use the count of elements in the array as the format, which is almost never
useful.

=end original

C<printf> と違って、 C<sprintf> の最初の引数に配列を渡しても
あなたが多分望むとおりには動作しません。
配列はスカラコンテキストで渡されるので、配列の 0 番目の要素ではなく、
配列の要素数をフォーマットとして扱います。
これはほとんど役に立ちません。

Perl's C<sprintf> permits the following universally-known conversions:

   %%	a percent sign
   %c	a character with the given number
   %s	a string
   %d	a signed integer, in decimal
   %u	an unsigned integer, in decimal
   %o	an unsigned integer, in octal
   %x	an unsigned integer, in hexadecimal
   %e	a floating-point number, in scientific notation
   %f	a floating-point number, in fixed decimal notation
   %g	a floating-point number, in %e or %f notation

In addition, Perl permits the following widely-supported conversions:

   %X	like %x, but using upper-case letters
   %E	like %e, but using an upper-case "E"
   %G	like %g, but with an upper-case "E" (if applicable)
   %b	an unsigned integer, in binary
   %p	a pointer (outputs the Perl value's address in hexadecimal)
   %n	special: *stores* the number of characters output so far
        into the next variable in the parameter list 

Finally, for backward (and we do mean "backward") compatibility, Perl
permits these unnecessary but widely-supported conversions:

   %i	a synonym for %d
   %D	a synonym for %ld
   %U	a synonym for %lu
   %O	a synonym for %lo
   %F	a synonym for %f

Note that the number of exponent digits in the scientific notation by
C<%e>, C<%E>, C<%g> and C<%G> for numbers with the modulus of the
exponent less than 100 is system-dependent: it may be three or less
(zero-padded as necessary).  In other words, 1.23 times ten to the
99th may be either "1.23e99" or "1.23e099".

Perl permits the following universally-known flags between the C<%>
and the conversion letter:

   space   prefix positive number with a space
   +       prefix positive number with a plus sign
   -       left-justify within the field
   0       use zeros, not spaces, to right-justify
   #       prefix non-zero octal with "0", non-zero hex with "0x"
   number  minimum field width
   .number "precision": digits after decimal point for
           floating-point, max length for string, minimum length
           for integer
   l       interpret integer as C type "long" or "unsigned long"
   h       interpret integer as C type "short" or "unsigned short"
           If no flags, interpret integer as C type "int" or "unsigned"

There are also two Perl-specific flags:

   V       interpret integer as Perl's standard integer type
   v       interpret string as a vector of integers, output as
           numbers separated either by dots, or by an arbitrary
	   string received from the argument list when the flag
	   is preceded by C<*>

Where a number would appear in the flags, an asterisk (C<*>) may be
used instead, in which case Perl uses the next item in the parameter
list as the given number (that is, as the field width or precision).
If a field width obtained through C<*> is negative, it has the same
effect as the C<-> flag: left-justification.

The C<v> flag is useful for displaying ordinal values of characters
in arbitrary strings:

    printf "version is v%vd\n", $^V;		# Perl's version
    printf "address is %*vX\n", ":", $addr;	# IPv6 address
    printf "bits are %*vb\n", " ", $bits;	# random bitstring

If C<use locale> is in effect, the character used for the decimal
point in formatted real numbers is affected by the LC_NUMERIC locale.
See L<perllocale>.

If Perl understands "quads" (64-bit integers) (this requires
either that the platform natively support quads or that Perl
be specifically compiled to support quads), the characters

	d u o x X b i D U O

print quads, and they may optionally be preceded by

	ll L q

For example

	%lld %16LX %qo

You can find out whether your Perl supports quads via L<Config>:

	use Config;
	($Config{use64bitint} eq 'define' || $Config{longsize} == 8) &&
		print "quads\n";

If Perl understands "long doubles" (this requires that the platform
support long doubles), the flags

	e f g E F G

may optionally be preceded by

	ll L

For example

	%llf %Lg

You can find out whether your Perl supports long doubles via L<Config>:

	use Config;
	$Config{d_longdbl} eq 'define' && print "long doubles\n";

=item sqrt EXPR

=item sqrt

=begin original

Return the square root of EXPR.  If EXPR is omitted, returns square
root of C<$_>.  Only works on non-negative operands, unless you've
loaded the standard Math::Complex module.

=end original

EXPR の平方根を返します。
EXPR を省略すると、C<$_> の平方根を返します。
標準の Math::Complex モジュールを使わない場合は、
負の数の引数は扱えません。

    use Math::Complex;
    print sqrt(-2);    # prints 1.4142135623731i

=item srand EXPR

=item srand

=begin original

Sets the random number seed for the C<rand> operator.  If EXPR is
omitted, uses a semi-random value supplied by the kernel (if it supports
the F</dev/urandom> device) or based on the current time and process
ID, among other things.  In versions of Perl prior to 5.004 the default
seed was just the current C<time>.  This isn't a particularly good seed,
so many old programs supply their own seed value (often C<time ^ $$> or
C<time ^ ($$ + ($$ << 15))>), but that isn't necessary any more.

=end original

rand 演算子のためのシード値を設定します。
EXPR を省略すると、カーネルで提供されるもと
(F</dev/urandom> デバイスに対応している場合)または
現在時刻やプロセス ID やその他のものを基礎にした
擬似乱数を使います。
5.004 以前の Perl では、デフォルトのシード値は現在の C<time> でした。
これは特によいシード値ではありませんでしたので、
多くの古いプログラムは自力でシード値を指定しています
(C<time ^ $$> または C<time ^ ($$ + ($$ << 15))> がよく使われました)が、
もはやこれは必要ありません。

=begin original

In fact, it's usually not necessary to call C<srand> at all, because if
it is not called explicitly, it is called implicitly at the first use of
the C<rand> operator.  However, this was not the case in version of Perl
before 5.004, so if your script will run under older Perl versions, it
should call C<srand>.

=end original

実際、普通は C<srand> を呼び出す必要は全くありません。
明示的に呼び出されない場合、C<rand> 演算子を最初に使ったときに
自動的に呼び出されるからです。
しかし、5.004 以前のバージョンの Perl ではこれは行われないので、
古い Perl で動作させる可能性がある場合は、C<srand> を呼び出すべきです。

=begin original

Note that you need something much more random than the default seed for
cryptographic purposes.  Checksumming the compressed output of one or more
rapidly changing operating system status programs is the usual method.  For
example:

=end original

暗号処理には、
もっとランダムな値を使う必要があります。急激に変化する
OS のステータス値プログラムの出力をひとつ
または複数用い、圧縮してチェックサムをとる、
というようなことが普通、行なわれます。
例:

    srand (time ^ $$ ^ unpack "%L*", `ps axww | gzip`);

=begin original

If you're particularly concerned with this, see the C<Math::TrulyRandom>
module in CPAN.

=end original

特にこのようなことに関心がある場合は、
CPAN の C<Math::TrulyRandom> モジュールを参照して下さい。

=begin original

Do I<not> call C<srand> multiple times in your program unless you know
exactly what you're doing and why you're doing it.  The point of the
function is to "seed" the C<rand> function so that C<rand> can produce
a different sequence each time you run your program.  Just do it once at the
top of your program, or you I<won't> get random numbers out of C<rand>!

=end original

何をしようとしているか、そしてなぜそうするのかを正確に把握していない限り、
C<srand> をプログラム中でI<複数回呼び出してはいけません>。
この関数のポイントは、プログラムを実行するごとに C<rand> 関数が
異なる乱数列を生成できるように C<rand> 関数の「種」を設定することです。
単にプログラムの先頭で一回だけ呼び出してください。
さもなければ C<rand> で乱数を I<得られません>!

Frequently called programs (like CGI scripts) that simply use

    time ^ $$

for a seed can fall prey to the mathematical property that

    a^b == (a+1)^(b+1)

one-third of the time.  So don't do that.

=item stat FILEHANDLE

=item stat EXPR

=item stat

=begin original

Returns a 13-element list giving the status info for a file, either
the file opened via FILEHANDLE, or named by EXPR.  If EXPR is omitted,
it stats C<$_>.  Returns a null list if the stat fails.  Typically used
as follows:

=end original

FILEHANDLE を通じてオープンされているファイルか、
EXPR で指定されるファイルの情報を与える、13 要素のリストを返します。
EXPR が省略されると、 C<$_> が用いられます。
stat に失敗した場合には、空リストを返します。普通は、以下のようにして使います:

    ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,
       $atime,$mtime,$ctime,$blksize,$blocks)
           = stat($filename);

=begin original

Not all fields are supported on all filesystem types.  Here are the
meaning of the fields:

=end original

全てのファイルシステムで全てのフィールドに対応しているわけではあります。
フィールドの意味は以下のとおりです。

=begin original

  0 dev      device number of filesystem
  1 ino      inode number
  2 mode     file mode  (type and permissions)
  3 nlink    number of (hard) links to the file
  4 uid      numeric user ID of file's owner
  5 gid      numeric group ID of file's owner
  6 rdev     the device identifier (special files only)
  7 size     total size of file, in bytes
  8 atime    last access time in seconds since the epoch
  9 mtime    last modify time in seconds since the epoch
 10 ctime    inode change time (NOT creation time!) in seconds since the epoch
 11 blksize  preferred block size for file system I/O
 12 blocks   actual number of blocks allocated

(The epoch was at 00:00 January 1, 1970 GMT.)

=end original

  0 dev      ファイルシステムのデバイス番号
  1 ino      inode 番号
  2 mode     ファイルモード (タイプとパーミッション)
  3 nlink    ファイルへの(ハード)リンクの数
  4 uid      ファイル所有者のユーザー ID の数値
  5 gid      ファイル所有者のグループ ID の数値
  6 rdev     デバイス識別子(特殊ファイルのみ)
  7 size     ファイルサイズ(バイト単位)
  8 atime    紀元から、最後にアクセスされた時刻までの秒数
  9 mtime    紀元から、最後に修正(modify)された時刻までの秒数
 10 ctime    紀元から、inode 変更(change)された時刻(作成された時刻ではない!)までの秒数
 11 blksize  ファイルシステム I/O に適したブロックサイズ
 12 blocks   実際に割り当てられているブロックの数

(紀元は GMT で 1970/01/01 00:00:00)

=begin original

If stat is passed the special filehandle consisting of an underline, no
stat is done, but the current contents of the stat structure from the
last stat or filetest are returned.  Example:

=end original

下線だけの _ という特別なファイルハンドルを stat に渡すと、
実際には stat を行なわず、stat 構造体に残っている
前回の stat やファイルテストの情報が返されます。
例:

    if (-x $file && (($d) = stat(_)) && $d < 0) {
	print "$file is executable NFS file\n";
    }

=begin original

(This works on machines only for which the device number is negative
under NFS.)

=end original

(これは、NFS のもとでデバイス番号が負になるマシンで
のみ動作します。)

Because the mode contains both the file type and its permissions, you
should mask off the file type portion and (s)printf using a C<"%o"> 
if you want to see the real permissions.

    $mode = (stat($filename))[2];
    printf "Permissions are %04o\n", $mode & 07777;

In scalar context, C<stat> returns a boolean value indicating success
or failure, and, if successful, sets the information associated with
the special filehandle C<_>.

=begin original

The File::stat module provides a convenient, by-name access mechanism:

=end original

File::stat モジュールは、便利な名前によるアクセス機構を提供します。

    use File::stat;
    $sb = stat($filename);
    printf "File is %s, size is %s, perm %04o, mtime %s\n", 
	$filename, $sb->size, $sb->mode & 07777,
	scalar localtime $sb->mtime;

=begin original

You can import symbolic mode constants (C<S_IF*>) and functions
(C<S_IS*>) from the Fcntl module:

=end original

モード定数 (C<S_IF*>) と関数 (C<S_IS*>) を Fcntl モジュールから
インポートできます。

    use Fcntl ':mode';

    $mode = (stat($filename))[2];

    $user_rwx      = ($mode & S_IRWXU) >> 6;
    $group_read    = ($mode & S_IRGRP) >> 3;
    $other_execute =  $mode & S_IXOTH;

    printf "Permissions are %04o\n", S_ISMODE($mode), "\n";

    $is_setuid     =  $mode & S_ISUID;
    $is_setgid     =  S_ISDIR($mode);

=begin original

You could write the last two using the C<-u> and C<-d> operators.
The commonly available S_IF* constants are

=end original

最後の二つは C<-u> と C<-d> 演算子を使っても書けます。
一般に利用可能な S_IF* 定数は以下のものです。

    # Permissions: read, write, execute, for user, group, others.

    S_IRWXU S_IRUSR S_IWUSR S_IXUSR
    S_IRWXG S_IRGRP S_IWGRP S_IXGRP
    S_IRWXO S_IROTH S_IWOTH S_IXOTH

    # Setuid/Setgid/Stickiness.

    S_ISUID S_ISGID S_ISVTX S_ISTXT

    # File types.  Not necessarily all are available on your system.

    S_IFREG S_IFDIR S_IFLNK S_IFBLK S_ISCHR S_IFIFO S_IFSOCK S_IFWHT S_ENFMT

    # The following are compatibility aliases for S_IRUSR, S_IWUSR, S_IXUSR.

    S_IREAD S_IWRITE S_IEXEC

=begin original

and the S_IF* functions are

=end original

一般に利用可能な S_IF* 関数は以下のものです。

    S_IFMODE($mode)	the part of $mode containing the permission bits
			and the setuid/setgid/sticky bits

    S_IFMT($mode)	the part of $mode containing the file type
			which can be bit-anded with e.g. S_IFREG 
                        or with the following functions

    # The operators -f, -d, -l, -b, -c, -p, and -s.

    S_ISREG($mode) S_ISDIR($mode) S_ISLNK($mode)
    S_ISBLK($mode) S_ISCHR($mode) S_ISFIFO($mode) S_ISSOCK($mode)

    # No direct -X operator counterpart, but for the first one
    # the -g operator is often equivalent.  The ENFMT stands for
    # record flocking enforcement, a platform-dependent feature.

    S_ISENFMT($mode) S_ISWHT($mode)

=begin original

See your native chmod(2) and stat(2) documentation for more details
about the S_* constants.

=end original

S_* 定数に関する詳細についてはネイティブの chmod(2) と stat(2) の
ドキュメントを参照して下さい。

=item study SCALAR

=item study

=begin original

Takes extra time to study SCALAR (C<$_> if unspecified) in anticipation of
doing many pattern matches on the string before it is next modified.
This may or may not save time, depending on the nature and number of
patterns you are searching on, and on the distribution of character
frequencies in the string to be searched--you probably want to compare
run times with and without it to see which runs faster.  Those loops
which scan for many short constant strings (including the constant
parts of more complex patterns) will benefit most.  You may have only
one C<study> active at a time--if you study a different scalar the first
is "unstudied".  (The way C<study> works is this: a linked list of every
character in the string to be searched is made, so we know, for
example, where all the C<'k'> characters are.  From each search string,
the rarest character is selected, based on some static frequency tables
constructed from some C programs and English text.  Only those places
that contain this "rarest" character are examined.)

=end original

次に変更される前に、何回も文字列に対するパターンマッチを
行なうアプリケーションで、
そのような文字列 SCALAR(省略時には C<$_>) を予め学習しておきます。
これは、検索のために、どのようなパターンを何回使うかによって、
また、検索される文字列内の文字頻度の分布によって、
時間を節約することになるかもしれませんし、逆に浪費する
ことになるかもしれません。
予習をした場合と、しない場合の実行時間を比較して、
どちらが速いか調べることが、必要でしょう。
短い固定文字列 (複雑なパターンの固定部分を含みます) をたくさん
検索するループで、もっとも効果があるでしょう。
同時には、一つの C<study>だけが有効です。
別のスカラを study した場合には、以前に学習した内容は
「忘却」されてしまいます。
(この C<study> の仕組みは、まず、検索される文字列内の
すべての文字のリンクされたリストが作られ、たとえば、
すべての C<'k'> がどこにあるかがわかるようになります。
各々の検索文字列から、C プログラムや英語のテキストから作られた
頻度の統計情報に基づいて、もっともめずらしい文字が選ばれます。
この「めずらしい」文字を含む場所だけが調べられるのです。)

=begin original

For example, here is a loop that inserts index producing entries
before any line containing a certain pattern:

=end original

たとえば、特定のパターンを含む行の前にインデクスを
付けるエントリを入れる例を示します。

    while (<>) {
	study;
	print ".IX foo\n" 	if /\bfoo\b/;
	print ".IX bar\n" 	if /\bbar\b/;
	print ".IX blurfl\n" 	if /\bblurfl\b/;
	# ...
	print;
    }

=begin original

In searching for C</\bfoo\b/>, only those locations in C<$_> that contain C<f>
will be looked at, because C<f> is rarer than C<o>.  In general, this is
a big win except in pathological cases.  The only question is whether
it saves you more time than it took to build the linked list in the
first place.

=end original

C<f> は C<o> よりもめずらしいので、C</\bfoo\b/> を探すと
き、C<$_> で C<f> を含む場所だけが探されます。
一般に、病的な場合を除いて、かなりの結果が得られます。
唯一の問題は、節約できる時間が、最初にリンクリストを作る
時間よりも多いかどうかです、

=begin original

Note that if you have to look for strings that you don't know till
runtime, you can build an entire loop as a string and C<eval> that to
avoid recompiling all your patterns all the time.  Together with
undefining C<$/> to input entire files as one record, this can be very
fast, often faster than specialized programs like fgrep(1).  The following
scans a list of files (C<@files>) for a list of words (C<@words>), and prints
out the names of those files that contain a match:

=end original

実行時まで、探そうとする文字列がわからないときには、
ループ全体を文字列として組み立てて、C<eval> すれば、
いつも、すべてのパターンを再コンパイルするという事態は
避けられます。
ファイル全体を一つのレコードとして入力するために、
C<$/> を未定義にすれば、かなり速くなり、
多くの場合 fgrep(1) のような専用のプログラムより速くなります。
以下の例は、ファイルのリスト (C<@files>) から
単語のリスト (C<@words>) を探して、マッチするものが
あったファイル名を出力します。

    $search = 'while (<>) { study;';
    foreach $word (@words) {
	$search .= "++\$seen{\$ARGV} if /\\b$word\\b/;\n";
    }
    $search .= "}";
    @ARGV = @files;
    undef $/;
    eval $search;		# this screams
    $/ = "\n";		# put back to normal input delimiter
    foreach $file (sort keys(%seen)) {
	print $file, "\n";
    }

=item sub BLOCK

=item sub NAME

=item sub NAME BLOCK

=begin original

This is subroutine definition, not a real function I<per se>.  With just a
NAME (and possibly prototypes or attributes), it's just a forward declaration.
Without a NAME, it's an anonymous function declaration, and does actually
return a value: the CODE ref of the closure you just created.  See L<perlsub>
and L<perlref> for details.

=end original

これはサブルーチン定義であり、I<本質的には>実際の関数ではありません。
NAME (とプロトタイプや属性) のみの場合は、単なる前方定義です。
NAME なしの場合は、無名関数定義であり、実際には値を返します。
作成したブロックのコードリファレンスです。
詳しくは L<perlsub> と L<perlref> を参照して下さい。

=item substr EXPR,OFFSET,LENGTH,REPLACEMENT

=item substr EXPR,OFFSET,LENGTH

=item substr EXPR,OFFSET

=begin original

Extracts a substring out of EXPR and returns it.  First character is at
offset C<0>, or whatever you've set C<$[> to (but don't do that).
If OFFSET is negative (or more precisely, less than C<$[>), starts
that far from the end of the string.  If LENGTH is omitted, returns
everything to the end of the string.  If LENGTH is negative, leaves that
many characters off the end of the string.

=end original

EXPR から、部分文字列を取り出して返します。　最初の
文字がオフセット C<0> もしくは、C<$[> に設定した値
(しかしこれを使ってはいけません)となります。
OFFSET に負の値(より厳密には、C<$[>より小さい値)を設定すると、
EXPR の終わりからのオフセットとなります。
LENGTH を省略すると、EXPR の最後まですべてが返されます。
LENGTH が負の値だと、文字列の最後から指定された数だけ文字を取り除きます。

=begin original

You can use the substr() function as an lvalue, in which case EXPR
must itself be an lvalue.  If you assign something shorter than LENGTH,
the string will shrink, and if you assign something longer than LENGTH,
the string will grow to accommodate it.  To keep the string the same
length you may need to pad or chop your value using C<sprintf>.

=end original

substr() を左辺値として使用することも可能で、その場合には、
EXPR が自身左辺値でなければなりません。
LENGTH より短いものを代入したときには、
EXPR は短くなり、LENGTH より長いものを代入したときには、
EXPR はそれに合わせて伸びることになります。
EXPR の長さを一定に保つためには、C<sprintf> を使って、
代入する値の長さを調整することが、必要になるかもしれません。

If OFFSET and LENGTH specify a substring that is partly outside the
string, only the part within the string is returned.  If the substring
is beyond either end of the string, substr() returns the undefined
value and produces a warning.  When used as an lvalue, specifying a
substring that is entirely outside the string is a fatal error.
Here's an example showing the behavior for boundary cases:

    my $name = 'fred';
    substr($name, 4) = 'dy';		# $name is now 'freddy'
    my $null = substr $name, 6, 2;	# returns '' (no warning)
    my $oops = substr $name, 7;		# returns undef, with warning
    substr($name, 7) = 'gap';		# fatal error

An alternative to using substr() as an lvalue is to specify the
replacement string as the 4th argument.  This allows you to replace
parts of the EXPR and return what was there before in one operation,
just as you can with splice().

=item symlink OLDFILE,NEWFILE

=begin original

Creates a new filename symbolically linked to the old filename.
Returns C<1> for success, C<0> otherwise.  On systems that don't support
symbolic links, produces a fatal error at run time.  To check for that,
use eval:

=end original

NEWFILE として、OLDFILE へのシンボリックリンクを生成します。
成功時には C<1> を返し、失敗時には C<0> を返します。
シンボリックリンクをサポートしていないシステムでは、
実行時に致命的エラーが発生します。
これをチェックするには、eval を使用します:

    $symlink_exists = eval { symlink("",""); 1 };

=item syscall LIST

=begin original

Calls the system call specified as the first element of the list,
passing the remaining elements as arguments to the system call.  If
unimplemented, produces a fatal error.  The arguments are interpreted
as follows: if a given argument is numeric, the argument is passed as
an int.  If not, the pointer to the string value is passed.  You are
responsible to make sure a string is pre-extended long enough to
receive any result that might be written into a string.  You can't use a
string literal (or other read-only string) as an argument to C<syscall>
because Perl has to assume that any string pointer might be written
through.  If your
integer arguments are not literals and have never been interpreted in a
numeric context, you may need to add C<0> to them to force them to look
like numbers.  This emulates the C<syswrite> function (or vice versa):

=end original

LIST の最初の要素で指定するシステムコールを、残りの
要素をそのシステムコールの引数として呼び出します。
実装されていないときには、致命的エラーとなります。
引数は、以下のように解釈されます: 引数が数字であれば、
int として引数を渡します。
そうでなければ、文字列値へのポインタが渡されます。
文字列に結果を受け取るときには、
その結果を受け取るのに十分なくらいに、
文字列を予め伸ばしておく必要があります。
文字列リテラル(あるいはその他の読み込み専用の文字列)を
C<syscall>の引数として使うことはできません。
Perl は全ての文字列ポインタは書き込まれると
仮定しなければならないからです。
整数引数が、リテラルでなく、
数値コンテキストで評価されたことのないものであれば、
数値として解釈されるように、
C<0> を足しておく必要があるかもしれません。
以下は C<syswrite>関数(あるいはその逆)をエミュレートします。

    require 'syscall.ph';		# may need to run h2ph
    $s = "hi there\n";
    syscall(&SYS_write, fileno(STDOUT), $s, length $s);

=begin original

Note that Perl supports passing of up to only 14 arguments to your system call,
which in practice should usually suffice.

=end original

Perl は、システムコールに最大 14 個の引数しか渡せませんが、
実用上問題はないでしょう。

=begin original

Syscall returns whatever value returned by the system call it calls.
If the system call fails, C<syscall> returns C<-1> and sets C<$!> (errno).
Note that some system calls can legitimately return C<-1>.  The proper
way to handle such calls is to assign C<$!=0;> before the call and
check the value of C<$!> if syscall returns C<-1>.

=end original

syscall は、呼び出したシステムコールが返した値を返します。
システムコールが失敗すると、C<syscall> は C<-1> を返し、
C<$!>(errno) を設定します。
システムコールが正常に C<-1> を返す場合があることに注意してください。
このようなシステムコールを正しく扱うには、
C<$!=0;> をシステムコールの前に実行し、
syscall が C<-1> を返した時には C<$!> の値を調べてください。

=begin original

There's a problem with C<syscall(&SYS_pipe)>: it returns the file
number of the read end of the pipe it creates.  There is no way
to retrieve the file number of the other end.  You can avoid this 
problem by using C<pipe> instead.

=end original

C<syscall(&SYS_pipe)> には問題があります。
これはパイプの読み出し側が作ったファイル番号を返します。
相手側のファイル番号を得る方法がありません。
この問題を避けるためには、代わりに C<pipe> を使ってください。

=item sysopen FILEHANDLE,FILENAME,MODE

=item sysopen FILEHANDLE,FILENAME,MODE,PERMS

=begin original

Opens the file whose filename is given by FILENAME, and associates it
with FILEHANDLE.  If FILEHANDLE is an expression, its value is used as
the name of the real filehandle wanted.  This function calls the
underlying operating system's C<open> function with the parameters
FILENAME, MODE, PERMS.

=end original

FILENAME で与えられたファイル名のファイルをオープンし、
FILEHANDLE と結び付けます。
FILEHANDLE が式の場合、その値は実際に求めているファイルハンドルの名前として
扱われます。
この関数呼び出しはシステムの C<open> 関数を FILENAME, MODE, PERMS の
引数で呼び出すことを基礎としています。

The possible values and flag bits of the MODE parameter are
system-dependent; they are available via the standard module C<Fcntl>.
See the documentation of your operating system's C<open> to see which
values and flag bits are available.  You may combine several flags
using the C<|>-operator.

Some of the most common values are C<O_RDONLY> for opening the file in
read-only mode, C<O_WRONLY> for opening the file in write-only mode,
and C<O_RDWR> for opening the file in read-write mode, and.

For historical reasons, some values work on almost every system
supported by perl: zero means read-only, one means write-only, and two
means read/write.  We know that these values do I<not> work under
OS/390 & VM/ESA Unix and on the Macintosh; you probably don't want to
use them in new code.

If the file named by FILENAME does not exist and the C<open> call creates
it (typically because MODE includes the C<O_CREAT> flag), then the value of
PERMS specifies the permissions of the newly created file.  If you omit
the PERMS argument to C<sysopen>, Perl uses the octal value C<0666>.
These permission values need to be in octal, and are modified by your
process's current C<umask>.

In many systems the C<O_EXCL> flag is available for opening files in
exclusive mode.  This is B<not> locking: exclusiveness means here that
if the file already exists, sysopen() fails.  The C<O_EXCL> wins
C<O_TRUNC>.

Sometimes you may want to truncate an already-existing file: C<O_TRUNC>.

You should seldom if ever use C<0644> as argument to C<sysopen>, because
that takes away the user's option to have a more permissive umask.
Better to omit it.  See the perlfunc(1) entry on C<umask> for more
on this.

=begin original

Note that C<sysopen> depends on the fdopen() C library function.
On many UNIX systems, fdopen() is known to fail when file descriptors
exceed a certain value, typically 255. If you need more file
descriptors than that, consider rebuilding Perl to use the C<sfio>
library, or perhaps using the POSIX::open() function.

=end original

C<sysopen> は C の fdopen() ライブラリ関数に依存していることに注意してください。
多くの UNIX システムでは、fdopen() はファイル記述子がある値(例えば 255)を超えると
失敗することが知られています。
これより多くのファイル記述子が必要な場合は、
Perl を C<sfio> ライブラリを使って再ビルドするか、
POSIX::open() 関数を使うことを健闘してください。

=begin original

See L<perlopentut> for a kinder, gentler explanation of opening files.

=end original

ファイル操作に関するより親切な説明については L<perlopentut> を参照して下さい。

=item sysread FILEHANDLE,SCALAR,LENGTH,OFFSET

=item sysread FILEHANDLE,SCALAR,LENGTH

=begin original

Attempts to read LENGTH bytes of data into variable SCALAR from the
specified FILEHANDLE, using the system call read(2).  It bypasses stdio,
so mixing this with other kinds of reads, C<print>, C<write>,
C<seek>, C<tell>, or C<eof> can cause confusion because stdio
usually buffers data.  Returns the number of bytes actually read, C<0>
at end of file, or undef if there was an error.  SCALAR will be grown or
shrunk so that the last byte actually read is the last byte of the
scalar after the read.

=end original

システムコール read(2) を用いて、指定した FILEHANDLE
から、変数 SCALAR へ、LENGTH バイトのデータの読み込みを試みます。
これは、stdio ルーチンを通りませんから、
他の入力関数, C<print>, C<write>,
C<seek>, C<tell>, C<eof> と混ぜて使うと、入力がおかしくなる
かもしれません。stdio は普通データをバッファリングするからです。
ファイルの最後では C<0>が、
エラー時には undef が、
それ以外では実際に読み込まれたデータの長さが返されます。
実際に読み込んだ最後のバイトが read した後の最後のバイトになるので、
SCALAR は伸び縮みします。

=begin original

An OFFSET may be specified to place the read data at some place in the
string other than the beginning.  A negative OFFSET specifies
placement at that many bytes counting backwards from the end of the
string.  A positive OFFSET greater than the length of SCALAR results
in the string being padded to the required size with C<"\0"> bytes before
the result of the read is appended.

=end original

OFFSET を指定すると、
SCALAR の先頭以外の場所から、読み込みを行なうことができます。
OFFSET に負の値を指定すると、文字列の最後から逆向きに何バイト目かで
位置を指定します。
OFFSET が正の値で、SCALAR の長さよりも大きかった場合、
必要なサイズになるまで C<"\0"> でパッディングされ、その後に
読み込み結果が追加されます。

=begin original

There is no syseof() function, which is ok, since eof() doesn't work
very well on device files (like ttys) anyway.  Use sysread() and check
for a return value for 0 to decide whether you're done.

=end original

syseof() 関数はありませんが、問題ありません。
どちらにしろ eof() は(tty のような)デバイスファイルに対しては
うまく動作しないからです。
sysread() を使って、 返り値が 0 かどうかで最後まで読んだかを
判断してください。

=item sysseek FILEHANDLE,POSITION,WHENCE

=begin original

Sets FILEHANDLE's system position using the system call lseek(2).  It
bypasses stdio, so mixing this with reads (other than C<sysread>),
C<print>, C<write>, C<seek>, C<tell>, or C<eof> may cause confusion.
FILEHANDLE may be an expression whose value gives the name of the
filehandle.  The values for WHENCE are C<0> to set the new position to
POSITION, C<1> to set the it to the current position plus POSITION,
and C<2> to set it to EOF plus POSITION (typically negative).  For
WHENCE, you may also use the constants C<SEEK_SET>, C<SEEK_CUR>, and
C<SEEK_END> (start of the file, current position, end of the file)
from the Fcntl module.

=end original

FILEHANDLE のシステム位置を lseek(2) システムコールを使って設定します。
これは stdio をバイパスしますので、
(C<sysread>以外の)読み込み、
C<print>, C<write>, C<seek>, C<tell>, C<eof> と混ぜて使うと
混乱を引き起こします。
FILEHANDLE は式でも構いません。その場合はその値がファイルハンドルの
名前となります。
WHENCE の値が、C<0> ならば、
新しい位置を POSITION の位置へ、C<1> ならば、現在位置から
POSITION 加えた位置へ、C<2> ならば、EOF から POSITION
だけ加えた位置へ、新しい位置を設定します。
この値には、Fcntl モジュールで使われている C<SEEK_SET>、
C<SEEK_CUR>、C<SEEK_END> 
(ファイルの先頭、現在位置、ファイルの最後)という定数を使うこともできます。

=begin original

Returns the new position, or the undefined value on failure.  A position
of zero is returned as the string C<"0 but true">; thus C<sysseek> returns
true on success and false on failure, yet you can still easily determine
the new position.

=end original

新しい位置を返します。失敗したときは未定義値を返します。
位置がゼロの場合は、C<"0 but true"> の文字列として返されます。
従って C<sysseek> は成功時に真を返し、失敗時に偽を返しますが、
簡単に新しい位置を判定できます。

=item system LIST

=item system PROGRAM LIST

=begin original

Does exactly the same thing as C<exec LIST>, except that a fork is
done first, and the parent process waits for the child process to
complete.  Note that argument processing varies depending on the
number of arguments.  If there is more than one argument in LIST,
or if LIST is an array with more than one value, starts the program
given by the first element of the list with arguments given by the
rest of the list.  If there is only one scalar argument, the argument
is checked for shell metacharacters, and if there are any, the
entire argument is passed to the system's command shell for parsing
(this is C</bin/sh -c> on Unix platforms, but varies on other
platforms).  If there are no shell metacharacters in the argument,
it is split into words and passed directly to C<execvp>, which is
more efficient.

=end original

C<exec LIST> とほとんど同じですが、まず fork を行ない、
親プロセスではチャイルドプロセスが終了するのを wait します。
exec の項で述べたように、引数の処理は、引数の数によって異なることに
注意してください。
LIST に複数の引数がある場合、または LIST が複数の要素からなる配列の場合、
リストの最初の要素で与えられるプログラムを、リストの残りの要素を
引数として起動します。
スカラの引数が一つだけの場合、
引数はシェルのメタキャラクタをチェックされ、もしあれば
パーズのために引数全体がシステムコマンドシェル
(これは Unix プラットフォームでは C</bin/sh -c> ですが、
他のプラットフォームでは異なります)に渡されます。
シェルメタキャラクタがなかった場合、
引数は単語に分解されて直接 C<execvp> に渡されます。
この方がより効率的です。

=begin original

Beginning with v5.6.0, Perl will attempt to flush all files opened for
output before any operation that may do a fork, but this may not be
supported on some platforms (see L<perlport>).  To be safe, you may need
to set C<$|> ($AUTOFLUSH in English) or call the C<autoflush()> method
of C<IO::Handle> on any open handles.

=end original

v5.6.0から、Perl は fork を行うようなあらゆる動作の前に
出力用にオープンしていた全てのファイルをフラッシュしようとします。
しかしこれをサポートしていないプラットフォームもあります(L<perlport> 参照)。
安全のためには、C<$l>(English モジュールを使っているなら $AUTOFLUSH)を
設定するか、あらゆるオープン済みハンドルにおいて C<IO::Handle> の C<autoflush()>
メソッドが必要となるかもしれません。

=begin original

The return value is the exit status of the program as
returned by the C<wait> call.  To get the actual exit value divide by
256.  See also L</exec>.  This is I<not> what you want to use to capture
the output from a command, for that you should use merely backticks or
C<qx//>, as described in L<perlop/"`STRING`">.  Return value of -1
indicates a failure to start the program (inspect $! for the reason).

=end original

返り値は、C<wait> が返すプログラムの exit 状態です。
実際の exit 値を得るには 256 で割ってください。
L</exec> も参照してください。
これはコマンドからの出力を捕らえるために使うものI<ではありません>。
そのような用途には、L<perlop/"`STRING`"> に記述されている
逆クォートや C<qx//> を使用してください。
-1 の返り値はプログラムを開始させることに失敗したことを示します
(理由は $! を調べてください)。

=begin original

Like C<exec>, C<system> allows you to lie to a program about its name if
you use the C<system PROGRAM LIST> syntax.  Again, see L</exec>.

=end original

C<exec> と同様に、C<system> でも C<system PROGRAM LIST> の文法を
使うことで、プログラムに対してその名前を嘘をつくことができます。
再び、L</exec> を参照して下さい。

=begin original

Because C<system> and backticks block C<SIGINT> and C<SIGQUIT>, killing the
program they're running doesn't actually interrupt your program.

=end original

C<system> とバッククォートは C<SIGINT> と C<SIGQUIT> をブロックするので、
これらが実行したプログラムを kill しても実際に元のプログラムを
中断させることはありません。

    @args = ("command", "arg1", "arg2");
    system(@args) == 0
	 or die "system @args failed: $?"

=begin original

You can check all the failure possibilities by inspecting
C<$?> like this:

=end original

以下のように C<$?> を調べることで、全ての失敗の可能性を
チェックすることができます:

    $exit_value  = $? >> 8;
    $signal_num  = $? & 127;
    $dumped_core = $? & 128;

=begin original

When the arguments get executed via the system shell, results
and return codes will be subject to its quirks and capabilities.
See L<perlop/"`STRING`"> and L</exec> for details.

=end original

引数がシステムシェル経由で実行された場合、
結果と返り値はシェルの癖と能力によって変更されることがあります。
詳細については L<perlop/"`STRING`"> と L</exec> を参照して下さい。

=item syswrite FILEHANDLE,SCALAR,LENGTH,OFFSET

=item syswrite FILEHANDLE,SCALAR,LENGTH

=item syswrite FILEHANDLE,SCALAR

=begin original

Attempts to write LENGTH bytes of data from variable SCALAR to the
specified FILEHANDLE, using the system call write(2).  If LENGTH
is not specified, writes whole SCALAR.  It bypasses stdio, so mixing
this with reads (other than C<sysread())>, C<print>, C<write>,
C<seek>, C<tell>, or C<eof> may cause confusion because stdio
usually buffers data.  Returns the number of bytes actually written,
or C<undef> if there was an error.  If the LENGTH is greater than
the available data in the SCALAR after the OFFSET, only as much
data as is available will be written.

=end original

write(2) システムコールを使って、指定した FILEHANDLEへ、
変数 SCALAR から、LENGTH バイトのデータの書き込みを試みます。
LENGTH が指定されなかった場合、 SCALAR 全体を書き込みます。
これは、stdio ルーチンを通りませんから、
他の入力関数(C<sysread()>以外), C<print>, C<write>,
C<seek>, C<tell>, or C<eof>と混ぜて使うと、
出力がおかしくなるかもしれません。
stdio は普通データをバッファリングするからです。
実際に読み込まれたデータの長さか、エラー時には C<undef> が返されます。
LENGTH が OFFSET 以降の SCALAR の利用可能なデータより大きかった場合、
利用可能なデータのみが書き込まれます。

=begin original

An OFFSET may be specified to write the data from some part of the
string other than the beginning.  A negative OFFSET specifies writing
that many bytes counting backwards from the end of the string.  In the
case the SCALAR is empty you can use OFFSET but only zero offset.

=end original

OFFSET を指定すると、SCALAR の先頭以外の場所から、
データを取り出して、書き込みを行なうことができます。
OFFSET に負の値を指定すると、文字列の最後から逆向きに数えて
何バイト目から書き込むかを示します。
SCALAR が空の場合、OFFSET はゼロのみ使用できます。

=item tell FILEHANDLE

=item tell

=begin original

Returns the current position for FILEHANDLE, or -1 on error.  FILEHANDLE
may be an expression whose value gives the name of the actual filehandle.
If FILEHANDLE is omitted, assumes the file last read.  

=end original

FILEHANDLE の現在の位置を返します。エラーの場合は -1 を返します。
FILEHANDLE は、実際のファイルハンドル名を示す式でもかまいません。
FILEHANDLE が省略された場合には、
最後に読み込みを行なったファイルについて調べます。

=begin original

The return value of tell() for the standard streams like the STDIN
depends on the operating system: it may return -1 or something else.
tell() on pipes, fifos, and sockets usually returns -1.

=end original

STDIN のような標準ストリームに対する tell() の返り値は OS に依存します。
-1 やその他の値が返ってくるかもしれません。
パイプ、FIFO、ソケットに対して tell() を使うと、普通は -1 が返ります。

=begin original

There is no C<systell> function.  Use C<sysseek(FH, 0, 1)> for that.

=end original

C<systell> 関数はありません。代わりに C<sysseek(FH, 0, 1)> を使ってください。

=item telldir DIRHANDLE

=begin original

Returns the current position of the C<readdir> routines on DIRHANDLE.
Value may be given to C<seekdir> to access a particular location in a
directory.  Has the same caveats about possible directory compaction as
the corresponding system library routine.

=end original

DIRHANDLE 上の C<readdir> ルーチンに対する現在位置を返します。
値は、そのディレクトリで特定の位置をアクセスするため、
C<seekdir> に渡すことができます。
同名のシステムライブラリルーチンと同じく、
ディレクトリ縮小時の問題が考えられます。

=item tie VARIABLE,CLASSNAME,LIST

=begin original

This function binds a variable to a package class that will provide the
implementation for the variable.  VARIABLE is the name of the variable
to be enchanted.  CLASSNAME is the name of a class implementing objects
of correct type.  Any additional arguments are passed to the C<new>
method of the class (meaning C<TIESCALAR>, C<TIEHANDLE>, C<TIEARRAY>,
or C<TIEHASH>).  Typically these are arguments such as might be passed
to the C<dbm_open()> function of C.  The object returned by the C<new>
method is also returned by the C<tie> function, which would be useful
if you want to access other methods in CLASSNAME.

=end original

この関数は、変数を、その変数の実装を行なうクラスと結び付けます。
VARIABLE は、魔法をかける変数の名前です。
CLASSNAME は、正しい型のオブジェクトを実装するクラスの名前です。
他に引数があれば、そのクラスの C<new> メソッドに渡されます
(つまり C<TIESCALAR>, C<TIEHANDLE>, C<TIEARRAY>, C<TIEHASH>)。
通常、これらは、C の C<dbm_open> などの関数に渡す引数となります。
C<new> メソッドで返されるオブジェクトはまた C<tie> 関数でも返されます。
これは CLASSNAME の他のメソッドにアクセスしたいときに便利です。

=begin original

Note that functions such as C<keys> and C<values> may return huge lists
when used on large objects, like DBM files.  You may prefer to use the
C<each> function to iterate over such.  Example:

=end original

DBM ファイルのような大きなオブジェクトでは、C<keys> や C<values> のような
関数は、大きなリストを返す可能性があります。
そのような場合では、C<each> 関数を使って繰り返しを行なった方が
よいかもしれません。
例:

    # print out history file offsets
    use NDBM_File;
    tie(%HIST, 'NDBM_File', '/usr/lib/news/history', 1, 0);
    while (($key,$val) = each %HIST) {
	print $key, ' = ', unpack('L',$val), "\n";
    }
    untie(%HIST);

=begin original

A class implementing a hash should have the following methods:

=end original

ハッシュを実装するクラスでは、次のようなメソッドを用意します:

    TIEHASH classname, LIST
    FETCH this, key
    STORE this, key, value
    DELETE this, key
    CLEAR this
    EXISTS this, key
    FIRSTKEY this
    NEXTKEY this, lastkey
    DESTROY this
    UNTIE this

=begin original

A class implementing an ordinary array should have the following methods:

=end original

通常の配列を実装するクラスでは、次のようなメソッドを用意します:

    TIEARRAY classname, LIST
    FETCH this, key
    STORE this, key, value
    FETCHSIZE this
    STORESIZE this, count
    CLEAR this
    PUSH this, LIST
    POP this
    SHIFT this
    UNSHIFT this, LIST
    SPLICE this, offset, length, LIST
    EXTEND this, count
    DESTROY this
    UNTIE this

=begin original

A class implementing a file handle should have the following methods:

=end original

ファイルハンドルを実装するクラスでは、次のようなメソッドを用意します:

    TIEHANDLE classname, LIST
    READ this, scalar, length, offset
    READLINE this
    GETC this
    WRITE this, scalar, length, offset
    PRINT this, LIST
    PRINTF this, format, LIST
    BINMODE this
    EOF this
    FILENO this
    SEEK this, position, whence
    TELL this
    OPEN this, mode, LIST
    CLOSE this
    DESTROY this
    UNTIE this

=begin original

A class implementing a scalar should have the following methods:

=end original

スカラ変数を実装するクラスでは、次のようなメソッドを用意します:

    TIESCALAR classname, LIST
    FETCH this,
    STORE this, value
    DESTROY this
    UNTIE this

=begin original

Not all methods indicated above need be implemented.  See L<perltie>,
L<Tie::Hash>, L<Tie::Array>, L<Tie::Scalar>, and L<Tie::Handle>.

=end original

上記の全てのメソッドを実装する必要はありません。
L<perltie>, L<Tie::Hash>, L<Tie::Array>, L<Tie::Scalar>,
L<Tie::Handle> を参照して下さい。

=begin original

Unlike C<dbmopen>, the C<tie> function will not use or require a module
for you--you need to do that explicitly yourself.  See L<DB_File>
or the F<Config> module for interesting C<tie> implementations.

=end original

C<dbmopen> と違い、C<tie> 関数はモジュールを use したり
require したりしません。自分で明示的に行う必要があります。
C<tie> の興味深い実装については L<DB_File> や F<Config> モジュールを
参照して下さい。

=begin original

For further details see L<perltie>, L<"tied VARIABLE">.

=end original

更なる詳細については L<perltie> や L<"tied VARIABLE"> を参照して下さい。

=item tied VARIABLE

=begin original

Returns a reference to the object underlying VARIABLE (the same value
that was originally returned by the C<tie> call that bound the variable
to a package.)  Returns the undefined value if VARIABLE isn't tied to a
package.

=end original

VARIABLE の基となるオブジェクトへのリファレンスを返します
(変数をパッケージに結びつけるために C<tie> 呼び出しをしたときの
返り値と同じものです)。
VARIABLE がパッケージと結び付けられていない場合は未定義値を返します。

=item time

=begin original

Returns the number of non-leap seconds since whatever time the system
considers to be the epoch (that's 00:00:00, January 1, 1904 for MacOS,
and 00:00:00 UTC, January 1, 1970 for most other systems).
Suitable for feeding to C<gmtime> and C<localtime>.

=end original

システムが紀元と考える時点 (MacOS では 1904 年 1 月 1 日 00:00:00、
その他のほとんどのシステムでは UTC 1970 年 1 月 1 日 00:00:00) からの連続秒数を
返します。C<gmtime> や C<localtime> への入力形式にあっています。

=begin original

For measuring time in better granularity than one second,
you may use either the Time::HiRes module from CPAN, or
if you have gettimeofday(2), you may be able to use the
C<syscall> interface of Perl, see L<perlfaq8> for details.

=end original

1 秒よりも細かい時間を計測するためには、
CPAN の Time::HiRes モジュールを使うか、
gettimeofday(2) があるなら、Perl の C<syscall> インターフェースを
使った方がよいでしょう。詳しくは L<perlfaq8> を参照して下さい。

=item times

=begin original

Returns a four-element list giving the user and system times, in
seconds, for this process and the children of this process.

=end original

現プロセスと、そのチャイルドプロセスに対する、
ユーザ時間とシステム時間を秒で示した、4 要素のリスト値を
返します。

    ($user,$system,$cuser,$csystem) = times;

=item tr///

=begin original

The transliteration operator.  Same as C<y///>.  See L<perlop>.

=end original

変換演算子。C<y///>と同じです。 L<perlop> を参照してください。

=item truncate FILEHANDLE,LENGTH

=item truncate EXPR,LENGTH

=begin original

Truncates the file opened on FILEHANDLE, or named by EXPR, to the
specified length.  Produces a fatal error if truncate isn't implemented
on your system.  Returns true if successful, the undefined value
otherwise.

=end original

FILEHANDLE 上にオープンされたファイルか、EXPR で名前を表わしたファイルを、
指定した長さに切り詰めます。
システム上に truncate が実装されていなければ、致命的エラーとなります。
成功すれば真を、さもなければ未定義値を返します。

=item uc EXPR

=item uc

=begin original

Returns an uppercased version of EXPR.  This is the internal function
implementing the C<\U> escape in double-quoted strings.
Respects current LC_CTYPE locale if C<use locale> in force.  See L<perllocale>.
Under Unicode (C<use utf8>) it uses the standard Unicode uppercase mappings.  (It
does not attempt to do titlecase mapping on initial letters.  See C<ucfirst> for that.)

If EXPR is omitted, uses C<$_>.

=end original

EXPR を大文字に変換したものを返します。
これは、ダブルクォート文字列における、C<\U> エスケープを
実装する内部関数です。
C<use locale> が有効な場合は、現在の LC_CTYPE ロケールを参照します。
L<perllocale> を参照してください。
(C<use utf8> を使った) Unicode では、標準 Unicode 大文字マッピングを
使います(元の文字の titlecase マッピングは試みません。
このためには C<ucfirst> を参照してください)。

EXPR が省略されると、C<$_> を使います。

=item ucfirst EXPR

=item ucfirst

=begin original

Returns the value of EXPR with the first character
in uppercase (titlecase in Unicode).  This is
the internal function implementing the C<\u> escape in double-quoted strings.
Respects current LC_CTYPE locale if C<use locale> in force.  See L<perllocale>
and L<utf8>.

If EXPR is omitted, uses C<$_>.

=end original

最初の文字だけを大文字にした、EXPR を返します
(Unicode では titlecase)。
これは、ダブルクォート文字列における、C<\u> エスケープを
実装する内部関数です。
C<use locale> が有効な場合は、現在の LC_CTYPE ロケールを参照します。
L<perllocale> と L<utf8> を参照してください。

EXPR が省略されると、C<$_> を使います。

=item umask EXPR

=item umask

=begin original

Sets the umask for the process to EXPR and returns the previous value.
If EXPR is omitted, merely returns the current umask.

=end original

現在のプロセスの umask を EXPR に設定し、以前の値を返します。
EXPR が省略されると、単にその時点の umask の値を返します。

=begin original

The Unix permission C<rwxr-x---> is represented as three sets of three
bits, or three octal digits: C<0750> (the leading 0 indicates octal
and isn't one of the digits).  The C<umask> value is such a number
representing disabled permissions bits.  The permission (or "mode")
values you pass C<mkdir> or C<sysopen> are modified by your umask, so
even if you tell C<sysopen> to create a file with permissions C<0777>,
if your umask is C<0022> then the file will actually be created with
permissions C<0755>.  If your C<umask> were C<0027> (group can't
write; others can't read, write, or execute), then passing
C<sysopen> C<0666> would create a file with mode C<0640> (C<0666 &~
027> is C<0640>).

=end original

Unix パーミッション C<rwxr-x---> は 3 ビットの 3 つの組、
または 3 桁の 8 進数として表現されます:
C<0750> (先頭の 0 は 8 進数を意味し、実際の値ではありません)。
C<umask> の値は無効にするパーミッションビットのこのような数値表現です。
C<mkdir> や C<sysopen> で渡されたパーミッション(または「モード」)の値は
umask で修正され、たとえ C<sysopen> で C<0777> のパーミッションで
ファイルを作るように指定しても、umask が C<0022> なら、
結果としてファイルは C<0755> のパーミッションで作成されます。
C<umask> が C<0027> (グループは書き込めない; その他は読み込み、書き込み、
実行できない) のとき、C<sysopen> に C<0666> を渡すと、
ファイルはモード C<0640> (C<0666 &~ 027> は C<0640>)で作成されます。

=begin original

Here's some advice: supply a creation mode of C<0666> for regular
files (in C<sysopen>) and one of C<0777> for directories (in
C<mkdir>) and executable files.  This gives users the freedom of
choice: if they want protected files, they might choose process umasks
of C<022>, C<027>, or even the particularly antisocial mask of C<077>.
Programs should rarely if ever make policy decisions better left to
the user.  The exception to this is when writing files that should be
kept private: mail files, web browser cookies, I<.rhosts> files, and
so on.

=end original

Here's some advice: supply a creation mode of C<0666> for regular
files (in C<sysopen>) and one of C<0777> for directories (in
C<mkdir>) and executable files.  This gives users the freedom of
choice: if they want protected files, they might choose process umasks
of C<022>, C<027>, or even the particularly antisocial mask of C<077>.
Programs should rarely if ever make policy decisions better left to
the user.  The exception to this is when writing files that should be
kept private: mail files, web browser cookies, I<.rhosts> files, and
so on.
(*TBT*)

=begin original

If umask(2) is not implemented on your system and you are trying to
restrict access for I<yourself> (i.e., (EXPR & 0700) > 0), produces a
fatal error at run time.  If umask(2) is not implemented and you are
not trying to restrict access for yourself, returns C<undef>.

=end original

umask(2) が実装されていないシステムで、I<自分自身> へのアクセスを
制限しようとした(つまり (EXPR & 0700) > 0)場合、実行時に致命的エラーが
発生します。
umask(2) が実装されていないシステムで、自分自身へのアクセスは
制限しようとしなかった場合、C<undef> を返します。

=begin original

Remember that a umask is a number, usually given in octal; it is I<not> a
string of octal digits.  See also L</oct>, if all you have is a string.

=end original

umask は通常 8 進数で与えられる数値であることを忘れないでください。
8 進数の文字列 I<ではありません>。
文字列しかない場合、 L</oct> も参照して下さい。

=item undef EXPR

=item undef

=begin original

Undefines the value of EXPR, which must be an lvalue.  Use only on a
scalar value, an array (using C<@>), a hash (using C<%>), a subroutine
(using C<&>), or a typeglob (using <*>).  (Saying C<undef $hash{$key}>
will probably not do what you expect on most predefined variables or
DBM list values, so don't do that; see L<delete>.)  Always returns the
undefined value.  You can omit the EXPR, in which case nothing is
undefined, but you still get an undefined value that you could, for
instance, return from a subroutine, assign to a variable or pass as a
parameter.  Examples:

=end original

左辺値である EXPR の値を未定義にします。
スカラ値、(C<@>を使った)配列、(C<%>を使った)ハッシュ、(C<&> を使った) 
サブルーチン、(C<*>を使った)タイプグロブだけに使用します。
(特殊変数や DBM リスト値に C<undef $hash{$key}>などとしても
おそらく期待通りの結果にはなりませんから、
しないでください。L<delete>を参照してください。)
常に未定義値を返します。
EXPR は省略することができ、その場合には何も未定義にされませんが
未定義値は返されますので、それをたとえば、
サブルーチンの返り値、変数への割り当て、引数などとして使うことができます。
例:

    undef $foo;
    undef $bar{'blurfl'};      # Compare to: delete $bar{'blurfl'};
    undef @ary;
    undef %hash;
    undef &mysub;
    undef *xyz;       # destroys $xyz, @xyz, %xyz, &xyz, etc.
    return (wantarray ? (undef, $errmsg) : undef) if $they_blew_it;
    select undef, undef, undef, 0.25;
    ($a, $b, undef, $c) = &foo;       # Ignore third value returned

=begin original

Note that this is a unary operator, not a list operator.

=end original

これはリスト演算子ではなく、単項演算子であることに注意してください。

=item unlink LIST

=item unlink

=begin original

Deletes a list of files.  Returns the number of files successfully
deleted.

=end original

LIST に含まれるファイルを削除します。
削除に成功したファイルの数を返します。

    $cnt = unlink 'a', 'b', 'c';
    unlink @goners;
    unlink <*.bak>;

=begin original

Note: C<unlink> will not delete directories unless you are superuser and
the B<-U> flag is supplied to Perl.  Even if these conditions are
met, be warned that unlinking a directory can inflict damage on your
filesystem.  Use C<rmdir> instead.

If LIST is omitted, uses C<$_>.

=end original

注: スーパーユーザ権限で、Perl に -U を付けて実行した
場合でなければ、C<unlink> でディレクトリを削除することはありません。
この条件にあう場合にも、ディレクトリの削除は、
ファイルシステムに多大な損害を与える可能性がありますので、
特に注意が必要です。C<rmdir> を代わりに使ってください。

LIST が省略されると、C<$_> を使います。

=item unpack TEMPLATE,EXPR

=begin original

C<unpack> does the reverse of C<pack>: it takes a string
and expands it out into a list of values.
(In scalar context, it returns merely the first value produced.)

=end original

C<unpack> は C<pack> の逆を行ないます: 構造体を表わす文字列をとり、
リスト値に展開し、その配列値を返します。
(スカラコンテキストでは、単に最初の値を返します。)

=begin original

The string is broken into chunks described by the TEMPLATE.  Each chunk
is converted separately to a value.  Typically, either the string is a result
of C<pack>, or the bytes of the string represent a C structure of some
kind.

=end original

文字列は TEMPLATE で示された固まりに分割されます。
それぞれの固まりは別々に値に変換されます。
典型的には、文字列は C<pack> の結果あるいはある種の C の構造体の
文字列表現のバイト列です。

=begin original

The TEMPLATE has the same format as in the C<pack> function.
Here's a subroutine that does substring:

=end original

TEMPLATE は、C<pack> 関数と同じフォーマットを使います。
部分文字列を取り出すうサブルーチンの例を示します:

    sub substr {
	my($what,$where,$howmuch) = @_;
	unpack("x$where a$howmuch", $what);
    }

and then there's

    sub ordinal { unpack("c",$_[0]); } # same as ord()

=begin original

In addition to fields allowed in pack(), you may prefix a field with
a %<number> to indicate that
you want a <number>-bit checksum of the items instead of the items
themselves.  Default is a 16-bit checksum.  Checksum is calculated by
summing numeric values of expanded values (for string fields the sum of
C<ord($char)> is taken, for bit fields the sum of zeroes and ones).

=end original

pack() で利用可能なフィールドの他に、
フィールドの前に %<数値> というものを付けて、
項目自身の代わりに、その項目の <数値>-ビットのチェックサムを
計算させることができます。
デフォルトは、16-ビットチェックサムです。
チェックサムは展開された値の数値としての値の合計
(文字列フィールドの場合は C<ord($char)> の合計、
ビットフィールドの場合は 0 と 1 の合計) が用いられます。

=begin original

For example, the following
computes the same number as the System V sum program:

=end original

たとえば、以下のコードは
System V の sum プログラムと同じ値を計算します。

    $checksum = do {
	local $/;  # slurp!
	unpack("%32C*",<>) % 65535;
    };

=begin original

The following efficiently counts the number of set bits in a bit vector:

=end original

以下は、効率的にビットベクターの設定されているビットを
数えるものです。

    $setbits = unpack("%32b*", $selectmask);

=begin original

The C<p> and C<P> formats should be used with care.  Since Perl
has no way of checking whether the value passed to C<unpack()>
corresponds to a valid memory location, passing a pointer value that's
not known to be valid is likely to have disastrous consequences.

=end original

C<p> と C<P> は注意深く使うべきです。
Perl は C<unpack()> に渡された値が有効なメモリ位置を指しているかどうかを
確認する方法がないので、有効かどうかわからないポインタ値を渡すと
悲惨な結果を引き起こすかもしれません。

=begin original

If the repeat count of a field is larger than what the remainder of
the input string allows, repeat count is decreased.  If the input string
is longer than one described by the TEMPLATE, the rest is ignored. 

=end original

フィールドの繰り返し回数が入力文字列の残りより大きい場合、繰り返し回数は
減らされます。もし入力文字列が TEMPLATE で表現されているものより
大きい場合、残りは無視されます。

=begin original

See L</pack> for more examples and notes.

=end original

さらなる例と注意に関しては L</pack> を参照してください。

=item untie VARIABLE

=begin original

Breaks the binding between a variable and a package.  (See C<tie>.)

=end original

変数とパッケージの間の結合を解きます。
(L<tie> を参照してください。)

=item unshift ARRAY,LIST

=begin original

Does the opposite of a C<shift>.  Or the opposite of a C<push>,
depending on how you look at it.  Prepends list to the front of the
array, and returns the new number of elements in the array.

=end original

C<shift> の逆操作を行ないます。
見方を変えれば、C<push>の逆操作とも考えられます。
LIST を ARRAY の先頭に入れて、新しくできた配列の要素の数を返します。

    unshift(ARGV, '-e') unless $ARGV[0] =~ /^-/;

=begin original

Note the LIST is prepended whole, not one element at a time, so the
prepended elements stay in the same order.  Use C<reverse> to do the
reverse.

=end original

LIST は、はらばらにではなく、一度に登録されるので、順番はそのままです。
逆順に登録するには、C<reverse> を使ってください。

=item use Module VERSION LIST

=item use Module VERSION

=item use Module LIST

=item use Module

=item use VERSION

=begin original

Imports some semantics into the current package from the named module,
generally by aliasing certain subroutine or variable names into your
package.  It is exactly equivalent to

    BEGIN { require Module; import Module LIST; }

except that Module I<must> be a bareword.

=end original

指定したモジュールから、現在のパッケージにさまざまな
内容をインポートします。　多くは、パッケージのサブルーチン名や、
変数名に別名を付けることで、実現されています。これは、
Module がI<単なる単語でなければならない>ことを除けば、

    BEGIN { require Module; import Module LIST; }

と等価です。

=begin original

VERSION, which can be specified as a literal of the form v5.6.1, demands
that the current version of Perl (C<$^V> or $PERL_VERSION) be at least
as recent as that version.  (For compatibility with older versions of Perl,
a numeric literal will also be interpreted as VERSION.)  If the version
of the running Perl interpreter is less than VERSION, then an error
message is printed and Perl exits immediately without attempting to
parse the rest of the file.  Compare with L</require>, which can do a
similar check at run time.

=end original

VERSION, which can be specified as a literal of the form v5.6.1, demands
that the current version of Perl (C<$^V> or $PERL_VERSION) be at least
as recent as that version.  (For compatibility with older versions of Perl,
a numeric literal will also be interpreted as VERSION.)  If the version
of the running Perl interpreter is less than VERSION, then an error
message is printed and Perl exits immediately without attempting to
parse the rest of the file.  Compare with L</require>, which can do a
similar check at run time.
(*TBT*)

    use v5.6.1;		# compile time version check
    use 5.6.1;		# ditto
    use 5.005_03;	# float version allowed for compatibility

=begin original

This is often useful if you need to check the current Perl version before
C<use>ing library modules that have changed in incompatible ways from
older versions of Perl.  (We try not to do this more than we have to.)

=end original

これは古いバージョンの Perl から互換性のない形で変更されたライブラリ
モジュール(我々は必要な場合以外にそのようなことがないように努力していますが)を
C<use> する前に、現在の Perl のバージョンを調べたい場合に
有用です。

=begin original

The C<BEGIN> forces the C<require> and C<import> to happen at compile time.  The
C<require> makes sure the module is loaded into memory if it hasn't been
yet.  The C<import> is not a builtin--it's just an ordinary static method
call into the C<Module> package to tell the module to import the list of
features back into the current package.  The module can implement its
C<import> method any way it likes, though most modules just choose to
derive their C<import> method via inheritance from the C<Exporter> class that
is defined in the C<Exporter> module.  See L<Exporter>.  If no C<import>
method can be found then the call is skipped.

=end original

C<BEGIN> によって、C<require> や C<import> は、コンパイル時に
実行されることになります。
C<require> は、モジュールがまだメモリにロードされていなければ、ロードします。
C<import> は、組込みの関数ではなく、さまざまな機能を現在のパッケージに
インポートするように C<Module> パッケージに伝えるために呼ばれる、
通常の静的メソッドです。
モジュール側では、C<import> メソッドをどのようにでも実装することができますが、
多くのモジュールでは、C<Exporter> モジュールで定義された、
C<Exporter> クラスからの継承によって、C<import> メソッドを行なうようにしています。
L<Exporter>モジュールを参照してください。
C<import>メソッドが見つからなかった場合、呼び出しはスキップされます。

=begin original

If you do not want to call the package's C<import> method (for instance,
to stop your namespace from being altered), explicitly supply the empty list:

=end original

パッケージの C<import> メソッドを呼び出したくない場合
(例えば、名前空間を変更したくない場合など)は、明示的に空リストを指定してください:

    use Module ();

=begin original

That is exactly equivalent to

=end original

これは以下と完全に等価です:

    BEGIN { require Module }

=begin original

If the VERSION argument is present between Module and LIST, then the
C<use> will call the VERSION method in class Module with the given
version as an argument.  The default VERSION method, inherited from
the UNIVERSAL class, croaks if the given version is larger than the
value of the variable C<$Module::VERSION>. 

=end original

Module と LIST の間に VERSION 引数がある場合、
C<use> は Module クラスの VERSION メソッドを、与えられた
バージョンを引数として呼び出します。
デフォルトの VERSION メソッドは、 UNIVERSAL クラスから継承したもので、
与えられたバージョンが 変数 C<$Module::VERSION> の値より大きい場合に
警告を出します。

=begin original

Again, there is a distinction between omitting LIST (C<import> called
with no arguments) and an explicit empty LIST C<()> (C<import> not
called).  Note that there is no comma after VERSION!

=end original

繰り返すと、LIST を省略する(C<import> が引数なしで呼び出される)ことと
明示的に空の LIST C<()> を指定する (C<import> は呼び出されない)ことは
違います。VERSION の後ろにカンマが不要なことに注意してください!

=begin original

Because this is a wide-open interface, pragmas (compiler directives)
are also implemented this way.  Currently implemented pragmas are:

=end original

これは、広く公開されているインタフェースですので、
プラグマ (コンパイラディレクティブ) も、この方法で実装されています。
現在実装されているプラグマには、以下のものがあります:

    use constant;
    use diagnostics;
    use integer;
    use sigtrap  qw(SEGV BUS);
    use strict   qw(subs vars refs);
    use subs     qw(afunc blurfl);
    use warnings qw(all);

=begin original

Some of these pseudo-modules import semantics into the current
block scope (like C<strict> or C<integer>, unlike ordinary modules,
which import symbols into the current package (which are effective
through the end of the file).

=end original

通常のモジュールが、現在のパッケージにシンボルをインポートする
(これは、ファイルの終わりまで有効です) のに対して、
これらの擬似モジュールの一部(C<strict> や C<integer> など)は、
現在のブロックスコープにインポートを行ないます。

=begin original

There's a corresponding C<no> command that unimports meanings imported
by C<use>, i.e., it calls C<unimport Module LIST> instead of C<import>.

=end original

これに対して、C<no> コマンドという、C<use> によってインポートされたものを、
インポートされていないことにするものがあります。つまり、C<import> の代わりに
C<unimport Module LIST> を呼び出します。

    no integer;
    no strict 'refs';
    no warnings;

=begin original

If no C<unimport> method can be found the call fails with a fatal error.

=end original

C<unimport> メソッドが見つからなかった場合、呼び出しは致命的エラーで失敗します。

=begin original

See L<perlmodlib> for a list of standard modules and pragmas.  See L<perlrun>
for the C<-M> and C<-m> command-line options to perl that give C<use>
functionality from the command-line.

=end original

標準モジュールやプラグマの一覧は、L<perlmodlib> を参照してください。
コマンドラインから C<use> 機能を指定するための
C<-M> と C<-m> のコマンドラインオプションについては
L<perlrun> を参照して下さい。

=item utime LIST

=begin original

Changes the access and modification times on each file of a list of
files.  The first two elements of the list must be the NUMERICAL access
and modification times, in that order.  Returns the number of files
successfully changed.  The inode change time of each file is set
to the current time.  This code has the same effect as the C<touch>
command if the files already exist:

=end original

ファイルのアクセス時刻と修正(modification) 時刻を変更します。
LIST の最初の 2 つの要素に、数値で表わしたアクセス時刻と修正時刻を順に指定します。
LIST の残りの要素が、変更の対象となるファイルです。
変更に成功したファイルの数を返します。
各ファイルの inode 変更(change)時刻には、その時点の時刻が設定されます。
C<touch> コマンドの例です:

    #!/usr/bin/perl
    $now = time;
    utime $now, $now, @ARGV;

=item values HASH

=begin original

Returns a list consisting of all the values of the named hash.  (In a
scalar context, returns the number of values.)  The values are
returned in an apparently random order.  The actual random order is
subject to change in future versions of perl, but it is guaranteed to
be the same order as either the C<keys> or C<each> function would
produce on the same (unmodified) hash.

=end original

指定したハッシュのすべての value からなるリストを返します。
(スカラコンテキストでは、value の数を返します。)
返される value の順序は、見た目にばらばらなものです。
実際のランダムな順序は将来のバージョンの perl では変わる可能性が
ありますが、同じ(変更されていない)ハッシュに対して、
C<keys>関数や C<each>関数が返すものと同じ順序であることは保証されます。

=begin original

Note that the values are not copied, which means modifying them will
modify the contents of the hash:

=end original

値はコピーされないので、返されたリストを変更すると
ハッシュの中身が変更されることに注意してください。

    for (values %hash) 	    { s/foo/bar/g }   # modifies %hash values
    for (@hash{keys %hash}) { s/foo/bar/g }   # same

=begin original

As a side effect, calling values() resets the HASH's internal iterator.
See also C<keys>, C<each>, and C<sort>.

=end original

副作用として、values() を呼び出すと HASH の内部イテレータをリセットします。
C<keys>, C<each>, C<sort> も参照してください。

=item vec EXPR,OFFSET,BITS

=begin original

Treats the string in EXPR as a bit vector made up of elements of
width BITS, and returns the value of the element specified by OFFSET
as an unsigned integer.  BITS therefore specifies the number of bits
that are reserved for each element in the bit vector.  This must
be a power of two from 1 to 32 (or 64, if your platform supports
that).

=end original

文字列 EXPR を BITS 幅の要素からなるビットベクターとして扱い、
OFFSET で指定される要素の値を返します。
OFFSET で指定された要素を符号なし整数として返します。
従って、 BITS はビットベクターの中の各要素について予約されるビット数です。
BIT は、1 から 32 まで(プラットホームが
対応していれば 64 まで) の 2 のべきでなければなりません。

=begin original

If BITS is 8, "elements" coincide with bytes of the input string.  

=end original

BITS が 8 の場合、「要素」は入力文字列の各バイトと一致します。

=begin original

If BITS is 16 or more, bytes of the input string are grouped into chunks
of size BITS/8, and each group is converted to a number as with
pack()/unpack() with big-endian formats C<n>/C<N> (and analogously
for BITS==64).  See L<"pack"> for details.

=end original

BITS が 16 以上の場合、入力のバイト列は BITS/8 のサイズの固まりに
グループ化され、各グループは pack()/unpack() のビッグエンディアンフォーマット
C<n>/C<N> を用いて(BITS==64の類似として)数値に変換されます。
詳細は L<"pack"> を参照してください。


=begin original

If bits is 4 or less, the string is broken into bytes, then the bits
of each byte are broken into 8/BITS groups.  Bits of a byte are
numbered in a little-endian-ish way, as in C<0x01>, C<0x02>,
C<0x04>, C<0x08>, C<0x10>, C<0x20>, C<0x40>, C<0x80>.  For example,
breaking the single input byte C<chr(0x36)> into two groups gives a list
C<(0x6, 0x3)>; breaking it into 4 groups gives C<(0x2, 0x1, 0x3, 0x0)>.

=end original

BITS が 4 以下の場合、文字列はバイトに分解され、バイトの各ビットは
8/BITS 個のグループに分割されます。
ビットはリトルエンディアン風に、C<0x01>, C<0x02>,
C<0x04>, C<0x08>, C<0x10>, C<0x20>, C<0x40>, C<0x80> の順になります。
例えば、入力バイト C<chr(0x36)> を 2 つのグループに分割すると、
C<(0x6, 0x3)> になります。
4 つに分割すると C<(0x2, 0x1, 0x3, 0x0)> になります。

=begin original

C<vec> may also be assigned to, in which case parentheses are needed
to give the expression the correct precedence as in

=end original

左辺値として、代入の対象にすることもできます。
この場合、式を正しく先行させるために以下のように括弧が必要です。

    vec($image, $max_x * $x + $y, 8) = 3;

=begin original

If the selected element is outside the string, the value 0 is returned.
If an element off the end of the string is written to, Perl will first
extend the string with sufficiently many zero bytes.   It is an error
to try to write off the beginning of the string (i.e. negative OFFSET).

=end original

選択された要素が文字列の外側だった場合、値 0 が返されます。
文字列の最後よりも後ろの要素に書き込もうとした場合、
Perl はまず文字列を必要な分だけ 0 のバイトで拡張します。
文字列の先頭より前に書き込もうとした(つまり OFFSET が負の数だった)
場合はエラーとなります。

=begin original

The string should not contain any character with the value > 255 (which
can only happen if you're using UTF8 encoding).  If it does, it will be
treated as something which is not UTF8 encoded.  When the C<vec> was
assigned to, other parts of your program will also no longer consider the
string to be UTF8 encoded.  In other words, if you do have such characters
in your string, vec() will operate on the actual byte string, and not the
conceptual character string.

=end original

文字列は値が 255 を越える文字を含むべきではありません
(これは UTF8 エンコーディングを使っているときにだけ起こりえます)。
もし含んでいると、UTF8 エンコードされていないものとして扱われます。
C<vec> が割り当てられたとき、プログラムのその他の部分では
もはやこの文字列が UTF8 エンコードされたと扱われなくなります。
言い換えると、もしこのような文字を文字列に含んでいると、
vec() は実際のバイト文字列として扱い、概念的な文字の文字列としては
扱いません。

=begin original

Strings created with C<vec> can also be manipulated with the logical
operators C<|>, C<&>, C<^>, and C<~>.  These operators will assume a bit
vector operation is desired when both operands are strings.
See L<perlop/"Bitwise String Operators">.

=end original

vec() で作られた文字列は、論理演算子 C<|>、C<&>、C<^> で扱
うこともできます。　これらの演算子は、両方の被演算子
に文字列を使うと、ビットベクター演算を行ないます。

=begin original

The following code will build up an ASCII string saying C<'PerlPerlPerl'>.
The comments show the string after each step.  Note that this code works
in the same way on big-endian or little-endian machines.

=end original

次のコードは C<'PerlPerlPerl'> という ASCII 文字列を作成します。
コメントは各行の実行後の文字列を示します。
このコードはビッグエンディアンでもリトルエンディアンでも同じように
動作することに注意してください。

    my $foo = '';
    vec($foo,  0, 32) = 0x5065726C;	# 'Perl'

    # $foo eq "Perl" eq "\x50\x65\x72\x6C", 32 bits
    print vec($foo, 0, 8);		# prints 80 == 0x50 == ord('P')

    vec($foo,  2, 16) = 0x5065;		# 'PerlPe'
    vec($foo,  3, 16) = 0x726C;		# 'PerlPerl'
    vec($foo,  8,  8) = 0x50;		# 'PerlPerlP'
    vec($foo,  9,  8) = 0x65;		# 'PerlPerlPe'
    vec($foo, 20,  4) = 2;		# 'PerlPerlPe'   . "\x02"
    vec($foo, 21,  4) = 7;		# 'PerlPerlPer'
                                        # 'r' is "\x72"
    vec($foo, 45,  2) = 3;		# 'PerlPerlPer'  . "\x0c"
    vec($foo, 93,  1) = 1;		# 'PerlPerlPer'  . "\x2c"
    vec($foo, 94,  1) = 1;		# 'PerlPerlPerl'
                                        # 'l' is "\x6c"

=begin original

To transform a bit vector into a string or list of 0's and 1's, use these:

=end original

ビットベクターを、0 と 1 の文字列や配列に変換するには、
以下のようにします。

    $bits = unpack("b*", $vector);
    @bits = split(//, unpack("b*", $vector));

=begin original

If you know the exact length in bits, it can be used in place of the C<*>.

=end original

ビット長が分かっていれば、* の代わりにその長さを使うことができます。

=begin original

Here is an example to illustrate how the bits actually fall in place:

=end original

これはビットが実際にどのような位置に入るかを図示する例です。

    #!/usr/bin/perl -wl

    print <<'EOT';
                                      0         1         2         3  
                       unpack("V",$_) 01234567890123456789012345678901
    ------------------------------------------------------------------
    EOT

    for $w (0..3) {
        $width = 2**$w;
        for ($shift=0; $shift < $width; ++$shift) {
            for ($off=0; $off < 32/$width; ++$off) {
                $str = pack("B*", "0"x32);
                $bits = (1<<$shift);
                vec($str, $off, $width) = $bits;
                $res = unpack("b*",$str);
                $val = unpack("V", $str);
                write;
            }
        }
    }

    format STDOUT =
    vec($_,@#,@#) = @<< == @######### @>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
    $off, $width, $bits, $val, $res
    .
    __END__

=begin original

Regardless of the machine architecture on which it is run, the above
example should print the following table:

=end original

実行するマシンのアーキテクチャに関わらず、
上記の例は以下の表を出力します。

                                      0         1         2         3  
                       unpack("V",$_) 01234567890123456789012345678901
    ------------------------------------------------------------------
    vec($_, 0, 1) = 1   ==          1 10000000000000000000000000000000
    vec($_, 1, 1) = 1   ==          2 01000000000000000000000000000000
    vec($_, 2, 1) = 1   ==          4 00100000000000000000000000000000
    vec($_, 3, 1) = 1   ==          8 00010000000000000000000000000000
    vec($_, 4, 1) = 1   ==         16 00001000000000000000000000000000
    vec($_, 5, 1) = 1   ==         32 00000100000000000000000000000000
    vec($_, 6, 1) = 1   ==         64 00000010000000000000000000000000
    vec($_, 7, 1) = 1   ==        128 00000001000000000000000000000000
    vec($_, 8, 1) = 1   ==        256 00000000100000000000000000000000
    vec($_, 9, 1) = 1   ==        512 00000000010000000000000000000000
    vec($_,10, 1) = 1   ==       1024 00000000001000000000000000000000
    vec($_,11, 1) = 1   ==       2048 00000000000100000000000000000000
    vec($_,12, 1) = 1   ==       4096 00000000000010000000000000000000
    vec($_,13, 1) = 1   ==       8192 00000000000001000000000000000000
    vec($_,14, 1) = 1   ==      16384 00000000000000100000000000000000
    vec($_,15, 1) = 1   ==      32768 00000000000000010000000000000000
    vec($_,16, 1) = 1   ==      65536 00000000000000001000000000000000
    vec($_,17, 1) = 1   ==     131072 00000000000000000100000000000000
    vec($_,18, 1) = 1   ==     262144 00000000000000000010000000000000
    vec($_,19, 1) = 1   ==     524288 00000000000000000001000000000000
    vec($_,20, 1) = 1   ==    1048576 00000000000000000000100000000000
    vec($_,21, 1) = 1   ==    2097152 00000000000000000000010000000000
    vec($_,22, 1) = 1   ==    4194304 00000000000000000000001000000000
    vec($_,23, 1) = 1   ==    8388608 00000000000000000000000100000000
    vec($_,24, 1) = 1   ==   16777216 00000000000000000000000010000000
    vec($_,25, 1) = 1   ==   33554432 00000000000000000000000001000000
    vec($_,26, 1) = 1   ==   67108864 00000000000000000000000000100000
    vec($_,27, 1) = 1   ==  134217728 00000000000000000000000000010000
    vec($_,28, 1) = 1   ==  268435456 00000000000000000000000000001000
    vec($_,29, 1) = 1   ==  536870912 00000000000000000000000000000100
    vec($_,30, 1) = 1   == 1073741824 00000000000000000000000000000010
    vec($_,31, 1) = 1   == 2147483648 00000000000000000000000000000001
    vec($_, 0, 2) = 1   ==          1 10000000000000000000000000000000
    vec($_, 1, 2) = 1   ==          4 00100000000000000000000000000000
    vec($_, 2, 2) = 1   ==         16 00001000000000000000000000000000
    vec($_, 3, 2) = 1   ==         64 00000010000000000000000000000000
    vec($_, 4, 2) = 1   ==        256 00000000100000000000000000000000
    vec($_, 5, 2) = 1   ==       1024 00000000001000000000000000000000
    vec($_, 6, 2) = 1   ==       4096 00000000000010000000000000000000
    vec($_, 7, 2) = 1   ==      16384 00000000000000100000000000000000
    vec($_, 8, 2) = 1   ==      65536 00000000000000001000000000000000
    vec($_, 9, 2) = 1   ==     262144 00000000000000000010000000000000
    vec($_,10, 2) = 1   ==    1048576 00000000000000000000100000000000
    vec($_,11, 2) = 1   ==    4194304 00000000000000000000001000000000
    vec($_,12, 2) = 1   ==   16777216 00000000000000000000000010000000
    vec($_,13, 2) = 1   ==   67108864 00000000000000000000000000100000
    vec($_,14, 2) = 1   ==  268435456 00000000000000000000000000001000
    vec($_,15, 2) = 1   == 1073741824 00000000000000000000000000000010
    vec($_, 0, 2) = 2   ==          2 01000000000000000000000000000000
    vec($_, 1, 2) = 2   ==          8 00010000000000000000000000000000
    vec($_, 2, 2) = 2   ==         32 00000100000000000000000000000000
    vec($_, 3, 2) = 2   ==        128 00000001000000000000000000000000
    vec($_, 4, 2) = 2   ==        512 00000000010000000000000000000000
    vec($_, 5, 2) = 2   ==       2048 00000000000100000000000000000000
    vec($_, 6, 2) = 2   ==       8192 00000000000001000000000000000000
    vec($_, 7, 2) = 2   ==      32768 00000000000000010000000000000000
    vec($_, 8, 2) = 2   ==     131072 00000000000000000100000000000000
    vec($_, 9, 2) = 2   ==     524288 00000000000000000001000000000000
    vec($_,10, 2) = 2   ==    2097152 00000000000000000000010000000000
    vec($_,11, 2) = 2   ==    8388608 00000000000000000000000100000000
    vec($_,12, 2) = 2   ==   33554432 00000000000000000000000001000000
    vec($_,13, 2) = 2   ==  134217728 00000000000000000000000000010000
    vec($_,14, 2) = 2   ==  536870912 00000000000000000000000000000100
    vec($_,15, 2) = 2   == 2147483648 00000000000000000000000000000001
    vec($_, 0, 4) = 1   ==          1 10000000000000000000000000000000
    vec($_, 1, 4) = 1   ==         16 00001000000000000000000000000000
    vec($_, 2, 4) = 1   ==        256 00000000100000000000000000000000
    vec($_, 3, 4) = 1   ==       4096 00000000000010000000000000000000
    vec($_, 4, 4) = 1   ==      65536 00000000000000001000000000000000
    vec($_, 5, 4) = 1   ==    1048576 00000000000000000000100000000000
    vec($_, 6, 4) = 1   ==   16777216 00000000000000000000000010000000
    vec($_, 7, 4) = 1   ==  268435456 00000000000000000000000000001000
    vec($_, 0, 4) = 2   ==          2 01000000000000000000000000000000
    vec($_, 1, 4) = 2   ==         32 00000100000000000000000000000000
    vec($_, 2, 4) = 2   ==        512 00000000010000000000000000000000
    vec($_, 3, 4) = 2   ==       8192 00000000000001000000000000000000
    vec($_, 4, 4) = 2   ==     131072 00000000000000000100000000000000
    vec($_, 5, 4) = 2   ==    2097152 00000000000000000000010000000000
    vec($_, 6, 4) = 2   ==   33554432 00000000000000000000000001000000
    vec($_, 7, 4) = 2   ==  536870912 00000000000000000000000000000100
    vec($_, 0, 4) = 4   ==          4 00100000000000000000000000000000
    vec($_, 1, 4) = 4   ==         64 00000010000000000000000000000000
    vec($_, 2, 4) = 4   ==       1024 00000000001000000000000000000000
    vec($_, 3, 4) = 4   ==      16384 00000000000000100000000000000000
    vec($_, 4, 4) = 4   ==     262144 00000000000000000010000000000000
    vec($_, 5, 4) = 4   ==    4194304 00000000000000000000001000000000
    vec($_, 6, 4) = 4   ==   67108864 00000000000000000000000000100000
    vec($_, 7, 4) = 4   == 1073741824 00000000000000000000000000000010
    vec($_, 0, 4) = 8   ==          8 00010000000000000000000000000000
    vec($_, 1, 4) = 8   ==        128 00000001000000000000000000000000
    vec($_, 2, 4) = 8   ==       2048 00000000000100000000000000000000
    vec($_, 3, 4) = 8   ==      32768 00000000000000010000000000000000
    vec($_, 4, 4) = 8   ==     524288 00000000000000000001000000000000
    vec($_, 5, 4) = 8   ==    8388608 00000000000000000000000100000000
    vec($_, 6, 4) = 8   ==  134217728 00000000000000000000000000010000
    vec($_, 7, 4) = 8   == 2147483648 00000000000000000000000000000001
    vec($_, 0, 8) = 1   ==          1 10000000000000000000000000000000
    vec($_, 1, 8) = 1   ==        256 00000000100000000000000000000000
    vec($_, 2, 8) = 1   ==      65536 00000000000000001000000000000000
    vec($_, 3, 8) = 1   ==   16777216 00000000000000000000000010000000
    vec($_, 0, 8) = 2   ==          2 01000000000000000000000000000000
    vec($_, 1, 8) = 2   ==        512 00000000010000000000000000000000
    vec($_, 2, 8) = 2   ==     131072 00000000000000000100000000000000
    vec($_, 3, 8) = 2   ==   33554432 00000000000000000000000001000000
    vec($_, 0, 8) = 4   ==          4 00100000000000000000000000000000
    vec($_, 1, 8) = 4   ==       1024 00000000001000000000000000000000
    vec($_, 2, 8) = 4   ==     262144 00000000000000000010000000000000
    vec($_, 3, 8) = 4   ==   67108864 00000000000000000000000000100000
    vec($_, 0, 8) = 8   ==          8 00010000000000000000000000000000
    vec($_, 1, 8) = 8   ==       2048 00000000000100000000000000000000
    vec($_, 2, 8) = 8   ==     524288 00000000000000000001000000000000
    vec($_, 3, 8) = 8   ==  134217728 00000000000000000000000000010000
    vec($_, 0, 8) = 16  ==         16 00001000000000000000000000000000
    vec($_, 1, 8) = 16  ==       4096 00000000000010000000000000000000
    vec($_, 2, 8) = 16  ==    1048576 00000000000000000000100000000000
    vec($_, 3, 8) = 16  ==  268435456 00000000000000000000000000001000
    vec($_, 0, 8) = 32  ==         32 00000100000000000000000000000000
    vec($_, 1, 8) = 32  ==       8192 00000000000001000000000000000000
    vec($_, 2, 8) = 32  ==    2097152 00000000000000000000010000000000
    vec($_, 3, 8) = 32  ==  536870912 00000000000000000000000000000100
    vec($_, 0, 8) = 64  ==         64 00000010000000000000000000000000
    vec($_, 1, 8) = 64  ==      16384 00000000000000100000000000000000
    vec($_, 2, 8) = 64  ==    4194304 00000000000000000000001000000000
    vec($_, 3, 8) = 64  == 1073741824 00000000000000000000000000000010
    vec($_, 0, 8) = 128 ==        128 00000001000000000000000000000000
    vec($_, 1, 8) = 128 ==      32768 00000000000000010000000000000000
    vec($_, 2, 8) = 128 ==    8388608 00000000000000000000000100000000
    vec($_, 3, 8) = 128 == 2147483648 00000000000000000000000000000001

=item wait

=begin original

Behaves like the wait(2) system call on your system: it waits for a child
process to terminate and returns the pid of the deceased process, or
C<-1> if there are no child processes.  The status is returned in C<$?>.
Note that a return value of C<-1> could mean that child processes are
being automatically reaped, as described in L<perlipc>.

=end original

wait(2) システムコールと同様に振る舞います。
チャイルドプロセスが終了するのを待ち、消滅したプロセスの pid を返します。
チャイルドプロセスが存在しないときには、C<-1> を返します。
ステータスは C<$?> に返されます。
L<perlpc> に書いているように、
返り値が C<-1> の場合は子プロセスが自動的に刈り取られたことを
意味するかもしれないことに注意してください。

=item waitpid PID,FLAGS

=begin original

Waits for a particular child process to terminate and returns the pid of
the deceased process, or C<-1> if there is no such child process.  On some
systems, a value of 0 indicates that there are processes still running.
The status is returned in C<$?>.  If you say

=end original

特定のチャイルドプロセスが終了するのを待ち、消滅した
プロセスの pid を返します。　指定したチャイルドプロセスが
存在しないときには、C<-1> を返します。
値 0 がプロセスがまだ実行中であることを示すシステムもあります。
ステータスは C<$?> に返されます。

    use POSIX ":sys_wait_h";
    #...
    do { 
	$kid = waitpid(-1,WNOHANG);
    } until $kid == -1;

=begin original

then you can do a non-blocking wait for all pending zombie processes.
Non-blocking wait is available on machines supporting either the
waitpid(2) or wait4(2) system calls.  However, waiting for a particular
pid with FLAGS of C<0> is implemented everywhere.  (Perl emulates the
system call by remembering the status values of processes that have
exited but have not been harvested by the Perl script yet.)

=end original

とすると、ブロックが起こらないようにして、全ての待機中ゾンビプロセスを
wait します。
ブロックなしの wait は、システムコール wait_pid(2) か、
システムコール wait4(2) をサポートしているマシンで利用可能です。
しかしながら、特定の pid を C<0> の FLAGS での wait はどこでも
実装されています。(exit したプロセスのステータス値を覚えておいて、
Perl がシステムコールをエミュレートしますが、
Perl スクリプトには取り入れられていません。)

=begin original

Note that on some systems, a return value of C<-1> could mean that child
processes are being automatically reaped.  See L<perlipc> for details,
and for other examples.

=end original

システムによっては、返り値が C<-1> の場合は子プロセスが自動的に刈り取られたことを
意味するかもしれないことに注意してください。
詳細やその他の例については L<perlipc> を参照してください。

=item wantarray

=begin original

Returns true if the context of the currently executing subroutine is
looking for a list value.  Returns false if the context is looking
for a scalar.  Returns the undefined value if the context is looking
for no value (void context).

=end original

現在実行中のサブルーチンがのコンテキストが、リスト
値を要求するものであれば、「真」を返します。
スカラを要求するコンテキストであれば、「偽」を返します。
何も値を要求しない(void コンテキスト)場合は未定義値を返します。

    return unless defined wantarray;	# don't bother doing more
    my @a = complex_calculation();
    return wantarray ? @a : "@a";

=begin original

This function should have been named wantlist() instead.

=end original

この関数は wantlist() という名前にするべきでした。

=item warn LIST

=begin original

Produces a message on STDERR just like C<die>, but doesn't exit or throw
an exception.

=end original

C<die> と同じように STDERR にメッセージを出力しますが、
exit したり、例外を発生させたりしません。

=begin original

If LIST is empty and C<$@> already contains a value (typically from a
previous eval) that value is used after appending C<"\t...caught">
to C<$@>.  This is useful for staying almost, but not entirely similar to
C<die>.

=end original

LIST が空かつ、(典型的には以前の eval によって) C<$@> に既に値が入っている
場合、C<$@> に C<"\t...caught"> を追加した値が用いられます。
これはほとんどそのままにするときに便利ですが、
C<die> と全体的に似ているわけではありません。

=begin original

If C<$@> is empty then the string C<"Warning: Something's wrong"> is used.

=end original

C<$@> が空の場合は、C<"Warning: Something's wrong"> という文字列が
使われます。

=begin original

No message is printed if there is a C<$SIG{__WARN__}> handler
installed.  It is the handler's responsibility to deal with the message
as it sees fit (like, for instance, converting it into a C<die>).  Most
handlers must therefore make arrangements to actually display the
warnings that they are not prepared to deal with, by calling C<warn>
again in the handler.  Note that this is quite safe and will not
produce an endless loop, since C<__WARN__> hooks are not called from
inside one.

=end original

C<$SIG{__WARN__}> ハンドラが設定されている場合は何のメッセージも表示されません。
メッセージをどう扱うか(例えば C<die> に変換するか)はハンドラの責任ということです。
従ってほとんどのハンドラは、扱おうと準備していない警告を表示するために、
ハンドラの中で C<warn> を再び呼び出します。
C<__WARN__> フックはハンドラ内では呼び出されないので、これは十分安全で、
無限ループを引き起こすことはないということに注意してください。

=begin original

You will find this behavior is slightly different from that of
C<$SIG{__DIE__}> handlers (which don't suppress the error text, but can
instead call C<die> again to change it).

=end original

この振る舞いは C<$SIG{__DIE__}> ハンドラ
(エラーテキストは削除しませんが、代わりに C<die> をもう一度呼び出すことで
変更できます)とは少し違うことに気付くことでしょう。

=begin original

Using a C<__WARN__> handler provides a powerful way to silence all
warnings (even the so-called mandatory ones).  An example:

=end original

C<__WARN__> ハンドラを使うと、(いわゆる必須のものを含む)全ての警告を黙らせる
強力な手段となります。例:

    # wipe out *all* compile-time warnings
    BEGIN { $SIG{'__WARN__'} = sub { warn $_[0] if $DOWARN } }
    my $foo = 10;
    my $foo = 20;          # no warning about duplicate my $foo,
                           # but hey, you asked for it!
    # no compile-time or run-time warnings before here
    $DOWARN = 1;

    # run-time warnings enabled after here
    warn "\$foo is alive and $foo!";     # does show up

=begin original

See L<perlvar> for details on setting C<%SIG> entries, and for more
examples.  See the Carp module for other kinds of warnings using its
carp() and cluck() functions.

=end original

C<%SIG> エントリのセットに関する詳細とさらなる例に関しては
L<perlvar> を参照して下さい。
carp() 関数と cluck() 関数を用いた警告の方法に関しては
Carp モジュールを参照して下さい。

=item write FILEHANDLE

=item write EXPR

=item write

=begin original

Writes a formatted record (possibly multi-line) to the specified FILEHANDLE,
using the format associated with that file.  By default the format for
a file is the one having the same name as the filehandle, but the
format for the current output channel (see the C<select> function) may be set
explicitly by assigning the name of the format to the C<$~> variable.

=end original

指定された FILEHANDLE に対して、そのファイルに対応させた
フォーマットを使って、(複数行の場合もある) 整形された
レコードを書き出します。
デフォルトでは、ファイルに対応するフォーマットは、ファイルハンドルと
同じ名前のものですが、その時点の出力チャネル (C<select> 関数の項を
参照してください) のフォーマットは、その名前を明示的に変数 C<$~> に
代入することで、変更が可能です。　

=begin original

Top of form processing is handled automatically:  if there is
insufficient room on the current page for the formatted record, the
page is advanced by writing a form feed, a special top-of-page format
is used to format the new page header, and then the record is written.
By default the top-of-page format is the name of the filehandle with
"_TOP" appended, but it may be dynamically set to the format of your
choice by assigning the name to the C<$^> variable while the filehandle is
selected.  The number of lines remaining on the current page is in
variable C<$->, which can be set to C<0> to force a new page.

=end original

ページの先頭の処理は、自動的に行なわれます。　現在のページに
整形されたレコードを出力するだけのスペースがない場合には、
改ページを行なってページを進め、新しいページヘッダを整形するため、
ページ先頭フォーマットが使われ、その後でレコードが書かれます。
デフォルトでは、ページ先頭フォーマットは、ファイルハンドルの名前に
"_TOP" をつなげたものですが、ファイルハンドルが選択されている間に、
変数 C<$^> に名前を設定すれば、動的にフォーマットを変更することができます。
そのページの残り行数は、変数 C<$-> に入っており、この変数を 0 に
設定することで、強制的に改ページを行なうことができます。

=begin original

If FILEHANDLE is unspecified, output goes to the current default output
channel, which starts out as STDOUT but may be changed by the
C<select> operator.  If the FILEHANDLE is an EXPR, then the expression
is evaluated and the resulting string is used to look up the name of
the FILEHANDLE at run time.  For more on formats, see L<perlform>.

=end original

FILEHANDLE を指定しないと、出力はその時点のデフォルト
出力チャネルに対して行なわれます。
これは、スクリプトの開始時点では STDOUT ですが、select() 演算子で
変更することができます。
FILEHANDLE が EXPR ならば、式が評価され、その結果の文字列が
実行時に FILEHANDLE の名前として見られます。
フォーマットについて、さらには、L<perlform> を参照してください。

=begin original

Note that write is I<not> the opposite of C<read>.  Unfortunately.

=end original

残念ながら、write は C<read> の反対のことをするもの
I<ではありません>。

=item y///

=begin original

The transliteration operator.  Same as C<tr///>.  See L<perlop>.

=end original

文字変換演算子です。C<tr///>と同じです。L<perlop>を参照して
ください。

=back

=begin meta

Translate: 吉村 寿人 <JAE00534@niftyserve.or.jp>
Update: Kentaro Shirakata <argrath@ub32.org>
License: GPL or Artistic

=end meta
