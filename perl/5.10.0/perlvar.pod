
=encoding euc-jp

=head1 NAME

=begin original

perlvar - Perl predefined variables

=end original

perlvar - Perl で定義済みの変数

=head1 DESCRIPTION

=head2 Predefined Names

(定義済みの変数)

=begin original

The following names have special meaning to Perl.  Most 
punctuation names have reasonable mnemonics, or analogs in the
shells.  Nevertheless, if you wish to use long variable names,
you need only say

=end original

以下の名前は Perl では特別な意味を持ちます。
記号的な名前の多くは記憶法があるか、シェルでの類推が可能です。
それでも長い名前を使用したい場合には

    use English;

=begin original

at the top of your program. This aliases all the short names to the long
names in the current package. Some even have medium names, generally
borrowed from B<awk>. In general, it's best to use the

=end original

とプログラムの最初に書いてください。
これは、すべての短い名前の別名として、
カレントパッケージで長い名前を付けるものです。
B<awk> から持ってきた中間的な名前を持っているものもあります。
一般的に、一番良い使い方は:

    use English '-no_match_vars';

=begin original

invocation if you don't need $PREMATCH, $MATCH, or $POSTMATCH, as it avoids
a certain performance hit with the use of regular expressions. See
L<English>.

=end original

と起動することで($PREMATCH, $MATCH, $POSTMATCH が不要の場合)、これにより
正規表現を使うときのパフォーマンスへの打撃を避けられます。
L<English> を参照してください。

=begin original

Variables that depend on the currently selected filehandle may be set by
calling an appropriate object method on the IO::Handle object, although
this is less efficient than using the regular built-in variables. (Summary
lines below for this contain the word HANDLE.) First you must say

=end original

現在選択されているファイルハンドルに
依存する変数の場合には、代わりに IO::Handle オブジェクトに
関するオブジェクトメソッドを呼び出して設定することができますが、
通常の組み込み変数よりは効率が落ちます。
(以下の要約では HANDLE という語を含んでいます。)
まず最初に必ず、

    use IO::Handle;

=begin original

after which you may use either

=end original

と書き、その後で以下のように書くか、

    method HANDLE EXPR

=begin original

or more safely,

=end original

もしくはより安全に以下のように書きます:

    HANDLE->method(EXPR)

=begin original

Each method returns the old value of the IO::Handle attribute.
The methods each take an optional EXPR, which, if supplied, specifies the
new value for the IO::Handle attribute in question.  If not supplied,
most methods do nothing to the current value--except for
autoflush(), which will assume a 1 for you, just to be different.

=end original

それぞれのメソッドは、IO::Handle 属性の昔の値を返します。
メソッドはそれぞれ EXPR をとることができ、指定した場合には、
問題の IO::Handle 属性の新しい値を指定することになります。
指定しない場合には、多くのメソッドでは現在の値に対して何もしませんが、
autoflush() では 1 を指定されたものとします。

=begin original

Because loading in the IO::Handle class is an expensive operation, you should
learn how to use the regular built-in variables.

=end original

IO::Handle クラスを読み込むのはコストの高い操作なので、
通常の組み込み変数の使い方を覚えるべきです。

=begin original

A few of these variables are considered "read-only".  This means that if
you try to assign to this variable, either directly or indirectly through
a reference, you'll raise a run-time exception.

=end original

これらの変数の中には "read-only" として扱われるものもあります。
つまり、そういった変数に対して、直接にしろ、リファレンスを
介して間接にしろ、代入を行なおうとした場合には、実行時に
例外処理が起動されます。

=begin original

You should be very careful when modifying the default values of most
special variables described in this document. In most cases you want
to localize these variables before changing them, since if you don't,
the change may affect other modules which rely on the default values
of the special variables that you have changed. This is one of the
correct ways to read the whole file at once:

=end original

この文書に記述されているほとんどの特殊変数のデフォルト値を変更するときには
とても慎重になるべきです。
ほとんどの場合、これらの変数を変更する前にこれらをローカル化したいでしょう;
さもなければ、あなたが変更した特殊変数のデフォルト値に依存している
その他のモジュールにも影響を与えるかもしれないからです。
これはファイル全体を一度に読み込む正しい方法の一つです:

    open my $fh, "foo" or die $!;
    local $/; # enable localized slurp mode
    my $content = <$fh>;
    close $fh;

=begin original

But the following code is quite bad:

=end original

しかし以下のコードは完全に悪いものです:

    open my $fh, "foo" or die $!;
    undef $/; # enable slurp mode
    my $content = <$fh>;
    close $fh;

=begin original

since some other module, may want to read data from some file in the
default "line mode", so if the code we have just presented has been
executed, the global value of C<$/> is now changed for any other code
running inside the same Perl interpreter.

=end original

なぜなら、その他のモジュールでは、デフォルトの「行モード」でファイルを
読もうとするかも知れませんが、もし単に前述のコードを実行すると、
C<$/> のグローバルな値が、同じ Perl インタプリタ内で実行される
その他のコードに対しても変更されるからです。

=begin original

Usually when a variable is localized you want to make sure that this
change affects the shortest scope possible. So unless you are already
inside some short C<{}> block, you should create one yourself. For
example:

=end original

通常、変数をローカル化するとき、この変更ができるだけ最短のスコープに
影響を与えることを確実にしたいでしょう。
従って、既に小さい C<{}> ブロックの内側であるのでない限り、それを
自身で作るべきです。
例えば:

    my $content = '';
    open my $fh, "foo" or die $!;
    {
        local $/;
        $content = <$fh>;
    }
    close $fh;

=begin original

Here is an example of how your own code can go broken:

=end original

以下はどのように自分のコードが壊れるかの例です:

    for (1..5){
        nasty_break();
        print "$_ ";
    }
    sub nasty_break {
        $_ = 5;
        # do something with $_
    }

=begin original

You probably expect this code to print:

=end original

おそらくこのコードは以下のように表示されることを期待しています:

    1 2 3 4 5

=begin original

but instead you get:

=end original

しかし、以下のようになります:

    5 5 5 5 5

=begin original

Why? Because nasty_break() modifies C<$_> without localizing it
first. The fix is to add local():

=end original

なぜでしょう? 
nasty_break() は C<$_> をローカル化する前に変更するからです。
修正するには、local() を追加します:

        local $_ = 5;

=begin original

It's easy to notice the problem in such a short example, but in more
complicated code you are looking for trouble if you don't localize
changes to the special variables.

=end original

このような短い例では問題に気付くのは簡単ですが、より複雑なコードでは、
もし特殊変数の変更をローカル化していないと問題を探すことになります。

=begin original

The following list is ordered by scalar variables first, then the
arrays, then the hashes.

=end original

以下のリストはまずスカラ変数、それから配列、ハッシュの順に
並んでいます。

=over 8

=item $ARG

=item $_
X<$_> X<$ARG>

=begin original

The default input and pattern-searching space.  The following pairs are
equivalent:

=end original

デフォルトの入力とパターン検索のスペース。
以下の 2つは同値です:

    while (<>) {...}	# equivalent only in while!
    while (defined($_ = <>)) {...}

    /^Subject:/
    $_ =~ /^Subject:/

    tr/a-z/A-Z/
    $_ =~ tr/a-z/A-Z/

    chomp
    chomp($_)

=begin original

Here are the places where Perl will assume $_ even if you
don't use it:

=end original

Perl が(あなたが使いたくなくても) $_ を仮定する場合がいくつかあります:

=over 3

=item *

=begin original

Various unary functions, including functions like ord() and int(), as well
as the all file tests (C<-f>, C<-d>) except for C<-t>, which defaults to
STDIN.

=end original

様々な単項関数。ord() や int()、また C<-t> 以外の全ての
ファイルテスト (C<-f>, C<-d>)など。C<-t> のデフォルトは STDIN です。

=item *

=begin original

Various list functions like print() and unlink().

=end original

print() や unlink() などの様々なリスト関数。

=item *

=begin original

The pattern matching operations C<m//>, C<s///>, and C<tr///> when used
without an C<=~> operator.

=end original

C<=~> 演算子なしで用いられたパターンマッチ演算 C<m//>, C<s///>, C<tr///>。

=item *

=begin original

The default iterator variable in a C<foreach> loop if no other
variable is supplied.

=end original

C<foreach> ループでの他の変数が補われなかった場合のデフォルトの
繰り返し変数。

=item *

=begin original

The implicit iterator variable in the grep() and map() functions.

=end original

grep() 関数と map() 関数の暗黙の繰り返し変数。

=item *

=begin original

The default place to put an input record when a C<< <FH> >>
operation's result is tested by itself as the sole criterion of a C<while>
test.  Outside a C<while> test, this will not happen.

=end original

C<< <FH> >> が単独で C<while> テストでテストされた場合の
結果を入れるデフォルトの場所。
C<while> テスト以外ではこれは起こりません。

=back

=begin original

As C<$_> is a global variable, this may lead in some cases to unwanted
side-effects.  As of perl 5.9.1, you can now use a lexical version of
C<$_> by declaring it in a file or in a block with C<my>.  Moreover,
declaring C<our $_> restores the global C<$_> in the current scope.

=end original

C<$_> はグローバル変数なので、望まないような副作用を引き起こす場合があります。
perl 5.9.1 から、ファイルやブロックで C<my> で宣言することで、
レキシカル版の C<$_> が使えます。
さらに、C<our $_> という宣言は現在のスコープでグローバルな C<$_> を
再構築します。

=begin original

(Mnemonic: underline is understood in certain operations.)

=end original

(記憶法: 下線はある操作を覚えるためのもの。)

=back

=over 8

=item $a

=item $b
X<$a> X<$b>

=begin original

Special package variables when using sort(), see L<perlfunc/sort>.
Because of this specialness $a and $b don't need to be declared
(using use vars, or our()) even when using the C<strict 'vars'> pragma.
Don't lexicalize them with C<my $a> or C<my $b> if you want to be
able to use them in the sort() comparison block or function.

=end original

sort() を使うときの特殊パッケージ変数です; L<perlfunc/sort> を
参照してください。
この特殊性により、$a と $b は、たとえ C<strict 'vars'> を使っているときでも
(use vars や our() を使って) 宣言する必要がありません。
これを sort() 比較ブロックや関数で使えるようにしたい場合は、
C<my $a> や C<my $b> としてレキシカル化しないでください。

=back

=over 8

=item $<I<digits>>
X<$1> X<$2> X<$3>

=begin original

Contains the subpattern from the corresponding set of capturing
parentheses from the last pattern match, not counting patterns
matched in nested blocks that have been exited already.  (Mnemonic:
like \digits.)  These variables are all read-only and dynamically
scoped to the current BLOCK.

=end original

最後のパターンマッチで対応する括弧のサブパターンにマッチした
文字列が入っているが、既に抜けてしまったブロックでの
パターンマッチは勘定に入れない。
(記憶法: \(数字) のようなもの。)
これらの変数はすべて read-onlyで、現在の BLOCK に動的なスコープを持ちます。

=item $MATCH

=item $&
X<$&> X<$MATCH>

=begin original

The string matched by the last successful pattern match (not counting
any matches hidden within a BLOCK or eval() enclosed by the current
BLOCK).  (Mnemonic: like & in some editors.)  This variable is read-only
and dynamically scoped to the current BLOCK.

=end original

最後に成功したパターンマッチでマッチした文字列 (現在の
BLOCK で囲まれた BLOCK や eval() で隠れている部分でのマッチは
勘定に入れない)。
(記憶法: あるエディタの & ようなもの。) 
この変数は read-only で、現在の BLOCK に動的なスコープを持ちます。

=begin original

The use of this variable anywhere in a program imposes a considerable
performance penalty on all regular expression matches.  See L</BUGS>.

=end original

この変数をプログラムのどこかで使うと、プログラム中の全ての正規表現
マッチングにおいてかなりの性能低下を引き起こします。
L</BUGS> を参照して下さい。

=begin original

See L</@-> for a replacement.

=end original

置き換えるためには L</@-> を参照してください。

=item ${^MATCH}
X<${^MATCH}>

=begin original

This is similar to C<$&> (C<$POSTMATCH>) except that it does not incur the
performance penalty associated with that variable, and is only guaranteed
to return a defined value when the pattern was compiled or executed with
the C</p> modifier.

=end original

これは C<$&> (C<$POSTMATCH>) と同様ですが、この変数と関連付けられた性能上の
ペナルティを被らず、パターンが C</p> 修飾子付きでコンパイルまたは実行された
場合にのみ定義された値を返すことが保証されます。

=item $PREMATCH

=item $`
X<$`> X<$PREMATCH>

=begin original

The string preceding whatever was matched by the last successful
pattern match (not counting any matches hidden within a BLOCK or eval
enclosed by the current BLOCK).  (Mnemonic: C<`> often precedes a quoted
string.)  This variable is read-only.

=end original

最後の成功したパターンマッチ (現在のBLOCK で囲まれた
BLOCK や eval() に隠れている部分でのマッチは勘定に入れない) で
マッチした部分の前の文字列。
(記憶法: C<`> は多くの場合クォートされた文字列の前にある。)
この変数は read-only です。

=begin original

The use of this variable anywhere in a program imposes a considerable
performance penalty on all regular expression matches.  See L</BUGS>.

=end original

この変数をプログラムのどこかで使うと、プログラム中の全ての正規表現
マッチングにおいてかなりの性能低下を引き起こします。
L</BUGS> を参照して下さい。

=begin original

See L</@-> for a replacement.

=end original

置き換えのためには L</@-> を参照してください。

=item ${^PREMATCH}
X<${^PREMATCH}>

=begin original

This is similar to C<$`> ($PREMATCH) except that it does not incur the
performance penalty associated with that variable, and is only guaranteed
to return a defined value when the pattern was compiled or executed with
the C</p> modifier.

=end original

これは C<$`> ($PREMATCH)  と同様ですが、この変数と関連付けられた性能上の
ペナルティを被らず、パターンが C</p> 修飾子付きでコンパイルまたは実行された
場合にのみ定義された値を返すことが保証されます。

=item $POSTMATCH

=item $'
X<$'> X<$POSTMATCH>

=begin original

The string following whatever was matched by the last successful
pattern match (not counting any matches hidden within a BLOCK or eval()
enclosed by the current BLOCK).  (Mnemonic: C<'> often follows a quoted
string.)  Example:

=end original

最後の成功したパターンマッチ (現在のBLOCK で囲まれた
BLOCK や eval() に隠れている部分でのマッチは勘定に入れない) で
マッチした部分に続く文字列。
(記憶法: C<'> は多くの場合クォートされた文字列の後にある。)　例:

    local $_ = 'abcdefghi';
    /def/;
    print "$`:$&:$'\n";  	# prints abc:def:ghi

=begin original

This variable is read-only and dynamically scoped to the current BLOCK.

=end original

この変数は read-only で、現在の BLOCK に動的なスコープを持ちます。

=begin original

The use of this variable anywhere in a program imposes a considerable
performance penalty on all regular expression matches.  See L</BUGS>.

=end original

この変数をプログラムのどこかで使うと、プログラム中の全ての正規表現
マッチングにおいてかなりの性能低下を引き起こします。
L</BUGS> を参照して下さい。

=begin original

See L</@-> for a replacement.

=end original

置き換えのためには L</@-> を参照してください。

=item ${^POSTMATCH}
X<${^POSTMATCH}>

=begin original

This is similar to C<$'> (C<$POSTMATCH>) except that it does not incur the
performance penalty associated with that variable, and is only guaranteed
to return a defined value when the pattern was compiled or executed with
the C</p> modifier.

=end original

これは C<$'> (C<$POSTMATCH>)  と同様ですが、この変数と関連付けられた性能上の
ペナルティを被らず、パターンが C</p> 修飾子付きでコンパイルまたは実行された
場合にのみ定義された値を返すことが保証されます。

=item $LAST_PAREN_MATCH

=item $+
X<$+> X<$LAST_PAREN_MATCH>

=begin original

The text matched by the last bracket of the last successful search pattern.
This is useful if you don't know which one of a set of alternative patterns
matched. For example:

=end original

最後に検索されたパターンの最後の括弧にマッチした文字列。
これはいくつかの選択肢の中でどれがマッチするのか
わからないような場合に使うと便利です。たとえば:

    /Version: (.*)|Revision: (.*)/ && ($rev = $+);

=begin original

(Mnemonic: be positive and forward looking.)
This variable is read-only and dynamically scoped to the current BLOCK.

=end original

(記憶法: ポジティブで前向き。)
この変数は read-only で、現在の BLOCK に動的なスコープを持ちます。

=item $LAST_SUBMATCH_RESULT

=item $^N
X<$^N>

=begin original

The text matched by the used group most-recently closed (i.e. the group
with the rightmost closing parenthesis) of the last successful search
pattern.  (Mnemonic: the (possibly) Nested parenthesis that most
recently closed.)

=end original

最近のマッチングに成功した検索パターンのうち、一番最近に閉じられた
使われたグループ(つまり、一番右の閉じかっこのグループ)にマッチングした
テキスト。
(記憶法: もっとも最近閉じられた、(おそらく) ネストした (Nested) かっこ。)

=begin original

This is primarily used inside C<(?{...})> blocks for examining text
recently matched. For example, to effectively capture text to a variable
(in addition to C<$1>, C<$2>, etc.), replace C<(...)> with

=end original

これは主として最近マッチしたテキストを調べるために C<(?{...})> ブロックの
中で使われます。
例えば、(C<$1>, C<$2> などに加えて) テキストを変数に効率的に捕捉するには、
C<(...)> を以下で置き換えます:

     (?:(...)(?{ $var = $^N }))

=begin original

By setting and then using C<$var> in this way relieves you from having to
worry about exactly which numbered set of parentheses they are.

=end original

C<$var> をこの方法で設定してから使うことで、かっこの組の番号について
気にしなくてすむようになります。

=begin original

This variable is dynamically scoped to the current BLOCK.

=end original

この変数は現在の BLOCK に動的なスコープを持ちます。

=item @LAST_MATCH_END

=item @+
X<@+> X<@LAST_MATCH_END>

=begin original

This array holds the offsets of the ends of the last successful
submatches in the currently active dynamic scope.  C<$+[0]> is
the offset into the string of the end of the entire match.  This
is the same value as what the C<pos> function returns when called
on the variable that was matched against.  The I<n>th element
of this array holds the offset of the I<n>th submatch, so
C<$+[1]> is the offset past where $1 ends, C<$+[2]> the offset
past where $2 ends, and so on.  You can use C<$#+> to determine
how many subgroups were in the last successful match.  See the
examples given for the C<@-> variable.

=end original

この配列は、現在アクティブな動的スコープで最後に成功した
サブマッチの最後へのオフセットを保持します。
C<$+[0]> はマッチ全体の文字列の最後へのオフセットです。
これはマッチした変数に対して C<pos> 関数を呼び出したときの
返り値と同じです。
この配列の I<n> 番目の要素は I<n> 番目のサブマッチのオフセットを
保持していますので、C<$+[1]> は過去の $1 の終わりのオフセット、
C<$+[2]> は $2 のオフセット、という形になります。
C<$#+> は最後に成功したマッチでいくつサブグループがあるかを
決定するのに使えます。
C<@-> 変数の例を参照して下さい。

=item %+
X<%+>

=begin original

Similar to C<@+>, the C<%+> hash allows access to the named capture
buffers, should they exist, in the last successful match in the
currently active dynamic scope.

=end original

C<@+> と同様、C<%+> ハッシュは、現在アクティブな動的スコープで最後に成功した
マッチングの名前付き捕捉バッファ(存在すれば)へのアクセスを可能にします。

=begin original

For example, C<$+{foo}> is equivalent to C<$1> after the following match:

=end original

例えば、C<$+{foo}> は以下のマッチングの後の C<$1> と等価です:

  'foo' =~ /(?<foo>foo)/;

=begin original

The keys of the C<%+> hash list only the names of buffers that have
captured (and that are thus associated to defined values).

=end original

C<%+> ハッシュのキーは捕捉された(従って定義された値と結びついている)
バッファの名前のみの一覧です。

=begin original

The underlying behaviour of C<%+> is provided by the
L<Tie::Hash::NamedCapture> module.

=end original

C<%+> の基礎となる振る舞いは L<Tie::Hash::NamedCapture> モジュールで
提供されています。

=begin original

B<Note:> C<%-> and C<%+> are tied views into a common internal hash
associated with the last successful regular expression. Therefore mixing
iterative access to them via C<each> may have unpredictable results.
Likewise, if the last successful match changes, then the results may be
surprising.

=end original

B<注意:> C<%-> and C<%+> は最後に成功した正規表現と関連付けられた共通の
内部ハッシュと tie されたビューです。
従って、C<each> 経由で混ざった反復アクセスを行うと、予測不能の結果と
なります。
同様に、最後に成功したマッチングを変更すると、結果は驚くべきものとなります。

=item HANDLE->input_line_number(EXPR)

=item $INPUT_LINE_NUMBER

=item $NR

=item $.
X<$.> X<$NR> X<$INPUT_LINE_NUMBER> X<line number>

=begin original

Current line number for the last filehandle accessed.

=end original

最後にアクセスされたファイルハンドルの現在の行番号。

=begin original

Each filehandle in Perl counts the number of lines that have been read
from it.  (Depending on the value of C<$/>, Perl's idea of what
constitutes a line may not match yours.)  When a line is read from a
filehandle (via readline() or C<< <> >>), or when tell() or seek() is
called on it, C<$.> becomes an alias to the line counter for that
filehandle.

=end original

Perl の各ファイルハンドルは、そこから読み込んだ行数を数えています。
(C<$/> の値に依存して、何が行を構成するかに関する Perl の考えはあなたの
考えと一致しないかもしれません。)
行が(readline() や C<< <> >> を使って)ファイルハンドルから読み込まれたか、
tell() や seek() がファイルハンドルに対して呼び出された場合、
C<$.> はそのファイルハンドルの行カウンタへのエイリアスとなります。

=begin original

You can adjust the counter by assigning to C<$.>, but this will not
actually move the seek pointer.  I<Localizing C<$.> will not localize
the filehandle's line count>.  Instead, it will localize perl's notion
of which filehandle C<$.> is currently aliased to.

=end original

C<$.> へ代入することでカウンタの値を修正できますが、これは実際にシーク
ポインタを動かすことはありません。
I<C<$.> をローカル化してもファイルハンドルの行カウンタは
ローカル化されません>。
代わりに、現在 C<$.> がどのファイルハンドルへのエイリアスかという情報が
ローカル化されます。

=begin original

C<$.> is reset when the filehandle is closed, but B<not> when an open
filehandle is reopened without an intervening close().  For more
details, see L<perlop/"IE<sol>O Operators">.  Because C<< <> >> never does
an explicit close, line numbers increase across ARGV files (but see
examples in L<perlfunc/eof>).

=end original

C<$.> はファイルハンドルがクローズされるとリセットされますが、
オープンしているファイルハンドルが close() されることなく再オープンされた
場合にはリセット B<されません>。
さらなる詳細については、L<perlop/"IE<sol>O Operators"> を参照してください。
なぜなら、 C<< <> >> は決して明示的なクローズは行わず、行番号は ARGV の
ファイル間で通算してカウントされるからです(但し L<perlfunc/eof> の例を
参照してください)。

=begin original

You can also use C<< HANDLE->input_line_number(EXPR) >> to access the
line counter for a given filehandle without having to worry about
which handle you last accessed.

=end original

また、C<< HANDLE->input_line_number(EXPR) >> とすることで、どのハンドルに
最後にアクセスしたかを気にすることなく行カウンタにアクセスできます。

=begin original

(Mnemonic: many programs use "." to mean the current line number.)

=end original

(記憶法: 多くのプログラムで "." が現在行番号を示すように使われています。)

=item IO::Handle->input_record_separator(EXPR)

=item $INPUT_RECORD_SEPARATOR

=item $RS

=item $/
X<$/> X<$RS> X<$INPUT_RECORD_SEPARATOR>

=begin original

The input record separator, newline by default.  This 
influences Perl's idea of what a "line" is.  Works like B<awk>'s RS
variable, including treating empty lines as a terminator if set to
the null string.  (An empty line cannot contain any spaces
or tabs.)  You may set it to a multi-character string to match a
multi-character terminator, or to C<undef> to read through the end
of file.  Setting it to C<"\n\n"> means something slightly
different than setting to C<"">, if the file contains consecutive
empty lines.  Setting to C<""> will treat two or more consecutive
empty lines as a single empty line.  Setting to C<"\n\n"> will
blindly assume that the next input character belongs to the next
paragraph, even if it's a newline.  (Mnemonic: / delimits
line boundaries when quoting poetry.)

=end original

入力レコードセパレータで、デフォルトでは改行文字。
これは Perl での「行」とは何か、ということに影響を与えます。
空文字列に設定されると、空行をセパレータとして扱うことを
含めて、B<awk> の変数 RS のように働きます
(空行はスペースやタブを含んでいてはいけません)。
複数文字の区切文字を示すために、文字列を設定することもできます。
また、ファイルの最後まで読み込むために undef を指定することもできます。
この変数に C<"\n\n"> を設定すると、空行が続く場合において、
C<""> を設定した場合とわずかに違う動作をするようになります。
C<""> を設定した場合には、複数の空行も 1 つの空行であるかのように扱います。
C<"\n\n"> を設定した場合には、単純に次の文字が (たとえ改行文字であっても)
次の段落に含まれるものとして扱います。
(記憶法: /は、詩を引用するときに、行の区切りを示します。)

    local $/;           # enable "slurp" mode
    local $_ = <FH>;    # whole file now here
    s/\n[ \t]+/ /g;

=begin original

Remember: the value of C<$/> is a string, not a regex.  B<awk> has to be
better for something. :-)

=end original

注意: C<$/> は文字列であり、正規表現ではありません。
B<awk> は何かもっとうまくやらなくてはいけません。:-)

=begin original

Setting C<$/> to a reference to an integer, scalar containing an integer, or
scalar that's convertible to an integer will attempt to read records
instead of lines, with the maximum record size being the referenced
integer.  So this:

=end original

C<$/> に整数、整数を含むスカラ、整数に変換できるスカラのいずれかへの
リファレンスをセットすると、行を読む代わりにレコードを読もうとします。
この場合、最大レコードサイズはリファレンス先の整数値となります。つまり:

    local $/ = \32768; # or \"32768", or \$var_containing_32768
    open my $fh, $myfile or die $!;
    local $_ = <$fh>;

=begin original

will read a record of no more than 32768 bytes from FILE.  If you're
not reading from a record-oriented file (or your OS doesn't have
record-oriented files), then you'll likely get a full chunk of data
with every read.  If a record is larger than the record size you've
set, you'll get the record back in pieces.  Trying to set the record
size to zero or less will cause reading in the (rest of the) whole file.

=end original

これは FILE から 32768 バイトを超えないようにレコードを読み込みます。
もしレコード指向のファイルを読み込まない場合
(あるいは OS がレコード指向ファイルを持たない場合)、
読み込み毎にデータのチャンク全部を取り込みます。
もしレコードがセットしたレコードサイズより大きい場合、
レコードの部分を取り込みます。
レコードサイズを 0 以下にセットしようとすると、(残りの)ファイル全体を
読み込むことになります。

=begin original

On VMS, record reads are done with the equivalent of C<sysread>,
so it's best not to mix record and non-record reads on the same
file.  (This is unlikely to be a problem, because any file you'd
want to read in record mode is probably unusable in line mode.)
Non-VMS systems do normal I/O, so it's safe to mix record and
non-record reads of a file.

=end original

VMS では、レコード読み込みは C<sysread> と等価に行われますので、
レコード読み込みと非レコード読み込みを同じファイルで混ぜないのが
最善です。(これはあまり問題になりません。なぜなら
レコード読み込みしたいファイルは多分行モードでは使えないものだからです。)
VMS 以外のシステムでは普通の I/O を使いますので、
同じファイルのレコード読み込みと非レコード読み込みを混ぜても安全です。

=begin original

See also L<perlport/"Newlines">.  Also see C<$.>.

=end original

L<perlport/"Newlines"> と C<$.> も参照してください。

=item HANDLE->autoflush(EXPR)

=item $OUTPUT_AUTOFLUSH

=item $|
X<$|> X<autoflush> X<flush> X<$OUTPUT_AUTOFLUSH>

=begin original

If set to nonzero, forces a flush right away and after every write
or print on the currently selected output channel.  Default is 0
(regardless of whether the channel is really buffered by the
system or not; C<$|> tells you only whether you've asked Perl
explicitly to flush after each write).  STDOUT will
typically be line buffered if output is to the terminal and block
buffered otherwise.  Setting this variable is useful primarily when
you are outputting to a pipe or socket, such as when you are running
a Perl program under B<rsh> and want to see the output as it's
happening.  This has no effect on input buffering.  See L<perlfunc/getc>
for that.  (Mnemonic: when you want your pipes to be piping hot.)

=end original

0 以外に設定されると、
その時点で選択されている出力チャネルを
直ちにその場でフラッシュし、
さらに write や print を行なうごとに、強制的にフラッシュします。
デフォルトでは 0 となっています
(チャンネルが実際にシステムによってバッファリングされているかどうかは
関知しません。C<$|> は Perl が明示的に毎回書き込みの後に
フラッシュするかどうかのみを示します)。
STDOUT は通常では、端末への出力時には行バッファリング、
それ以外ではブロックバッファリングであることに注意してください。
これは、Perl のスクリプトを rsh 配下で実行して、
実行状況を確認したい場合のように、パイプやソケットに出力するときに特に便利でしょう。
これは入力バッファリングには何の影響も与えません。
(記憶法: パイプをホットな状態にしておくために使う。)

=item IO::Handle->output_field_separator EXPR

=item $OUTPUT_FIELD_SEPARATOR

=item $OFS

=item $,
X<$,> X<$OFS> X<$OUTPUT_FIELD_SEPARATOR>

=begin original

The output field separator for the print operator.  If defined, this
value is printed between each of print's arguments.  Default is C<undef>.
(Mnemonic: what is printed when there is a "," in your print statement.)

=end original

print 演算子のための出力フィールドセパレータ。
定義されると、この値がそれぞれの print の引数の間に表示されます。
デフォルトは C<undef> です。
(記憶法: print 文で "," を書いた場所に印字されるもの。)

=item IO::Handle->output_record_separator EXPR

=item $OUTPUT_RECORD_SEPARATOR

=item $ORS

=item $\
X<$\> X<$ORS> X<$OUTPUT_RECORD_SEPARATOR>

=begin original

The output record separator for the print operator.  If defined, this
value is printed after the last of print's arguments.  Default is C<undef>.
(Mnemonic: you set C<$\> instead of adding "\n" at the end of the print.
Also, it's just like C<$/>, but it's what you get "back" from Perl.)

=end original

print 演算子のための出力レコードセパレータ。
If defined, this
value is printed after the last of print's arguments.  Default is C<undef>.
(記憶法: print の最後に "\n" を付け加える代わりに C<$\> を設定する。
また、C<$/> に似通っているが、Perl から「バック」されるものです。)

=item $LIST_SEPARATOR

=item $"
X<$"> X<$LIST_SEPARATOR>

=begin original

This is like C<$,> except that it applies to array and slice values
interpolated into a double-quoted string (or similar interpreted
string).  Default is a space.  (Mnemonic: obvious, I think.)

=end original

C<$,> と同様ですが、これは 2 重引用符で括られた文字列
(または、同様に扱われる文字列) 内で配列とスライスの値が展開される
際に適用されます。
デフォルトではスペースになっています。(記憶法: 明らかでしょう。)

=item $SUBSCRIPT_SEPARATOR

=item $SUBSEP

=item $;
X<$;> X<$SUBSEP> X<SUBSCRIPT_SEPARATOR>

=begin original

The subscript separator for multidimensional array emulation.  If you
refer to a hash element as

=end original

多次元配列のエミュレートのための添え字の区切文字。
ハッシュの要素を

    $foo{$a,$b,$c}

=begin original

it really means

=end original

のようにして参照すると、実際には

    $foo{join($;, $a, $b, $c)}

=begin original

But don't put

=end original

という意味になります。
しかし、

    @foo{$a,$b,$c}	# a slice--note the @

=begin original

which means

=end original

としてはいけません。
これは以下の意味になります。

    ($foo{$a},$foo{$b},$foo{$c})

=begin original

Default is "\034", the same as SUBSEP in B<awk>.  If your
keys contain binary data there might not be any safe value for C<$;>.
(Mnemonic: comma (the syntactic subscript separator) is a
semi-semicolon.  Yeah, I know, it's pretty lame, but C<$,> is already
taken for something more important.)

=end original

デフォルトは "\034" で、C<awk> の SUBSEP と同じです。
使おうとしている key の値がバイナリのデータを含むならば、
C<$;> に設定する安全な値などはないことになります。
(記憶法: コンマ (構文上の添え字区切り文字) は
セミ−セミコロンなのです。
ええ、詭弁だとはわかってますが、C<$,> はもう既にもっと
重要な任務を持ってるんです。)

=begin original

Consider using "real" multidimensional arrays as described
in L<perllol>.

=end original

L<perllol> で記述している「本物の」多次元配列を使うようにしてください。

=item HANDLE->format_page_number(EXPR)

=item $FORMAT_PAGE_NUMBER

=item $%
X<$%> X<$FORMAT_PAGE_NUMBER>

=begin original

The current page number of the currently selected output channel.
Used with formats.
(Mnemonic: % is page number in B<nroff>.)

=end original

その時点で選択されている出力チャネルの、その時点でのページ番号。
フォーマットで用いられます。
(記憶法: % は、B<nroff> でのページ番号です。)

=item HANDLE->format_lines_per_page(EXPR)

=item $FORMAT_LINES_PER_PAGE

=item $=
X<$=> X<$FORMAT_LINES_PER_PAGE>

=begin original

The current page length (printable lines) of the currently selected
output channel.  Default is 60.  
Used with formats.
(Mnemonic: = has horizontal lines.)

=end original

その時点で選択されている出力チャネルの、その時点での
ページ長 (印字可能行数)。デフォルトは 60 です。
フォーマットで用いられます。
(記憶法: = には複数の水平線 (行) が含まれます。)

=item HANDLE->format_lines_left(EXPR)

=item $FORMAT_LINES_LEFT

=item $-
X<$-> X<$FORMAT_LINES_LEFT>

=begin original

The number of lines left on the page of the currently selected output
channel.  
Used with formats.
(Mnemonic: lines_on_page - lines_printed.)

=end original

その時点で選択されている出力チャネルの、ページに残っている行数。
フォーマットで用いられます。
(記憶法: "ページ行数" - "印字済み行数")

=item @LAST_MATCH_START

=item @-
X<@-> X<@LAST_MATCH_START>

=begin original

$-[0] is the offset of the start of the last successful match.
C<$-[>I<n>C<]> is the offset of the start of the substring matched by
I<n>-th subpattern, or undef if the subpattern did not match.

=end original

$-[0] は最後に成功したマッチの先頭のオフセットです。
C<$-[>I<n>C<]> は I<n> 番目のサブパターンにマッチした部分文字列の
先頭のオフセットです。サブパターンがマッチしなかった場合は undef です。

=begin original

Thus after a match against $_, $& coincides with C<substr $_, $-[0],
$+[0] - $-[0]>.  Similarly, $I<n> coincides with C<substr $_, $-[n],
$+[n] - $-[n]> if C<$-[n]> is defined, and $+ coincides with
C<substr $_, $-[$#-], $+[$#-] - $-[$#-]>.  One can use C<$#-> to find the last
matched subgroup in the last successful match.  Contrast with
C<$#+>, the number of subgroups in the regular expression.  Compare
with C<@+>.

=end original

従って $_ のマッチの後、$& は C<substr $_, $-[0], $+[0] - $-[0]> と
一致します。同様に、$I<n> は、C<$-[n]> が定義されていれば
C<substr $_, $-[n], $+[n] - $-[n]> と一致し、
$+ は C<substr $_, $-[$#-], $+[$#-] - $-[$#-]> と一致します。
C<$#-> は直前に成功したマッチで最後のマッチしたサブグループを
探すのに使えます。
正規表現でのサブグループの数である C<$#+> と対照的です。
C<@+> と比較してください。

=begin original

This array holds the offsets of the beginnings of the last
successful submatches in the currently active dynamic scope.
C<$-[0]> is the offset into the string of the beginning of the
entire match.  The I<n>th element of this array holds the offset
of the I<n>th submatch, so C<$-[1]> is the offset where $1
begins, C<$-[2]> the offset where $2 begins, and so on.

=end original

この配列は現在アクティブな動的スコープ内で最後に成功した
サブマッチの先頭位置のオフセットを保持します。
C<$-[0]> はマッチ全体の先頭の文字列へのオフセットです。
この配列の I<n> 番目の要素は I<n> 番目のサブマッチへの
オフセットを保持しますので、C<$-[1]> は $1 の先頭への
オフセット、C<$-[2]> は $2 の先頭へのオフセット、などとなります。

=begin original

After a match against some variable $var:

=end original

ある変数 $var でマッチした後、以下のようになります。

=over 5

=begin original

=item C<$`> is the same as C<substr($var, 0, $-[0])>

=item C<$&> is the same as C<substr($var, $-[0], $+[0] - $-[0])>

=item C<$'> is the same as C<substr($var, $+[0])>

=item C<$1> is the same as C<substr($var, $-[1], $+[1] - $-[1])>  

=item C<$2> is the same as C<substr($var, $-[2], $+[2] - $-[2])>

=item C<$3> is the same as C<substr($var, $-[3], $+[3] - $-[3])>

=end original

=item C<$`> は C<substr($var, 0, $-[0])> と同じです。

=item C<$&> は C<substr($var, $-[0], $+[0] - $-[0])> と同じです。

=item C<$'> は C<substr($var, $+[0])> と同じです。

=item C<$1> は C<substr($var, $-[1], $+[1] - $-[1])> と同じです。

=item C<$2> は C<substr($var, $-[2], $+[2] - $-[2])> と同じです。

=item C<$3> は C<substr $var, $-[3], $+[3] - $-[3])> と同じです。

=back

=item %-
X<%->

=begin original

Similar to C<%+>, this variable allows access to the named capture buffers
in the last successful match in the currently active dynamic scope. To
each capture buffer name found in the regular expression, it associates a
reference to an array containing the list of values captured by all
buffers with that name (should there be several of them), in the order
where they appear.

=end original

C<%+> と同様、この変数は現在アクティブな動的スコープで最後に成功した
マッチングの名前付き捕捉バッファへのアクセスを可能にします。
To each capture buffer name found in the regular expression, it associates a
reference to an array containing the list of values captured by all
buffers with that name (should there be several of them), in the order
where they appear.
(TBT)

=begin original

Here's an example:

=end original

以下に例を示します:

    if ('1234' =~ /(?<A>1)(?<B>2)(?<A>3)(?<B>4)/) {
        foreach my $bufname (sort keys %-) {
            my $ary = $-{$bufname};
            foreach my $idx (0..$#$ary) {
                print "\$-{$bufname}[$idx] : ",
                      (defined($ary->[$idx]) ? "'$ary->[$idx]'" : "undef"),
                      "\n";
            }
        }
    }

=begin original

would print out:

=end original

とすると、以下のものが表示されます:

    $-{A}[0] : '1'
    $-{A}[1] : '3'
    $-{B}[0] : '2'
    $-{B}[1] : '4'

=begin original

The keys of the C<%-> hash correspond to all buffer names found in
the regular expression.

=end original

C<%-> ハッシュのキーは正規表現で見つかった全てのバッファ名に対応します。

=begin original

The behaviour of C<%-> is implemented via the
L<Tie::Hash::NamedCapture> module.

=end original

C<%-> の振る舞いは L<Tie::Hash::NamedCapture> モジュールを使って
実装されています。

=begin original

B<Note:> C<%-> and C<%+> are tied views into a common internal hash
associated with the last successful regular expression. Therefore mixing
iterative access to them via C<each> may have unpredictable results.
Likewise, if the last successful match changes, then the results may be
surprising.

=end original

B<注意:> C<%-> and C<%+> は最後に成功した正規表現と関連付けられた共通の
内部ハッシュと tie されたビューです。
従って、C<each> 経由で混ざった反復アクセスを行うと、予測不能の結果と
なります。
同様に、最後に成功したマッチングを変更すると、結果は驚くべきものとなります。

=item HANDLE->format_name(EXPR)

=item $FORMAT_NAME

=item $~
X<$~> X<$FORMAT_NAME>

=begin original

The name of the current report format for the currently selected output
channel.  Default is the name of the filehandle.  (Mnemonic: brother to
C<$^>.)

=end original

その時点で選択されている出力チャネルの、その時点でのフォーマット名。
デフォルトでは、ファイルハンドルと同名です。
(記憶法: C<$^> の兄弟。)

=item HANDLE->format_top_name(EXPR)

=item $FORMAT_TOP_NAME

=item $^
X<$^> X<$FORMAT_TOP_NAME>

=begin original

The name of the current top-of-page format for the currently selected
output channel.  Default is the name of the filehandle with _TOP
appended.  (Mnemonic: points to top of page.)

=end original

その時点で選択されている出力チャネルの、その時点での
ページ先頭フォーマット名。
デフォルトでは、ファイルハンドル名に _TOP を続けたもの。
(記憶法: ページの先頭へのポインタ。)

=item IO::Handle->format_line_break_characters EXPR

=item $FORMAT_LINE_BREAK_CHARACTERS

=item $:
X<$:> X<FORMAT_LINE_BREAK_CHARACTERS>

=begin original

The current set of characters after which a string may be broken to
fill continuation fields (starting with ^) in a format.  Default is
S<" \n-">, to break on whitespace or hyphens.  (Mnemonic: a "colon" in
poetry is a part of a line.)

=end original

フォーマットの充填継続フィールド (^ で始まるもの) への
文字列で行分割を許す文字集合。
デフォルトは S<" \n-"> で空白か改行の後で行分割が可能となっています。
(記憶法: 詩では「コロン」は、行の一部。)

=item IO::Handle->format_formfeed EXPR

=item $FORMAT_FORMFEED

=item $^L
X<$^L> X<$FORMAT_FORMFEED>

=begin original

What formats output as a form feed.  Default is \f.

=end original

フォーマット出力で、改ページのために出力されるもの。
デフォルトは \f。

=item $ACCUMULATOR

=item $^A
X<$^A> X<$ACCUMULATOR>

=begin original

The current value of the write() accumulator for format() lines.  A format
contains formline() calls that put their result into C<$^A>.  After
calling its format, write() prints out the contents of C<$^A> and empties.
So you never really see the contents of C<$^A> unless you call
formline() yourself and then look at it.  See L<perlform> and
L<perlfunc/formline()>.

=end original

format() 行のための、その時点での write() アキュムレータの値。
format には、C<$^A> に結果を残す、formline() 呼び出しが含まれます。
自分のフォーマットを呼び出した後で、
write() は C<$^A> の内容を出力してから消去します。
したがって、自分で formline() を呼び出すのでなければ、
C<$^A> の値が見えることはありません。
L<perlform> と L<perlfunc/formline()> を参照してください。

=item $CHILD_ERROR

=item $?
X<$?> X<$CHILD_ERROR>

=begin original

The status returned by the last pipe close, backtick (C<``>) command,
successful call to wait() or waitpid(), or from the system()
operator.  This is just the 16-bit status word returned by the
traditional Unix wait() system call (or else is made up to look like it).  Thus, the
exit value of the subprocess is really (C<<< $? >> 8 >>>), and
C<$? & 127> gives which signal, if any, the process died from, and
C<$? & 128> reports whether there was a core dump.  (Mnemonic:
similar to B<sh> and B<ksh>.)

=end original

最後に close したパイプ、バッククォート (C<``>) コマンド、
成功した wait() または waitpid() 呼び出し、system() 演算子が返したステータス。
このステータスワードは伝統的な Unix の  wait() システムコールが返した
16 ビットのステータス(またはそのように見えるもの)です。
従ってサブプロセスの exit 値は、実際には (C<<< $? >> 8 >>>)
で、C<$? & 127> は、もしあれば、そのプロセスを止めたシグナルで、
C<$? & 128> はコアダンプがあるかどうかを示します。
(記憶法: B<sh> や B<ksh> と同様。)

=begin original

Additionally, if the C<h_errno> variable is supported in C, its value
is returned via $? if any C<gethost*()> function fails.

=end original

さらに、C で C<h_errno> 変数に対応している場合は、
C<gethost*()> が失敗したときに $? を通して返されます。

=begin original

If you have installed a signal handler for C<SIGCHLD>, the
value of C<$?> will usually be wrong outside that handler.

=end original

C<SIGCHLD> のシグナルハンドラを設定した場合、
C<$?> の値は通常ハンドラの外側では正しくない値となります。

=begin original

Inside an C<END> subroutine C<$?> contains the value that is going to be
given to C<exit()>.  You can modify C<$?> in an C<END> subroutine to
change the exit status of your program.  For example:

=end original

C<END> サブルーチンの内側では C<$?> には C<exit()> に渡されようとしている
値を含みます。
プログラムの終了ステータスを変更するために、C<END> サブルーチン 内で
C<$?> を変更できます。
例えば:

    END {
	$? = 1 if $? == 255;  # die would make it 255
    } 

=begin original

Under VMS, the pragma C<use vmsish 'status'> makes C<$?> reflect the
actual VMS exit status, instead of the default emulation of POSIX
status; see L<perlvms/$?> for details.

=end original

VMS では、C<use vmsish 'status'> を指定すると、
C<$?> はPOSIX ステータスをエミュレートしたものではなく、
実際の VMS 終了ステータスを反映します; 詳細は L<perlvms/$?> を
参照してください。

=begin original

Also see L<Error Indicators>.

=end original

L<Error Indicators> も参照して下さい。

=item ${^CHILD_ERROR_NATIVE}
X<$^CHILD_ERROR_NATIVE>

=begin original

The native status returned by the last pipe close, backtick (C<``>)
command, successful call to wait() or waitpid(), or from the system()
operator.  On POSIX-like systems this value can be decoded with the
WIFEXITED, WEXITSTATUS, WIFSIGNALED, WTERMSIG, WIFSTOPPED, WSTOPSIG
and WIFCONTINUED functions provided by the L<POSIX> module.

=end original

The native status returned by
最後のパイプクローズ、逆クォート (C<``>) コマンド、wait() と waitpid() の
成功した呼び出し、system() 演算子から返された、ネイティブなステータスです。
POSIX 風システムでは、この値は L<POSIX> モジュールで提供される
WIFEXITED, WEXITSTATUS, WIFSIGNALED, WTERMSIG, WIFSTOPPED, WSTOPSIG,
WIFCONTINUED 関数でデコードできます。

=begin original

Under VMS this reflects the actual VMS exit status; i.e. it is the same
as $? when the pragma C<use vmsish 'status'> is in effect.

=end original

VMS ではこれは実際の VMS の終了ステータスを反映します;
言い換えると、これは C<use vmsish 'status'> プラグマが有効なときの $? と
同じです。

=item ${^ENCODING}
X<$^ENCODING>

=begin original

The I<object reference> to the Encode object that is used to convert
the source code to Unicode.  Thanks to this variable your perl script
does not have to be written in UTF-8.  Default is I<undef>.  The direct
manipulation of this variable is highly discouraged.

=end original

ソースコードを Unicode に変換するために使われる Encode オブジェクトへの
I<オブジェクトリファレンス> 。
この変数のおかげで、perl スクリプトを UTF-8 で書く必要がありません。
デフォルトは I<undef> 。
この変数を直接操作することはとても推奨できません。

=item $OS_ERROR

=item $ERRNO

=item $!
X<$!> X<$ERRNO> X<$OS_ERROR>

=begin original

If used numerically, yields the current value of the C C<errno>
variable, or in other words, if a system or library call fails, it
sets this variable.  This means that the value of C<$!> is meaningful
only I<immediately> after a B<failure>:

=end original

数値として使われると、その時点の C の C<errno> 変数の値が得られます;
言い換えると、もしシステムコールやライブラリ呼び出しが失敗すると、
この変数がセットされます。
これは、C<$!> の値が意味を持つのは B<失敗> の I<直後> だけということを
意味します。

    if (open(FH, $filename)) {
	# Here $! is meaningless.
	...
    } else {
	# ONLY here is $! meaningful.
	...
	# Already here $! might be meaningless.
    }
    # Since here we might have either success or failure,
    # here $! is meaningless.

=begin original

In the above I<meaningless> stands for anything: zero, non-zero,
C<undef>.  A successful system or library call does B<not> set
the variable to zero.

=end original

上記の I<meaningless> は何でもあり得ます: 0、非 0、C<undef>。
システムコールやライブラリ呼び出しが成功した場合は、この変数は 0 に
セット B<されません>。

=begin original

If used as a string, yields the corresponding system error string.
You can assign a number to C<$!> to set I<errno> if, for instance,
you want C<"$!"> to return the string for error I<n>, or you want
to set the exit value for the die() operator.  (Mnemonic: What just
went bang?)

=end original

文字列として使われると、対応するシステムエラーのメッセージ文字列が得られます。
たとえば、C<$!> にエラーの文字列を返して欲しいならば、あるいは、
die() 演算子の exit 値を設定するために、I<errno> を設定するため 
C<$!> へ代入を行なうことが可能です。
(記憶法: 何が bang(!) したか。)

=begin original

Also see L<Error Indicators>.

=end original

L<Error Indicators> も参照して下さい。

=item %OS_ERROR

=item %ERRNO

=item %!
X<%!>

=begin original

Each element of C<%!> has a true value only if C<$!> is set to that
value.  For example, C<$!{ENOENT}> is true if and only if the current
value of C<$!> is C<ENOENT>; that is, if the most recent error was
"No such file or directory" (or its moral equivalent: not all operating
systems give that exact error, and certainly not all languages).
To check if a particular key is meaningful on your system, use
C<exists $!{the_key}>; for a list of legal keys, use C<keys %!>.
See L<Errno> for more information, and also see above for the
validity of C<$!>.

=end original

C<%!> の各要素は、C<$!> がその値にセットされている場合にのみ真の値を
持ちます。
例えば、C<$!{ENOENT}> は、現在の C<$!> の値が C<ENOENT> の場合にのみ
真となります; これは、最近のエラーが
"No such file or directory" (あるいは倫理的に等価なもの: 全ての OS が正確に
同じエラーを出すわけではないですし、全ての言語で出るわけでもありません) の
場合です。
あなたのシステムで特定のキーが意味があるかどうかを調べるには、
C<exists $!{the_key}> を使ってください; 有効なキーのリストを得るには、
C<keys %!> としてください。
さらなる情報と、C<$!> のバラエティに関しては、L<Errno> を参照してください。

=item $EXTENDED_OS_ERROR

=item $^E
X<$^E> X<$EXTENDED_OS_ERROR>

=begin original

Error information specific to the current operating system.  At
the moment, this differs from C<$!> under only VMS, OS/2, and Win32
(and for MacPerl).  On all other platforms, C<$^E> is always just
the same as C<$!>.

=end original

現在のオペレーティングシステムに特化したエラー情報です。
現在のところ、VMS, OS/2, Win32 (と MacPerl) のみで
C<$!> と異なる値をもちます。
その他のプラットフォームでは、C<$^E> はいつも C<$!> と同じです。

=begin original

Under VMS, C<$^E> provides the VMS status value from the last
system error.  This is more specific information about the last
system error than that provided by C<$!>.  This is particularly
important when C<$!> is set to B<EVMSERR>.

=end original

VMS では、C<$^E> は最後のシステムエラーの VMS ステータス値です。
これは、最後のシステムエラーについて C<$!> で提供されるものより
具体的な情報を示します。
これは特に C<$!> が B<EVMSERR> にセットされた場合に重要です。

=begin original

Under OS/2, C<$^E> is set to the error code of the last call to
OS/2 API either via CRT, or directly from perl.

=end original

OS/2 では、C<$^E> は CRT 経由、または Perl から直接呼び出された
最後の OS/2 API のエラーコードがセットされます。

=begin original

Under Win32, C<$^E> always returns the last error information
reported by the Win32 call C<GetLastError()> which describes
the last error from within the Win32 API.  Most Win32-specific
code will report errors via C<$^E>.  ANSI C and Unix-like calls
set C<errno> and so most portable Perl code will report errors
via C<$!>. 

=end original

Win32 では、C<$^E> は Win32 API での最後のエラーの内容を返す
C<GetLastError()> Win32 呼び出しで報告される最新のエラー情報を
返します。
ほとんどの Win32 固有のコードはエラーを C<$^E> 経由で返します。
ANSI C と Unix 風の呼び出しは C<errno> をセットするので、
ほとんどの移植性のある Perl コードは C<$!> 経由で
エラーを報告します。

=begin original

Caveats mentioned in the description of C<$!> generally apply to
C<$^E>, also.  (Mnemonic: Extra error explanation.)

=end original

C<$!> の説明で触れた問題点は一般的に C<$^E> にも適用されます。
(記憶法: 追加の(Extra)エラーの説明。)

=begin original

Also see L<Error Indicators>.

=end original

L<Error Indicators> も参照して下さい。

=item $EVAL_ERROR

=item $@
X<$@> X<$EVAL_ERROR>

=begin original

The Perl syntax error message from the last eval() operator.
If $@ is the null string, the last eval() parsed and executed
correctly (although the operations you invoked may have failed in the
normal fashion).  (Mnemonic: Where was the syntax error "at"?)

=end original

最後の eval() 操作子による Perl の構文エラーメッセージです。
$@ が空文字列であれば、最後の eval() が正常に
解析され、実行されたことになります (が、実行した演算子が、
通常の意味で失敗しているかもしれません)。
(記憶法: どこで ("at" where) 構文エラーが起ったか。)

=begin original

Warning messages are not collected in this variable.  You can,
however, set up a routine to process warnings by setting C<$SIG{__WARN__}>
as described below.

=end original

警告メッセージはこの変数に入りません。
しかし、後述する C<$SIG{__WARN__}> にセットすることで
警告を処理するルーチンを設定できます。

=begin original

Also see L<Error Indicators>.

=end original

L<Error Indicators> も参照して下さい。

=item $PROCESS_ID

=item $PID

=item $$
X<$$> X<$PID> X<$PROCESS_ID>

=begin original

The process number of the Perl running this script.  You should
consider this variable read-only, although it will be altered
across fork() calls.  (Mnemonic: same as shells.)

=end original

スクリプトを実行している Perl のプロセス番号です。
この変数は read-only と考えるべきですが、
fork() 呼び出しによって値は変わります。
(記憶法: シェルと同じ。)

=begin original

Note for Linux users: on Linux, the C functions C<getpid()> and
C<getppid()> return different values from different threads. In order to
be portable, this behavior is not reflected by C<$$>, whose value remains
consistent across threads. If you want to call the underlying C<getpid()>,
you may use the CPAN module C<Linux::Pid>.

=end original

Linux ユーザーへの注意: Linux では、C 関数 C<getpid()> と C<getppid()> は
スレッドが異なると異なった値を返します。
移植性のために、この振る舞いは C<$$> には反映されず、この値はスレッド間で
一貫しています。
もし内在する C<getpid()> を呼び出したい場合は、CPAN モジュール
C<Linux::Pid> が使えます。

=item $REAL_USER_ID

=item $UID

=item $<
X<< $< >> X<$UID> X<$REAL_USER_ID>

=begin original

The real uid of this process.  (Mnemonic: it's the uid you came I<from>,
if you're running setuid.)  You can change both the real uid and
the effective uid at the same time by using POSIX::setuid().  Since
changes to $< require a system call, check $! after a change attempt to 
detect any possible errors.

=end original

本プロセスの実 uid を示します。
(記憶法: setuid で実行中であれば、そこ「から」来た uid です。)
You can change both the real uid and the effective uid at the same
time by using POSIX::setuid().
Since changes to $< require a system call, check $!
after a change attempt to detect any possible errors.
(TBT)

=item $EFFECTIVE_USER_ID

=item $EUID

=item $>
X<< $> >> X<$EUID> X<$EFFECTIVE_USER_ID>

=begin original

The effective uid of this process.  Example:

=end original

本プロセスの実効 uid を示します。
例:

    $< = $>;		# set real to effective uid
    ($<,$>) = ($>,$<);	# swap real and effective uid

=begin original

You can change both the effective uid and the real uid at the same
time by using POSIX::setuid().  Changes to $> require a check to $!
to detect any possible errors after an attempted change. 

=end original

You can change both the effective uid and the real uid at the same
time by using POSIX::setuid().
Changes to $> require a check to $! to detect any possible errors
after an attempted change. 
(TBT)

=begin original

(Mnemonic: it's the uid you went I<to>, if you're running setuid.)
C<< $< >> and C<< $> >> can be swapped only on machines
supporting setreuid().

=end original

(記憶法: setuid で実行中であれば、そこ「へ」行く uidです。)
C<< $< >> と C<< $> >> の交換は、setreuid() をサポートしている
マシンでのみ可能です。

=item $REAL_GROUP_ID

=item $GID

=item $(
X<$(> X<$GID> X<$REAL_GROUP_ID>

=begin original

The real gid of this process.  If you are on a machine that supports
membership in multiple groups simultaneously, gives a space separated
list of groups you are in.  The first number is the one returned by
getgid(), and the subsequent ones by getgroups(), one of which may be
the same as the first number.

=end original

本プロセスの実 gid を示します。
同時に複数のグループに所属できるマシンでは、所属するグループをスペースで
区切ったリストが得られます。
最初の数値は、getgid() で返されるものです。
その後に getgroups() が返す値が続き、その中の 1 つは、
最初の値と同じかもしれません。

=begin original

However, a value assigned to C<$(> must be a single number used to
set the real gid.  So the value given by C<$(> should I<not> be assigned
back to C<$(> without being forced numeric, such as by adding zero. Note
that this is different to the effective gid (C<$)>) which does take a
list.

=end original

しかし、C<$(> に代入された値は実際の gid に設定された値の
一つでなければなりません。
従って、 C<$(> で与えられた値はゼロを足すことによって
数値化することなく C<$(> に書き戻すべきではありません。
これはリストが得られる実行 GID (C<$)>) とは違うことに注意してください。

=begin original

You can change both the real gid and the effective gid at the same
time by using POSIX::setgid().  Changes to $( require a check to $!
to detect any possible errors after an attempted change.

=end original

POSIX::setgid() を使って、実 GID と実効 GID の両方を同時に変更できます。
$( を変更した場合は、変更しようとしたときに起こりうるエラーを検出するために
$! をチェックする必要があります。

=begin original

(Mnemonic: parentheses are used to I<group> things.  The real gid is the
group you I<left>, if you're running setgid.)

=end original

(記憶法: 括弧は、I<グループ化>に使われます。
setgid で実行中であれば、実 gid は I<left> した、
つまり離れたグループです。)

=item $EFFECTIVE_GROUP_ID

=item $EGID

=item $)
X<$)> X<$EGID> X<$EFFECTIVE_GROUP_ID>

=begin original

The effective gid of this process.  If you are on a machine that
supports membership in multiple groups simultaneously, gives a space
separated list of groups you are in.  The first number is the one
returned by getegid(), and the subsequent ones by getgroups(), one of
which may be the same as the first number.

=end original

本プロセスの実効 gid を示します。
同時に複数のグループに所属できるマシンでは、
所属するグループをスペースで区切ったリストが得られます。
最初の数値は、getegid() で返されるものです。
その後に getgroups()が返す値が続き、その中の 1 つは、
最初の値と同じかもしれません。

=begin original

Similarly, a value assigned to C<$)> must also be a space-separated
list of numbers.  The first number sets the effective gid, and
the rest (if any) are passed to setgroups().  To get the effect of an
empty list for setgroups(), just repeat the new effective gid; that is,
to force an effective gid of 5 and an effectively empty setgroups()
list, say C< $) = "5 5" >.

=end original

同様に、C<$)> へ代入する値はスペースで区切られた数値の
リストでなければなりません。
最初の数値は実効 gid を設定し、残りの数値は(もしあれば) setgroups() に
渡されます。
setgroups() に空リストを渡したい場合は、単に新しい実効 gid を
繰り返してください。
つまり、実効 gid を 5 にして、setgroups() に空リストを渡したい場合は、
C< $) = "5 5" > としてください。

=begin original

You can change both the effective gid and the real gid at the same
time by using POSIX::setgid() (use only a single numeric argument).
Changes to $) require a check to $! to detect any possible errors
after an attempted change.

=end original

You can change both the effective gid and the real gid at the same
time by using POSIX::setgid() (use only a single numeric argument).
Changes to $) require a check to $! to detect any possible errors
after an attempted change.
(TBT)

=begin original

(Mnemonic: parentheses are used to I<group> things.  The effective gid
is the group that's I<right> for you, if you're running setgid.)

=end original

(記憶法: 括弧は、I<グループ化>に使われます。
setgid で実行中であれば、実効 gid は right な、つまり正しいグループです。)

=begin original

C<< $< >>, C<< $> >>, C<$(> and C<$)> can be set only on
machines that support the corresponding I<set[re][ug]id()> routine.  C<$(>
and C<$)> can be swapped only on machines supporting setregid().

=end original

C<< $< >>, C<< $> >>, C<$(>, C<$)> は、実行するマシンで、
対応する I<set[re][ug]id()> ルーティンがサポートされているときにのみ
設定可能です。
C<$(> と C<$)> の交換は、
setregid() がサポートされているマシンでのみ可能です。

=item $PROGRAM_NAME

=item $0
X<$0> X<$PROGRAM_NAME>

=begin original

Contains the name of the program being executed.

=end original

実行されているプログラムの名前を示します。

=begin original

On some (read: not all) operating systems assigning to C<$0> modifies
the argument area that the C<ps> program sees.  On some platforms you
may have to use special C<ps> options or a different C<ps> to see the
changes.  Modifying the $0 is more useful as a way of indicating the
current program state than it is for hiding the program you're
running.  (Mnemonic: same as B<sh> and B<ksh>.)

=end original

C<$0> に代入を行なうことで B<ps>) プログラムが覗く、
引数エリアを修正できるシステムもあります(全てではありません)。
$0 の修正は、実行しているプログラムを隠すよりは、
実行中のプログラムの状態を表示するときに、使うとよいでしょう。
(記憶法: B<sh> や B<ksh> と同じ。)

=begin original

Note that there are platform specific limitations on the maximum
length of C<$0>.  In the most extreme case it may be limited to the
space occupied by the original C<$0>.

=end original

C<$0> の最大長にはプラットフォーム固有の制限があることに注意してください。
最も極端な場合では、元の C<$0> で占められているサイズに制限されます。

=begin original

In some platforms there may be arbitrary amount of padding, for
example space characters, after the modified name as shown by C<ps>.
In some platforms this padding may extend all the way to the original
length of the argument area, no matter what you do (this is the case
for example with Linux 2.2).

=end original

プラットフォームによっては、任意の量のパッディングがある場合があります;
例えば、C<ps> で見られる修正された名前の後の空白文字です。
プラットフォームによっては、このパッディングは、あなたが何をしたかに
関わらず、元の引数のエリア全体に拡張されるものもあります
(例えば、これは Linux 2.2 の場合です)。

=begin original

Note for BSD users: setting C<$0> does not completely remove "perl"
from the ps(1) output.  For example, setting C<$0> to C<"foobar"> may
result in C<"perl: foobar (perl)"> (whether both the C<"perl: "> prefix
and the " (perl)" suffix are shown depends on your exact BSD variant
and version).  This is an operating system feature, Perl cannot help it.

=end original

BSD ユーザーへの注意: C<$0> に値をセットしても、ps(1) の出力から
完全に "perl" の文字列は取り除かれません。
例えば、C<$0> に C<"foobar"> と設定すると、C<"perl: foobar (perl)"> という
結果になります
(C<"perl: "> 接頭辞と" (perl)" 接尾辞が表示されるかどうかは 、正確な
BSD の種類とバージョンに依存します)。
これはオペレーティングシステムの機能で、Perl は何もできません。

=begin original

In multithreaded scripts Perl coordinates the threads so that any
thread may modify its copy of the C<$0> and the change becomes visible
to ps(1) (assuming the operating system plays along).  Note that
the view of C<$0> the other threads have will not change since they
have their own copies of it.

=end original

マルチスレッドスクリプトでは、どのスレッドも自身の C<$0> のコピーを
変更できて、その変更が(OS が対応しているとして) ps(1) で見えるように、
Perl がスレッドを調整します。
他のスレッドが持っている C<$0> の見え方は(各自が自身のコピーを
持っているので)変わらないことに注意してください。

=item $[
X<$[>

=begin original

The index of the first element in an array, and of the first character
in a substring.  Default is 0, but you could theoretically set it
to 1 to make Perl behave more like B<awk> (or Fortran) when
subscripting and when evaluating the index() and substr() functions.
(Mnemonic: [ begins subscripts.)

=end original

配列の最初の要素や、文字列の最初の文字のインデックスを
示します。
デフォルトは 0 ですが、理論的には、index() 関数や
substr() 関数を評価するときに、Perl の動作をより B<awk>
(や Fortran) に近づけるため、1 に設定することもできます。
(記憶法: [ は添え字付けの始め。)

=begin original

As of release 5 of Perl, assignment to C<$[> is treated as a compiler
directive, and cannot influence the behavior of any other file.
(That's why you can only assign compile-time constants to it.)
Its use is highly discouraged.

=end original

Perl 5 からは C<$[> への代入は、コンパイラ指示子として扱われ、
他のファイルの動作に影響を与えることがなくなりました。
(これが、コンパイル時定数しか代入できない理由です。)
この変数はできるだけ使わないようにしてください。

=begin original

Note that, unlike other compile-time directives (such as L<strict>),
assignment to C<$[> can be seen from outer lexical scopes in the same file.
However, you can use local() on it to strictly bind its value to a
lexical block.

=end original

(L<strict> のような) その他のコンパイル時指示子と違って、C<$[> への代入は、
同じファイルのより外側のレキシカルスコープからも見えることに注意してください。
但し、これに local() を使うことでこの値をレキシカルブロック内に束縛できます。

=item $]
X<$]>

=begin original

The version + patchlevel / 1000 of the Perl interpreter.  This variable
can be used to determine whether the Perl interpreter executing a
script is in the right range of versions.  (Mnemonic: Is this version
of perl in the right bracket?)  Example:

=end original

Perl インタプリタの version + patchlevel / 1000 が返されます。
スクリプトの最初で、そのスクリプトを実行しているインタプリタのバージョンが
適切な範囲内にあるかを調べる、といったことができます。
(記憶法: Perl のバージョンは、正しい範囲 (right bracket) にあるか。)
例:

    warn "No checksumming!\n" if $] < 3.019;

=begin original

See also the documentation of C<use VERSION> and C<require VERSION>
for a convenient way to fail if the running Perl interpreter is too old.

=end original

実行する Perl インタプリタが古すぎる場合に終了する便利な方法に
ついては C<use VERSION> と C<require VERSION> のドキュメントも
参照して下さい。

=begin original

The floating point representation can sometimes lead to inaccurate
numeric comparisons.  See C<$^V> for a more modern representation of
the Perl version that allows accurate string comparisons.

=end original

浮動小数点表現は数値比較が不正確になることがあります。
文字列比較が使える新しい Perl バージョンの表現方法である C<$^V> を
参照して下さい。

=item $COMPILING

=item $^C
X<$^C> X<$COMPILING>

=begin original

The current value of the flag associated with the B<-c> switch.
Mainly of use with B<-MO=...> to allow code to alter its behavior
when being compiled, such as for example to AUTOLOAD at compile
time rather than normal, deferred loading.  Setting
C<$^C = 1> is similar to calling C<B::minus_c>.

=end original

B<-c> スイッチに関連付けられた現在の値です。
主に B<-MO=...> と共に用いられ、例えば AUTOLOAD を通常の遅延ロードでは
なくコンパイル時に実行するといった、コンパイル時の振る舞いを
変えるために用います。
C<$^C = 1> に設定することは C<B::minus_c> を呼び出すのと似ています。

=item $DEBUGGING

=item $^D
X<$^D> X<$DEBUGGING>

=begin original

The current value of the debugging flags.  (Mnemonic: value of B<-D>
switch.) May be read or set. Like its command-line equivalent, you can use
numeric or symbolic values, eg C<$^D = 10> or C<$^D = "st">.

=end original

デバッグフラグの現在の値を示します。
(記憶法: B<-D> スイッチの値。)
読み書き可能です。
コマンドラインによる等価な機能と同様に、数値とシンボル値が使えます
(例: C<$^D = 10> または C<$^D = "st">)。

=item ${^RE_DEBUG_FLAGS}

=begin original

The current value of the regex debugging flags. Set to 0 for no debug output
even when the re 'debug' module is loaded. See L<re> for details.

=end original

正規表現デバッグフラグの現在の値です。
0 をセットすると、re 'debug' モジュールが読み込まれていても
デバッグ出力を行いません。
詳細については L<re> を参照してください。

=item ${^RE_TRIE_MAXBUF}

=begin original

Controls how certain regex optimisations are applied and how much memory they
utilize. This value by default is 65536 which corresponds to a 512kB temporary
cache. Set this to a higher value to trade memory for speed when matching
large alternations. Set it to a lower value if you want the optimisations to
be as conservative of memory as possible but still occur, and set it to a
negative value to prevent the optimisation and conserve the most memory.
Under normal situations this variable should be of no interest to you.

=end original

どれくらい正規表現の最適化を行い、どれくらいのメモリを利用するかを
制御します。
デフォルトではこの値は 65536 で、512kB の一時キャッシュに相当します。
この値を大きくすると、大きなものとマッチングするときに速度を重視して
多くのメモリを使います。
もしできるだけ保守的なメモリ消費をするけれども使うこともある、というように
最適化したい場合は小さい値を設定します; 負の値を設定すると最適化は行わず、
最大限メモリを節約します。
通常の状況では、この変数はあなたの興味を引くものではないでしょう。

=item $SYSTEM_FD_MAX

=item $^F
X<$^F> X<$SYSTEM_FD_MAX>

=begin original

The maximum system file descriptor, ordinarily 2.  System file
descriptors are passed to exec()ed processes, while higher file
descriptors are not.  Also, during an open(), system file descriptors are
preserved even if the open() fails.  (Ordinary file descriptors are
closed before the open() is attempted.)  The close-on-exec
status of a file descriptor will be decided according to the value of
C<$^F> when the corresponding file, pipe, or socket was opened, not the
time of the exec().

=end original

システムが使用するファイル記述子の最大値を示し、通常は 2 です。
システムファイル記述子は、exec() されたプロセスに渡されますが、
それ以降のファイル記述子は渡されません。
また、open() の実行中は、システムファイル記述子は、
たとえ open() が失敗しても、保存されます。
(通常のファイル記述子は、open() が実行される前にクローズされます。)
ファイル記述子の close-on-exec のステータスは、exec() 時ではなく、
対応するファイル、パイプソケットの open 時の C<$^F> の値によって
決められます。

=item $^H

=begin original

WARNING: This variable is strictly for internal use only.  Its availability,
behavior, and contents are subject to change without notice.

=end original

警告: この変数は厳密に内部使用に限定されます。
その可用性、挙動、内容は告知なく変更される可能性があります。

=begin original

This variable contains compile-time hints for the Perl interpreter.  At the
end of compilation of a BLOCK the value of this variable is restored to the
value when the interpreter started to compile the BLOCK.

=end original

この変数には Perl インタプリタのコンパイル時のヒントが入ります。
BLOCK のコンパイル終了時に、この変数の値は
インタプリタが BLOCK のコンパイルを開始した時の値に戻されます。

=begin original

When perl begins to parse any block construct that provides a lexical scope
(e.g., eval body, required file, subroutine body, loop body, or conditional
block), the existing value of $^H is saved, but its value is left unchanged.
When the compilation of the block is completed, it regains the saved value.
Between the points where its value is saved and restored, code that
executes within BEGIN blocks is free to change the value of $^H.

=end original

Perl がレキシカルスコープを持つブロック構造(eval の中身、required された
ファイル、サブルーチンの中身、loop の中身、条件付きブロック)の
パーズを開始するとき、現在の $^H の値は保存されますが、
値は変更されません。
ブロックのコンパイルが終わると、保存された値が戻されます。
値の保存と回復の間の地点で、
BEGIN ブロックの中で実行されるコードは自由に
$^H の値を変更できます。

=begin original

This behavior provides the semantic of lexical scoping, and is used in,
for instance, the C<use strict> pragma.

=end original

この振る舞いはレキシカルスコープを持ち、その中で使えます。
例としては C<use strict> があります。

=begin original

The contents should be an integer; different bits of it are used for
different pragmatic flags.  Here's an example:

=end original

内容は整数であるべきです。
ビット毎に異なるプラグマフラグとして使われます。以下は例です:

    sub add_100 { $^H |= 0x100 }

    sub foo {
	BEGIN { add_100() }
	bar->baz($boon);
    }

=begin original

Consider what happens during execution of the BEGIN block.  At this point
the BEGIN block has already been compiled, but the body of foo() is still
being compiled.  The new value of $^H will therefore be visible only while
the body of foo() is being compiled.

=end original

BEGIN ブロックの実行中に起こることを考えてみます。
この時点で BEGIN ブロックは既にコンパイルされていますが、
foo() の中身はまだコンパイル中です。
従って $^H の新しい値は foo() の中身がコンパイル中にのみ
見ることが出来ます。

=begin original

Substitution of the above BEGIN block with:

=end original

上記の BEGIN ブロックを以下のように変更すると:

    BEGIN { require strict; strict->import('vars') }

=begin original

demonstrates how C<use strict 'vars'> is implemented.  Here's a conditional
version of the same lexical pragma:

=end original

どのように C<use strict 'vars'> が実装されているかがわかります。
以下は同じレキシカルプラグマの条件付き版です:

    BEGIN { require strict; strict->import('vars') if $condition }

=item %^H

=begin original

The %^H hash provides the same scoping semantic as $^H.  This makes it
useful for implementation of lexically scoped pragmas. See L<perlpragma>.

=end original

%^H ハッシュは $^H と同じスコープを持ちます。
これはレキシカルスコープを持つプラグマを実装するのに便利です。
L<perlpragma> を参照してください。

=item $INPLACE_EDIT

=item $^I
X<$^I> X<$INPLACE_EDIT>

=begin original

The current value of the inplace-edit extension.  Use C<undef> to disable
inplace editing.  (Mnemonic: value of B<-i> switch.)

=end original

置き換え編集の拡張子の値を示します。
置き換え編集を禁止するためには、C<undef> を設定します。
(記憶法: B<-i> スイッチの値。)

=item $^M
X<$^M>

=begin original

By default, running out of memory is an untrappable, fatal error.
However, if suitably built, Perl can use the contents of C<$^M>
as an emergency memory pool after die()ing.  Suppose that your Perl
were compiled with C<-DPERL_EMERGENCY_SBRK> and used Perl's malloc.
Then

=end original

デフォルトでは、メモリ不足はトラップできない致命的エラーとなります。
しかし、もし適切に構築されていれば、Perl は C<$^M> の中身を
die() した後の緊急用メモリとして使えます。
Perl が C<-DPERL_EMERGENCY_SBRK> 付きでコンパイルされ、
Perl の malloc を使うと仮定します。そして、

    $^M = 'a' x (1 << 16);

=begin original

would allocate a 64K buffer for use in an emergency.  See the
F<INSTALL> file in the Perl distribution for information on how to
add custom C compilation flags when compiling perl.  To discourage casual
use of this advanced feature, there is no L<English|English> long name for
this variable.

=end original

とすると緊急用の 64K のバッファを割り当てます。
perl をコンパイルするときに独自の C コンパイルフラグを追加する
方法についての情報は、Perl 配布パッケージに含まれている
F<INSTALL> ファイルを参照して下さい。
この拡張機能を気軽に使えないようにするために、
この変数には L<English|English> の長い名前はありません。

=item $OSNAME

=item $^O
X<$^O> X<$OSNAME>

=begin original

The name of the operating system under which this copy of Perl was
built, as determined during the configuration process.  The value
is identical to C<$Config{'osname'}>.  See also L<Config> and the 
B<-V> command-line switch documented in L<perlrun>.

=end original

この Perl が構築されたオペレーティングシステムの名前です。
これは設定プロセス中に決定されます。
この値は C<$Config{'osname'}> と同じです。
L<Config> と、L<perlrun> でドキュメント化されている
B<-V> コマンドラインスイッチも参照して下さい。

=begin original

In Windows platforms, $^O is not very helpful: since it is always
C<MSWin32>, it doesn't tell the difference between
95/98/ME/NT/2000/XP/CE/.NET.  Use Win32::GetOSName() or
Win32::GetOSVersion() (see L<Win32> and L<perlport>) to distinguish
between the variants.

=end original

Windows プラットフォームでは、$^O はあまり役に立ちません: これは常に
C<MSWin32> となり、95/98/ME/NT/2000/XP/CE/.NET の違いを示していないからです。
これらを区別するためには、Win32::GetOSName() や Win32::GetOSVersion() を
使ってください (L<Win32> と L<perlport> を参照してください)。

=item ${^OPEN}

=begin original

An internal variable used by PerlIO.  A string in two parts, separated
by a C<\0> byte, the first part describes the input layers, the second
part describes the output layers.

=end original

PerlIO で使われる内部変数です。
文字列は C<\0> で分割された二つの部分からなり、前半は入力層を、
後半は出力層を示します。

=item $PERLDB

=item $^P
X<$^P> X<$PERLDB>

=begin original

The internal variable for debugging support.  The meanings of the
various bits are subject to change, but currently indicate:

=end original

デバッグ機能のための内部変数です。
それぞれのビットの意味は変わるかもしれませんが、
現在のところは以下の通りです:

=over 6

=item 0x01

=begin original

Debug subroutine enter/exit.

=end original

サブルーチンの出入りをデバッグします。

=item 0x02

=begin original

Line-by-line debugging.

=end original

行毎にデバッグします。

=item 0x04

=begin original

Switch off optimizations.

=end original

最適化を行いません。

=item 0x08

=begin original

Preserve more data for future interactive inspections.

=end original

将来の対話的な検査のためにより多くのデータを保存します。

=item 0x10

=begin original

Keep info about source lines on which a subroutine is defined.

=end original

サブルーチンが定義されたソース行に関する情報を保持します。

=item 0x20

=begin original

Start with single-step on.

=end original

シングルステップ実行で開始します。

=item 0x40

=begin original

Use subroutine address instead of name when reporting.

=end original

報告時にサブルーチン名でなくサブルーチンのアドレスを使います。

=item 0x80

=begin original

Report C<goto &subroutine> as well.

=end original

C<goto &subroutine> も同様に報告します。

=item 0x100

=begin original

Provide informative "file" names for evals based on the place they were compiled.

=end original

eval に対して、コンパイルされた位置を元にした「ファイル」名を提供します。

=item 0x200

=begin original

Provide informative names to anonymous subroutines based on the place they
were compiled.

=end original

無名サブルーチンに対して、
コンパイルされた位置を基にした参考名を提供します。

=item 0x400

=begin original

Debug assertion subroutines enter/exit.

=end original

サブルーチンの進入/脱出をアサートします。

=back

=begin original

Some bits may be relevant at compile-time only, some at
run-time only.  This is a new mechanism and the details may change.

=end original

無名サブルーチンに対して、
コンパイルされた位置を基にした参考名を提供します。

=item $LAST_REGEXP_CODE_RESULT

=item $^R
X<$^R> X<$LAST_REGEXP_CODE_RESULT>

=begin original

The result of evaluation of the last successful C<(?{ code })>
regular expression assertion (see L<perlre>).  May be written to.

=end original

最後に成功した C<(?{ code })> 正規表現アサートの評価の結果です
(L<perlre> を参照して下さい)。おそらくもっと書き足します。

=item $EXCEPTIONS_BEING_CAUGHT

=item $^S
X<$^S> X<$EXCEPTIONS_BEING_CAUGHT>

=begin original

Current state of the interpreter.

=end original

現在のインタプリタの状態を示します。

=begin original

    $^S         State
    ---------   -------------------
    undef       Parsing module/eval
    true (1)    Executing an eval
    false (0)   Otherwise

=end original

    $^S         State
    ---------   -------------------
    undef       モジュール/eval のパース中
    真 (1)      eval の実行中
    偽 (0)      その他

=begin original

The first state may happen in $SIG{__DIE__} and $SIG{__WARN__} handlers.

=end original

最初の状態は $SIG{__DIE__} と $SIG{__WARN__} のハンドラで起きる可能性が
あります。

=item $BASETIME

=item $^T
X<$^T> X<$BASETIME>

=begin original

The time at which the program began running, in seconds since the
epoch (beginning of 1970).  The values returned by the B<-M>, B<-A>,
and B<-C> filetests are based on this value.

=end original

プログラムを実行開始した時刻を、紀元 (1970年の始め) からの秒数で示したものです。
ファイルテスト B<-M>、B<-A>、B<-C> で返される値は、この値に基づいています。

=item ${^TAINT}

=begin original

Reflects if taint mode is on or off.  1 for on (the program was run with
B<-T>), 0 for off, -1 when only taint warnings are enabled (i.e. with
B<-t> or B<-TU>).  This variable is read-only.

=end original

汚染検査モードのオン・オフを反映します。
1 はオン(プログラムは B<-T> 付きで実行されている)、0 はオフ、-1 は汚染
警告のみが有効になっている(つまり B<-t> か B<-TU>)ことを意味します。
この変数は読み込み専用です。

=item ${^UNICODE}

=begin original

Reflects certain Unicode settings of Perl.  See L<perlrun>
documentation for the C<-C> switch for more information about
the possible values. This variable is set during Perl startup
and is thereafter read-only.

=end original

Perl のいくつかの Unicode 設定を反映します。
設定できる値に関するさらなる情報については L<perlrun> の C<-C> オプションを
参照してください。
この変数は Perl 起動時に設定され、その後は読み込み専用です。

=item ${^UTF8CACHE}

=begin original

This variable controls the state of the internal UTF-8 offset caching code.
1 for on (the default), 0 for off, -1 to debug the caching code by checking
all its results against linear scans, and panicking on any discrepancy.

=end original

この変数は内部 UTF-8 オフセットキャッシュコードの状態を制御します。
1 はオン(デフォルト)、0 はオフ、-1 は全ての結果を線形走査と比較して、
矛盾があれば異常終了する、という形でキャッシュコードをデバッグします。

=item ${^UTF8LOCALE}

=begin original

This variable indicates whether an UTF-8 locale was detected by perl at
startup. This information is used by perl when it's in
adjust-utf8ness-to-locale mode (as when run with the C<-CL> command-line
switch); see L<perlrun> for more info on this.

=end original

この変数は、起動時に perl によって UTF-8 ロケールが検出されたかどうかを
示します。
この情報は(C<-CL> コマンドラインスイッチで起動されることによって)
「utf8 性をロケールに合わせる」モードのときに perl によって使われます;
これに関するさらなる情報は L<perlrun> を参照してください。

=item $PERL_VERSION

=item $^V
X<$^V> X<$PERL_VERSION>

=begin original

The revision, version, and subversion of the Perl interpreter, represented
as a C<version> object.

=end original

C<version> オブジェクトとして表現される revision, version, subversion。

=begin original

This variable first appeared in perl 5.6.0; earlier versions of perl will
see an undefined value. Before perl 5.10.0 $^V was represented as a v-string.

=end original

この変数は perl 5.6.0 で最初に現れました; それより前のバージョンでは
未定義値となります。
perl 5.10.0 以前では $^V は v-string 形式で表現されます。

=begin original

$^V can be used to determine whether the Perl interpreter executing a
script is in the right range of versions.  (Mnemonic: use ^V for Version
Control.)  Example:

=end original

$^V はスクリプトを実行している Perl インタプリタのバージョンが
正しい範囲に入っているかを調べるのに使えます。(記憶法:
^V をバージョンコントロールに使います。) 例:

    warn "Hashes not randomized!\n" if !$^V or $^V lt v5.8.1

=begin original

To convert C<$^V> into its string representation use sprintf()'s
C<"%vd"> conversion:

=end original

C<$^V> を文字列表現に変換するには sprintf() の C<"%vd"> 変換を使います:

    printf "version is v%vd\n", $^V;  # Perl's version

=begin original

See the documentation of C<use VERSION> and C<require VERSION>
for a convenient way to fail if the running Perl interpreter is too old.

=end original

実行する Perl インタプリタが古すぎる場合に終了する便利な方法に
ついては C<use VERSION> と C<require VERSION> のドキュメントを
参照して下さい。

=begin original

See also C<$]> for an older representation of the Perl version.

=end original

Perl バージョンの古い表現については C<$]> も参照して下さい。

=item $WARNING

=item $^W
X<$^W> X<$WARNING>

=begin original

The current value of the warning switch, initially true if B<-w>
was used, false otherwise, but directly modifiable.  (Mnemonic:
related to the B<-w> switch.)  See also L<warnings>.

=end original

警告スイッチの値で、B<-w> スイッチが使われると内部的に真となり、
そうでない場合は直接変更可能です。
(記憶法: B<-w> スイッチに関係します。)
L<warnings> も参照して下さい。

=item ${^WARNING_BITS}

=begin original

The current set of warning checks enabled by the C<use warnings> pragma.
See the documentation of C<warnings> for more details.

=end original

C<use warnings> プラグマで有効にされた、現在の警告チェックの集合です。
詳細については C<warnings> のドキュメントを参照して下さい。

=item ${^WIN32_SLOPPY_STAT}

=begin original

If this variable is set to a true value, then stat() on Windows will
not try to open the file. This means that the link count cannot be
determined and file attributes may be out of date if additional
hardlinks to the file exist. On the other hand, not opening the file
is considerably faster, especially for files on network drives.

=end original

この変数が真の値にセットされると、Windows での stat() はファイルを
オープンしようとはしません。
これは、このファイルへの追加のハードリンクが存在する場合、リンクカウントを
決定できませんし、ファイル属性が古いものになるかもしれないことを
意味します。
一方、ファイルを開かないので、(特にファイルがネットワークドライブにある
場合は)大幅に高速です。

=begin original

This variable could be set in the F<sitecustomize.pl> file to
configure the local Perl installation to use "sloppy" stat() by
default.  See L<perlrun> for more information about site
customization.

=end original

デフォルトで「ずさんな」stat() を使うために、この変数は、ローカルな
Perl を設定するための  F<sitecustomize.pl> で設定できます。
サイトカスタマイズに関するさらなる情報については L<perlrun> を
参照してください。

=item $EXECUTABLE_NAME

=item $^X
X<$^X> X<$EXECUTABLE_NAME>

=begin original

The name used to execute the current copy of Perl, from C's
C<argv[0]> or (where supported) F</proc/self/exe>.

=end original

Perl バイナリ自身が実行された時の名前を C の argv[0] または (対応していれば)
F</proc/self/exe> から持ってきたものです。

=begin original

Depending on the host operating system, the value of $^X may be
a relative or absolute pathname of the perl program file, or may
be the string used to invoke perl but not the pathname of the
perl program file.  Also, most operating systems permit invoking
programs that are not in the PATH environment variable, so there
is no guarantee that the value of $^X is in PATH.  For VMS, the
value may or may not include a version number.

=end original

ホスト OS に依存して、$^X の値は perl プログラムファイルの絶対パスかも
しれませんし、相対パスかもしれませんし、perl を起動するために使われる
文字列ではありますが perl プログラムファイルのパス名ではないかもしれません。
また、ほとんどの OS は PATH 環境変数にない位置のプログラムを起動することを
許しているので、$^X の値が PATH にある保証はありません。
VMS では、この値はバージョン番号を含む場合も含まない場合もあります。

=begin original

You usually can use the value of $^X to re-invoke an independent
copy of the same perl that is currently running, e.g.,

=end original

通常は、現在実行中のものと同じ perl の独立したコピーを再起動するために
$^X の値を使えます; つまり:

  @first_run = `$^X -le "print int rand 100 for 1..100"`;

=begin original

But recall that not all operating systems support forking or
capturing of the output of commands, so this complex statement
may not be portable.

=end original

しかし、全ての OS が fork やコマンドの出力の捕捉に対応しているわけでは
ないので、この複雑な文は移植性がないかもしれないことを忘れないでください。

=begin original

It is not safe to use the value of $^X as a path name of a file,
as some operating systems that have a mandatory suffix on
executable files do not require use of the suffix when invoking
a command.  To convert the value of $^X to a path name, use the
following statements:

=end original

$^X の値をファイルのパス名として使うのは安全ではありません;
実行ファイルに固定の接尾辞があり、コマンドの起動時には接尾辞が不要な OS も
あるからです。
$^X の値をパス名に変換するには、以下のコードを使ってください:

  # Build up a set of file names (not command names).
  use Config;
  $this_perl = $^X;
  if ($^O ne 'VMS')
     {$this_perl .= $Config{_exe}
          unless $this_perl =~ m/$Config{_exe}$/i;}

=begin original

Because many operating systems permit anyone with read access to
the Perl program file to make a copy of it, patch the copy, and
then execute the copy, the security-conscious Perl programmer
should take care to invoke the installed copy of perl, not the
copy referenced by $^X.  The following statements accomplish
this goal, and produce a pathname that can be invoked as a
command or referenced as a file.

=end original

多くの OS が Perl のプログラムファイルのコピーを作って、コピーに
パッチを当て、それを実行するための読み込み権限を全員に与えているので、
セキュリティ意識のある Perl プログラマは $^X で参照されているコピーではなく、
インストールされている perl を起動するように気をつけるべきです。
以下のコードはこの目的を達成し、コマンドとして起動したりファイルとして
参照するためのパス名を作成します。

  use Config;
  $secure_perl_path = $Config{perlpath};
  if ($^O ne 'VMS')
     {$secure_perl_path .= $Config{_exe}
          unless $secure_perl_path =~ m/$Config{_exe}$/i;}

=item ARGV
X<ARGV>

=begin original

The special filehandle that iterates over command-line filenames in
C<@ARGV>. Usually written as the null filehandle in the angle operator
C<< <> >>. Note that currently C<ARGV> only has its magical effect
within the C<< <> >> operator; elsewhere it is just a plain filehandle
corresponding to the last file opened by C<< <> >>. In particular,
passing C<\*ARGV> as a parameter to a function that expects a filehandle
may not cause your function to automatically read the contents of all the
files in C<@ARGV>.

=end original

C<@ARGV> にあるコマンドラインで指定されたファイル名に対して反復する
特殊ファイルハンドルです。
通常角かっこ C<< <> >> の中で空ファイルハンドルとして書かれます。
現在のところ、C<ARGV> は C<< <> >> 演算子の中でのみ特別な効果があることに
注意してください; その他の場所では、C<< <> >> で開かれた最後のファイルに
対応する普通のファイルハンドルです。
特に、ファイルハンドルを想定している関数に C<\*ARGV> を引数として渡しても、
関数内で C<@ARGV> にある全てのファイルの内容を自動的に読み込むことには
なりません。

=item $ARGV
X<$ARGV>

=begin original

contains the name of the current file when reading from <>.

=end original

<> から読込みを行なっているとき、その時点のファイル名を示します。

=item @ARGV
X<@ARGV>

=begin original

The array @ARGV contains the command-line arguments intended for
the script.  C<$#ARGV> is generally the number of arguments minus
one, because C<$ARGV[0]> is the first argument, I<not> the program's
command name itself.  See C<$0> for the command name.

=end original

配列 @ARGV は、コマンドラインからスクリプトに渡す引数が入れられます。
C<$ARGV[0]> がI<プログラムのコマンド名自身ではなく>、
最初の引数ですから、C<$#ARGV> は一般には、引数の個数 - 1 となります。
コマンド名については、C<$0> を参照してください。

=item ARGVOUT
X<ARGVOUT>

=begin original

The special filehandle that points to the currently open output file
when doing edit-in-place processing with B<-i>.  Useful when you have
to do a lot of inserting and don't want to keep modifying $_.  See
L<perlrun> for the B<-i> switch.

=end original

B<-i> を使ってその場修正を行っているときに、現在開いている出力ファイルを
示す特殊ファイルハンドルです。
たくさんの挿入をする必要があるときに $_ を修正し続けたくない場合に
有用です。
B<-i> オプションについては L<perlrun> を参照してください。

=item @F
X<@F>

=begin original

The array @F contains the fields of each line read in when autosplit
mode is turned on.  See L<perlrun> for the B<-a> switch.  This array
is package-specific, and must be declared or given a full package name
if not in package main when running under C<strict 'vars'>.

=end original

自動 split モードが有効の場合、配列 @F には読み込んだ行のフィールドを
含みます。
B<-a> オプションについては L<perlrun> を参照してください。
この配列はパッケージ固有であり、もし C<strict 'vars'> で実行していて
パッケージ main 以外の場合は完全なパッケージ名で定義したり与えたり
しなければなりません。

=item @INC
X<@INC>

=begin original

The array @INC contains the list of places that the C<do EXPR>,
C<require>, or C<use> constructs look for their library files.  It
initially consists of the arguments to any B<-I> command-line
switches, followed by the default Perl library, probably
F</usr/local/lib/perl>, followed by ".", to represent the current
directory.  ("." will not be appended if taint checks are enabled, either by
C<-T> or by C<-t>.)  If you need to modify this at runtime, you should use
the C<use lib> pragma to get the machine-dependent library properly
loaded also:

=end original

配列 @INC には、do EXPR、require、use によってライブラリファイルを
探すときに評価する場所のリストが納められています。
初期状態では、コマンドラインスイッチ B<-I> の引数と
デフォルトの Perl ライブラリディレクトリ (おそらく
F</usr/local/lib/perl5>) とカレントディレクトリを表わす
"." を順につなげたものです。
(C<-T> か C<-t> によって汚染チェックが有効の場合は、"." は追加されません。)
実行時にこれを変更する必要がある場合は、マシン依存のライブラリも正しく
読み込むために C<use lib> を使うべきです:

    use lib '/mypath/libdir/';
    use SomeMod;

=begin original

You can also insert hooks into the file inclusion system by putting Perl
code directly into @INC.  Those hooks may be subroutine references, array
references or blessed objects.  See L<perlfunc/require> for details.

=end original

Perl のコードを直接 @INC に入れることで、ファイルインクルード機構に
フックを挿入できます。
このフックはサブルーチンリファレンス、配列リファレンス、bless された
オブジェクトが可能です。
詳細については L<perlfunc/require> を参照してください。

=item @ARG

=item @_
X<@_> X<@ARG>

=begin original

Within a subroutine the array @_ contains the parameters passed to that
subroutine.  See L<perlsub>.

=end original

サブルーチンの内部では、配列 @_ はサブルーチンに渡されたパラメータです。
L<perlsub> を参照して下さい。

=item %INC
X<%INC>

=begin original

The hash %INC contains entries for each filename included via the
C<do>, C<require>, or C<use> operators.  The key is the filename
you specified (with module names converted to pathnames), and the
value is the location of the file found.  The C<require>
operator uses this hash to determine whether a particular file has
already been included.

=end original

ハッシュ %INC は、C<do>, C<require>, C<use>演算子によって
インクルードされた、個々のファイル名をエントリとして持っています。
key は指定したファイル名(モジュール名はパス名に変換されます)で、
value は見つかった場所となっています。
C<require> 演算子は、指定されたファイル名が既に
インクルードされているかを、このハッシュを使って調べます。

=begin original

If the file was loaded via a hook (e.g. a subroutine reference, see
L<perlfunc/require> for a description of these hooks), this hook is
by default inserted into %INC in place of a filename.  Note, however,
that the hook may have set the %INC entry by itself to provide some more
specific info.

=end original

ファイルがフック(つまりサブルーチンリファレンス; フックに関する
説明については L<perlfunc/require> を参照してください)経由で読み込まれた
場合、このフックはデフォルトではファイル名の代わりに %INC に挿入されます。
しかし、フックはさらなる特定の情報を提供するために、自身で %INC エントリを
セットするかもしれないことに注意してください。

=item %ENV

=item $ENV{expr}
X<%ENV>

=begin original

The hash %ENV contains your current environment.  Setting a
value in C<ENV> changes the environment for any child processes
you subsequently fork() off.

=end original

ハッシュ %ENV には、その時点の環境変数が設定されています。
C<ENV> に値を設定することで、
以後に fork() した子プロセスの環境変数を変更します。

=item %SIG

=item $SIG{expr}
X<%SIG>

=begin original

The hash C<%SIG> contains signal handlers for signals.  For example:

=end original

ハッシュ C<%SIG> にはシグナルのためのシグナルハンドラが含まれています。
例えば:

    sub handler {	# 1st argument is signal name
	my($sig) = @_;
	print "Caught a SIG$sig--shutting down\n";
	close(LOG);
	exit(0);
    }

    $SIG{'INT'}  = \&handler;
    $SIG{'QUIT'} = \&handler;
    ...
    $SIG{'INT'}  = 'DEFAULT';	# restore default action
    $SIG{'QUIT'} = 'IGNORE';	# ignore SIGQUIT

=begin original

Using a value of C<'IGNORE'> usually has the effect of ignoring the
signal, except for the C<CHLD> signal.  See L<perlipc> for more about
this special case.

=end original

C<'IGNORE'> という値は通常はシグナルの効果を無視するために使いますが、
C<CHLD> シグナルは例外です。
この特別な場合に関する詳細は L<perlipc> を参照して下さい。

=begin original

Here are some other examples:

=end original

以下にその他の例を示します:

=begin original

    $SIG{"PIPE"} = "Plumber";   # assumes main::Plumber (not recommended)
    $SIG{"PIPE"} = \&Plumber;   # just fine; assume current Plumber
    $SIG{"PIPE"} = *Plumber;    # somewhat esoteric
    $SIG{"PIPE"} = Plumber();   # oops, what did Plumber() return??

=end original

    $SIG{"PIPE"} = "Plumber";   # main::Plumber を仮定します(非推奨)
    $SIG{"PIPE"} = \&Plumber;   # 問題なし; カレントの Plumber を仮定します
    $SIG{"PIPE"} = *Plumber;    # 少々難解
    $SIG{"PIPE"} = Plumber();   # げげ、Plumber() は何を返すの??

=begin original

Be sure not to use a bareword as the name of a signal handler,
lest you inadvertently call it. 

=end original

裸の単語をシグナルハンドラの名前として使わないようにしてください。
不注意で呼び出すのを避けるためです。

=begin original

If your system has the sigaction() function then signal handlers are
installed using it.  This means you get reliable signal handling.

=end original

システムに sigaction() 関数がある場合は、
シグナルハンドラはこの関数を使って設定されます。
これにより、信頼性のあるシグナルハンドリングが可能になります。

=begin original

The default delivery policy of signals changed in Perl 5.8.0 from 
immediate (also known as "unsafe") to deferred, also known as 
"safe signals".  See L<perlipc> for more information.

=end original

デフォルトのシグナル配送ポリシーは Perl 5.8.0 に即時("unsafe"としても
知られます)から保留(「安全なシグナル」としても知られます)に変更されました。
さらなる情報については L<perlipc> を参照してください。

=begin original

Certain internal hooks can be also set using the %SIG hash.  The
routine indicated by C<$SIG{__WARN__}> is called when a warning message is
about to be printed.  The warning message is passed as the first
argument.  The presence of a C<__WARN__> hook causes the ordinary printing
of warnings to C<STDERR> to be suppressed.  You can use this to save warnings
in a variable, or turn warnings into fatal errors, like this:

=end original

ある種の内部フックも %SIG ハッシュを使ってセットされます。
警告メッセージを表示しようとするときに C<$SIG{__WARN__}> で
示されたルーチンが呼び出されます。
警告メッセージは最初の引数として渡されます。
C<__WARN__> フックがあると、通常の C<STDERR> への警告の出力は行われません。
これを使って、警告メッセージを変数にいれたり、
あるいは以下のようにして警告を致命的エラーに変えたり出来ます:

    local $SIG{__WARN__} = sub { die $_[0] };
    eval $proggie;

=begin original

As the C<'IGNORE'> hook is not supported by C<__WARN__>, you can
disable warnings using the empty subroutine:

=end original

C<__WARN__> では C<'IGNORE'> フックには対応していないので、空サブルーチンを
使って警告を無効に出来ます:

    local $SIG{__WARN__} = sub {};

=begin original

The routine indicated by C<$SIG{__DIE__}> is called when a fatal exception
is about to be thrown.  The error message is passed as the first
argument.  When a C<__DIE__> hook routine returns, the exception
processing continues as it would have in the absence of the hook,
unless the hook routine itself exits via a C<goto>, a loop exit, or a C<die()>.
The C<__DIE__> handler is explicitly disabled during the call, so that you
can die from a C<__DIE__> handler.  Similarly for C<__WARN__>.

=end original

C<$SIG{__DIE__}> で示されるルーチンは
致命的な例外がまさに投げられようとするときに呼び出されます。
エラーメッセージは最初の引数として渡されます。
C<__DIE__> フックから戻ると、
例外処理はフックがなかったかのように再開されますが、
フックルーチン自体が C<goto>、ループ終了、C<die()> によって
終了した場合を除きます。
C<__DIE__> ハンドラは呼び出し中は明示的に無効になりますので、
C<__DIE__> ハンドラから die できます。
C<__WARN__> も同様です。

=begin original

Due to an implementation glitch, the C<$SIG{__DIE__}> hook is called
even inside an eval().  Do not use this to rewrite a pending exception
in C<$@>, or as a bizarre substitute for overriding C<CORE::GLOBAL::die()>.
This strange action at a distance may be fixed in a future release
so that C<$SIG{__DIE__}> is only called if your program is about
to exit, as was the original intent.  Any other use is deprecated.

=end original

実装上の不具合により、C<$SIG{__DIE__}> は eval() の中でも
呼び出されます。これを、C<$@> の待っている例外を書き換えたり、
C<CORE::GLOBAL::die()> を上書きするのに使わないでください。
この奇妙な行動は将来のリリースで修正される予定なので、
C<$SIG{__DIE__}> は当初の目的通り、
プログラムが終了するときにのみ呼び出されるようになります。
その他の用途は非推奨です。

=begin original

C<__DIE__>/C<__WARN__> handlers are very special in one respect:
they may be called to report (probable) errors found by the parser.
In such a case the parser may be in inconsistent state, so any
attempt to evaluate Perl code from such a handler will probably
result in a segfault.  This means that warnings or errors that
result from parsing Perl should be used with extreme caution, like
this:

=end original

C<__DIE__> と C<__WARN__> のハンドラは一つの点で非常に特別です。
パーザによってエラー(であろうもの)を報告するために呼び出されることがある
ことです。
このような場合、パーザは不安定な状態になっているかもしれないので、
ハンドラから Perl コードを評価しようとするとセグメンテーションフォールトが
発生するかもしれません。
Perl のパーズ中の警告やエラーは、以下のように非常に注意して扱うべきです。

    require Carp if defined $^S;
    Carp::confess("Something wrong") if defined &Carp::confess;
    die "Something wrong, but could not load Carp to give backtrace...
         To see backtrace try starting Perl with -MCarp switch";

=begin original

Here the first line will load Carp I<unless> it is the parser who
called the handler.  The second line will print backtrace and die if
Carp was available.  The third line will be executed only if Carp was
not available.

=end original

一行目は、I<パーザがハンドラを呼び出したのでなければ>
Carp を読み込みます。
二行目は、Carp が使えるならバックとレースを表示して die します。
三行目は Carp が使えないときにのみ実行されます。

=begin original

See L<perlfunc/die>, L<perlfunc/warn>, L<perlfunc/eval>, and
L<warnings> for additional information.

=end original

追加の情報については L<perlfunc/die>, L<perlfunc/warn>, L<perlfunc/eval>,
L<warnings> を参照して下さい。

=back

=head2 Error Indicators
X<error> X<exception>

(エラー指示子)

=begin original

The variables C<$@>, C<$!>, C<$^E>, and C<$?> contain information
about different types of error conditions that may appear during
execution of a Perl program.  The variables are shown ordered by
the "distance" between the subsystem which reported the error and
the Perl process.  They correspond to errors detected by the Perl
interpreter, C library, operating system, or an external program,
respectively.

=end original

変数 C<$@>, C<$!>, C<$^E>, C<$?> は Perl プログラムの実行中に
発生した、異なる種類のエラー情報を保持します。
変数はエラーを報告した副システムと Perl プロセスとの「距離」
の順番に並んでいます。
これらはそれぞれ、Perl インタプリタ、C ライブラリ、
オペレーティングシステム、外部プログラムによって検出された
エラーに対応しています。

=begin original

To illustrate the differences between these variables, consider the 
following Perl expression, which uses a single-quoted string:

=end original

これらの変数の違いを示すために、
以下のようなシングルクォートを用いた Perl 式を考えます:

    eval q{
	open my $pipe, "/cdrom/install |" or die $!;
	my @res = <$pipe>;
	close $pipe or die "bad pipe: $?, $!";
    };

=begin original

After execution of this statement all 4 variables may have been set.  

=end original

この文を実行した後、4 つの変数全てがセットされる可能性があります。

=begin original

C<$@> is set if the string to be C<eval>-ed did not compile (this
may happen if C<open> or C<close> were imported with bad prototypes),
or if Perl code executed during evaluation die()d .  In these cases
the value of $@ is the compile error, or the argument to C<die>
(which will interpolate C<$!> and C<$?>).  (See also L<Fatal>,
though.)

=end original

C<$@> は C<eval> された文字列がコンパイルされなかったとき
(これは C<open> か C<close> が正しくない
プロトタイプでインポートされたときに起こり得ます)、
または評価中に実行している Perl コードが die() したときにセットされます。
これらの場合には $@ の値はコンパイルエラー、または
C<die> への引数(これには C<$!> と C<$?> が差し挟まれます)です。
(しかし、L<Fatal> も参照して下さい。)

=begin original

When the eval() expression above is executed, open(), C<< <PIPE> >>,
and C<close> are translated to calls in the C run-time library and
thence to the operating system kernel.  C<$!> is set to the C library's
C<errno> if one of these calls fails. 

=end original

上記の eval() 式が実行された後、
open(), C<< <PIPE> >>, C<close> は C ランタイムライブラリの呼び出しに
変換され、それからオペレーティングシステムコールに変換されます。
C<$!> はこれらの呼び出しのどれかが失敗したとき、
C ライブラリの C<errno> の値がセットされます。

=begin original

Under a few operating systems, C<$^E> may contain a more verbose
error indicator, such as in this case, "CDROM tray not closed."
Systems that do not support extended error messages leave C<$^E>
the same as C<$!>.

=end original

いくつかのオペレーティングシステムでは、
C<$^E> により詳細なエラー指示子が入っているかもしれません。
今回の場合で言えば、"CDROM tray not closed." などです。
追加のエラーメッセージに対応していないシステムでは、
C<$^E> は C<$!> と同じ値です。

=begin original

Finally, C<$?> may be set to non-0 value if the external program
F</cdrom/install> fails.  The upper eight bits reflect specific
error conditions encountered by the program (the program's exit()
value).   The lower eight bits reflect mode of failure, like signal
death and core dump information  See wait(2) for details.  In
contrast to C<$!> and C<$^E>, which are set only if error condition
is detected, the variable C<$?> is set on each C<wait> or pipe
C<close>, overwriting the old value.  This is more like C<$@>, which
on every eval() is always set on failure and cleared on success.

=end original

最後に、C<$?> は外部プログラム F</cdrom/install> が失敗したときに
非 0 にセットされるかもしれません。
上位の 8 ビットはプログラムが遭遇した特定のエラー状況
(プログラムの exit() の値)を反映します。
下位の 8 ビットは、シグナルの死亡やコアダンプ情報と言った失敗のモードを反映します。
詳細については wait(2) を参照して下さい。
C<$!> と C<$^E> はエラー状況が検出されたときにのみ設定されますが、
変数 C<$?> は C<wait> やパイプの C<close> の度に、前の値を上書きします。
これは、C<$@> が eval() の実行毎に、エラーならセットされ、
成功ならクリアされるという動作と似ています。

=begin original

For more details, see the individual descriptions at C<$@>, C<$!>, C<$^E>,
and C<$?>.

=end original

より詳細については、C<$@>, C<$!>, C<$^E>, C<$?> それぞれの説明を
参照して下さい。

=head2 Technical Note on the Syntax of Variable Names

(変数名の文法に関するテクニカルノート)

=begin original

Variable names in Perl can have several formats.  Usually, they
must begin with a letter or underscore, in which case they can be
arbitrarily long (up to an internal limit of 251 characters) and
may contain letters, digits, underscores, or the special sequence
C<::> or C<'>.  In this case, the part before the last C<::> or
C<'> is taken to be a I<package qualifier>; see L<perlmod>.

=end original

Perl の変数名は様々な形があります。
通常、変数名は英文字か下線で始まらなければならず、
任意の長さ(内部制限の 251 文字まで)を取ることができ、
英文字、数字、下線、特別な文字列である C<::> と C<'> を含むことができます。
この場合、最後の C<::> または C<'> の前は
I<パッケージ限定子> として扱われます。
L<perlmod> を参照して下さい。

=begin original

Perl variable names may also be a sequence of digits or a single
punctuation or control character.  These names are all reserved for
special uses by Perl; for example, the all-digits names are used
to hold data captured by backreferences after a regular expression
match.  Perl has a special syntax for the single-control-character
names: It understands C<^X> (caret C<X>) to mean the control-C<X>
character.  For example, the notation C<$^W> (dollar-sign caret
C<W>) is the scalar variable whose name is the single character
control-C<W>.  This is better than typing a literal control-C<W>
into your program.

=end original

Perl の変数は、数字の列または一文字の句読点かコントロール文字の
場合もあります。
これらの名前は全て Perl によって特別な用途のために予約されています。
例えば、全て数字の名前は正規表現マッチの後の後方参照のデータを
保持するために用いられます。
Perl には一文字のコントロール文字の名前のための特別な文法があります。
C<^X>(キャレット C<X>)は control-C<X> キャラクタを意味します。
例えば、C<$^W>(ドル記号 キャレット C<W>)は control-C<W> 一文字の
名前をもつスカラ変数です。
これはプログラム中にリテラルな control-C<W> をタイプするより
良いです。

=begin original

Finally, new in Perl 5.6, Perl variable names may be alphanumeric
strings that begin with control characters (or better yet, a caret).
These variables must be written in the form C<${^Foo}>; the braces
are not optional.  C<${^Foo}> denotes the scalar variable whose
name is a control-C<F> followed by two C<o>'s.  These variables are
reserved for future special uses by Perl, except for the ones that
begin with C<^_> (control-underscore or caret-underscore).  No
control-character name that begins with C<^_> will acquire a special
meaning in any future version of Perl; such names may therefore be
used safely in programs.  C<$^_> itself, however, I<is> reserved.

=end original

最後に、Perl 5.6 の新機能として、コントロール文字(もっと言えばキャレット)で
始まる、英数字からなる文字列の変数名も使えます。
これらの変数は C<${^Foo}> の形で書かれなければなりません。
括弧は必須です。
C<${^Foo}> はコントロール-C<F> の後に二つ C<o> が続く名前を持つ
スカラ変数です。
これらの変数は Perl によって特別な用途のために予約されていますが、
C<^_> (コントロール-下線またはキャレット-下線)で始まるものは例外です。
C<^_> で始まるコントロール文字名は Perl の将来のバージョンで
特別な意味を持つことはありません。
従ってこれらの名前はプログラム中で安全に使用できます。
但し、C<$^_> そのものは I<予約されます>。

=begin original

Perl identifiers that begin with digits, control characters, or
punctuation characters are exempt from the effects of the C<package>
declaration and are always forced to be in package C<main>; they are
also exempt from C<strict 'vars'> errors.  A few other names are also
exempt in these ways:

=end original

数字、コントロール文字、句読点で始まる Perl の識別子は
C<package> 宣言の効果から逃れて、常に C<main> パッケージにあるものとして
扱われます。さらに以下のものも逃れます:

	ENV		STDIN
	INC		STDOUT
	ARGV		STDERR
	ARGVOUT		_
	SIG

=begin original

In particular, the new special C<${^_XYZ}> variables are always taken
to be in package C<main>, regardless of any C<package> declarations
presently in scope.  

=end original

特に、新しい特別な C<${^_XYZ}> 変数はスコープ内の C<package> 宣言に関わらず
常に C<main> パッケージとして扱われます。

=head1 BUGS

(バグ)

=begin original

Due to an unfortunate accident of Perl's implementation, C<use
English> imposes a considerable performance penalty on all regular
expression matches in a program, regardless of whether they occur
in the scope of C<use English>.  For that reason, saying C<use
English> in libraries is strongly discouraged.  See the
Devel::SawAmpersand module documentation from CPAN
( http://www.cpan.org/modules/by-module/Devel/ )
for more information. Writing C<use English '-no_match_vars';>
avoids the performance penalty.

=end original

Perl の実装における不幸な事故により、
C<use English> はプログラム中の全ての正規表現マッチングにおいて
かなりの性能低下を引き起こします。
これは C<use English> のスコープ内かどうかに関わりません。
この理由により、ライブラリで C<use English> を使うのは
できるだけ避けてください。
さらなる情報については CPAN の Devel::SawAmpersand モジュール
(http://www.perl.com/CPAN/modules/by-module/Devel/) の
ドキュメントを参照して下さい。

=begin original

Having to even think about the C<$^S> variable in your exception
handlers is simply wrong.  C<$SIG{__DIE__}> as currently implemented
invites grievous and difficult to track down errors.  Avoid it
and use an C<END{}> or CORE::GLOBAL::die override instead.

=end original

例外ハンドラの中で C<$^S> を使おうなどとは考えてもいけません。
現在の実装の C<$SIG{__DIE__}> は面倒を引き寄せ、エラーの追跡を困難にします。
これの代わりに C<END{}> を使うか、CORE::GLOBAL::die をオーバーライドしてください。

=begin meta

Translate: 吉村 寿人 <JAE00534@niftyserve.or.jp> (5.000)
Update: Kentaro Shirakata <argrath@ub32.org> (5.6.1-)
License: GPL or Artistic

=end meta

