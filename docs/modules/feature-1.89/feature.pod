
=encoding utf8

=head1 NAME

=begin original

feature - Perl pragma to enable new features

=end original

feature - 新しい機能を有効にするプラグマ

=head1 SYNOPSIS

    use feature qw(fc say);

=begin original

    # Without the "use feature" above, this code would not be able to find
    # the built-ins "say" or "fc":
    say "The case-folded version of $x is: " . fc $x;

=end original

    # 前述の "use feature" がなければ、このコードは組み込みの
    # "say" や "fc" を見つけられない:
    say "The case-folded version of $x is: " . fc $x;

=begin original

    # set features to match the :5.36 bundle, which may turn off or on
    # multiple features (see "FEATURE BUNDLES" below)
    use feature ':5.36';

=end original

    # :5.36 の束に一致する機能を設定する; これは複数の機能を
    # オンまたはオフにするかもしれない (後述の "FEATURE BUNDLES" 参照)
    use feature ':5.36';

=begin original

    # implicitly loads :5.36 feature bundle
    use v5.36;

=end original

    # 明示的に :5.36 の機能の束を読み込む
    use v5.36;

=head1 DESCRIPTION

=begin original

It is usually impossible to add new syntax to Perl without breaking
some existing programs.  This pragma provides a way to minimize that
risk. New syntactic constructs, or new semantic meanings to older
constructs, can be enabled by C<use feature 'foo'>, and will be parsed
only when the appropriate feature pragma is in scope.  (Nevertheless, the
C<CORE::> prefix provides access to all Perl keywords, regardless of this
pragma.)

=end original

既に存在しているプログラムを壊すことなく、Perl に新しい文法を追加することは、
普通は不可能です。
このプラグマは、リスクを最小化する方法を提供します。
新しい文法構造や、古い構造の新しい意味は、C<use feature 'foo'> で有効化され、
適切な feature プラグマがスコープ内にある場合にのみパースされます。
(それでも、このプラグマに関わらず、C<CORE::> 接頭辞は全ての
Perl キーワードへのアクセスを提供します。)

=head2 Lexical effect

(レキシカルな効果)

=begin original

Like other pragmas (C<use strict>, for example), features have a lexical
effect.  C<use feature qw(foo)> will only make the feature "foo" available
from that point to the end of the enclosing block.

=end original

(例えば C<use strict> のような) その他のプラグマと同様、機能は
レキシカルな効果を持ちます。
C<use feature qw(foo)> は、この地点からブロックの終わりまでの間だけ、
"foo" 機能を利用可能にします。

    {
        use feature 'say';
        say "say is available here";
    }
    print "But not here.\n";

=head2 C<no feature>

=begin original

Features can also be turned off by using C<no feature "foo">.  This too
has lexical effect.

=end original

機能は C<no feature "foo"> を使うことで無効にすることも出来ます。
これもまたレキシカルな効果を持ちます。

    use feature 'say';
    say "say is available here";
    {
        no feature 'say';
        print "But not here.\n";
    }
    say "Yet it is here.";

=begin original

C<no feature> with no features specified will reset to the default group.  To
disable I<all> features (an unusual request!) use C<no feature ':all'>.

=end original

C<no feature> と、機能を指定せずに使うと、デフォルトグループにリセットします。
I<全ての> 機能を無効にする(普通でない要求!)には、C<no feature ':all'> を
使ってください。

=head1 AVAILABLE FEATURES

(利用可能な機能)

=begin original

Read L</"FEATURE BUNDLES"> for the feature cheat sheet summary.

=end original

機能の一覧につては L</"FEATURE BUNDLES"> を読んでください。

=head2 The 'say' feature

('say' 機能)

=begin original

C<use feature 'say'> tells the compiler to enable the Raku-inspired
C<say> function.

=end original

C<use feature 'say'> は、コンパイラに Raku に着想を得たの C<say> 関数を
有効にするように伝えます。

=begin original

See L<perlfunc/say> for details.

=end original

詳しくは L<perlfunc/say> を参照してください。

=begin original

This feature is available starting with Perl 5.10.

=end original

この機能は Perl 5.10 から利用可能です。

=head2 The 'state' feature

('state' 機能)

=begin original

C<use feature 'state'> tells the compiler to enable C<state>
variables.

=end original

C<use feature 'state'> は、コンパイラに C<state> 変数を有効にするように
伝えます。

=begin original

See L<perlsub/"Persistent Private Variables"> for details.

=end original

詳しくは L<perlsub/"Persistent Private Variables"> を参照してください。

=begin original

This feature is available starting with Perl 5.10.

=end original

この機能は Perl 5.10 から利用可能です。

=head2 The 'switch' feature

('switch' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変更されたり削除されたりするかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::smartmatch";

=begin original

C<use feature 'switch'> tells the compiler to enable the Raku
given/when construct.

=end original

C<use feature 'switch'> は、コンパイラに Raku の given/when 構文を
有効にするように伝えます。

=begin original

See L<perlsyn/"Switch Statements"> for details.

=end original

詳しくは L<perlsyn/"Switch Statements"> を参照してください。

=begin original

This feature is available starting with Perl 5.10.
It is deprecated starting with Perl 5.38, and using
C<given>, C<when> or smartmatch will throw a warning.
It will be removed in Perl 5.42.

=end original

この機能は Perl 5.10 から利用可能です。
これは Perl 5.38 から廃止予定で、
C<given>, C<when> およびスマートマッチングを使うと警告が発生します。
これは Perl 5.42 で削除される予定です。

=head2 The 'unicode_strings' feature

('unicode_strings' 機能)

=begin original

C<use feature 'unicode_strings'> tells the compiler to use Unicode rules
in all string operations executed within its scope (unless they are also
within the scope of either C<use locale> or C<use bytes>).  The same applies
to all regular expressions compiled within the scope, even if executed outside
it.  It does not change the internal representation of strings, but only how
they are interpreted.

=end original

C<use feature 'unicode_strings'> は、(C<use locale> か C<use bytes> の
スコープないでない限り) そのスコープ内で実行される全ての文字列操作に
Unicode の規則を使うようにコンパイラに伝えます。
これは文字列の内部表現は変更しません; それをどう解釈するかだけです。

=begin original

C<no feature 'unicode_strings'> tells the compiler to use the traditional
Perl rules wherein the native character set rules is used unless it is
clear to Perl that Unicode is desired.  This can lead to some surprises
when the behavior suddenly changes.  (See
L<perlunicode/The "Unicode Bug"> for details.)  For this reason, if you are
potentially using Unicode in your program, the
C<use feature 'unicode_strings'> subpragma is B<strongly> recommended.

=end original

C<no feature 'unicode_strings'> は、Unicode が求められているのが
Perl にとって明らかでない限り、ネイティブな文字集合規則が使われるところで
伝統的な Perl の規則を使うようにコンパイラに伝えます。
これは、振る舞いが突然変更されたときに驚きを引き起こすかもしれません。
(詳しくは L<perlunicode/The "Unicode Bug"> を参照してください。)
この理由により、もしプログラムで Unicode を扱う可能性があるなら、
C<use feature 'unicode_strings'> 副プラグマを B<強く> 勧めます。

=begin original

This feature is available starting with Perl 5.12; was almost fully
implemented in Perl 5.14; and extended in Perl 5.16 to cover C<quotemeta>;
was extended further in Perl 5.26 to cover L<the range
operator|perlop/Range Operators>; and was extended again in Perl 5.28 to
cover L<special-cased whitespace splitting|perlfunc/split>.

=end original

この機能は Perl 5.12 から利用可能になりました; Perl 5.14 でほぼ完全に
実装されました; Perl 5.16 で C<quotemeta> に対応するように拡張されました;
Perl 5.26 では
L<範囲演算子|perlop/Range Operators> に対応するようにさらに拡張されました;
そして Perl 5.28 では
L<特殊な場合の空白の split|perlfunc/split> に対応するように
さらに拡張されました。

=head2 The 'unicode_eval' and 'evalbytes' features

('unicode_eval' と 'evalbytes' 機能)

=begin original

Together, these two features are intended to replace the legacy string
C<eval> function, which behaves problematically in some instances.  They are
available starting with Perl 5.16, and are enabled by default by a
S<C<use 5.16>> or higher declaration.

=end original

これら二つの機能は共に、古い文字列 C<eval> 関数を置き換えることを
目的としています; これはいくつかの状況で問題のある振る舞いをします。
これらは Perl 5.16 から利用可能で、
S<C<use 5.16>> またはそれ以上の宣言により、デフォルトで有効になります。

=begin original

C<unicode_eval> changes the behavior of plain string C<eval> to work more
consistently, especially in the Unicode world.  Certain (mis)behaviors
couldn't be changed without breaking some things that had come to rely on
them, so the feature can be enabled and disabled.  Details are at
L<perlfunc/Under the "unicode_eval" feature>.

=end original

C<unicode_eval> は、特に Unicode の世界で、より一貫性のある動作をするように、
単なる文字列の C<eval> の振る舞いを変更します。
いくつかの(間違った)振る舞いは、これに依存しているものを
壊さずに変更することができないので、
この機能は有効にしたり無効にしたりできます。
詳細は L<perlfunc/Under the "unicode_eval" feature> にあります。

=begin original

C<evalbytes> is like string C<eval>, but it treats its argument as a byte
string. Details are at L<perlfunc/evalbytes EXPR>.  Without a
S<C<use feature 'evalbytes'>> nor a S<C<use v5.16>> (or higher) declaration in
the current scope, you can still access it by instead writing
C<CORE::evalbytes>.

=end original

C<evalbytes> は文字列 C<eval> に似ていますが、
引数をバイト文字列として扱います。
詳細は L<perlfunc/evalbytes EXPR> にあります。
現在のスコープに S<C<use feature 'evalbytes'>> や
S<C<use v5.16>> (またはそれ以上) の宣言がない場合でも、
C<CORE::evalbytes> と書くことでこれにアクセスできます。

=head2 The 'current_sub' feature

('current_sub' 機能)

=begin original

This provides the C<__SUB__> token that returns a reference to the current
subroutine or C<undef> outside of a subroutine.

=end original

これは C<__SUB__> トークンを提供します; これは現在のサブルーチンへの
リファレンスか、サブルーチンの外側では C<undef> を返します。

=begin original

This feature is available starting with Perl 5.16.

=end original

この機能は Perl 5.16 から利用可能です。

=head2 The 'array_base' feature

('array_base' 機能)

=begin original

This feature supported the legacy C<$[> variable.  See L<perlvar/$[>.
It was on by default but disabled under C<use v5.16> (see
L</IMPLICIT LOADING>, below) and unavailable since perl 5.30.

=end original

この機能はレガシーな C<$[> 変数に対応していました。
L<perlvar/$[> を参照してください。
これはデフォルトではオンでしたが C<use v5.16> (後述の
L</IMPLICIT LOADING> 参照) の下では無効になっていて、
perl 5.30 から利用できなくなりました。

=begin original

This feature is available under this name starting with Perl 5.16.  In
previous versions, it was simply on all the time, and this pragma knew
nothing about it.

=end original

この機能は Perl 5.16 からこの名前で利用可能です。
以前のバージョンでは、単に常時適用されていて、このプラグマはこれについて
何も知りませんでした。

=head2 The 'fc' feature

('fc' 機能)

=begin original

C<use feature 'fc'> tells the compiler to enable the C<fc> function,
which implements Unicode casefolding.

=end original

C<use feature 'fc'> は、Unicode 畳み込みを実装した C<fc> 関数を
有効にするようにコンパイラに伝えます。

=begin original

See L<perlfunc/fc> for details.

=end original

詳しくは L<perlfunc/fc> を参照してください。

=begin original

This feature is available from Perl 5.16 onwards.

=end original

この機能は Perl 5.16 から利用可能です。

=head2 The 'lexical_subs' feature

('lexical_subs' 機能)

=begin original

In Perl versions prior to 5.26, this feature enabled
declaration of subroutines via C<my sub foo>, C<state sub foo>
and C<our sub foo> syntax.  See L<perlsub/Lexical Subroutines> for details.

=end original

Perl バージョンが 5.26 より前の場合、これは
C<my sub foo>, C<state sub foo>, C<our sub foo> 文法による
サブルーチンの定義を有効にします。
詳しくは L<perlsub/Lexical Subroutines> を参照してください。

=begin original

This feature is available from Perl 5.18 onwards.  From Perl 5.18 to 5.24,
it was classed as experimental, and Perl emitted a warning for its
usage, except when explicitly disabled:

=end original

この機能は Perl 5.18 から利用可能です。
Perl 5.18 から 5.24 では、これは実験的と位置づけられていて、
明示的に無効にしない限り Perl は警告を出力していました:

  no warnings "experimental::lexical_subs";

=begin original

As of Perl 5.26, use of this feature no longer triggers a warning, though
the C<experimental::lexical_subs> warning category still exists (for
compatibility with code that disables it).  In addition, this syntax is
not only no longer experimental, but it is enabled for all Perl code,
regardless of what feature declarations are in scope.

=end original

Perl 5.24 から、この機能の使用はもはや警告を出力しなくなりましたが、
C<experimental::lexical_subs> 警告カテゴリは(これを無効にするコードとの
互換性のために)存在するままです。
さらに、この文法はもはや実験的ではないだけでなく、
どんな機能宣言がスコープ内にあるかに関わらず、
全ての Perl コードで有効です。

=head2 The 'postderef' and 'postderef_qq' features

('postderef' と 'postderef_qq' 機能)

=begin original

The 'postderef_qq' feature extends the applicability of L<postfix
dereference syntax|perlref/Postfix Dereference Syntax> so that
postfix array dereference, postfix scalar dereference, and
postfix array highest index access are available in double-quotish interpolations.
For example, it makes the following two statements equivalent:

=end original

'postderef_qq' 機能は、
L<後置デリファレンス文法|perlref/Postfix Dereference Syntax> の機能を、
後置配列出リファレンス、後置スカラデリファレンス、
後置の配列最大インデックスアクセスが、
ダブルクォート風変数展開で利用可能になるように拡張します。
例えば、次の二つの文が等価になります:

  my $s = "[@{ $h->{a} }]";
  my $s = "[$h->{a}->@*]";

=begin original

This feature is available from Perl 5.20 onwards. In Perl 5.20 and 5.22, it
was classed as experimental, and Perl emitted a warning for its
usage, except when explicitly disabled:

=end original

この機能は Perl 5.20 から利用可能です。
Perl 5.20 と 5.22 では、これは実験的と位置づけられていて、
明示的に無効にしない限り Perl は警告を出力していました:

  no warnings "experimental::postderef";

=begin original

As of Perl 5.24, use of this feature no longer triggers a warning, though
the C<experimental::postderef> warning category still exists (for
compatibility with code that disables it).

=end original

Perl 5.24 から、この機能の使用はもはや警告を出力しなくなりましたが、
C<experimental::postderef> 警告カテゴリは(これを無効にするコードとの
互換性のために)存在するままです。

=begin original

The 'postderef' feature was used in Perl 5.20 and Perl 5.22 to enable
postfix dereference syntax outside double-quotish interpolations. In those
versions, using it triggered the C<experimental::postderef> warning in the
same way as the 'postderef_qq' feature did. As of Perl 5.24, this syntax is
not only no longer experimental, but it is enabled for all Perl code,
regardless of what feature declarations are in scope.

=end original

'postderef' 機能は、ダブルクォート風変数展開の外側での
後置デリファレンス文法を有効にするために Perl 5.20 から Perl 5.22 で
使われていました。
これらのバージョンでは、'postderef_qq' 機能と同様に、これを使うと
C<experimental::postderef> 警告を引き起こします。
Perl 5.24 から、この文法はもはや実験的ではなくなっただけではなく、
スコープ中でどんな機能が宣言されているかに関わらず、全ての Perl コードで
有効になりました。

=head2 The 'signatures' feature

('signatures' 機能)

=begin original

This enables syntax for declaring subroutine arguments as lexical variables.
For example, for this subroutine:

=end original

これは、サブルーチンの引数をレキシカル変数として宣言する文法を有効にします。
例えば、このサブルーチンは:

    sub foo ($left, $right) {
        return $left + $right;
    }

=begin original

Calling C<foo(3, 7)> will assign C<3> into C<$left> and C<7> into C<$right>.

=end original

C<foo(3, 7)> と呼び出すと、C<$left> に C<3> を、C<$right> に C<7> を
代入します。

=begin original

See L<perlsub/Signatures> for details.

=end original

詳しくは L<perlsub/Signatures> を参照してください。

=begin original

This feature is available from Perl 5.20 onwards. From Perl 5.20 to 5.34,
it was classed as experimental, and Perl emitted a warning for its usage,
except when explicitly disabled:

=end original

この機能は Perl 5.20 から利用可能です。
Perl 5.20 から 5.34 では、これは実験的と位置づけられていて、
明示的に無効にしない限り Perl は警告を出力していました:

  no warnings "experimental::signatures";

=begin original

As of Perl 5.36, use of this feature no longer triggers a warning, though the
C<experimental::signatures> warning category still exists (for compatibility
with code that disables it). This feature is now considered stable, and is
enabled automatically by C<use v5.36> (or higher).

=end original

Perl 5.24 から、この機能の使用はもはや警告を出力しなくなりましたが、
C<experimental::lexical_subs> 警告カテゴリは (これを無効にする
コードとの互換性のために) 存在するままです。
さらに、この機能は現在安定していると考えられていて、
C<use v5.36> (またはそれ以上) によって自動的に有効になります。

=head2 The 'refaliasing' feature

('refaliasing' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::refaliasing";

=begin original

This enables aliasing via assignment to references:

=end original

これはリファレンスへの代入による別名化を有効にします:

    \$a = \$b; # $a and $b now point to the same scalar
    \@a = \@b; #                     to the same array
    \%a = \%b;
    \&a = \&b;
    foreach \%hash (@array_of_hash_refs) {
        ...
    }

=begin original

See L<perlref/Assigning to References> for details.

=end original

詳しくは L<perlref/Assigning to References> を参照してください。

=begin original

This feature is available from Perl 5.22 onwards.

=end original

この機能は Perl 5.22 から利用可能です。

=head2 The 'bitwise' feature

('bitwise' 機能)

=begin original

This makes the four standard bitwise operators (C<& | ^ ~>) treat their
operands consistently as numbers, and introduces four new dotted operators
(C<&. |. ^. ~.>) that treat their operands consistently as strings.  The
same applies to the assignment variants (C<&= |= ^= &.= |.= ^.=>).

=end original

これは四つの標準ビット単位演算子 (C<& | ^ ~>) がそのオペランドを
数値として一貫して扱うようになり、
オペランドを一貫して文字列として扱う新しいドット付き演算子
(C<&. |. ^. ~.>) を導入します。
同じものは代入の亜種 (C<&= |= ^= &.= |.= ^.=>) にも適用されます。

=begin original

See L<perlop/Bitwise String Operators> for details.

=end original

詳しくは L<perlop/Bitwise String Operators> を参照してください。

=begin original

This feature is available from Perl 5.22 onwards.  Starting in Perl 5.28,
C<use v5.28> will enable the feature.  Before 5.28, it was still
experimental and would emit a warning in the "experimental::bitwise"
category.

=end original

この機能は Perl 5.22 から利用可能です。
Perl 5.28 から、C<use v5.28> はこの機能を有効にします。
5.28 より前では、これはまだ実験的で、
"experimental::bitwise" カテゴリの警告が出力されます。

=head2 The 'declared_refs' feature

('declared_refs' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::declared_refs";

=begin original

This allows a reference to a variable to be declared with C<my>, C<state>,
or C<our>, or localized with C<local>.  It is intended mainly for use in
conjunction with the "refaliasing" feature.  See L<perlref/Declaring a
Reference to a Variable> for examples.

=end original

これは C<my>, C<state>, C<our>, C<local> でのローカル化において、
変数へのリファレンスを宣言できるようにします。
これは主に "refaliasing" 機能と併せて使うことを意図しています。
例については L<perlref/Declaring a Reference to a Variable> を
参照してください。

=begin original

This feature is available from Perl 5.26 onwards.

=end original

この機能は Perl 5.26 から利用可能です。

=head2 The 'isa' feature

('isa' 機能)

=begin original

This allows the use of the C<isa> infix operator, which tests whether the
scalar given by the left operand is an object of the class given by the
right operand. See L<perlop/Class Instance Operator> for more details.

=end original

これは、左オペランドに指定されたスカラが右オペランドに指定された
クラスのオブジェクトであるかどうかをテストする
C<isa> 中置演算子を使えるようにします。
さらなる詳細については L<perlop/Class Instance Operator> を参照してください。

=begin original

This feature is available from Perl 5.32 onwards.  From Perl 5.32 to 5.34,
it was classed as experimental, and Perl emitted a warning for its usage,
except when explicitly disabled:

=end original

この機能は Perl 5.32 から利用可能です。
Perl 5.32 から 5.34 では、これは実験的と位置づけられていて、
明示的に無効にしない限り Perl は警告を出力していました:

    no warnings "experimental::isa";

=begin original

As of Perl 5.36, use of this feature no longer triggers a warning (though the
C<experimental::isa> warning category still exists for compatibility with
code that disables it). This feature is now considered stable, and is enabled
automatically by C<use v5.36> (or higher).

=end original

Perl 5.24 から、この機能の使用はもはや警告を出力しなくなりました
(しかし、C<experimental::lexical_subs> 警告カテゴリは、これを無効にする
コードとの互換性のために存在するままです)。
さらに、この機能は現在安定していると考えられていて、
C<use v5.36> (またはそれ以上) によって自動的に有効になります。

=head2 The 'indirect' feature

('indirect' 機能)

=begin original

This feature allows the use of L<indirect object
syntax|perlobj/Indirect Object Syntax> for method calls, e.g.  C<new
Foo 1, 2;>. It is enabled by default, but can be turned off to
disallow indirect object syntax.

=end original

この機能は、C<new Foo 1, 2;> のような、
L<間接オブジェクト構文|perlobj/Indirect Object Syntax> を
メソッド呼び出しに対して有効にします。
これはデフォルトで有効ですが、間接オブジェクト構文を無効にするために
オフにすることができます。

=begin original

This feature is available under this name from Perl 5.32 onwards. In
previous versions, it was simply on all the time.  To disallow (or
warn on) indirect object syntax on older Perls, see the L<indirect>
CPAN module.

=end original

この機能は Perl 5.32 から利用可能です。
以前のバージョンでは、単に常時有効でした。
古い Perl で間接オブジェクトを無効にする(または警告を出す)には、
L<indirect> CPAN モジュールを参照してください。

=head2 The 'multidimensional' feature

('multidimensional' 機能)

=begin original

This feature enables multidimensional array emulation, a perl 4 (or
earlier) feature that was used to emulate multidimensional arrays with
hashes.  This works by converting code like C<< $foo{$x, $y} >> into
C<< $foo{join($;, $x, $y)} >>.  It is enabled by default, but can be
turned off to disable multidimensional array emulation.

=end original

この機能は多次元配列エミュレーションを有効にします;
これは、ハッシュで多次元配列をエミュレーションするために使われていた、
 perl 4 (あるいはそれ以前) の機能です。
これは C<< $foo{$x, $y} >> のようなコードを
C<< $foo{join($;, $x, $y)} >> に変換することで動作します。
これはデフォルトで有効ですが、多次元配列エミュレーションを無効にするために
オフにすることができます。

=begin original

When this feature is disabled the syntax that is normally replaced
will report a compilation error.

=end original

この機能が無効にされると、通常置換される構文は、コンパイルエラーを
報告します。

=begin original

This feature is available under this name from Perl 5.34 onwards. In
previous versions, it was simply on all the time.

=end original

この機能は Perl 5.34 から利用可能です。
以前のバージョンでは、単に常時有効でした。

=begin original

You can use the L<multidimensional> module on CPAN to disable
multidimensional array emulation for older versions of Perl.

=end original

古い Perl で多次元配列エミュレーションを無効にするには、
L<multidimensional> CPAN モジュールが使えます。

=head2 The 'bareword_filehandles' feature

('bareword_filehandles' 機能)

=begin original

This feature enables bareword filehandles for builtin functions
operations, a generally discouraged practice.  It is enabled by
default, but can be turned off to disable bareword filehandles, except
for the exceptions listed below.

=end original

この機能は、組み込み関数の操作で、
一般的に非推奨の慣習とされている裸のファイルハンドルを有効にします。
これはデフォルトで有効ですが、後述する例外を除いて裸の単語の
ファイルハンドルを無効にするためにオフにすることができます。

=begin original

The perl built-in filehandles C<STDIN>, C<STDOUT>, C<STDERR>, C<DATA>,
C<ARGV>, C<ARGVOUT> and the special C<_> are always enabled.

=end original

perl 組み込みのファイルハンドル C<STDIN>, C<STDOUT>, C<STDERR>, C<DATA>,
C<ARGV>, C<ARGVOUT> と、特別な C<_> は常に有効です。

=begin original

This feature is available under this name from Perl 5.34 onwards.  In
previous versions it was simply on all the time.

=end original

この機能は Perl 5.34 から利用可能です。
以前のバージョンでは、単に常時有効でした。

=begin original

You can use the L<bareword::filehandles> module on CPAN to disable
bareword filehandles for older versions of perl.

=end original

古い Perl で裸の単語のファイルハンドルを無効にするには、
L<multidimensional> CPAN モジュールが使えます。

=head2 The 'try' feature

('try' 機能)

=begin original

B<WARNING>: This feature is still partly experimental, and the implementation
may change or be removed in future versions of Perl.

=end original

B<警告>: この機能はまだ部分的に実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。

=begin original

This feature enables the C<try> and C<catch> syntax, which allows exception
handling, where exceptions thrown from the body of the block introduced with
C<try> are caught by executing the body of the C<catch> block.

=end original

この機能は、例外を扱える C<try> と C<catch> 構文を有効にします;
C<try> で導入されたブロックの中から投げられた例外は、
C<catch> ブロックの中身を実行することで捕捉されます。

=begin original

This feature is available starting in Perl 5.34. Before Perl 5.40 it was
classed as experimental, and Perl emitted a warning for its usage, except when
explicitly disabled:

=end original

この機能は Perl 5.32 から利用可能です。
Perl 5.40 より前では、これは実験的と位置づけられていて、
明示的に無効にしない限り Perl は警告を出力していました:

    no warnings "experimental::try";

=begin original

As of Perl 5.40, use of this feature without a C<finally> block no longer
triggers a warning.  The optional C<finally> block is still considered
experimental and emits a warning, except when explicitly disabled as above.

=end original

Perl 5.40 から、C<finally> ブロック以外のこの機能の使用はもはや
警告を出力しなくなりました。
オプションの C<finally> ブロックはまだ実験的と考えられていて、
前述のように明示的に向こうにしない限り 警告を出力します。

=begin original

For more information, see L<perlsyn/"Try Catch Exception Handling">.

=end original

さらなる情報については、L<perlsyn/"Try Catch Exception Handling"> を
参照してください。

=head2 The 'defer' feature

('defer' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::defer";

=begin original

This feature enables the C<defer> block syntax, which allows a block of code
to be deferred until when the flow of control leaves the block which contained
it. For more details, see L<perlsyn/defer>.

=end original

この機能は、C<defer> ブロック構文を有効にします;
これは、コードのブロックの実行を、これが含まれているブロックが
実行フローから離れるまで遅延できるようにします。
さらなる詳細については、L<perlsyn/defer> を参照してください。

=begin original

This feature is available starting in Perl 5.36.

=end original

この機能は Perl 5.36 から利用可能です。

=head2 The 'extra_paired_delimiters' feature

('extra_paired_delimiters' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::extra_paired_delimiters";

=begin original

This feature enables the use of more paired string delimiters than the
traditional four, S<C<< <  > >>>, S<C<( )>>, S<C<{ }>>, and S<C<[ ]>>.  When
this feature is on, for example, you can say S<C<qrE<171>patE<187>>>.

=end original

この機能は、文字列の区切り文字として伝統的な四つ S<C<< <  > >>>,
S<C<( )>>, S<C<{ }>>, and S<C<[ ]>> より多くを使えるようにします。
この機能がオンのとき、例えば、S<C<qrE<171>patE<187>>> のようにできます。

=begin original

As with any usage of non-ASCII delimiters in a UTF-8-encoded source file, you
will want to ensure the parser will decode the source code from UTF-8 bytes
with a declaration such as C<use utf8>.

=end original

UTF-8 エンコードされたソースファイルで非 ASCII の区切り文字を使う場合と同様、
C<use utf8> のような宣言によって、パーサがUTF-8 のバイト列から
ソースコードをデコードできるようにした方が良いでしょう。

=begin original

This feature is available starting in Perl 5.36.

=end original

この機能は Perl 5.36 から利用可能です。

=begin original

For a full list of the available characters, see
L<perlop/List of Extra Paired Delimiters>.

=end original

利用可能な文字の完全な一覧については、
L<perlop/List of Extra Paired Delimiters> を参照してください。

=head2 The 'module_true' feature

('module_true' 機能)

=begin original

This feature removes the need to return a true value at the end of a module
loaded with C<require> or C<use>. Any errors during compilation will cause
failures, but reaching the end of the module when this feature is in effect
will prevent C<perl> from throwing an exception that the module "did not return
a true value".

=end original

この機能は、C<require> や C<use> で読み込まれるモジュールの末尾で
真の値を返す必要をなくします。
コンパイル中の全てのエラーは失敗を引き起こしますが、
この機能が有効なときにモジュールの末尾に到達すると、
モジュールが "did not return a true value" という例外を
C<perl> が投げないようにします。

=head2 The 'class' feature

('class' 機能)

=begin original

B<WARNING>: This feature is still experimental and the implementation may
change or be removed in future versions of Perl.  For this reason, Perl will
warn when you use the feature, unless you have explicitly disabled the warning:

=end original

B<警告>: この機能はまだ実験的で、実装は将来のバージョンの Perl で
変わるかもしれません。
このため、この機能を使うと、明示的に無効にしない限り警告が発生します:

    no warnings "experimental::class";

=begin original

This feature enables the C<class> block syntax and other associated keywords
which implement the "new" object system, previously codenamed "Corinna".

=end original

この機能は、以前は "Corinna" と呼ばれていた 「新しい」C<class> ブロック構文と
関連するキーワードを有効にします。

=head1 FEATURE BUNDLES

(機能の束)

=begin original

It's possible to load multiple features together, using
a I<feature bundle>.  The name of a feature bundle is prefixed with
a colon, to distinguish it from an actual feature.

=end original

複数の機能のまとめて読み込むためには、I<機能の束> (feature bundle) が
使えます。
機能の束の名前には、実際の機能と区別するためにコロンが前置されます。

  use feature ":5.10";

=begin original

The following feature bundles are available:

=end original

以下の機能の束が利用可能です:

=begin original

  bundle    features included
  --------- -----------------
  :default  indirect multidimensional
            bareword_filehandles

=end original

  束        含まれる機能
  --------- -----------------
  :default  indirect multidimensional
            bareword_filehandles

  :5.10     bareword_filehandles indirect
            multidimensional say state switch

  :5.12     bareword_filehandles indirect
            multidimensional say state switch
            unicode_strings

  :5.14     bareword_filehandles indirect
            multidimensional say state switch
            unicode_strings

  :5.16     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional say state
            switch unicode_eval unicode_strings

  :5.18     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional say state
            switch unicode_eval unicode_strings

  :5.20     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional say state
            switch unicode_eval unicode_strings

  :5.22     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional say state
            switch unicode_eval unicode_strings

  :5.24     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional postderef_qq
            say state switch unicode_eval
            unicode_strings

  :5.26     bareword_filehandles current_sub evalbytes
            fc indirect multidimensional postderef_qq
            say state switch unicode_eval
            unicode_strings

  :5.28     bareword_filehandles bitwise current_sub
            evalbytes fc indirect multidimensional
            postderef_qq say state switch unicode_eval
            unicode_strings

  :5.30     bareword_filehandles bitwise current_sub
            evalbytes fc indirect multidimensional
            postderef_qq say state switch unicode_eval
            unicode_strings

  :5.32     bareword_filehandles bitwise current_sub
            evalbytes fc indirect multidimensional
            postderef_qq say state switch unicode_eval
            unicode_strings

  :5.34     bareword_filehandles bitwise current_sub
            evalbytes fc indirect multidimensional
            postderef_qq say state switch unicode_eval
            unicode_strings

  :5.36     bareword_filehandles bitwise current_sub
            evalbytes fc isa postderef_qq say signatures
            state unicode_eval unicode_strings

  :5.38     bitwise current_sub evalbytes fc isa
            module_true postderef_qq say signatures
            state unicode_eval unicode_strings

  :5.40     bitwise current_sub evalbytes fc isa
            module_true postderef_qq say signatures
            state try unicode_eval unicode_strings

=begin original

The C<:default> bundle represents the feature set that is enabled before
any C<use feature> or C<no feature> declaration.

=end original

C<:default> 束は、C<use feature> や C<no feature> 宣言が有効になる前の
機能集合を表現しています。

=begin original

Specifying sub-versions such as the C<0> in C<5.14.0> in feature bundles has
no effect.  Feature bundles are guaranteed to be the same for all sub-versions.

=end original

機能の束での  C<5.14.0> の C<0> のような副バージョンを指定しても効果は
ありません。
機能の束は全ての副バージョンに関して同じ事が保証されています。

  use feature ":5.14.0";    # same as ":5.14"
  use feature ":5.14.1";    # same as ":5.14"

=begin original

You can also do:

=end original

次のようにすることもできます:

  use feature ":all";

=begin original

or

=end original

または

  no feature ":all";

=begin original

but the first may enable features in a later version of Perl that
change the meaning of your code, and the second may disable mechanisms
that are part of Perl's current behavior that have been turned into
features, just as C<indirect> and C<bareword_filehandles> were.

=end original

しかし、前者は後のバージョンの Perl があなたのコードの意味を変える
機能を有効にすることがあり、後者は C<indirect> や
C<bareword_filehandles> のような、機能扱いになった Perl の現在の
振る舞いの一部の気候を無効にするかもしれません。

=head1 IMPLICIT LOADING

(暗黙の読み込み)

=begin original

Instead of loading feature bundles by name, it is easier to let Perl do
implicit loading of a feature bundle for you.

=end original

機能の束を名前で読み込むより、Perl に機能の束を暗黙に読み込ませるように
した方が簡単です。

=begin original

There are two ways to load the C<feature> pragma implicitly:

=end original

C<feature> プラグマを暗黙に読み込むには二つの方法があります:

=over 4

=item *

=begin original

By using the C<-E> switch on the Perl command-line instead of C<-e>.
That will enable the feature bundle for that version of Perl in the
main compilation unit (that is, the one-liner that follows C<-E>).

=end original

Perl のコマンドラインで C<-e> オプションの代わりに C<-E> オプションを
使用した場合。
これにより、main コンパイル単位(つまり、C<-E> に引き続く 1 行野郎)で
そのバージョンの Perl の機能の束が有効になります。

=item *

=begin original

By explicitly requiring a minimum Perl version number for your program, with
the C<use VERSION> construct.  That is,

=end original

C<use VERSION> 構文を使ってプログラムが必要とする最低限の Perl バージョン
番号を明示的に指定した場合。
つまり、以下のようにすると:

    use v5.36.0;

=begin original

will do an implicit

=end original

暗黙のうちに以下のように:

    no feature ':all';
    use feature ':5.36';

=begin original

and so on.  Note how the trailing sub-version
is automatically stripped from the
version.

=end original

なるということです。
末尾の副バージョンは自動的にバージョンから取り除かれるようになったことに
注意してください。

=begin original

But to avoid portability warnings (see L<perlfunc/use>), you may prefer:

=end original

しかし移植性の警告(L<perlfunc/use> を参照してください)を避けるために、
以下のようにするのを好むかもしれません:

    use 5.036;

=begin original

with the same effect.

=end original

これでも同じ効果が得られます。

=begin original

If the required version is older than Perl 5.10, the ":default" feature
bundle is automatically loaded instead.

=end original

要求したバージョンが Perl 5.10 より前の場合、代わりに機能の束 ":default" が
自動的に読み込まれます。

=begin original

Unlike C<use feature ":5.12">, saying C<use v5.12> (or any higher version)
also does the equivalent of C<use strict>; see L<perlfunc/use> for details.

=end original

C<use feature ":5.12"> と異なり、C<use v5.12> (またはそれ以上) とすると、
C<use strict> と等価なことを行います;
詳しくは L<perlfunc/use> を参照してください。

=back

=head1 CHECKING FEATURES

(機能のチェック)

=begin original

C<feature> provides some simple APIs to check which features are enabled.

=end original

C<feature> はどの機能が有効になっているかをチェックするための
単純な API をいくつか提供します。

=begin original

These functions cannot be imported and must be called by their fully
qualified names.  If you don't otherwise need to set a feature you will
need to ensure C<feature> is loaded with:

=end original

これらの関数はインポートできず、完全修飾名によって呼び出さなければ
なりません。
他に機能を設定する必要がない場合、確実に C<feature> が
読み込まれるようにする必要があります:

  use feature ();

=over

=item feature_enabled($feature)

=item feature_enabled($feature, $depth)

  package MyStandardEnforcer;
  use feature ();
  use Carp "croak";
  sub import {
    croak "disable indirect!" if feature::feature_enabled("indirect");
  }

=begin original

Test whether a named feature is enabled at a given level in the call
stack, returning a true value if it is.  C<$depth> defaults to 1,
which checks the scope that called the scope calling
feature::feature_enabled().

=end original

指定された機能が、呼び出しスタックの指定されたレベルで
有効かどうかをテストし、有効なら真の値を返します。
C<$depth> のデフォルトは 1 です;
つまり feature::feature_enabled() を呼び出したスコープを
呼び出したスコープを調べます。

=begin original

croaks for an unknown feature name.

=end original

不明な機能名に対しては croak します。

=item features_enabled()

=item features_enabled($depth)

  package ReportEnabledFeatures;
  use feature "say";
  sub import {
    say STDERR join " ", feature::features_enabled();
  }

=begin original

Returns a list of the features enabled at a given level in the call
stack.  C<$depth> defaults to 1, which checks the scope that called
the scope calling feature::features_enabled().

=end original

呼び出しスタックの指定されたレベルで有効な機能の一覧を返します。
C<$depth> のデフォルトは 1 です;
つまり feature::features_enabled() を呼び出したスコープを
呼び出したスコープを調べます。

=item feature_bundle()

=item feature_bundle($depth)

=begin original

Returns the feature bundle, if any, selected at a given level in the
call stack.  C<$depth> defaults to 1, which checks the scope that called
the scope calling feature::feature_bundle().

=end original

呼び出しスタックの指定されたレベルで選ばれている機能の束があれば、
それを返します。
C<$depth> のデフォルトは 1 です;
つまり feature::feature_bundle() を呼び出したスコープを
呼び出したスコープを調べます。

=begin original

Returns an undefined value if no feature bundle is selected in the
scope.

=end original

このスコープで選択されている機能の束がない場合、未定義値を返します。

=begin original

The bundle name returned will be for the earliest bundle matching the
selected bundle, so:

=end original

返される束の名前は、選択された束に一致する最も早い束です;
従って:

  use feature ();
  use v5.12;
  BEGIN { print feature::feature_bundle(0); }

=begin original

will print C<5.11>.

=end original

これは C<5.11> を表示します。

=begin original

This returns internal state, at this point C<use v5.12;> sets the
feature bundle, but C< use feature ":5.12"; > does not set the feature
bundle.  This may change in a future release of perl.

=end original

これは内部状態を返します;
現時点では C<use v5.12;> は機能の束を設定しますが、
C< use feature ":5.12"; > は機能の束を設定しません。
これは将来のリリースの Perl で変更されるかもしれません。

=back

=cut

=begin meta

Translate: SHIRAKATA Kentaro <argrath@ub32.org>
Status: completed

=end meta

